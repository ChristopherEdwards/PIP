public I18NATRA //Automatic TSet Translation
  /*
	ORIG: kumarb - 10/21/2005
	DESC: Automatic TSet Translation

	 I18N=QUIT	This program does not need to follow I18N standards
	 DESC:		This program is responsible for an automatic 
			translation of normalized PROFILE phrases.
	 
			The user is prompted from a UTLREAD screen to select
			which Tset to automatically translate.
	 
			Then the program collates thru the normalized sequences
			of this Tset. For each sequence it gets the keys to the
			extracted phrases global to find a phrase. This phrase
			is translated by Perfect Match method. That is, there 
			has to be an exact translated copy of the phrase in the
			MLD. If there is such a translation existing, it is
			written back as a translation of the phrase in question
			and Tset information is updated.
	 
			Finaly a function return message is built in RM.
	 
	 CALLED BY:	Menu system. No other routines.
	 CALL TO:      $$DI^SCATAB      	- Builds %TAB array
			UTLREAD                 - Execute %TAB array
			GETKEY^I18NUTL          - Get key for extracted phrase
			TRANSLATE^I18NUTL       - Translation utility
	 GLOBALS READ:
			I18NTS		- Tsets
			I18NEXT		- Header level for normalized phrases
	 GLOBALS SET:
			I18NTS		- Tsets
			I18NEXT		- Header level for normalized phrases
	 ARGUMENTS:
	 INPUTS:
	
	 RETURNS:
	 EXAMPLE:
	 LIBRARY:
	
	---Revision History-------------------------------------------
	
	10/21/05 - KUMARB - CR 16921
        	Converted to PSL.
	
	--------------------------------------------------------------
	*/

	do INIT

	quit
	
INIT	// New specific variables for this program
	
	type Number auttrn,normcnt
	type String OLNT,VFMQ
	
	set normcnt=0,auttrn=0

	do QUERY

	quit

QUERY // Query

	type public String VFMQ

	type Number TMET,TSID
	type Date EXTDT
	type String %READ,%TAB(),HDG

	set HDG=$$^MSG(5918)
	set HDG=$J("",40-(HDG.length()/2))_HDG
	
	set %TAB("TSID")=$$DI^SCATAB("[I18NTS2]TSID","Tset ID",12,"[I18NTS2]","","","T")
	set %TAB("EXTDT")=$$DI^SCATAB("[I18NEXTH]EXTDT",,,"[I18NEXTH]EXTDT:DISTINCT")
		
	set %READ="@HDG,,TSID#1,EXTDT#1",TMET=0
	
	do ^UTLREAD

	if "Q"[VFMQ quit
	
	do RUN
	
	quit
	
RUN	// Loop the Translation Set for phrases - Only Normalized phr

	type public Number auttrn,normcnt,TSID
	type public String ER,EXTDT,RM,VFMQ

	type Number tsseqN
	type String extdt,np,nptrans
	
	// Tset ~p1 already in use
	lock +I18NTS2(TSID):1 else  do Runtime.setErrMSG("I18NTS2",5438,TSID) quit
	
	type DbSet ds=Db.selectDbSet("I18NTSETN","TSID=:TSID")
	while ds.next() do {
		type RecordI18NTSETN i18ntsn=ds.getRecord("I18NTSETN")
		set normcnt=normcnt+1
		do GETKEY^I18NUTL(.i18ntsn,.extdt,.np)
		// Find a translation
		do TRANEXT^I18NUTL(EXTDT,np,.nptrans)
	
		// Update Exth/Tset if transl is found 
		if 'nptrans.get().isNull() set auttrn=auttrn+$$UPDDB(TSID,extdt,np,nptrans)
		}
	
	// Unlock TSet
	lock -I18NTS2(TSID)
	
	set ER="W"
	// Tset ~p1 ~p2 Auto Translation: ~p3 read, ~p4 translated phrases
	set RM=$$^MSG(5450,TSID.get(),$S(VFMQ="Q":" not ",1:" "),normcnt,auttrn)

	quit

UPDDB(Number TSID,String extdt,String np,String nptrans) // Update I18NEXT db and TSET statistics
	
	// Set fields for I18NEXT
	type RecordI18NEXTH i18nexth=Db.getRecord("I18NEXTH","EXTDT=:extdt,NORMPHR=:np",1)

	// If stat not untransl or aut.transl
	if i18nexth.ttype=1 quit 0

	set i18nexth.targphr=nptrans
	set i18nexth.trasuid=%UserID
	set i18nexth.trasdt=%CurrentDate
	// Automatic Translation
	set i18nexth.ttype=2
	do i18nexth.save()

	// Set fields for I18NTS
	if 'i18nexth.ttype do {
		type RecordI18NTS2 i18nts2=Db.getRecord("I18NTS2","TSID=:TSID",1)
		set i18nts2.nonormaut=i18nts2.nonormaut+1
		do i18nts2.save()
		}
	
	quit 1
 #OPTION ResultClass ON
Public String vSIG()	quit "60235^24982^Balasubramonian Sankar^3508"	// Signature - LTD^TIME^USER^SIZE
