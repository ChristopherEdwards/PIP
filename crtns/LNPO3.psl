LNPO3	 /* 
    ORIG: GRAY - 02/03/2000
    DESC: Display fees due at payoff
    
    ---- Comments --------------------------------------------------------
    
    ---- Revision History ------------------------------------------------
 
	08/04/02 - GRAY - 49794
		   Modified calls into LNPO2 since LNPO2 has been PSL'd.  Also
		   removed use of toArray method.
	
	2/1/2000 - GRAY - 32507
		   Removed code no longer needed due to the Elimination
		   of Teller/Branch Character Interface and placed remaining
		   code into PSL'd procedure definition.

 */
	
	quit
	
	
Public LOAD	// Create data array

	type RecordLN ln=Db.getRecord("LN","CID")
	
	// Calculate payoff accrual
	set %ACR="" do PAYACR^LNPO2(.ln) quit:ER
	
	// Calculate fee amounts due but not previously assessed
	set POFEE=$$LNFEE^LNPO2(.ln,10,.FEE)
	
	new CNT,N
	set (CNT,N)="" 
	for  set N=$O(FEE(N)) quit:N=""  do {
		set CNT=CNT+1
		set POFEE(CNT)=N_"|0|"_FEE(N)_"|"_FEE(N)
		set XPOFEE(N)=CNT
		}
	
	// Add in amounts current due and already assessed
	
	type ResultSet rs=Db.select("FEETYP","LNFEE","CID=:CID")
	if rs.isEmpty() quit
	while rs.next() do {
		new FEEREM
		set N=rs.getCol(1)
		type RecordLNFEE lnfee=Db.getRecord("LNFEE","CID,N")
		set FEEREM=lnfee.feerem
	
		new NOD
		set POFEE=POFEE+FEEREM
		if $G(XPOFEE(N))="" do { quit
			set CNT=CNT+1
			set POFEE(CNT)=N_"|"_+FEEREM_"|0|"_+FEEREM
			set XPOFEE(N)=CNT
			}
	
		set NOD=XPOFEE(N)
		set $P(POFEE(NOD),"|",2)=+FEEREM
		set $P(POFEE(NOD),"|",4)=$P(POFEE(NOD),"|",4)+FEEREM
		}

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43539^Sanchez SCM Administrator^1452"	// Signature - LTD^TIME^USER^SIZE
