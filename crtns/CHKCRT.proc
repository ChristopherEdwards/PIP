CHKCRT	  /*
DESC: Ties a CRT to a specific printer

---- Comments --------------------------------------------------------

---- Revision History ------------------------------------------------

	10/17/05 - PUTTASWH - CR17011
	 	   Converted to PSL. 

 */ 	
 
	do CREATE
	quit
	
CREATE	//------------------------------------

	set %ProcessMode=0  do VINIT quit 
	

VINIT	// init variables
	
	type public Boolean ER	
	type Number %PG,%PAGE		
	type String CDEV,CTY,OLNTB,PRNT,VFMQ

	set %PG=0,%PAGE=1,ER=0	
	
	do VPG00(.CDEV,.CTY,.VFMQ) quit:ER 
	
	type RecordCHKDEVC chkdevc=Db.getRecord("CHKDEVC","DEVICE=:CDEV,CTYPE=:CTY",1)
	if chkdevc.getMode() set PRNT=chkdevc.prnt
	
	if "DFQ".find(VFMQ) do VER(.chkdevc,CDEV,VFMQ) quit
	set %PG=%PG+1
	
	do VPG01(CDEV,CTY,.VFMQ)	
	do VER(.chkdevc,CDEV,VFMQ)
	
	quit

	
VPG00(String CDEV,	// Device
	String CTY,   	// Check Type
	String VFMQ)	// VFMQ
	// Set-up
	
	type public Boolean ER	
	type public String TLO
	type String %NOPRMT,PRNT,%READ,%TAB
		
	set %TAB("CDEV")=".CDEV1/XPP=do POSCDEV^CHKCRT",CDEV=TLO
	set %TAB("CTY")=".CHKTYP1/TBL=[UTBLCHKS]"	
	set %READ="@@%FN,,,CDEV/REQ,CTY/REQ",%NOPRMT="N"
	
	do ^UTLREAD if VFMQ="Q" set ER=1 quit			
		
	quit
	
VPG01(String CDEV,	// Device
	String CTY,   	// Check Type
	String VFMQ)	// VFMQ	
	// actual notice information	
		
	type public Boolean ER
	type public String OLNTB		
	type String HDG(),%READ,%TAB	
	
	// Tie CRT to Check Device
	set HDG(1)=$$^MSG(6608),HDG(1)=HDG(1).justify(80,0,"")
	// CRT Device: ~p1
	set HDG(2)=$$^MSG(6607,CDEV)
	// Check Type: ~p1
	set HDG(3)=$$^MSG(6606,CTY)
	
	set %TAB("PRNT")=".IO4/XPP=D POSPRNT^CHKCRT(CTY)"	
	
	set %READ="@HDG(1),,@HDG(2),@HDG(3),,PRNT/REQ"
	
	do ^UTLREAD if VFMQ="Q" set ER=1
	
	quit	

POSCDEV	 // Invalid device

	type public Boolean ER
	type public String X	
	
	quit:X=""  	
	
	type RecordDEVICE device=Db.getRecord("DEVICE","DEVNAME=:X",1)
	// Invalid device
	if 'device.getMode() do Runtime.setErrMSG("DEVICE",7879) quit		
	
	// Not a CRT
	if device.type'="TRM" do Runtime.setErrMSG("DEVICE",2017)	
		
	quit

	
	
POSPRNT(String CTY)   	// Check Type	
	// Post-processor for PRNT
	
	type public Boolean ER
	type public String V,X	
	type String CPTYP,TYPE	

	quit:X=""  if X=V quit  // no change
	if X="NONE" quit  // not tied to any printer
	
	//If device is not an RMS file, make sure it exists 
		
	type RecordDEVICE device=Db.getRecord("DEVICE","DEVNAME=:X",1)
	// Invalid device
	if 'X.isLike("%.%"),'device.getMode() do Runtime.setErrMSG("DEVICE",7879) quit	
	
	if 'X.isLike("%.%") do {	
		set X=device.device
		set TYPE=device.type
		}
	
	// Invalid device
	if 'X.isLike("%.%"),'device.getMode() do Runtime.setErrMSG("DEVICE",7879) quit	
	
	// Not a printer device
	if 'X.isLike("%.%"),((TYPE'="PTR")&(TYPE'="PNTQ")) do Runtime.setErrMSG("DEVICE",2021) quit
		
	type RecordCHKDEVP chkdevp=Db.getRecord("CHKDEVP","DEVICE=:X",1)
	if chkdevp.getMode() do {		
		set CPTYP=chkdevp.ctype
		// "Printer is loaded with ~p1 checks"
		if CPTYP'=CTY do Runtime.setErrMSG("DEVICE",2230,"CPTYP") quit 
	}	
	
	quit
	
VER(RecordCHKDEVC chkdev,	// CHKDEVC Object
	String CDEV,		// Device
	String VFMQ)		// VFMQ	
	// Verify and process	
	
	type public Boolean ER	
	
	if VFMQ="Q"!(ER) do VEXIT(CDEV,VFMQ) quit
	do FILE(.chkdev)
	do VEXIT(CDEV,VFMQ) quit
	

VEXIT(String CDEV,		// Device	
	String VFMQ)		// VFMQ	

	type public Boolean ER	
	
	if (%ProcessMode=2)!(%ProcessMode=4)!(ER) kill CDEV quit
	
	// CRT device not tied
	if VFMQ="Q" do Runtime.setErrMSG("DEVICE",653)
	// CRT device tied
	else  do Runtime.setErrMSG("DEVICE",654)
	
	set ER="W"
	
	quit
	

FILE(RecordCHKDEVC chkdevc)	
	// File data
	
	type public String PRNT	
	
	set chkdevc.PRNT=PRNT
	do chkdevc.bypassSave()
	
	quit
 

vSIG()	quit "60233^27025^Hema Puttaswamy^3541"	// Signature - LTD^TIME^USER^SIZE
