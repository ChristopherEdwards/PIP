SWINTSRV		/*
	 ORIG: KELLYP - 11/12/1999
	 DESC: Internal SWIFT Server Control Procedure

	 ---- Comments --------------------------------------------------------
	 This procedure is used to start/detach and stop all five internal SWIFT 
	 servers: SWIFTEFT, SWIFT900, SWIFT910, SWIFT920, and SWIFT942.  When 
	 starting the servers, the user is given the option to select which servers
	 they want to start via the SWINTSTR screen.

	 The @SWINTSTR function, which calls the START section of this procedure
	 is used to start the servers.  The @SWINTSTP function, which calls the STOP
	 section of this procedure, sets the SWINTSRV file definition variables to 
	 "STOP", regardless of whether those servers are running or not.  Each 
	 individual server will cease processing when it detects that its 
	 corresponding data item in SWINTSRV has been set to "STOP".  
 
	---- Revision History ------------------------------------------------
	
	09/07/05 - KELLYP - CR 17118
		   Modified entire procedure to remove references to the MT100
		   which has been obsoleted by SWIFT.  Also rewrote entire 
		   procedure to conform to current PSL standards and removed
		   pre-2003 revision history.

	*/

	quit


public START	// Start Internal IBS Swift Servers 

	type public String ER

	type Boolean MT103STR,MT900STR,MT910STR,MT920STR,MT942STR
	type Number KEY
	type String PROC,SRVRNM,VFMQ

 	set KEY=1
 	set (MT103STR,MT900STR,MT910STR,MT920STR,MT942STR)=0		
 
	type RecordSWINTSRV swintsrv=Db.getRecord("SWINTSRV","KEY=:KEY",1)
	set swintsrv.mt103stp=""
	set swintsrv.mt900stp=""
	set swintsrv.mt910stp=""
	set swintsrv.mt920stp=""
	set swintsrv.mt942stp=""
	do swintsrv.save()	// Reset all stop commands to null
	
	// Load the SWINTSTR screen
	do DRV^USID(1,"SWINTSTR",.swintsrv)
	
	if VFMQ="Q" quit  

	// Start up the selected servers
	if MT103STR do DETACH("SWIFT_MT103","EFT")
	if MT900STR do DETACH("SWIFT_MT900","900")
	if MT910STR do DETACH("SWIFT_MT910","910")
	if MT920STR do DETACH("SWIFT_MT920","920")
	if MT942STR do DETACH("SWIFT_MT942","942")

	// All servers selected to start should be running at this point
	set ER="W"

	quit

 
DETACH(String PRCNAM, String SRVR)   // Detach Internal IBS SWIFT Servers

	type public Boolean ER
	type public String RM()

	type Boolean X

 	if $$VALIDNM^%ZPID(PRCNAM) set RM(RM("").order(-1)+1)=$$^MSG(1168,PRCNAM) quit

	// Detach the server
	set X=$$^%ZJOB("^SWIFT"_SRVR,"PRO="_PRCNAM)

	// Process Submitted
	if X set RM(RM("").order(-1)+1)=$$^MSG(6800,PRCNAM) 

	// Process Not Submitted
	else  set RM(RM("").order(-1)+1)=$$^MSG(6799,PRCNAM)
 
	if 'X do Runtime.setErrMSG("SWINTSRV","4160","SRVR") do ERR^SWIFTUTL quit:ER
	
	quit


public STOP	// Stop all Internal IBS SWIFT Servers

	type public String ER,RM()

	type Number KEY

	set KEY=1

	type RecordSWINTSRV swintsrv=Db.getRecord("SWINTSRV","KEY=:KEY",1)

	set swintsrv.mt103stp="STOP"
	set swintsrv.mt900stp="STOP"
	set swintsrv.mt910stp="STOP"
	set swintsrv.mt920stp="STOP"
	set swintsrv.mt942stp="STOP"
	
	do swintsrv.save()
	
	set ER="W"
	
	// ~p1 server scheduled to stop
	set RM(1)=$$^MSG("1370","SWIFT_MT103")
	set RM(2)=$$^MSG("1370","SWIFT_MT900")
	set RM(3)=$$^MSG("1370","SWIFT_MT910")
	set RM(4)=$$^MSG("1370","SWIFT_MT920")
	set RM(5)=$$^MSG("1370","SWIFT_MT942")
	
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60150^62083^Pat Kelly^3168"	// Signature - LTD^TIME^USER^SIZE
