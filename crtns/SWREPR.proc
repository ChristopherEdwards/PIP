SWREPR	// SWIFT Message Repair
	/*
	  
	       ORIG:  TSAI - 3 MAY 1994
	  CALLED BY:  DATA-QWIK Function (SWREPR)
	      CALLS:  ^UTLERR,^UTLREAD

	   PROJ #'S:  SWIFT Message Repair
	       DESC:  This routine is used for the SWIFT message screens MT103*
	        , MT202*, MT210*, and MT300*. It is used for message
	        repair.  It is called from an IBS function (SWREPR) to
	        modify SWIFT messages.  It prompts for the sequence (repair
	        queue) number, then paints the SWIFT message screen.

	  ARGUMENTS:  None.

	   FILES -
	       READ:  SWIFT
	        SET:  SWIFT

	      INPUT:
	     OUTPUT:

	  ---- Revision History -------------------------------------------------
	   02/03/06 - ratht - CR 18292
	   	Modified Section BULDTBL- Initilized variable status and kept 
	   	status in if condition.
	   	Removed "=" at if condition in END section
	   	
	   09/07/05 - KELLYP - CR 17118
	   	Removed references to the MT100 message type which was obsoleted
	   	by SWIFT.  Also removed pre-2003 revision history.

	  -----------------------------------------------------------------------

	*/	

	quit
	
public UPD	// 

	do INIT(1)
	quit


INIT(%ProcessMode)	//

	new OLNTB,VFMQ
	
	type RecordSWIFT fSWIFT

	set %PG=0 
	set %PAGE=1
	do VPG(.fSWIFT)

	quit


VPG(RecordSWIFT fSWIFT)	//Page control

	new FINISH
	set FINISH=0
	for  do { quit:FINISH
		if %PG=0 do VPG00 if ER set FINISH=1 quit 
		if %PG>0 do VPG01(.fSWIFT)

	   	//Process completed/aborted
		if "DFQ"[VFMQ do VER(.fSWIFT) set FINISH=1 quit
		set %PG=%PG+1
		}
	quit


VPG00	//Set up

	new %TAB,DONE

	//Prompts
	do BULDTBL

	set OLNTB=45

	set %TAB("REPSEQ")=".SEQ2/TBL=""[REPTBL]TRREF,[REPTBL]TYPE,[REPTBL]SWFTDT,[REPTBL]RESPDT/REQ"""
	set %READ="@@%FN,,,REPSEQ/REQ" 
	set %NOPRMT="C"

	do ^UTLREAD 

	if VFMQ="Q" set ER=1

	quit


BULDTBL	//Buid look up table from repair queue

	set qtype=$S(%ProcessMode=1:"REP",1:"REV")
	set (REPSEQ,TRREF,TYPE,status)=""

	// loop through and build look up table with new key structure
	type ResultSet rs=Db.select("SEQ,TRREFNO,TYPE","SWIFTQ2","QUE=:qtype")
	while rs.next()  do {
		set REPSEQ=rs.getCol("SEQ")
		set TRREF=rs.getCol("TRREFNO")
		set TYPE=rs.getCol("TYPE")

		if qtype="REV" do {
			set status=$$CHKUID(TRREF,TYPE)
			if 'status quit 
			}

		type RecordREPTBL reptbl=Db.getRecord("REPTBL","REPSEQ=:REPSEQ",1)
		set reptbl.trref=TRREF
		set reptbl.type=TYPE
		do reptbl.bypassSave()
		}
	quit


CHKUID(TRREF,TYPE)	//

	/*
	  This routine is used for review function to make sure the look
	  up table will only show those records which can be released by current
	  user. Return 1 if the record need to be included into the lookup table.
	*/

	type RecordSWIFT swift=Db.getRecord("SWIFT","SWDIRECT='OUT',TRREFNO=:TRREF,MSG=:TYPE",1)

	quit (%UserID'=swift.uid)


VPG01(RecordSWIFT fSWIFT)	// SWIFT messages

	type RecordREPTBL reptbl=Db.getRecord("REPTBL","REPSEQ=:REPSEQ",1)

	set TRREFNO=reptbl.trref 
	set MSG=reptbl.type 
	set SWDIRECT="OUT"
	set fSWIFT=Db.getRecord("SWIFT","SWDIRECT=:SWDIRECT,TRREFNO=:TRREFNO,MSG=:MSG",1)

	do fSWIFT.setAuditFlag(1)

	if MSG=199 set SID="MT199PG1"
	if MSG=210 set SID="MT210"

	// 40832 - Repair of MT950 messages not supported
	if MSG=950 do { quit
		set VFMQ="Q" 
		set ER=1 
		set RM=$$^MSG(4516)
		}

	if (MSG'=199)&(MSG'=210) set SID="MT"_MSG_"DRV"

	// Record locked by another user
	lock +SWIFT("OUT",TRREFNO,MSG):2 else  set VFMQ="Q",ER=1,RM=$$^MSG(2333) quit

	do DRV^USID(%ProcessMode,SID,.fSWIFT)

	if ER'=1 do SUBMENU
	quit


VER(RecordSWIFT fSWIFT)	// 

	if VFMQ="Q" do END quit

	do FILE(.fSWIFT)

	do END 

	quit


FILE(RecordSWIFT fSWIFT)	//File screen

	if %ProcessMode=3 do Db.delete("SWIFT","SWDIRECT=:SWDIRECT AND TRREFNO=:TRREFNO AND MSG=:MSG")
	else  do fSWIFT.save() 

	quit


SUBMENU	//sub-menu for asking to review

	// Ready to review ?
	set OPT=$$YN^DBSMBAR("",$$^MSG(7379),0)
	if OPT do {

		// Record locked by another user
		lock +SWIFTQ("REP"):2 else  set VFMQ="Q",ER=1,RM=$$^MSG(2333) quit

		// Record locked by another user
		lock +SWIFTQ("REV"):2 else  set VFMQ="Q",ER=1,RM=$$^MSG(2333) quit

		type RecordSWQCNT swqcnt=Db.getRecord("SWQCNT","QUE='REV'")

		//total in review queue
		set swqcnt.ptoday=swqcnt.ptoday+1

		//total in review queue
		set swqcnt.tot=swqcnt.tot+1

		set revseq=swqcnt.nseq+1
		if revseq>9999 set revseq=1

		//sequence number
		set swqcnt.nseq=revseq

		do Db.delete("SWIFTQ2","QUE='REP' AND SEQ=:REPSEQ AND TRREFNO=:TRREFNO AND TYPE=:MSG")

		do Db.delete("SWIFTQ2","QUE='REV' AND SEQ=:revseq AND TRREFNO=:TRREFNO AND TYPE=:MSG")

		type RecordSWIFTQ2 SWIFTQ2=Db.getRecord("SWIFTQ2","QUE='REV',SEQ=:revseq,TRREFNO=:TRREFNO,TYPE=:MSG",1)

		do SWIFTQ2.bypassSave()

		do swqcnt.bypassSave()

		type RecordSWQCNT swqcnt=Db.getRecord("SWQCNT","QUE='REP'")

		set swqcnt.tot=swqcnt.tot-1
		if swqcnt.tot<0 set swqcnt.tot=0
		do swqcnt.bypassSave()

		lock -SWIFTQ("REP")
		lock -SWIFTQ("REV")
		}

	quit


END	//

	if $D(SWDIRECT)&$D(TRREFNO)&$D(MSG) lock -SWIFT(SWDIRECT,TRREFNO,MSG)

	do Db.fastDelete("REPTBL")

	kill %TAB

	if $G(ER) quit  
	set ER="W"
	if %ProcessMode=1 do {
	
		// SWIFT message ~p1 ~p2 of ~p3 not modified.
		if VFMQ="Q" set RM=$$^MSG(7381,"MT",MSG,TRREFNO)

		// SWIFT message ~p1 ~p2 of ~p3 modified.
		else  set RM=$$^MSG(7380,"MT",MSG,TRREFNO)
		}
	quit

vSIG()	quit "60299^25314^Tarini Charan Rath^5047"	// Signature - LTD^TIME^USER^SIZE
