LNPTS22(RecordLN ln)	//

	/*

	Payoff Accrual Utility

	       ORIG:  David Caliendo (Graem's dad) - 07/17/86
	  CALLED BY:
	      CALLS:  ^LNACR0,^LNACR1,^LNICM,^UTLERR
	   PROJ #'S:
	       DESC: Calculates accrual adjustment at time of loan payoff

	   GLOBALS -  ^STBL
	       READ:
	        SET:

	      INPUT: PAYACM,CIF,EFD,TJD,IACM,ACR,PLDF

	     OUTPUT: %ACR   Calculated payoff accrual
	             ADJ    Difference between calculated and current accrual
	  ---- Revision History ------------------------------------------------

	05/22/03 - CARROLLJ - 51349
		   Corrected undefined error on variable ACR.

	06/04/01 - ARCILLAZ - 43583
		   Converted to PSL. Removed old revision history.

	*/

	set MINACR=ln.minacr
	set %ACR=ln.acr 
	set ADJ=0 
	set %ACM=ln.payacm

	// Payoff - Include Payoff Date Flag
	set %PLDF=ln.pldf

	// Payment Calculation Method
	Type RecordSTBLPCM stblpcm=Db.getRecord("STBLPCM","KEY=:ln.pcm",1)
	set PCM=stblpcm.pcmp
	if '%ACM!('$E(PCM,2)) do SIM1(.ln) quit
	if %ACM=1!(%ACM=2)!(%ACM=4) do ACT1(.ln) quit
	if %ACM=3 do ACT3(.ln) quit
	set ER=1 set ET="INVPACM" do DSPBP^UTLERR
	quit


SIM1(RecordLN ln)	// Simple interest accrual adjustment

	if 'ln.pldf,%EffectiveDate=%SystemDate quit
	do OL^LNACR0(.ln)
	quit

ACT1(RecordLN ln)	// Pre-calculated interest - other than add-on

	do OL^LNACR1(.ln)
	set ADJ=$$^SCARND(%ACR-ln.acr,0,CID)
	quit

ACT3(RecordLN ln)	// Pre-calculated interest - add-on

	set %INT=0
	if %EffectiveDate>(ln.pmtgrc+ln.mdt) do {
		set %ND=%EffectiveDate+ln.pldf
		set %INT=$$^UIC(ln.balint,ln.mdt,%ND,ln.iacm,ln.irn,0,ln.dist1af,ln.dist1fre,ln.icpf,ln.schnd,ln.schld)
		}
	set %ACR=$$^SCARND(%INT,0,CID) 
	set ADJ=$$^SCARND(%ACR-ln.acr,0,CID)
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43566^Sanchez SCM Administrator^1624"	// Signature - LTD^TIME^USER^SIZE
