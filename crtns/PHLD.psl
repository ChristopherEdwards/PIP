PHLD	 /*
    ORIG: GRAY - 01/20/2000
    DESC: Permanent Hold
    
    ---- Comments --------------------------------------------------------
    
    ---- Revision History ------------------------------------------------

	02/13/06 - NATRAJAH - 19498
		   In REMHOLD section, corrected the logic around use of
		   SEQ for DB access.
		   
	11/24/05 - NATRAJAH - 13734
		   General DBI3 cleanup.     

	09/26/03 - CARROLLJ - 51630
		   Modified PUTHOLD section to remove toArray.

	
 */
	
	quit
	
	
Public PUTHOLD(CID,START,EXP,AMOUNT,CODE,UID,TLO,TCMT)	;
	/*
	Function places hold on account CID which begins on START, ends on
	EXP, for the amount of AMOUNT.

	ARGUMENTS       CID     Account Number  	/TYP=N/REQ
			START	Start Date      	/TYP=D/REQ
			EXP	Expiration Date		/TYP=D/REQ
			AMOUNT	Hold Amount		/TYP=$/REQ
			CODE	Permanent Hold Code	/TYP=N/REQ
			UID	User ID			/TYP=T/REQ
			TLO	Terminal Location	/TYP=T/NOREQ
			TCMT	Comment			/TYP=T/NOREQ
 
	RETURNS		Hold Sequence Number - if success
					  -1 - if failure 

	FORMAT		set seq=$$PUTHOLD^PHLD(CID,START,EXP,AMOUNT,CODE,UID,TLO,TCMT)
	*/
	
	type Number SEQ
	
	if ($G(CID)="")!($G(START)="")!($G(AMOUNT)="")!($G(CODE)="")!($G(UID)="") quit -1
	if 'Db.isDefined("UTBLPHC","CODE") quit -1
	set SEQ=Db.nextVal("PHLD","CID")
	
	type RecordPHLD phld=Class.new("RecordPHLD")	// SPG 7/12/00
		
	set phld.cid=CID	
	set phld.seq=SEQ
	set phld.stdt=START		// Hold start date
	set phld.expdt=EXP     		// Hold expiration Date
	set phld.amt=AMOUNT     	// Hold Amount
	set phld.phc=CODE   		// Permanent Hold code (from ^UTBL("PHC"
	set phld.%uid=UID    		// User ID
	set phld.tlo=TLO    		// Terminal Location
	set phld.cdt=%CurrentDate	// Calendar Date
	set phld.tim=%CurrentTime	// Time
	set phld.tcmt=TCMT		// Transaction Comment
	do phld.save()
	
	quit SEQ
	
Public REMHOLD(CID,EXP,AMOUNT)	 

	/*
	Function removes hold from account CID which expires on
	EXP, for the amount of AMOUNT.

	ARGUMENTS       CID     Account Number  	/TYP=N/REQ
			EXP     Expiration Date 	/TYP=D/REQ
			AMOUNT  Hold Amount     	/TYP=$/REQ
	RETURNS  	 1 - if success
			-1 - if failure

	FORMAT		set status=$$REMHOLD^PHLD(CID,EXP,AMOUNT)
	*/
	
	type Number COUNTER,FOUND,SEQ,TAMOUNT
	type Date TSTART,TEXP
	type Public String ffA
	kill ffA
	if ($G(CID)="")!($G(EXP)="")!($G(AMOUNT)="") quit -1
	set SEQ=99999,FOUND=0
	for  do { quit:SEQ=""!FOUND
		type ResultSet rs=Db.select("SEQ","PHLD","CID=:CID AND SEQ<:SEQ","SEQ DESC")
		if rs.isEmpty() set SEQ=""
		if rs.next() do {
			set SEQ=rs.getCol("SEQ")
			type ResultSet rs1=Db.select("STDT,EXPDT,AMT","PHLD","CID=:CID AND SEQ=:SEQ")
			if rs1.next() do {
				set TSTART=rs1.getCol("STDT")
				set TEXP=rs1.getCol("EXPDT")
				set TAMOUNT=rs1.getCol("AMT")
				if TEXP=EXP,TAMOUNT=AMOUNT set FOUND=1
				}		
			}
		}

	if 'FOUND quit -1
	quit $$DELHOLD(CID,SEQ)
	
	
Public DELHOLD(CID,SEQ)	// Delete hold

	/*
	Function removes hold from account CID which has sequence SEQ
 
	ARGUMENTS	CID	Account Number		/TYP=N/REQ
			SEQ	Sequence Number		/TYP=D/REQ

	RETURNS	  	 1 - if success
			-1 - if failure
 
	FORMAT	  set status=$$DELHOLD^PHLD(CID,EXP)
	*/
	type Public Boolean ER
	do Db.delete("PHLD","CID=:CID and SEQ=:SEQ") quit:ER -1	
	quit 1
 #OPTION ResultClass ON
Public String vSIG()	quit "60309^31548^Hari Natrajan^3095"	// Signature - LTD^TIME^USER^SIZE
