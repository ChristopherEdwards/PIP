public	UREV(RecordTTX ttx,RecordTRN trn,Number SEQ,Number TNSEQ,Number CKNOEC())
	    
 	/*
	   Transaction E/C and Reversal Utility
	   
	   Arguments:
	   
	   . ttx	Transaction object			/MECH=REF:R
	   . trn	Transaction code object			/MECH=REF:R
	   . SEQ	Primary transaction sequence		/MECH=VAL
	   . TNSEQ	History table sequence			/MECH=VAL
	   . CKNOEC()	Data for Account Reconciliation File	/MECH=REF:W
 
 	---- Revision History ------------------------------------------------

	03/28/05 - TITOVE - CR 13733
		   Modified as part of DBI2 project.

	----------------------------------------------------------------------
	*/

	type Number CK, HSEQ, TRC 

	set CK = $$FIELD^UTSO(ttx.tso,"CK")
	set HSEQ = $$FIELD^UTSO(ttx.tso,"REV")
	set TRC = $$FIELD^UTSO(ttx.tso,"EC")

	if TRC,ttx.itc6,'ttx.itc7 do TTX(TRC)
	
	if HSEQ,(trn.cls '= "M") do HIST(.ttx,SEQ,TNSEQ,HSEQ,CK,.CKNOEC())

	quit


TTX(Number TRC)		// Transaction trace number	
	/* 
	  Poke E/C byte into ITC of TTX for the error-corrected transaction
	*/

	type public Number BRCD
	type public Date TPD

	type RecordTTX ttxec = Db.getRecord("TTX", "TJD = :TPD, BRCD = :BRCD, UID = :%UserID, TSEQ = :TRC", 1)
		
	if ttxec.itc.isNull() quit
		
	set ttxec.itc = $$ITC^TTXEXT(ttxec.itc,6)

	do ttxec.bypassSave()
		
	quit


HIST(RecordTTX ttx,
	Number SEQ,		// Primary transaction sequence
      Number TNSEQ,		// History table sequence
       Number HSEQ,		// Reversal transaction sequence
         Number CK,		// Check number
   Number CKNOEC())		// Data for Account Reconciliation File

	/* 
	  Set up entry in history reversal journal file (HISTR) and build CKNOEC
	  entries that will be processed by FILEARS^TRNDRV
	*/

	type Number CKNO

	type RecordHISTR histr = Db.getRecord("HISTR", "CID = :ttx.cid, TSEQ = :HSEQ", 1)

	set histr.revflg = $S(ttx.itc6:1,ttx.itc12:2)
	set histr.rseq = TNSEQ

	do histr.bypassSave()

	if 'CK quit

	type ResultSet rs = Db.select("TSEQ", "ARS", "CID = :ttx.cid AND CKNO = :CK")

	if rs.next() do {
		
		if rs.getCol("TSEQ") = HSEQ set CKNOEC(SEQ) = CK quit

		set CKNO = ""

		type ResultSet rs1 = Db.select("TSEQ,CKNO", "ARS","CID = :ttx.cid", "CKNO DESC")
			
		while rs1.next() if rs1.getCol("TSEQ") = HSEQ set CKNO = rs1.getCol("CKNO") quit

		if CKNO.isNull() set CKNO = -1
			
		set CKNOEC(SEQ) = CKNO
		}

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59990^37201^Eugene Titov^2146"	// Signature - LTD^TIME^USER^SIZE
