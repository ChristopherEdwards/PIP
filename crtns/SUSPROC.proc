SUSPROC	
	/* ---------------------- Revision History -----------------------------

	08/10/05 - TITOVE - CR 16844
		   Minor clean up as part of DBI3 project. 

	06/04/03 - CARROLLJ - 51349
		   Remove call to TRANSQL and replace with direct setting of
		   columns in this procedure.

	*/

	quit
	
public	MRPT(RecordLN ln,		// Loan Account Record
	      Number CID,		// Account Number
	      Number AMT,		// Transaction Amount
	        Date EOM,		// End of Month date
	      String INT,		// Update the suspense file interest flag
	    Boolean WOFF,		// Written Off flag
	     Boolean REV)		// Reversed written back/written off flag

	// When this section is called by STR2^LNPTS2, the AMT argument is passed by
	// reference, allowing its changed value to be returned

	if CID.get().isNull() quit
	
	set AMT = AMT.get()
	set INT = INT.get()
	set WOFF = WOFF.get()
	set REV = REV.get()

	type RecordSUSPENSE suspense = Db.getRecord("SUSPENSE", "EOM = :EOM, CID = :CID", 1)

	if INT do INTSUS(.suspense)
	
	if 'INT,'AMT do PROV(.suspense,.ln)
	
	quit 

	
INTSUS(RecordSUSPENSE suspense) // Update the suspense file interest

	type public Boolean REV, WOFF
	type public Number AMT

	type Number CAMT

	// Not an EC or a reversal
	if 'REV do {

		// Written back or written off not to exceed interest in suspense
		set CAMT = suspense.susbom + suspense.susdmth - suspense.suswb - suspense.suswo

		if (AMT > CAMT) set AMT = CAMT

		// Update Interest Suspense Written Off
		if WOFF set suspense.suswo = suspense.suswo + AMT
		// Update Interest Suspense Written Back
		else  set suspense.suswb = suspense.suswb + AMT
		}
	

	// Reversed or EC transaction
	if REV do {
		
		// Update Interest Suspense Written Off Reversed/EC
		if WOFF set suspense.suswor = suspense.suswor + AMT
		// Update Interest Suspense Written Back Reversed/EC
		else  set suspense.suswbr = suspense.suswbr + AMT
		}
		
	do suspense.bypassSave()
	
	quit
	
	
PROV(RecordSUSPENSE suspense,RecordLN ln) // Update the suspense file provision

	type public Number AMT

	// If positive provision balance adjustment then Update Provision During the Month
	if (AMT > 0) set suspense.provdmth = suspense.provdmth + AMT
	

	// If negative provision balance adjustment then Update Provision 
	// written back or off
	if (AMT < 0) do {

		if ln.woff set suspense.provwo = suspense.provwo - AMT
		else  set suspense.provwb = suspense.provwb - AMT
		}
		
	do suspense.bypassSave()
	
	quit
	

vSIG()	quit "60128^41573^Eugene Titov^2342"	// Signature - LTD^TIME^USER^SIZE
