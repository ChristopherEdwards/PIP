LNSV	// Security Value Inquiry/Maintenance Screen (LNSV)
	/*
	       ORIG:  Kathy Bradley (4646) - 04/13/89

	---- Revision History ------------------------------------------------
	
	 04/28/05 - ARPAVC - 15570
	 	    Added error message form invalid CUSIP number
	 	    Set default EFD to the system date if the record
	 	    is being created.
	 	    
	 09/19/02 - DATTAR - 49451
	            Converted to PSL         
	----------------------------------------------------------------------
	
	*/

NEW	//
	do INIT(0)
	quit 


UPD     //
	do INIT(1)
        quit


INQ     //
	do INIT(2)
        quit


INIT(%ProcessMode) //
	new OLNTB,VFMQ
	set %PG=0 
	set %PAGE=1
	type RecordUTBLSVAL fUTBLSVA
	do VPG(.fUTBLSVA)
	quit


VPG(RecordUTBLSVAL fUTBLSVA)	// Page control
	new FINISH
        set FINISH=0
        for  do { quit:FINISH
                if %PG=0 do VPG00 if ER!(VFMQ="Q") set FINISH=1 quit
                if %PG>0 do VPG01(.fUTBLSVA) if ER!(VFMQ="Q") set FINISH=1 quit
                if "DFQ"[VFMQ do VER(.fUTBLSVA) set FINISH=1 quit
                set %PG=%PG+1
                }
        quit
 
	
VPG00	// Set up
	set %TAB("CUSIP")=".CUSIP1/HLP=[UTBLSVAL]CUSIP/XPP=D CUSIP^LNSV"
	if %ProcessMode set %TAB("CUSIP")=%TAB("CUSIP")_"/TBL=[UTBLSVAL]"
	if %ProcessMode=2 set %TAB("IO")=$$IO^SCATAB($I)

	set %READ="@@%FN,,,CUSIP/REQ"
	if %ProcessMode=2 set %READ=%READ_",IO/REQ"
	do ^UTLREAD 

	if VFMQ="Q" quit
	quit


VPG01(RecordUTBLSVAL fUTBLSVA)	// Screen

	set fUTBLSVA=Db.getRecord("UTBLSVAL","CUSIP=:CUSIP",1)
	if %ProcessMode=0 set fUTBLSVA.efd=%SystemDate
	
	do DRV^USID(%ProcessMode,"LNSV",.fUTBLSVA)

	quit


CUSIP	// Post Processor to CUSIP Number

	type Number LEN
	type Public String X
	
	if X="" quit
	set Z=$$VCUSIP^SPSTD(.X) 
	if ER quit

	set LEN=X.length()
	// CUSIP number must be 8 or 9 digits/characters
        if LEN'=0,(LEN<8!(LEN>9)) do Runtime.setErrMSG("COL",691) quit:ER
        
	// CUSIP number already exists
	if %OSAVE=0,Db.isDefined("UTBLSVAL","CUSIP=:X") set ER=1 set RM=$$^MSG(688) quit

	// CUSIP number does not exist
	if %OSAVE'=0,'Db.isDefined("UTBLSVAL","CUSIP=:X") set ER=1 set RM=$$^MSG(689)
	quit


ERR	//
	set ER=1 
	do ^UTLERR

	set VFMQ="Q"
	quit


VER(RecordUTBLSVAL fUTBLSVA) //
 	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit
	do fUTBLSVA.save()
	do END
	quit


END	//
	kill %TAB lock 
	if ER!(%ProcessMode=2)!(%ProcessMode=4) quit
	set CUSIP=$G(CUSIP)

	if VFMQ="Q" do {
		// CUSIP number ~p1 not created
		if %ProcessMode=0 set RM=$$^MSG(695,CUSIP) quit
		
 		// CUSIP number ~p1 not modified
		if %ProcessMode=1 set RM=$$^MSG(697,CUSIP) quit
		
		// CUSIP number ~p1 not deleted
		set RM=$$^MSG(696,CUSIP) quit
		}
	else  do {
		// CUSIP number ~p1 created
		if %ProcessMode=0 set RM=$$^MSG(692,CUSIP) quit
		
		// CUSIP number ~p1 modified
		if %ProcessMode=1 set RM=$$^MSG(694,CUSIP) quit

		// CUSIP number ~p1 deleted
		set RM=$$^MSG(693,CUSIP) quit
		}
	set ER="W"
	quit

vSIG()	quit "60029^62600^Vincent Arpa^2755"	// Signature - LTD^TIME^USER^SIZE
