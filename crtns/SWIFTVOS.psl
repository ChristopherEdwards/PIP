SWIFTVOS	//
	/*
	   ORIG: TYNERD - 10/17/95
	   DESC: Create a vostro statement based on user input.


	   INPUTS:
		. CID        Account number       /TYPE=N/REQ


	   RETURNS:
	   	. ER         Error indicator      /TYPE=N/REQ

		. RM         Message            /TYPE=T
		  Returns error if account is not a vostro account
		  Returns a verification message if VFMQ '= "Q"
		  otherwise returns a statement not generated message

	   EXAMPLE:
		  S X=VOSNUM,%EXT=1
		  VOSNUM=vostro account number

	  ---- Revision History ------------------------------------------------

          09/26/05 - KUMARB- 17050
	        In section PROMPT, added quit after the call of END
	        in the check of VFMQ="Q".
          
          07/26/05 - SkariahV- CR16679
	        Removed #WARN and #OPTIMIZE directives.
	              
	  05/28/2003 - BHANGALEV - 51351
	  	Converted to PSL

	  ---------------------------------------------------------------------
	*/


PROMPT	// Prompt for vostro account number

	type Public Number CID,ER
	type Public String VFMQ,%READ
	type String %TAB
	
	type RecordDEP dep
	set %ProcessMode=2

	set %TAB("CID")=".CID1/HLP=[DEP]CID/XPP=D PP^SWIFTVOS"

	set %READ="@@%FN,,,CID/REQ"

	do ^UTLREAD

	if VFMQ="Q" do END quit

	set dep=Db.getRecord("DEP","CID=:CID")

	if VFMQ="F" do FILE(.dep)

	do END
	
	quit


FILE(RecordDEP dep) // File data

	/*
	  ARGUMENTS:
	  	. dep	DEP Object	/REQ/MECH=REF
	*/
	
	do dep.setAuditFlag(1)

	// Vostro Ad Hoc Statement Flag
	set dep.vosstmt=1
	
	do dep.save()
	quit


END	//

	type Public Number CID
	type Public String ER,RM,VFMQ
	
	if ER!(%ProcessMode=4) quit

	// Vostro statement not scheduled for account ~p1
	if VFMQ="Q" set RM=$$^MSG(19,CID.get())

	// Vostro statement scheduled for account ~p1
	else  set RM=$$^MSG(17,CID.get())

	set ER="W"
	quit


PP	// Allow alpha look-up for vostro account number and check vostro flag
	
	type Public Number %EXT,CID
	type Public String ER,RM
	
	set %EXT=1

	do ^UACN 

	if ER quit

	// Account must be a vostro account
	type ResultSet rs=Db.select("CID","DEP","CID=:CID AND VOSTRO=1 OR MT950=1")
	if rs.isEmpty() set ER=1,RM=$$^MSG(13)

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60169^9704^Balasubramonian Sankar^2028"	// Signature - LTD^TIME^USER^SIZE
