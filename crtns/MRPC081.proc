MRPC081(return,versn,table,list)	//Public;Compile DQ Runtime Routines
	/*
	   ORIG: MATTSON - 09/17/93

	   This procedure compiles DQ run-time programs for filers, executives,
	   data item protection and batch definitions.

	   [SCATBL5]RPCID=81

	   KEYWORDS: Client/Server

	   ARGUMENTS:
	      . return   none    			/TYP=T/REQ
	        					/MECH=REFNAM:W

	      . versn 	^MRPC081 version number  	/TYP=N/REQ
	    		 Current version = 1  		/MECH=VAL

	      . table	 DQ Table Name   		/TYP=T/REQ
	        					/MECH=VAL
			 DBTBL1  - Filer definition
			 DBTBL14 - Data item prot.
			 DBTBL25 - Procedure def
			 DBTBL33 - Batch def

	      . list     Comma separated list of DQ 	/TYP=T/REQ
	    		 definitions to be compiled 	/MECH=VAL

	   RETURNS:
	      . $$ 	 Error message   		/TYP=T
	    		 Null = No error

	   RELATED:
	      . $$^PBSMRPC - MRPC Service Class Driver

	   EXAMPLES:

		S RM=$$^MRPC081(.val,1,"DBTBL1","CIF")
		S RM=$$^MRPC081(.val,1,"DBTBL25","BCHUTL")
		S RM=$$^MRPC081(.val,1,"DBTBL33","BCHCHKHLD7")

	  ---- Revision History ------------------------------------------------

	   03/20/03 - GRAY - 51351
		      Removed references to DBTBL3 since DQ Executives
		      are now obsolete.

	   01/17/03 - GRAY - 51349
		      Corrected error, UNDEFINED,DEPLN+7^MRPC081 caused by
		      %LIBS not being defined.

	   12/12/02 - ZWITKOWITSM - 51349
		      Correct undefined on pgm.

	   05/17/02 - KRISHNAG - ARQ# 49794
		      Converted to PSL
	
	   10/12/00 - ARPAV - 42312
	              Added code to spawn compiling of DEP and LN filers
		      into a background job.  To accomplish this, two new
	   	      subroutines were added: DEPLN and SPAWN.
	  ----------------------------------------------------------------------
	*/

	// Version number of client message is not compatible with server
	if $G(versn)'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))

	// Missing parameter
	if $G(table)=""!($G(list)="") quit $$ERRMSG^PBSUTL($$^MSG(8607))

	set ER=0
	set %LIBS="SYSDEV"
	new i,name,pgm
	for i=1:1:$L(list,",") do { quit:ER
		set name=$P(list,",",i)
		new i,list 
		do COMPILE(table,name,.pgms) quit:ER

		if (table="DBTBL1")&((name="LN")!(name="DEP")) quit
		// Send control message to server to re-link
		// program(s) into server images

		for i=1:1:$L(pgms,",") do {
				set pgm=$P(pgms,",",i)
				if pgm="" quit
				new i
				do CTRLMSG^PBSUTL("EXEC ZL """_pgm_"""")
				}
		}

	if $G(ER) quit $$ERRMSG^PBSUTL($G(RM),$G(ET))

	set return=$$V2LV^MSG("")

	quit ""


COMPILE(TABLE,NAME,PGM)	//

	/*
	   VCA - Adding code to check for deposit and loan filers.  If they
	   are, then spawn off the compilation process to a background job.
	*/

	if TABLE="DBTBL1" do { quit
		if (NAME="DEP"!(NAME="LN")) do SPAWN(TABLE,NAME) quit
	
		do COMPILE^DBSFILB(NAME) quit:ER

		type RecordDBTBL1 dbtbl1=Db.getRecord("DBTBL1","%LIBS=:%LIBS,FID=:NAME")
		set PGM=dbtbl1.udfile

		if PGM'="",(NAME="DEP"!(NAME="LN")) set PGM=PGM_$$ADDPGM(NAME)
		}


	if TABLE="DBTBL14" do { quit
		do BUILD^DBSPROT3(NAME) quit:ER

		type ResultSet rs=Db.select("PROTPGM","DBTBL14","FID=:NAME")
		if rs.isEmpty() set PGM="" quit
		if rs.next() set PGM=rs.getCol(1)
		}

	if TABLE="DBTBL25" do { quit
		do COMPILE^DBSPROC(NAME) quit:ER

                type RecordDBTBL25 dbtbl25=Db.getRecord("DBTBL25","%LIBS=:%LIBS,PROCID=:NAME")
                set PGM=dbtbl25.pgm
		}

	if TABLE="DBTBL33" do { quit

		do COMPILE^DBSBCH(NAME) quit:ER

                type RecordDBTBL33 dbtbl33=Db.getRecord("DBTBL33","%LIBS=:%LIBS,BCHID=:NAME")
                set PGM=dbtbl33.pgm
		}

	// Invalid table name - ~p1
	set ER=1 
	set RM=$$^MSG(1484,TABLE)

	quit

ADDPGM(NAME)	//Private;Add supporting programs to list

	new X
	set X=$S(NAME="DEP":",DEPFILE1",1:",LNFILE1")

	if $$VALID^%ZRTNS("VRULES") set X=X_",VRULES"

	if $$VALID^%ZRTNS("PROD1DB") set X=X_",PROD1DB"

	quit X

SPAWN(TABLE,NAME)	//
	/*

	ARGUMENTS:
		. TABLE		DQ Table Name                   /TYP=T/REQ
	                                                 	/MECH=VAL
				DBTBL1  - Filer definition
				DBTBL14 - Data item prot.
				DBTBL25 - Procedure def
				DBTBL33 - Batch def

		. NAME 		Filer Name   			/TYP=T/REQ
							        /MECH=VAL

	DESCRIPTION:
		If the table is DEP or LN, then the compile process will cause a
		timeout.  Therefore, we will spawn the compile message out to
		a background process.

	EXAMPLE:
		D SPAWN("DBTBL1","DEP")

	*/

	new PROCNAME,PARM,x,i

	type RecordCUVAR cuvar=Db.getRecord("CUVAR")
	set PROCNAME=cuvar.ptmdirid

	set PARM="DEPLN^MRPC081("""_TABLE_""","""_NAME_""")"

	set x=$$^%ZJOB(PARM,"PRO="_PROCNAME,1)
	quit


DEPLN(TABLE,NAME)	// compile DEP and LN filers

	/*
	ARGUMENTS:
		. TABLE	 	DQ Table Name			/TYP=T/REQ
								/MECH=VAL
				DBTBL1  - Filer definition
				DBTBL14 - Data item prot.
				DBTBL25 - Procedure def
				DBTBL33 - Batch def

		. NAME 		Filer Name			/TYP=T/REQ
	        						/MECH=VAL

	DESCRIPTION:
		This subroutine will compile the DEP and LN triggers and then
		re-link them into the servers.  It is designed to be called
		from a spawned process.

	EXAMPLE:
		D DEPLN("DBTBL1","DEP")

	*/

	new PGM
	set ER=0
	
	if $G(%LIBS)="" new %LIBS set %LIBS="SYSDEV"

	do COMPILE^DBSFILB(NAME) quit:ER

	type RecordDBTBL1 dbtbl1=Db.getRecord("DBTBL1","%LIBS=:%LIBS,FID=:NAME")
	set PGM=dbtbl1.udfile

	if PGM'="" set PGM=PGM_$$ADDPGM(NAME)

	// Also submit the linking of the new programs into the servers
	for i=1:1:$L(PGM,",") do {
		new pgm
		set pgm=$P(PGM,",",i)

		new i 
		do CTRLMSG^PBSUTL("EXEC ZL """_pgm_"""")

		}
	quit

vSIG()	quit "59886^43588^Sanchez SCM Administrator^5248"	// Signature - LTD^TIME^USER^SIZE
