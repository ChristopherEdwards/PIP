IRSC99A		/*
	ORIG: Michael Winigrad (6969)
	DESC: IRS Processing - Form 1099-A Correction
	I18N=QUIT: Excluded from I18N standards.

	---- Comments --------------------------------------------------------
	======================================================================
		*** THIS ROUTINE IS TO BE COMPILED WITH IRSTPMTR ***
		     *** DO NOT RUN THIS ROUTINE STANDALONE ***
	======================================================================
	
	---- Revision History ------------------------------------------------
	
	05/08/06 - TITOVE - CR 21140
		   Modified BRECBLD to set "C" corrected return indicator
		   for a second step of a two-step correction.

	09/27/05 - HAILEYM - CR17143
		Converted to PSL.
		
	*/
	
	// Accept "Dead code" warning during runtime of @IRSTAPE.
	#ACCEPT Date=10/14/2005;PGM=Marie Hailey;CR=17143
	quit
	
	
MTRCNTRL	// Master control
	
	type public Boolean ER
	
	type Number PASSFLG
	
	do Db.fastDelete("B1099A")
	
	set PASSFLG=1
	do ARECBLD quit:ER	// Build and write the "A" record - First pass
	do BRECBLD quit:ER	// Build and write the "B" records - First pass
	do CRECBLD quit:ER	// Build and write the "C" record - First pass
	
	set PASSFLG=2
	do ARECBLD quit:ER	// Build and write the "A" record - Second pass
	do BRECBLD quit:ER	// Build and write the "B" records - Second pass
	do CRECBLD quit:ER	// Build and write the "C" record - Second pass

	do STORETOT		// Store the TTR report totals
	quit
	
	
ARECBLD	// "A" record builder
	
	type public String AMTIND,FORMCODE,TAMT()
	
	set FORMCODE=4	// IRS form code
	set AMTIND=24	// IRS amount type indicator
	set TAMT(2).piece("|",2)="Balance of principal Outstanding"
	set TAMT(4).piece("|",2)="Fair Market Value of Property"
	do ARECWRT	// Write the record to tape
	quit
	
	
BRECBLD	// "B" record builder
	
	type public Date FORMS()
	type public Number AMT(),PASSFLG
	type public String FORM
	
	type Date DATE,JD,XJD
	type Number CID,I
	type String ADDR,CITY,CNTRY,CORRTN,DOCCODE,FORCNTY,FULLADDR,LIND,LNAME
	type String NAME,SPECIAL,STATE,TIN,TINTYPE,X,XTIN,ZIP
	
	type DbSet ds
	type RecordB1099A b1099a
	type RecordIRSUPD irsupd
	type RecordM1099 m1099
	
	set XJD=FORMS(FORM).get()
	
	for I=1:1:9,"A","B","C" set AMT(I)=0
	
	set DOCCODE="  "
	set CORRTN=$select(PASSFLG=1:"G",PASSFLG=2:"C")
	
	set b1099a=Class.new("RecordB1099A")
	
	set ds=Db.selectDbSet("IRSUPD","TJD NOT <:XJD AND FTYPE=9")
	while ds.next() do {
		set irsupd=ds.getRecord("IRSUPD")
		
		if irsupd.ccode=1,(PASSFLG=1) quit	// Skip duplicates when writting to tape
		if irsupd.ccode=4,(PASSFLG=1) quit	// Skip "New Entry" for first pass
		if irsupd.ccode=3,(PASSFLG=2) quit	// Skip "change amount" for second pass
		
		if PASSFLG=1 set CID=irsupd.obseq,TIN=irsupd.otin
		if PASSFLG=2 set CID=irsupd.bseq,TIN=irsupd.miscseq
		
		set TINTYPE=$select(TIN?2N1"-"7N:1,TIN?3N1"-"2N1"-"4N:2,1:" ")
		set XTIN=TIN.piece("|",1).translate(" ","-")
		
		set m1099=Db.getRecord("M1099","TAXID=:TIN,SEQ=:CID",1)

		if PASSFLG=1,(irsupd.ccode=2) set (AMT(2),AMT(4))=0
		else  set AMT(2)=m1099.debtout*100,AMT(4)=m1099.fmktv*100
		
		if PASSFLG=1 set NAME=irsupd.oname,LNAME=irsupd.olnam	
		if PASSFLG=2 set NAME=m1099.nam,LNAME=m1099.lnam
		set FULLADDR=m1099.ad1_"|"_m1099.ad2_"|"_m1099.ad3_"|"_m1099.city_"|"_m1099.state_"|"_m1099.mzip
		set ADDR=m1099.ad1_" "_m1099.ad2_" "_m1099.ad3
		set CITY=m1099.city
		set STATE=m1099.state
		set ZIP=m1099.mzip.piece("-",1)_m1099.mzip.piece("-",2)
		set CNTRY=m1099.cntry
		set FORCNTY=$select(CNTRY="US":" ",1:1)	// Foreign country indicator
		set LIND=$select(m1099.bliab:1,1:" ")
		set DATE=m1099.td
		
		// ---------  Special Data Entry Section  --------
		
		//; Blank|544|546
		set SPECIAL="".justify(3)
		//; Liability Indicator|547
		set SPECIAL=SPECIAL_LIND
		//; Lender's Date of Acquisition|548|555
		set SPECIAL=SPECIAL_DATE.toString("YEARMMDD")
		//; Description of property|556|594
		set X=m1099.desc_" "_m1099.desc2_" "_m1099.desc3
		set SPECIAL=SPECIAL_X.justify(39,-1,,1)
		//; Blank|595|662
		set SPECIAL=SPECIAL_"".justify(68)
		//; Special Data Entries|663|722
		set SPECIAL=SPECIAL_"".justify(60)
		//; Blank|723|750
		set SPECIAL=SPECIAL_"".justify(28)
				
		if irsupd.ccode<>1 do BRECWRT	// Skip duplicates
		
		if TINTYPE=2 set X=LNAME
		else  set X=NAME
		do NAMCNTRL^IRSTPMTR set b1099a.bnamcon=X

		// Insert B Records into file B1099A
		/*
		B1099A.BLDA will be in MM/DD/YY format because 1099PRO accept 
		only this xx/xx/xx date format.
		*/
		if b1099a.getMode() do b1099a.setMode(0)
		set b1099a.pdate=irsupd.tjd
		set b1099a.btaxid=XTIN
		set b1099a.bseq=m1099.seq
		set b1099a.btintype=TINTYPE
		set b1099a.btin=XTIN
		set b1099a.bcid=CID
		set b1099a.bamt2=AMT(2)/100
		set b1099a.bamt4=AMT(4)/100
		set b1099a.bname=NAME
		set b1099a.baddr1=m1099.ad1
		set b1099a.baddr2=m1099.ad2
		set b1099a.baddr3=m1099.ad3
		set b1099a.bcity=CITY
		set b1099a.bstate=STATE
		set b1099a.bzip=m1099.mzip
		set b1099a.bli=LIND
		set b1099a.blda=DATE.toString("MM/DD/YY")
		set b1099a.bdp1=m1099.desc
		set b1099a.bdp2=m1099.desc2
		set b1099a.bdp3=m1099.desc3
		do b1099a.bypassSave()
		}
	quit
	
	
CRECBLD	// "C" record builder
	
	do CRECWRT	// Write the record to tape
	quit


%STOPLOD	// Stop %ZRTNLOD from this point on down

	quit
	
	
TRECWRT		// Dummy line reference for GT.M

	quit
	

ARECWRT		// Dummy line reference for GT.M

	quit
	
	
BRECWRT		// Dummy line reference for GT.M

	quit
	
	
CRECWRT		// Dummy line reference for GT.M

	quit
	
	
STORETOT	// Dummy line reference for GT.M

	quit

vSIG()	quit "60393^38887^Eugene Titov^5305"	// Signature - LTD^TIME^USER^SIZE
