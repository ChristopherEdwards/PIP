UHIS	// Account History Inquiry Set-Up
	/*
	  ----Revision History------------------------------------------------

	   08/15/06 - KELLYP - CR 20748
	   	Modified VPG01 to change reference of HIST.CTSEQ to HIST.TSEQ
	   	(HIST.CTSEQ was obsoleted).

	   02/07/06 - bholt 19066
	   	Removed the Db.getRecord to ln in VPG and removed the
	   	references to ln in VPG00 and VPG01.
	   
	   11/22/05 - Srinivar 16890
	   	Modified the  VPG section to pass .ln object to VPG01
	   	instead of ln.  

	   02/13/03 - GRAY - 51349
		Commented out all code until history archiving issues
		are resolved in Phase 2.  This procedure was
		inadvertently released as a part of Phase 1.  

	  ----------------------------------------------------------------------
	*/
	quit

public EXT

	type public Number ARCHIVE
	set ARCHIVE=0
	do INIT(0)
	quit

public	ARC 

	type public Number ARCHIVE

	set ARCHIVE=1 
	do INIT(2)
	quit


INIT(%ProcessMode)

	type public Number %PG, %PAGE, SEQBY
	type public String LOC,%LOGID,OLNTB,Q,VFMQ,vsql
	type RecordHIST fHIST1

	set %PG=0 
	set %PAGE=1
	kill VFMQ
		
	//new vsql
	set LOC=$P($G(%LOGID),"|",3)_%ProcessID 
	do VPG(.fHIST1)
	quit

VPG(RecordHIST fHIST1)	// Page control
	
	type public Number %PG
	type public String VFMQ
	
	type Number FINISH
			
	set FINISH=0
		
	for  do { quit:FINISH
		if %PG=0 do VPG00()
		if %PG>0 do VPG01(fHIST1)
		if "DFQ"[VFMQ do VER(.fHIST1) set FINISH=1 quit
		set %PG=%PG+1
		}
	quit

VPG00()	
	type public Number CID
	type public String VFMQ
	
	type String %NOPRMT,%READ,%TAB()
	
	if $G(CID)'="",Db.isDefined("ACN","CID") set VFMQ=1 do PROT quit 
	set %TAB("CID")=".CID1/XPP=D POSCID^UHIS"
	

	set %READ="@@%FN,,,CID/REQ" 
	set %NOPRMT="N"
	do ^UTLREAD 
	if VFMQ="Q" quit
	quit

POSCID	// Account number post-processor
	type public Boolean ER
	type public Number ARCHIVE
	
	type Number CID
	
	// Archive account
	if ARCHIVE=1 do UARCACN
	if ER set CID="" quit
	do PROT
	quit

PROT	// Record Level Protection

	type public Boolean ER
	type public Number TSEQ
	type public String VFMQ
	
	type Number CID
	type String VP

	set VP="" 
	set TSEQ=1 
	do EXT^UPID("HIST")
	// History protected
	if $G(VP("*"))=2 do Runtime.setErrMSG("HIST",1178)
	if ER set CID="" set VFMQ="N" quit
	quit

VPG01(RecordHIST fHIST1)	
	type public Cache IO
	type public Boolean ER
	type public Number ARCHIVE,ARUF,CID,ENDBAL,TSEQ
	type public String A(),Q,VFMQ,vsql
	
	type Number I
	type String %READ,SEQBY,%TAB()
	
	type Number TRB
		
	for I=1:1:10 set A(I)="ALL"
	set %TAB("IO")=$$IO^SCATAB($I)
	set %TAB("A(1)")=".A9/HLP=[HIST]ETC/XPP=D EXT^DBSQRY"
	set %TAB("A(2)")=".A22/HLP=[HIST]PRIN/XPP=D PPAMT^UHIS"
	set %TAB("A(3)")=".A23/HLP=[HIST]TJD/XPP=D TJD^UHIS"
	set %TAB("A(4)")=".A24/HLP=[HIST]TIME/XPP=D TJD^UHIS"
	set %TAB("A(5)")=".A25/HLP=[HIST]TSO/XPP=D EXT^DBSQRY"
	set %TAB("A(6)")=".A5/HLP=[HIST]BRCD/XPP=D EXT^DBSQRY"
	set %TAB("A(7)")=".A4/HLP=[HIST]TLO/XPP=D EXT^DBSQRY"
	set %TAB("A(8)")=".A26/HLP=[HIST]TRC/XPP=D EXT^DBSQRY"
	set %TAB("A(9)")=".A3/HLP=[HIST]UID/XPP=D EXT^DBSQRY"
	set %TAB("A(10)")=".A12/HLP=[HIST]TCMT/XPP=D EXT^DBSQRY"
	set %TAB("A(11)")=".A27/TBL=[STBLHFMO]" set A(11)=1
	set %TAB("A(12)")=".A28/TBL=[STBLHISOPT]" set A(12)="TSEQ"
	set %TAB("A(13)")=".A29" set A(13)=1
	set %TAB("A(14)")=".A30/TBL=[STBLHNO]" set A(14)=0
	set A(15)="ALL"
	set %TAB("A(15)")=".TSEQ4/HLP=[HIST]TSEQ/LEN=9/TYP=N/XPP=D EXT^DBSQRY"

	// Archived account
	if ARCHIVE do {
		// Code below will not work need to go out and grab 
		//archived data
		//new REQ
		//for I=49,50,51 set REQ(I)=""
		//do ACN^UEXTRMAP(CID,.REQ,.%A)
		//set dep.flg=dep.flg
		//set dep.type=dep.type
		}

	type RecordLN ln=Db.getRecord("LN","CID=:CID",1)
	if 'ln.getMode() quit
	// Transaction to Reduce Balance
	set ARUF=ln.aruf
	set TRB=ln.trb	
					
	if ARUF set ENDBAL=ln.bal-ln.udbal
			
	set %READ="@@%FN,,IO/REQ,A(11)/REQ,A(12)/REQ,A(13)/REQ,A(14)/REQ"
	set %READ=%READ_",A(1),A(2),A(3),A(4),A(5),A(6),A(7),A(8),A(9),A(10),A(15)"

	do ^UTLREAD if VFMQ="Q" quit
	if IO'=$I do OPEN^SCAIO 
	if ER set VFMQ="Q" quit

	set SEQBY="[SYSDEV,HIST]CID|[SYSDEV,HIST]TSEQ" do ^DBSQRYA

	if A(11)'=1 set A(11)=$S(A(11)=0:1,1:2))

	if "TSEQ"[A(12),'ARUF do { quit
		type ResultSet rs=Db.select("CID","HIST","CID=:CID AND TSEQ=:TSEQ")
		if rs.next() set CID=rs.getCol("CID")
		if CID.isNull() quit
		set A(12)=""
		}
	/*
	Open in reverse order, regardless of the value of A(13), so the
	balance may be computed for loans with undisbursed processing.
	The collating order is handled in SORT based upon A(13).
	*/

	type ResultSet rs=Db.select("CID","HIST","CID=:CID AND TSEQ=:TSEQ")
	if rs.next() set CID=rs.getCol("CID")
	if CID.isNull() quit
	
	do SORT(A(12))
	set A(12)=1
	quit


VER(RecordHIST fHIST1)

	type public Boolean ER 
	type public Number ARCHIVE,CID
	type public String ET,PGM,RID,VFMQ
	
	type Number XCID
	type String %NOOPEN
	if $G(VFMQ)="Q" do EXIT quit 
	
	type RecordACN acn=Db.getRecord("ACN","CID=:CID",1)
	
	if 'RID.exists(),'ARCHIVE  do {
		type RecordPRODCTL prodctl=Db.getRecord("PRODCTL","TYPE=:acn.type")
		set RID=prodctl.hrep
		}
	
	// Institution default
	if RID.get().isNull(),'ARCHIVE  do {
		type RecordCUVAR cuvar=Db.getRecord("CUVAR")
		set RID=cuvar.histrpt
		}

	set XCID=CID

	// PROFILE standard
	if RID.get().isNull() set RID="SCA229"

	do ^URID 
	if PGM="" do { quit
		set ET="INVLDRPT" 
		set ER=1 do ^UTLERR
		set VFMQ="Q"	
		do EXIT
		}
	set %NOOPEN=""
	set PGM="V0^"_PGM
	do @PGM
	quit


EXIT	
	type public Number XCID
	
	type Number CID
	
	set:XCID.get() CID=XCID kill XCID
	
	quit

SORT(DI)	// Sort History by DI

	type public Number ARUF,CID,ENDBAL,LOC,TRB,TSEQ,vsql
	type public String A(),fHIST
	
	type Number ITC,T,TAMT
	type String exe(),svsql,Y

	set svsql("fast")=$G(vsql("fast"))	
			
	type ResultSet rs=Db.select("CID","HIST","CID=:CID AND TSEQ=:TSEQ")
	if rs.next() do {
		set CID=rs.getCol("CID")
		if CID.isNull() quit
	
		new hist
		type RecordHIST hist=Db.getRecord("HIST","CID,TSEQ")

		// External Transaction Code
		if DI="ETC" set Y=hist.etc set:Y="" Y=" "

		// Transaction Effective Date
		if DI="EFD" set Y=hist.efd set:'Y Y=+fHIST

		// Transaction Source of Funds
		if DI="TSO" set Y=hist.tso set:Y="" Y=" "
		if DI="TSEQ" set Y=TSEQ
		if ARUF do {
			// Internal Transaction Code
			set ITC=hist.itc

			// Transaction Amount
			set TAMT=hist.tamt

			// Ending Balance
			set hist.endbal=ENDBAL
			set T=$P(TAMT,"#",2)+$P(TAMT,"#",5)
			set ENDBAL=ENDBAL-$S(TRB-$E(ITC):T,1:-T)

			}
		Type RecordTMPRPT2 tmprpt2=Class.new("RecordTMPRPT2")
		set tmprpt2.pid=LOC
		set tmprpt2.key1=Y
		set tmprpt2.key2=TSEQ
		set tmprpt2.data=fHIST
		do tmprpt2.save()
		}

	type public String gbl,order
	
	set order=$S(A(13):-1,1:1)
	
	// I18N=OFF: Excluded from I18N Standards
	set exe(1)="S vsql(1)="_gbl_"),"_order_") I vsql(1)="""" S vxp=0"
	set exe(2)="S vsql(2)="_gbl_",vsql(2)),"_order_") I vsql(2)="""" S vxp=1"

	// I18N=ON: Included to I18N Standards

	set exe=3 set vsql(0)=2 set vsql=3
	set vsql("fast")=svsql("fast")
	quit

PPAMT	// Post processor for the amount field
	type public String X
	
	if "ALL"[$TR(X,"al","AL") quit

	type Number I
	type String oprelat,SAVX

	set SAVX=X
	//I18N=OFF
	if (X["--")!(X["-,")!(X[",-")!(X[",,") do Runtime.setErrMSG("HIST",1350,X) quit
	if X["-" set X=$P(X,"-",1)_" TO "_$P(X,"-",2) set X="FROM"_" "_X

	//I18N=ON
	for I=1:1:$L(X) quit:$E(X,I)?1N
	set oprelat=$E(X,1,I-1) set X=$E(X,I,$L(X))
	if oprelat="" set oprelat="="

	// I18N=OFF: Excluded from I18N Standards
	set X="+[HIST]TAMT "_oprelat_X do ^DBSQRY

	// I18N=ON: Included to I18N Standards
	set X=SAVX
	quit

TJD	
	
	// Post processor for TJD
	type public Number SAVX,X 

	set SAVX=X

	//I18N=OFF
	if X'["FROM",X["-" set X=$P(X,"-",1)_" TO "_$P(X,"-",2) set X="FROM"_" "_X

	//I18N=OFF
	do EXT^DBSQRY
	set X=SAVX
	quit

UARCACN	// Verify archived account number
	type public Boolean ER
	type public String X
	
	if X="" quit
	if Db.isDefined("PURGDACN","CID=:X") quit

	// Account has not been archived
	if Db.isDefined("ACN","CID=:X") do Runtime.setErrMSG("HIST",3846)

	// Invalid account
	else  do Runtime.setErrMSG("HIST",1263)
	set ER=1
	quit

vSIG()	quit "60492^61115^Pat Kelly^7724"	// Signature - LTD^TIME^USER^SIZE
