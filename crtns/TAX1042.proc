TAX1042		/*
	ORIG: ZWITKOWITSM - 02/05/2002
	DESC: Process 1042-S tax information

	---- Comments --------------------------------------------------------

	---- Revision History ------------------------------------------------

	03/07/06 - TITOVE - CR 19995
		   Modified to set required TAX1042.SEQ column to a value
		   before saving the table.

	06/04/03 - ZWITKOWITSM - 51349
		   Added labels NARCN and WHEXR for DATA-QWIK pre/post
		   processors.

	12/13/02 - ZWITKOWITSM - 51349
		   Add Public linetag label to those linetags which are called
		   publicly.  Removed getOneRow method.

	02/05/02 - ZWITKOWITSM - 43583
		   Creation date.  This code was pulled out of routine
		   DEPINT.M and given its own routine since its relation
		   to DEPINT was very minimal.

	----------------------------------------------------------------------

	*/

	quit


public PROC(RecordDEP dep,RecordTTX ttx,AMT,BWA)	// Create 1042-S tax information file.

	new BWPCT,CID,EFDYEAR,IPYD,NBWA,NR,NRWACY,TAXYR,TSO,TXSEQ,UTSO,WHEXR

	set CID=dep.cid
	set TSO=ttx.tso
	set UTSO=""

	do OUT^UTSO(.UTSO,TSO)

	// Nonresident Alien Country Code
	set NR=$G(UTSO("NRCN"))
	if NR="" do {
		new ACN

		set ACN=dep.acn

		type RecordCIF cif=Db.getRecord("CIF","ACN")

		set NR=cif.nrcntry
		}
	if NR="" quit

	set TAXYR=$$YEAR^SCADAT(%SystemDate,1)
	set EFDYEAR=$$YEAR^SCADAT(%EffectiveDate,1)

	// If interest prior year adjustment flag is turned on and transation is posted
	// over yearend, the TAX1042 file will reflect the prior year adjustment.
	if EFDYEAR=(TAXYR-1),CUVAR.IPYADJ=1 set TAXYR=TAXYR-1

	// Withholding Exemption code reason
	set WHEXR=$G(UTSO("WHEXR"))
	if WHEXR="" set WHEXR=dep.whexr
	if 'WHEXR set WHEXR=0

	// Interest Paid
	set IPYD=AMT+BWA

	// Withholding Amount
	set NBWA=BWA

	// Amount subject to withholding
	set NRWACY=AMT+BWA

	// Backup withholding percentage
	set BWPCT=$G(UTSO("BWPCT"))
	if BWPCT=""  set BWPCT=CUVAR.%BWPCT
	set BWPCT=$$^SCARND(BWPCT,0,0,,0)

	// Find last sequence for 1042-S reporting
	set TXSEQ=Db.nextVal("TAX1042","CID,TAXYR,NR,BWPCT,WHEXR")

	set TSO=$$FIELDIN^UTSO(TSO,"TXSEQ",TXSEQ)
	set ttx.tso=TSO

	type RecordTAX1042 tax1042=Class.new("RecordTAX1042")

	set tax1042.cid=CID
	set tax1042.taxyr=TAXYR
	set tax1042.nr=NR
	set tax1042.bwpct=BWPCT
	set tax1042.whexr=WHEXR
	set tax1042.seq=+TXSEQ
	set tax1042.ipyd=IPYD
	set tax1042.nbwa=NBWA
	set tax1042.nrwacy=NRWACY
	set tax1042.irsexm=dep.irsexm

	do tax1042.bypassSave()

	quit


Public REV(RecordTTX ttx,CID) // Reverse/Error correct 1042-S tax entries

	new BWPCT,NR,TAXYR,TXSEQ,UTSO,WHEXR,%EffectiveDate

	set UTSO=""
	do OUT^UTSO(.UTSO,ttx.tso)

	set BWPCT=$G(UTSO("BWPCT"))
	set BWPCT=$$^SCARND(BWPCT,0,0,,0)
	set NR=UTSO("NRCN")
	set TXSEQ=UTSO("TXSEQ")
	set WHEXR=UTSO("WHEXR")

	// Tax Year
	set %EffectiveDate=$S(ttx.efd:ttx.efd,1:%SystemDate)
	set TAXYR=$$YEAR^SCADAT(%EffectiveDate,1)

	// Update for 1042-S EC/REV
	type RecordTAX1042 tax1042=Db.getRecord("TAX1042","CID,TAXYR,NR,BWPCT,WHEXR,TXSEQ")

	set tax1042.revflg=1

	do tax1042.bypassSave()

	quit


Private NRCN(CID)	// Nonresident Country Code

	type Number ACN

	if CID.get()="" quit 0

	type ResultSet rs=Db.select("ACN","DEP","CID=:CID")
	if rs.isEmpty() quit 0
	if rs.next() set ACN=rs.getCol(1)

	type ResultSet rs1=Db.select("NRCNTRY","CIF","ACN=:ACN")
	if rs1.isEmpty() quit 0
	if rs1.next()

	quit rs1.getCol(1)


Private WHEXR(CID)	// Withholding Exemption Reason

	type ResultSet rs=Db.select("WHEXR","DEP","CID=:CID")

	if rs.isEmpty() quit ""

	if rs.next()

	quit rs.getCol(1)

vSIG()	quit "60331^67279^Eugene Titov^3392"	// Signature - LTD^TIME^USER^SIZE
