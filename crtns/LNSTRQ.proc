LNSTRQ		/*
	PROCEDURE:  LNSTRQ
	ORIG: HAYMANP - 05/14/98
	DESC: Strike Rate Comaprison Query Compiler
 
	---- Comments --------------------------------------------------------
	This routine will compile a routine to be used during Strike Rate
	comparison in routine LNSTRCMP.  Any time the UTBLSTRQ file is 
	modified this routine will be called to compile a new routine.  
	The compiled routine will be called LNSTRQ1.
	
	---- Revision History ------------------------------------------------

	04/12/02 - Dan Russell
		   Modified to generate PSL instead of M code.
	*/

	catch error {
		do ZE^UTLERR
		set ER=1
		}

	new CMPERR,CODE,TAB
	set ER=0
	set TAB=$C(9)

	// I18N=OFF
	
	do addcode("LNSTRQ1"_TAB_"// Strike Rate Comparison Query Compiled Program")
	do addcode(TAB_"// Last compiled:  "_$$DAT^%ZM(%CurrentDate,$G(%MSKD))_" "_$$TIM^%ZM_" - "_$G(%UID))
	do addcode("")
	do addcode(TAB_"// THIS IS A COMPILED ROUTINE.  Compiled by procedure LNSTRQ")
	do addcode("")
	do addcode(TAB_"quit   // Cannot access from top")
	
	// Loop through strike rate comparison query user table and compile
	// all queries.
	
	type ResultSet strqrs
	set strqrs=Db.select("STRQRY,DESC","UTBLSTRQ")	

	if strqrs.isEmpty()  quit

	while strqrs.next() if 'ER do {
		do bldqry(strqrs.getCol(1),strqrs.getCol(2))
		}

	quit:ER

	// Build compiled routine
	do BUILDRTN^UCGM(.CODE,"LNSTRQ1",.CMPERR)
	if $D(CMPERR) do {
		new N
		set N=""
		for  set N=$O(CMPERR(N)) quit:N=""  do {
			if 'ER set ER=1,RM=CMPERR(N)
			write CMPERR(N),!
		}
	}
	
	//Completed at ~p1
	if 'ER set ER="W",RM=$$^MSG(591,$$TIM^%ZM(%CurrentTime,$G(%MSKC)))

	quit


bldqry(QRYNAM,DESC)	// Build code to execute query


	if 'Db.isDefined("DBTBL4","LIBS=""SYSDEV"",QID=:QRYNAM") do {
		set ER=1
		// Query ~p1 not found
		set RM=$$^MSG(2297,QRYNAM)
	}
	else  do {
		new N,PSLOBJ,PSLQRY,TABLE,TABLES

		type RecordDBTBL4 dbtbl4=Db.getRecord("DBTBL4","LIBS=""SYSDEV"",QID=:QRYNAM")

		do addcode("")
		do addcode("public "_QRYNAM_"(RecordLN ln)"_TAB_"// "_DESC_" - Query desc:  "_dbtbl4.desc)

		// Convert query to PSL code
		do ^UCQRYBLD(QRYNAM,"LN=ln",.TABLES,.PSLOBJ,.PSLQRY)
		quit:ER

		// Check for valid tables in query
		for I=1:1:$L(TABLES,",") if 'ER do {
			set TABLE=$P(TABLES,",",I)
			quit:(TABLE="CIF")!(TABLE="DEP")!(TABLE="LN")
			quit:(TABLE="ZCIF")!(TABLE="ZDEP")!(TABLE="ZLN")
			set ER=1,RM=$$^MSG(44)
		}
		quit:ER

		set N=""
		// Insert lines to instantiate new objects
		for  set N=$O(PSLOBJ(N)) quit:N=""  do addcode(TAB_PSLOBJ(N,1))

		// Insert the query lines
		for  set N=$O(PSLQRY(N)) quit:N=""  do {
			do addcode(TAB_"if "_PSLQRY(N))
			do addcode(TAB_"else  quit 0")
			}

		do addcode(TAB_"quit 1")
	}

	quit


addcode(LINE)	// Add new line to CODE arry

	set CODE($O(CODE(""),-1)+1)=LINE
	quit

vSIG()	quit "59886^43573^Sanchez SCM Administrator^2658"	// Signature - LTD^TIME^USER^SIZE
