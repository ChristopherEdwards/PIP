UOPEN
	/*

	ORIG:
	Procedure ID: UOPEN
	DESC: Utility to Re-Open an Account	

	---- Revision History ------------------------------------------------
	
	02/09/06 - SmithCD - CR 19505
		   . Removed parameter 4 and 5 from both calls to EXEC^UANTIC
		   . Removed call to LOAD^LNPTS (both changes are in with 
		     changes for CR 19343)
		   . Added Class.new() for ttx to avoid potential problems 
		     with incremental loading of objects
	
	09/08/05 - NATRAJAH - 16674
		   Removed the screen calling line of from VPG01 section, since 
		   related screens are obsoleted. 

	----------------------------------------------------------------------
	*/

	type String ER,RM,VFMQ
	type Number %PG,OLNTB
	type Boolean EXIT
	type RecordACN acn
	
	catch vERROR {
		type String ET,RM
                set ET=vERROR.type
		
		if ET.isLike("%%GTM-%") do ZE^UTLERR quit

		set ET=ET_"-"_vERROR.thrownAt
		set RM=vERROR.description
		
		do ^UTLERR
		}

	set %PG=0
	set (RM,%EffectiveDate)=""
	set %ProcessMode=1

	set EXIT=0
	for  do { quit:ER!EXIT
		if %PG=0 do VPG00(.acn) quit:ER   
		if %PG>0 do VPG01(.acn)
		if VFMQ'="Q" do VER(.acn) set EXIT=1 quit
		set %PG=%PG+1
		}
	
	quit


VPG00(RecordACN acn)	// Set-up screen
	
	type public String %READ,%TAB(),VFMQ
	type public Number CID

	set %TAB("CID")=".CID1/XPP=D POSCID^UOPEN(.acn)"

	set %READ="@@%FN,,,CID/REQ"
	do ^UTLREAD if VFMQ="Q" quit

	quit


VPG01(RecordACN acn)	// File maintenence screen

	type public Number CID,TYPE
	
	set TYPE=acn.type
	
	quit


POSCID(RecordACN acn)	// Post processor check to account number prompt

	type public String X,ER,RM

	type Boolean %EXT
	
	if X.isNull() quit

	set %EXT=1 do ^UACN quit:ER  quit:X.isNull()

	set acn=Db.getRecord("ACN","CID=:X")

	// Account not closed
	if acn.stat'=4 do Runtime.setErrMSG("ACN",76) quit

	quit


VER(RecordACN acn)	//

	type public String VFMQ,ER,verrors(,)
	
	// Update common account fields
	if VFMQ'="Q" do {
		do FILE(.acn)
		if 'ER,verrors.data() do VIEW
		}
	
	do END

	quit


END	// Exit

	type public Number CID
	type public String ER,RM,VFMQ
	
	set CID=CID.get()

	// Account not re-opened
	if VFMQ="Q" set RM=$$^MSG(138,CID) quit

	/*
	If an error was set in the call to the filers, the account was
	not opened. Display the reason given, if any, and the account not
	re-opened message.
	Account not re-opened
	*/
	if ER set RM(1)=RM.get() set RM="" set RM(2)=$$^MSG(138,CID)

	// Account re-opened
	else  set RM=$$^MSG(153,CID)
	set ER="W"

	quit


FILE(RecordACN acn)	// File data
	
	type public Number CID

	set acn=Db.getRecord("ACN","CID=:CID")
	
	do acn.setAuditFlag(1)

	// Account Status
	set acn.stat=0
		
	do acn.save()		

	quit


VIEW	// View error report

	type public Number CID
	type public String verrors(,)

	do XBAD^DBSEXECU(.verrors(,))
	do DSPERR^DBSEXECU(CID)

	quit


public LOAN(RecordLN ln)	// Loan specific

	//This label is called from the loan filer BU_STAT section.
	  
	/*
	Call LNPTS to get account into sync at the point in time
	that it was paid off.
	*/
	
	type public String ER

	do ^LNPTS(.ln)

	/*
	Roll scheduled/billing dates from Date Account Closed
	If no disbursements, call ^UANTIC with the skip disbursement flag set
	*/
	
	type RecordTTX ttx=Class.new("RecordTTX")

	// ??? This code will project from %SystemDate to %SystemDate ???
	if 'ln.cntdr do EXEC^UANTIC(.ln,.ttx,,%SystemDate,"10","01") quit:ER
	else  do EXEC^UANTIC(.ln,.ttx,,%SystemDate,"10") quit:ER
	
	quit

ERR	// Log error before exiting

	type public String ER,VFMQ

	set ER=1 do ^UTLERR
	set VFMQ="Q"

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60306^38120^Chad Smith^3349"	// Signature - LTD^TIME^USER^SIZE
