IRSC498E		/*
	ORIG: titove - 11/04/2005
	DESC: IRS Processing - Form 5498 ESA Correctio

	---- Comments --------------------------------------------------------
			  THIS PROCEDURE IS TO BE COMPILED
			      DO NOT RUN STANDALONE

	I18N=QUIT: Excluded from I18N standards.

	---- Revision History ------------------------------------------------
	
	04/19/06 - RussellDS - CR20209
		   Remove code related to obsoleted Mutual Funds/Securities.
		   
		   Modified BRECBLD section to append RPASEQ when building a 
		   value for CID, instead of IRATYP.

	04/07/06 - TITOVE - CR 20641	
		   Modified BRECBLD to initialize FMV and PYBAL to prevent
		   undefined errors.

	03/24/06 - Mugilvannan - CR 20334
		   Changes made in the BRECBLD section to properly initialize
		   the variable SPECIAL to GTM error.
		   
	11/04/05 - TITOVE - CR 17287
		   Converted into PSL. Removed tags ACN, INV1, REPORT, RPASEQ
		   and TP1.

	08/19/05 - HILLANBRAND -16942
		   Created routine from Profile01 to be converted to a procedure.

	12/10/04 - SIVAKUMAR - 13439 
		   Created from a copy of IRSC498 to perform corrections for
		   Form 5498-ESA.A check has been added to use only the RPASEQ
		   where IRATYPE.IRATYP=12.Modified section to remove setting 
		   of all payment amounts except 1,2 and IRA indicators.
		   Modified code for IRA.C1,IRA.C2 and IRA.C1X to report on 
		   AMT1 variable,Coverdell ESA Contributions,Field positions 
		   55-66 and IRA.C4 on AMT2,Rollover Contributions,Field Positions 67-78.
		   Set FORMCODE to the value "V".Remove the tax report totals for all 
		   amounts that are not being reported on form 5498-ESA.
		   Modified Section B5498 to remove the un-necessary fields 
		   on the new form.The SQL insert inserting the values in to the
		   new file B5498ESA.
	
	*/
	
        // Accept "Dead code" warning during runtime of @IRSTAPE
        #ACCEPT Date=10/26/2005;PGM=Eugene Titov
        quit
         
        
MTRCNTRL	// Master control

	type public Number ER
	
	type Number PASSFLG

	do Db.delete("B5498ESA")

	set PASSFLG = 1
		
	do ARECBLD quit:ER	// Build and write the "A" record - First pass
	do BRECBLD quit:ER	// Build and write the "B" records - First pass
	do CRECBLD quit:ER	// Build and write the "C" record - First pass
	
	set PASSFLG = 2
		
	do ARECBLD quit:ER	// Build and write the "A" record - Second pass
	do BRECBLD quit:ER	// Build and write the "B" records - Second pass
	do CRECBLD quit:ER	// Build and write the "C" record - Second pass
		
	do STORETOT		// Store the TTR report totals
		
	quit
	
	
ARECBLD	// "A" record builder

	type public String TAMT()

	type Number AMTIND, FORMCODE

	set FORMCODE = "V"		// IRS form code
	
	set AMTIND = "12"		// IRS amount type indicators
	
	set TAMT(1).piece("|",2) = "Regular IRA Contributions"
	set TAMT(2).piece("|",2) = "Rollover Contributions"
	
	do ARECWRT			// Write the record to tape
	
	quit
	

BRECBLD	// "B" record builder

	type public Number AMT(), BRAMT(), IRATYP, PASSFLG, YEAR
	type public String FORM, FORMS()

	type Date JD
	type Number ACN, BSEQ, CID, FMV, I, IBAL, IRA(), PYBAL, RPASEQ, TAXYR
	type Number TAXYR1, TOT, ZACN
	type String ADDR, CITY, CNTRY, CORRTN, FORCNTY, FULLADDR, LNAME, NAME
	type String  SIN, SPECIAL, STATE, TIN, TINTYPE, X, ZIP

	set JD = FORMS(FORM)-1
	
	type DbSet ds = Db.selectDbSet("IRSUPD","TJD>:JD AND FTYPE=6")

	while ds.next() do {
	
		type RecordIRSUPD irsupd = ds.getRecord("IRSUPD")
		
		set ZACN = irsupd.miscseq
		set RPASEQ = irsupd.bseq
		
		type RecordIRATYPE iratype = Db.getRecord("IRATYPE", "ACN = :ZACN,RPASEQ = :RPASEQ", 1)

		set IRATYP = iratype.iratyp
	
		if (IRATYP '= 12) quit		// Skip all but Coverdell ESA
		
		// Skip duplicates when writting to tape
		if (irsupd.ccode = 1),(PASSFLG = 1) quit
		// Skip "New Entry" for first pass
		if (irsupd.ccode = 4),(PASSFLG = 1) quit
		// Skip "change amount" for second pass
		if (irsupd.ccode = 3),(PASSFLG = 2) quit
		
		set FMV = ""
		set PYBAL = ""

		// Original/New CIF number
		set ACN = $S(PASSFLG=1:irsupd.ocif,PASSFLG=2:ZACN)
		
		set TAXYR1 = YEAR + 1
		
		type RecordIRA ira = Db.getRecord("IRA", "ACN = :ACN,RPASEQ=:RPASEQ,TAXYR=:YEAR", 1)

		type RecordIRA ira1 = Db.getRecord("IRA", "ACN = :ACN,RPASEQ = :RPASEQ,TAXYR = :TAXYR1", 1)
		
		//  Regular Contributions
		set AMT(1) = $S((PASSFLG=1)&(irsupd.ccode=2):0,1:ira.c1+ira1.d2+ira.c9+ira.c13+ira1.d10) * 100
		
		// Rollover Contributions
		set AMT(2) = $S((PASSFLG=1)&(irsupd.ccode=2):0,1:ira.c4*100)

		// Calculate IRA Balance at Year-End
		set TAXYR = YEAR		
		do ^IRAYEBAL

		type RecordCIF cif = Db.getRecord("CIF", "ACN = :ACN", 1)

		set CID = ACN_"-"_RPASEQ		// Account number (sequence number)
		
		set TIN = $S(PASSFLG=1:irsupd.otin,PASSFLG=2:cif.taxid) // Tax ID number
		
		set TINTYPE = $S(TIN?2N1"-"7N:1,TIN?3N1"-"2N1"-"4N:2,1:" ") // Type of TIN
		
		set CORRTN = $S(PASSFLG=1:"G",PASSFLG=2:"C")
		set NAME = $S(PASSFLG=1:irsupd.oname,PASSFLG=2:cif.nam)
		set LNAME = $S(PASSFLG=1:irsupd.olnam,PASSFLG=2:cif.lnm)

		set FULLADDR = $$FULLADDR^RSPADD(ACN,RPASEQ)
		
		set ADDR = FULLADDR.piece("|",1)_" "_FULLADDR.piece("|",2)_" "_FULLADDR.piece("|",3)
		set CITY = FULLADDR.piece("|",4)	// Payee City
		set STATE = FULLADDR.piece("|",5)	// Payee State
		set ZIP = FULLADDR.piece("|",6).piece("-",1)_FULLADDR.piece("|",6).piece("-",2)  // Payee
		set CNTRY = FULLADDR.piece("|",7)	// Foreign country
		set FORCNTY = $S(FULLADDR.piece("|",7)="US":" ",1:1) // Foreign country indicator		

		// Special date position 544-750
		//; Blank |544|662
		set SPECIAL = "".justify(119)
		//; Special data entries|663|722
		set SPECIAL = SPECIAL_"".justify(60)
		//; Blank |723|750
		set SPECIAL = SPECIAL_"".justify(28)

		// Skip duplicates
		if (irsupd.ccode '= 1) do BRECWRT

		// Insert B Record data into B5498ESA table

		// If change in taxid, check the entry with new taxid.
		// If zero amounts, skip the entry.
		set TOT = 0
		
		for I = 1:1:2 set TOT = TOT + AMT(I)
		
		if 'TOT,(irsupd.ccode = 2),(PASSFLG = 2) quit

		set SIN = $S(PASSFLG=1:irsupd.otin,PASSFLG=2:cif.taxid)

		if (TINTYPE = 2) set X = LNAME
		else  set X = NAME
		
		do NAMCNTRL^IRSTPMTR		// Format name
		
		set BSEQ = Db.nextVal("B5498ESA","PDATE=:JD,BACN=:ACN")
		
		// B Record-IRS 5498 Form Data
		type RecordB5498ESA b5498esa = Db.getRecord("B5498ESA", "PDATE = :JD, BACN = :ACN, BSEQ = :BSEQ", 1)

		set b5498esa.bnamcon = X
		set b5498esa.btintype = TINTYPE
		set b5498esa.btin = SIN.translate(" ","-")
		set b5498esa.bcid = CID
		set b5498esa.bamt1 = AMT(1) / 100
		set b5498esa.bamt2 = AMT(2) / 100
		set b5498esa.bname = NAME
		set b5498esa.baddr1 = ADDR.piece(" ",1)
		set b5498esa.baddr2 = ADDR.piece(" ",2)
		set b5498esa.baddr3 = ADDR.piece(" ",3)
		set b5498esa.bcity = CITY
		set b5498esa.bstate = STATE
		set b5498esa.bzip = FULLADDR.piece("|",6)
		
		do b5498esa.save()
		}

	quit
	

CRECBLD	// "C" record builder

	do CRECWRT	// Write the record to tape

	quit


%STOPLOD	// Stop %ZRTNLOD from this point on down
	quit
TRECWRT	// Dummy line reference for GT.M
	quit
ARECWRT	// Dummy line reference for GT.M
	quit
BRECWRT	// Dummy line reference for GT.M
	quit
CRECWRT	// Dummy line reference for GT.M
	quit
STORETOT	// Dummy line reference for GT.M
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60387^52950^Dan Russell^6985"	// Signature - LTD^TIME^USER^SIZE
