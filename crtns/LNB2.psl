LNB2		/*
	ORIG: kini - 04/25/2005
	DESC: Standard Loan Bill Print Design (Revolving Credit)

	---- Comments --------------------------------------------------------
	This procedure is called indirectly from LNBLPNT procedure.
	
	I18N=QUIT: Excluded from I18N standards.

	---- Revision History ------------------------------------------------

	04/26/05 - KinI - 15524
		Created procedure based on LNB2.m routine.
	
	*/
	
	type public String ER
	type public Number BLSQ, CID, PG, TYPE, ZSUP
	type public Date TD
		
	type RecordLN ln = Db.getRecord("LN","CID=:CID",1)
	
	// Initialize data
	do INIT^LNB(.ln) if ER.get() quit
	
	type RecordLNBIL1 lnbil1 = Db.getRecord("LNBIL1","CID=:CID,SCHSEQ=:BLSQ",1)
	
	type RecordLNBLP lnblp = Db.getRecord("LNBLP","CID=:CID,BILDT=:TD,TYPE=:TYPE",1)
	
	// Initialize custom data
	set ZSUP=0 	// Do not suppress bank name and address print
	
	// Advance to new page
	do PNT1^LNB()
	
	// Print billing information
	do S1^LNB(ZSUP,,.ln)
	do S2(.lnblp,.lnbil1)
	do S3H^LNB
	do S3D^LNB("S5^LNB2(.lnblp,.lnbil1),S6(1,.ln),S7(0)","S1^LNB(ZSUP,,.ln),S3H",$$MSG())

	// Display Transaction summary for daily periodic rate and APR
	// based on balance or interest changes
	do S3FS^LNB(.ln)
	  
	if (PG = 1) do {
		do S4 
		do S5(.lnblp,.lnbil1) 
		do S6^LNB(1,.ln)
		}
		
	do S7^LNB(ZSUP)
	
	quit

	
S2(RecordLNBLP lnblp,
   RecordLNBIL1 lnbil1)	
   
	// Section 2 - Static Bill Date Data
	
	type public String LINE, WIRN()
	type public Number APD, CID, CHG, DSEQ, IDP, LCHG, MCHG
	
	type Number CDUE, CUES, CUIN, CUPR, FDUE, TOTINT, TOTPRN
	type String ELMNT, FEE, PC, ROW
	
	set (FDUE,TOTINT, TOTPRN) = 0 
	set ELMNT = "" 
		
	set CDUE = +lnbil1.casd      // Cur due
	
	type RecordLNBIL0 lnbil0 = Db.getRecord("LNBIL0","CID=:lnblp.cid")
	set ROW = $$ELEMENT^BILFUNCS(.lnbil0)
	for PC = 1:1:20 quit:$P(ROW,$C(9),PC) = ""  do {
		set ELMNT = $P($P(ROW,$C(9),PC),"#",1)
		if $E(ELMNT,1,3) = "I" set TOTINT = TOTINT + $$AMTDUE^BILFUNCS(.lnbil1,"I")
		if $E(ELMNT,1,3) = "P" set TOTPRN = TOTPRN + $$AMTDUE^BILFUNCS(.lnbil1,"P")
		}
		
	set CUIN = +TOTINT           // Cur int
	set CUPR = +TOTPRN           // Cur prl
		
	// Current Due (CDUE) includes principal, interest, escrow, and loan 
	// fees.  Subtract all extra elements to calculate escrow due	
	type ResultSet rs = Db.select("FEETYP","LNBIL5","CID=:CID AND SCHSEQ=:BLSQ AND DUEAMT=1")
        while rs.next() do {
                set FEE = rs.getCol(1)
                type RecordLNBIL5 lnbil5 = Db.getRecord("LNBIL5","CID,DSEQ,1,FEE")
                set FDUE = FDUE + lnbil5.brfamt
                }	

	set CUES = CDUE - CUIN - CUPR - FDUE                   // Cur esc	
	set CHG = CDUE + APD + LCHG + MCHG                     // Tot due

	do S2A^LNB("Past Due",lnblp.apd,1,"$")		       // Amount Past Due
	do S2A^LNB("Current Interest Rate",$$^SCARND((WIRN(WIRN("").order(-1)).piece("|",1)),8,"","",5)_"%",2,"T")
	do S2A^LNB("FINANCE CHARGE",CUIN,1,"$")
	do S2A^LNB("Days in Cycle",lnblp.dic,2,"T")
	do S2A^LNB("Principal Due",CUPR,1,"$",1)
	do S2A^LNB("Escrow Due",CUES,1,"$","T")
	do S2A^LNB("Interest Paid "_lnblp.yob,+lnblp.iytd,2,"$")
	do S2A^LNB("Late Charge Due",+LCHG,1,"$")
	do S2A^LNB("Interest Paid "_(lnblp.yob-1),+lnblp.ipy,2,"$")
	do S2A^LNB("Miscellaneous Due",+MCHG,1,"$")
	do S2A^LNB("Credit Limit",+lnblp.crlmt,2,"$")
	do S2A^LNB("TOTAL DUE",CHG,1,"$")
	do S2A^LNB("Available Credit",lnblp.avlbal,2,"$")
	do PNT^LNB("")
	do S2A^LNB("Payment(s) Due",lnblp.schdue,1,"D",1)

	quit


S4	// Marketing Messages

	do S4^LNB($$MSG())

	quit


S5(RecordLNBLP lnblp,
   RecordLNBIL1 lnbil1)	

	// Top section of remittance advice

	type public String LINE
	type public Number CHG, LCHG, MCHG, %SCHDUE	

	do BLN^LNB(45)

	set LINE = ""

	do S2A^LNB("Due "_%SCHDUE,+lnbil1.casd,1,"$")
	do S2A^LNB("Total Due",CHG,2,"$")
	do S2A^LNB("Previously Billed",lnblp.apd,1,"$")
	do S2A^LNB("Extra Principal","_____________",2)	
	do S2A^LNB("Charges Due",+LCHG+MCHG,1,"$")
	do S2A^LNB("Extra Tax Escrow","_____________",2)	
	do S2A^LNB("Extra Insurance","_____________",2)	

	do PNT^LNB("",2)

	quit


MSG()	// Define fixed message to print after variable marketing message

	quit "Please detach bottom portion and return with your payment"
	
 #OPTION ResultClass ON
Public String vSIG()	quit "60079^61104^Irina Kin^4068"	// Signature - LTD^TIME^USER^SIZE
