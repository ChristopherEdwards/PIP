DEPIAV(RecordDEP dep,RecordTTX ttx,RecordTRN trn)
	/*
	PBS - DEP - V2.0 - Re-deposit interest (add to INTAVL)

	DESC: Adjusts the Interest Available field (and Principal Balance) on
	the account.  Basic tran codes DAIC, IAIC use this routine.

	---- Revision History ------------------------------------------------

	12/13/02 - ZWITKOWITSM - 51349
		   Correct call to ENT^RECALC.

	11/14/02 - ZWITKOWITSM - 43583
		   PSL clean-up.

	06/13/02 - ZWITKOWITSM - 43583
		   Converted to PSL.

	-----------------------------------------------------------------------
	*/

	new DRCR,EFD,TAMT

	do INIT(.dep,.ttx)

	// Backdated transaction to a segmented account
	if dep.segflg,%EffectiveDate,%EffectiveDate<%SystemDate do ^RECALSEG(.dep,.ttx) quit

	if dep.ira do ^IRA(.dep,.ttx)

	do AUT(.dep,.ttx)
	if ER quit

	// Update debit/credit counters
	if DRCR=0 do { quit:ER 
		set dep.cntdr=dep.cntdr+1
		set dep.ldamt=ttx.tamt
		set dep.lddt=%SystemDate
		}

	else  do { quit:ER 
		set dep.cntcr=dep.cntcr+1
		set dep.lcamt=ttx.tamt
		set dep.lcdt=%SystemDate
		}

	do FILE(.dep,.ttx,.trn)

	quit


FILE(RecordDEP dep,RecordTTX ttx,RecordTRN trn)

	set ttx.tamt=TAMT

	// If this is a debit, decrease interest available buckets
	if DRCR=0 set TAMT=-TAMT

	set dep.bal=dep.bal+TAMT
	set dep.tld=%SystemDate
	set dep.balcol=dep.balcol+TAMT
	set dep.intavl=dep.intavl+TAMT

	if %EffectiveDate,%SystemDate-%EffectiveDate do ENT^RECALC(.dep,.ttx)

	// Update account status.
	do ^UPDSTAT(.dep,.trn)

	quit


INIT(RecordDEP dep,RecordTTX ttx)	// Initialize data

	set TAMT=+ttx.tamt
	set %EffectiveDate=ttx.efd
	set DRCR=ttx.itc1-dep.trb

	quit


Public EC(RecordDEP dep,RecordTTX ttx,RecordTRN trn)	// Error Correct

	new DRCR,EFD,TAMT

	if ttx.tso["REV#" do ^UPDCCLD(.dep,.ttx)

	do INIT(.dep,.ttx)

	// Update debit/credit counters
	if DRCR=0 set dep.cntdr=dep.cntdr-1
	else  set dep.cntcr=dep.cntcr-1

	do FILE(.dep,.ttx,.trn)

	quit


AUT(RecordDEP dep,RecordTTX ttx)	// Authorization Verification

	// Withdrawal of interest not restricted
	if 'dep.iaf do Runtime.setErrMSG("DEP",2978) quit

	// Not allowed for Interest Available Option ~p1
	if dep.iaf=2 do Runtime.setErrMSG("DEP",8448,dep.iaf) quit

	// Restriction Flag
	if +dep.rflg do ^UFLG(.dep)

	// Account Status
	if dep.stat set OVR(CID,"OVR","STAT"_+dep.stat)=""

	quit

vSIG()	quit "59886^43509^Sanchez SCM Administrator^2165"	// Signature - LTD^TIME^USER^SIZE
