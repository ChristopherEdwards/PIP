DEPINT5(RecordDEP dep,RecordTTX ttx,RecordTRN trn)	// Increase Capitalized Interest Available
	/*
	   ORIG:

	   DESC: Adjusts (increases) the Interest Available Not Credited field
	         on an account.  The basic tran codes that use this routine are
	         CRIANC, CRIANCD, and CRIANCS.  Routine DEPINT4 is used for
	         decreasing Available Interest Not Credited.

	   KEYWORDS: Interest, Available, Adjustment

	   INPUTS:
		. dep  deposit account object		/TYP=RecordDEP

		. ttx  transaction object		/TYP=RecordTTX

		. trn  transaction code			/TYP=RecordTRN

	   RETURNS:

	   RELATED:

	   EXAMPLE:
	         do ^DEPINT5(.dep,.ttx,.trn)
	         do EC^DEPINT5(.dep,.ttx,.trn)

	---- Revision History ------------------------------------------------

	12/13/02 - ZWITKOWITSM - 51349
		   Correct call to CYRINT^IRA.

	11/14/02 - ZWITKOWITSM - 43583
		   PSL clean-up.

	06/13/02 - ZWITKOWITSM - 43583
		   Converted to PSL.

	-----------------------------------------------------------------------
	*/
	new TAMT,%EffectiveDate

	do INIT(.dep,.ttx)
	if ER quit

	// IRA earnings adj
	if dep.ira do CYRINT^IRA(.dep,.ttx,1) if ER quit

	do AUT(.dep)
	if ER quit

	set $P(TAMT,"#",8)=+TAMT

	do FILE(.dep,.ttx)

	quit


FILE(RecordDEP dep,RecordTTX ttx)

	set ttx.tamt=TAMT
	set dep.tld=%SystemDate
	set dep.intavlncr=dep.intavlncr+TAMT

	quit


INIT(RecordDEP dep,RecordTTX ttx)	// Initialize data

	// Withdrawal of interest/dividend not restricted
	if 'dep.iaf do Runtime.setErrMSG("DEP",2978) quit

	// Not allowed for Int/Div Available Option 2
	if dep.iaf=1 do Runtime.setErrMSG("DEP",8448) quit

	set TAMT=+ttx.tamt
	set %EffectiveDate=ttx.efd

	quit


Public EC(RecordDEP dep,RecordTTX ttx,RecordTRN trn)	// Error Correct
	/*
	   INPUTS:
		. dep  deposit account object		/TYP=RecordDEP

		. ttx  transaction object		/TYP=RecordTTX

		. trn  transaction code			/TYP=RecordTRN

	   EXAMPLE:
		do EC^DEPINT5(.dep,.ttx,.trn)
	*/
	do INIT(.dep,.ttx)

	set $P(TAMT,"#",8)=+TAMT

	do FILE(.dep,.ttx)

	quit


AUT(RecordDEP dep)	// Authorization Verification

	// Restriction Flag
	if +dep.rflg do ^UFLG(.dep)

	if dep.stat set OVR(dep.cid,"OVR","STAT"_dep.stat)=""

	// Minimum Balance to Credit Int/Div
	if dep.bal<dep.minint set OVR(dep.cid,"OVR","MININT")=""

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43509^Sanchez SCM Administrator^2078"	// Signature - LTD^TIME^USER^SIZE
