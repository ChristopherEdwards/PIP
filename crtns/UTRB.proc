UTRB		/*
	   ORIG:  HOFFMAN - 24 MAR 1992

	   Processing relating to the Transaction to Reduce Balance flag (TRB)
	   is included in this routine.

	   LIBRARY:
	   . $$DC - Returns transaction debit/credit indicator
	   . $$TRB - Returns TRB value for a given account

	---- Revision History ------------------------------------------------

	01/13/05 - KELLYP - CR 13667
		   Modified to conform to current PSL standards and cleaned 
		   up procedure.

	06/13/02 - ZWITKOWITSM - 43583
		   Converted to PSL.

	----------------------------------------------------------------------
	*/

	quit 


public DC(Number DC,Number TRB)	// Returns Debit/Credit indicator based on TRB flag
	/*
	   Debit balance deposits (DBDs) and Credit-balance loans (CBLs)
	   process transactions similar to other accounts, except that the
	   reverse value is used to increase or decrease the amount.
	   This function switches the debit/credit indicator based on TRB.

	   KEYWORDS: Deposits, loans, treasury

	   ARGUMENTS:
	       . DC	- Debit/Credit indicator	/TYP=N/REQ/LEN=1
	       . TRB	- TRB flag			/TYP=N/NOREQ/LEN=1
			0 = Debit to reduce balance
			1 = Credit to reduce balance

	   RETURNS:
	       . $$	- Value of DC			/TYP=N
			"" = DC value was null
			0 = withdrawal
			1 = deposit
	*/

	set DC=DC.extract() 
	if DC.isNull() quit ""

	if TRB.get() set DC='DC

	quit DC


Public TRB(Number CID)  // Returns TRB indicator based on account or product defaults
	/*
	   KEYWORDS: Deposits, loans, treasury

	   ARGUMENTS:
	   . CID 	- Account number		/TYP=N/REQ/LEN=12

	   RETURNS:
	   . $$ 	- Transaction to Reduce Balance (TRB)	/TYP=N
	*/

	if CID.isNull() quit 0

	type RecordACN acn=Db.getRecord("ACN","CID=:CID")

	type Number TRB

	set TRB=acn.trb

	if TRB.isNull() do {
		type RecordPRODCTL prodctl=Db.getRecord("PRODCTL","TYPE=:acn.type")
		set TRB=prodctl.trb
		}

	quit +TRB

vSIG()	quit "59918^54042^Pat Kelly^1792"	// Signature - LTD^TIME^USER^SIZE
