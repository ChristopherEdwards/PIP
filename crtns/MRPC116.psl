MRPC116(return,versn,CTL,CTL2,CUISO,CUACN,BRCD)
  /*
ORIG: CAROL SCOTT - 03/19/2001
DESC: Shared Branch Network MRPC

ARGUMENTS
	. return	SBN Customer Data	/TYP=T/REQ/MECH=REFNAM

	. versn		MRPC version number	/TYP=N/REQ/MECH=VAL

	. CTL		SBN Control Parameter	/TYP=N/REQ/MECH=VAL

	   The CTL parameter controls which primary RPC function is being 
	   performed.  Valid options are:
		1 - Member Verification
		2 - Account Inquiry
		3 - Account Statement

	. CTL2		SBN Control Parameter 2	/TYP=N/REQ/MECH=VAL

	   Controls the processing flow after determining the primary
	   RPC function from CTL.
		0 - Share and Loan
		1 - Share
		2 - Loan
		3 - Investment

	   For example, CTL = 2 & CTL2 = 3 means that we should perform an
	   Investment Account Inquiry. 

	. CUISO		Credit Union ISO Code	/TYP=T/REQ/MECH=VAL
	   The ISO/BIN number of the origination (host) credit union.

	. CUACN		Credit Union Member ID	/TYP=T/REQ/MECH=VAL
	   The Member ID, or account number, at the origination (host)
	   credit union. 

	. BRCD		Branch Code		/TYP=T/NOREQ/MECH=VAL

---- Comments --------------------------------------------------------
This MRPC handles all outgoing requests for generating Shared Branch
Network messages from PFW Teller.  
---- Revision History ------------------------------------------------
	11/02/01 - SCOTTC - 46356:09
	      Modified routine to eliminat duplicate code.  Removed sections
	      MEMVER,INQ,STMT since all of the sections were doing
	      the same thing.
	      Modified the error handling to conform with standard error 
	      handling for MRPCs.  Changed from D SETERR^DBSEXECU to 
	      $$ERRMSG^PBSUTL

	10/29/01 - KELLYP - 46356:008
	      Added BRCD parameter and processing to handle it.

	09/04/01 - KELLYP - 46356
	      Modified error message processing to return the actual error
	      encountered.

	08/29/01 - KELLYP - 46293:030
	      Removed old %SVCNTXT functionality. This is now handled by SBNOUT.
	
	07/10/01 - SCOTTC - 46293
	      Modified error messages.

 */
	new BIT,ER,TBL
	set ER=""

	// 46356:008 - Default branch is CUVAR.BOBR
	if $G(BRCD)="" set BRCD=Db.getOneRow("BOBR","CUVAR")

	//Required fields cannot be null - ~p1
	if (CTL="")!(CTL2="")!(CUISO="")!(CUACN="") quit $$ERRMSG^PBSUTL($$^MSG(8602))	

	//check CTL & CTL2 to see if valid - Invalid table value ~p1
	if (CTL>3)!(CTL<1) quit $$ERRMSG^PBSUTL($$^MSG(8602))
	if (CTL2>3)!(CTL<0) quit $$ERRMSG^PBSUTL($$^MSG(8602))

	// set up the Processing code and qualifiers
	do SETPRCD(CTL,CTL2,.PRCD,.QAL1,.QAL2)

	// build outgoing message and recieve return
	do CUSVER^SBNOUT(CUISO,CUACN,PRCD,QAL1,QAL2,"0100",.return)
	
	if ER=1 quit $$ERRMSG^PBSUTL($G(RM))	// Quit returning error

	set return=$$V2LV^MSG(.return)		// Compose return	

 	quit ""


SETPRCD(CTL,CTL2,PRCD,QAL1,QAL2)    //Set Processing Code and Qualifier 1

	set QAL2="   "

	//account verification
	if CTL=1 set PRCD="910000",QAL1="VER"

	//account inquiry
	if CTL=2 do {
		set PRCD="390000"
		set QAL1=$S(CTL2=0:"SLI",CTL2=1:"SAI",CTL2=2:"LAI",CTL2=3:"IAI")
		    }

	//statement
	if CTL=3 do {
		set QAL1="STM"
		set PRCD=$S(CTL2=1:"911000",CTL2=2:"919000",CTL2=3:"915000")
		    }
	 
	//set qualifier 2 for verification and statement

	if (CTL=1)!(CTL=3) set QAL2=$S(CTL2=0:"SLI",CTL2=1:"SAI",CTL2=2:"LAI",CTL2=3:"IAI")
	
	quit	
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43592^Sanchez SCM Administrator^3160"	// Signature - LTD^TIME^USER^SIZE
