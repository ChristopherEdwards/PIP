IRSC99SA		/*
	ORIG: DHANALAKSHMI R - 08/18/2006
	DESC: IRS Processing - Form 1099-SA Correction

	---- Comments --------------------------------------------------------
			  THIS PROCEDURE IS TO BE COMPILED
			      DO NOT RUN STANDALONE

	I18N=QUIT: Excluded from I18N standards.
	
	---- Revision History ------------------------------------------------

	07/31/07 - DHANALAKSHMI R - CR 28459
		Modified the section BRECBLD to implement the recent changes 
		in PO, to correct the payment amount and distribution code  
		for distributions made using the reason code 18, 19 and 24.

	05/30/07 - DHANALAKSHMI R - CR 25413
		Added for IRS processing of Health Savings Account 
		(Form 1099-SA Correction).

	*/

        quit


MTRCNTRL

	// Master control

	type public Number ER

	type Number PASSFLG

	do Db.delete("B1099SA")

	set PASSFLG = 1

	// Build and write the "A" record - First pass
	do ARECBLD quit:ER

	// Build and write the "B" record - First pass
	do BRECBLD quit:ER

	// Build and write the "C" record - First pass
	do CRECBLD quit:ER
	
	set PASSFLG = 2
		
	// Build and write the "A" record - Second pass
	do ARECBLD quit:ER

	// Build and write the "B" records - Second pass
	do BRECBLD quit:ER

	// Build and write the "C" record - Second pass
	do CRECBLD quit:ER

	// Store the TTR report totals
	do STORETOT

	quit
	

ARECBLD

	// "A" record builder

	type public String TAMT()

	type Number AMTIND
	type String FORMCODE

	// IRS form code
	set FORMCODE = "M"

	// IRS amount type indicator
	set AMTIND = 124

	set TAMT(1).piece("|",2) = "Gross distribution"
	set TAMT(2).piece("|",2) = "Earnings on Excess Contributions"
	set TAMT(4).piece("|",2) = "Fair Market Value of Account on DOD"

	// Write the record to tape
	do ARECWRT

	quit


BRECBLD

	// "B" record builder

	type public Number AMT(), CUTOFF, PASSFLG, YEAR
	type public String FORM, FORMS()

	type Number ACN, CID, CIFDODYR, DCODE, IRATYP, RPASEQ, ZACN
	type String ADDR, CITY, CNTRY, CORRTN, FORCNTY, FULLADDR, LNAME, NAME
	type String SPECIAL, STATE, TIN, TINTYPE, ZIP
	type Date JD

	set JD=FORMS(FORM)-1

	type DbSet ds=Db.selectDbSet("IRSUPD","TJD>:JD AND FTYPE=19")

	while ds.next() do {

		type RecordIRSUPD irsupd=ds.getRecord("IRSUPD")
		
		set ZACN=irsupd.miscseq
		set RPASEQ=irsupd.bseq
	
		// Skip duplicates when writting to tape
		if (irsupd.ccode=1),(PASSFLG=1) quit

		// Skip "New Entry" for first pass
		if (irsupd.ccode=4),(PASSFLG=1) quit

		// Skip "change amount" for second pass
		if (irsupd.ccode=3),(PASSFLG=2) quit

		// Original/New CIF number,Tax ID Number, 
		// Corrected return indicator, Payee First Name & Payee Last Name
		if (PASSFLG=1) do {
			set ACN = irsupd.ocif
			set TIN = irsupd.otin
			set CORRTN="G"
			set NAME = irsupd.oname
			set LNAME = irsupd.olnam
		}
		else  if (PASSFLG=2) do {
			set ACN = ZACN
			type RecordCIF cif = Db.getRecord("CIF", "ACN = :ACN")
			set TIN = cif.taxid
			set CORRTN="C"
			set NAME = cif.nam
			set LNAME = cif.lnm
		}

		set AMT(1) = 0
		set AMT(2) = 0
		set AMT(4) = 0

		type RecordIRATYPE iratype = Db.getRecord("IRATYPE", "ACN=:ACN,RPASEQ=:RPASEQ")
	
		set IRATYP = iratype.iratyp
		
		if (IRATYP '= 18),(IRATYP '= 19) quit	
		
		type RecordCIF cif = Db.getRecord("CIF", "ACN = :ACN")
		
		// Account number (sequence number)
		set CID=ACN_"-"_RPASEQ
		
		// Type of TIN
		if (TIN?2N1"-"7N) set TINTYPE=1
		else  if (TIN?3N1"-"2N1"-"4N) set TINTYPE=2
		else  set TINTYPE=" "

		set FULLADDR = cif.mad1_"|"_cif.mad2_"|"_cif.mad3_"|"_cif.mcity_"|"_cif.mstate_"|"_cif.mzip_"|"_cif.mcntry
		set ADDR = FULLADDR.piece("|",1)_" "_FULLADDR.piece("|",2)_" "_FULLADDR.piece("|",3)

		// Payee City
		set CITY = FULLADDR.piece("|",4)

		// Payee State
		set STATE = FULLADDR.piece("|",5)

		// Payee
		set ZIP = FULLADDR.piece("|",6).piece("-",1)_FULLADDR.piece("|",6).piece("-",2)

		// Foreign country
		set CNTRY = FULLADDR.piece("|",7)

	 	// Foreign country indicator
	 	set FORCNTY = " "
	 	if (FULLADDR.piece("|",7) '= "US") set FORCNTY = 1

		// Distribution Code
		set DCODE = " "

		// Zero amount fields (Counting Records for T-Record)
		if (PASSFLG=1),(irsupd.ccode=2) do SPECIAL,BRECWRT,B1099SA(ACN) quit

		type RecordIRA ira=Db.getRecord("IRA","ACN=:ACN,RPASEQ=:RPASEQ,TAXYR=:YEAR", 1)

		// Normal Distributions
		set AMT(1) = (ira.d1 + ira.d15) * 100

		if AMT(1) do {

 			// Distribution Code
			set DCODE = 1
			do SPECIAL,B1099SA(ACN)
			if (irsupd.ccode '= 1) do BRECWRT
		}

		// Distributions against Excess Contribution
		set AMT(1) = (ira.d18 + ira.d19 + ira.d22 + ira.d23 + ira.d24) * 100
		 
		if AMT(1) do {

 			// Distribution Code
			set DCODE = 2
			set AMT(2) = ira.d22 + ira.d23
			do SPECIAL,B1099SA(ACN)
			if (irsupd.ccode '= 1) do BRECWRT
			set AMT(2) = 0
		}

		// Disability
		set AMT(1) = ira.d5 * 100

		if AMT(1) do {

 			// Distribution Code
			set DCODE = 3
			do SPECIAL,B1099SA(ACN)
			if (irsupd.ccode '= 1) do BRECWRT
		}

		// Prohibited Transaction
		set AMT(1) = ira.d7 * 100

		if AMT(1) do {

 			// Distribution Code
			set DCODE = 5
			do SPECIAL,B1099SA(ACN)
			if (irsupd.ccode '= 1) do BRECWRT
		}

		if cif.dod.isNull() quit
		else  if (irsupd.ccode '= 2) ! (ira.d6) do {

			set CIFDODYR = cif.dod.year()

			// B record for Beneficiary after A/C owner's Death
			do DEATH(irsupd.ccode)
		}
	}
	quit


DEATH( Number CCODE)

	type public Number ACN, AMT(), CID, CIFDODYR, DCODE, RPASEQ, YEAR

	type Number CIDSAVE, FMVDOD, IRACID
	type String ADDR, CITY, CNTRY, FORCNTY, FULLADDR, LNAME, NAME
	type String SPECIAL, STATE, TIN, TINTYPE, ZIP

	set CIDSAVE=CID

	type DbSet ds1=Db.selectDbSet("IRABEN1","ACN=:ACN AND RPASEQ=:RPASEQ AND CYR=:YEAR")

	while ds1.next() do {

		type RecordIRABEN1 iraben1=ds1.getRecord("IRABEN1")

		set AMT(1) = (iraben1.tamt + iraben1.wth + iraben1.totstwh) * 100

		type RecordIRABEN iraben=Db.getRecord("IRABEN","ACN=:ACN,RPASEQ=:RPASEQ,BENSEQ=:iraben1.benseq")

		// Beneficiary is a PROFILE customer and address missing in IRABEN table
		if 'iraben.benacn.isNull(),iraben.benmad1.isNull() do {

			type RecordCIF cif = Db.getRecord("CIF", "ACN = :iraben.benacn")

			set FULLADDR=$$FULLADDR^RSPADD(iraben.benacn,RPASEQ)

			quit:FULLADDR.isNull()

			set ADDR=FULLADDR.piece("|",1)_" "_FULLADDR.piece("|",2)_" "_FULLADDR.piece("|",3)

			// Beneficiary City
			set CITY=FULLADDR.piece("|",4)

			// Beneficiary State
			set STATE=FULLADDR.piece("|",5)

			// Beneficiary Zip
			set ZIP=FULLADDR.piece("|",6).piece("-",1)_FULLADDR.piece("|",6).piece("-",2)

			// Foreign country
			set CNTRY=FULLADDR.piece("|",7)

		  	// Foreign country indicator
			set FORCNTY=$select(CNTRY="US":" ",1:1)
			set TIN=cif.taxid
			set NAME = cif.nam
			set LNAME = cif.lnm
		}

		// Beneficiary is not a PROFILE customer
		else  do {

			set NAME=iraben.benname
			set LNAME=iraben.benlnm
			set TIN=iraben.bentaxid

			// Set line 2 and line 3 of Beneficiary address
			set ADDR=iraben.benmad1_" "_iraben.benmad2_" "_iraben.benmad3
			set FULLADDR.piece("|",1)=iraben.benmad1
			set FULLADDR.piece("|",2)=iraben.benmad2
			set FULLADDR.piece("|",3)=iraben.benmad3
			set CITY=iraben.bencity
			set STATE=iraben.benstate
			set ZIP=iraben.benzip.piece("-",1)
			set FULLADDR.piece("|",6)=ZIP
			set CNTRY=iraben.bencntry
			set FORCNTY=$select(CNTRY="US":" ",1:1)
		}	

		type RecordUTBLBENREL utbenrel=Db.getRecord("UTBLBENREL","BENREL=:iraben.benrel")
		
		// Distribution Code
		set DCODE = 4
		if CIFDODYR < YEAR do {
			if (utbenrel.spi = 1) set DCODE = 1
			else  if (utbenrel.spi '= 2) set DCODE = 6
		}

		set AMT(4) = 0
		if (utbenrel.spi '= 1) do {

			set FMVDOD = 0
			type ResultSet rs = Db.select("CID,DBAL,DFMV","RELCIF","ACN = :ACN")
			while rs.next() do {

				set IRACID = rs.getCol("CID")
				type RecordDEP dep = Db.getRecord("DEP","CID =:IRACID")
				if (dep.rpaseq = RPASEQ) set FMVDOD = FMVDOD + rs.getCol("DFMV")
			}

			//Fair Market Value
			set AMT(4) = (iraben.benpct * FMVDOD)
		}

		type RecordIRATYPE iratype1=Db.getRecord("IRATYPE","ACN=:ACN,RPASEQ=:RPASEQ", 1)

		// Account number (sequence number)
		set CID=iraben1.benseq_"-"_RPASEQ

		// Type of TIN
		if (TIN?2N1"-"7N) set TINTYPE=1
		else  if (TIN?3N1"-"2N1"-"4N) set TINTYPE=2
		else  set TINTYPE=" "

		do SPECIAL,B1099SA(iraben1.benseq)
		if (CCODE '= 1) do BRECWRT
	}

	set CID=CIDSAVE
	quit


SPECIAL

	// Build Special Data

	type public String SPECIAL
	type public Number DCODE
	
	// Special date position 544-750
	// Blank |544
	set SPECIAL = " "

	// Distribution Code |545
	set SPECIAL = SPECIAL_DCODE

	// Blank 546|547
	set SPECIAL = SPECIAL_"  "

	// HSA Indicator |548
	set SPECIAL = SPECIAL_1

	// Blank|549|750
	set SPECIAL = SPECIAL_"".justify(202)

	quit


B1099SA( Number ACN)

	// Insert "B" Records into B1099SA File

	type public Number AMT(), CID, DCODE, FAMT(), FCNT, TINTYPE
	type public String CITY, FULLADDR, LNAME, NAME, STATE, TIN
	type public Date JD

	type Number BSEQ
	type String X

	if (TINTYPE = 2) set X = LNAME
	else  set X = NAME
	
	do NAMCNTRL^IRSTPMTR
	
	type ResultSet rs = Db.select("BSEQ","B1099SA","PDATE=:JD AND BACN=:ACN","BSEQ DESC")
	if rs.next() set BSEQ = rs.getCol("BSEQ") + 1
	else  set BSEQ = 1

	type RecordB1099SA b1099sa = Db.getRecord("B1099SA","PDATE=:JD,BACN=:ACN,BSEQ=:BSEQ", 1)
	
	set b1099sa.bnamcon = X
	set b1099sa.btintype = TINTYPE
	set b1099sa.btin = TIN.piece("|",1).translate(" ","-")
	set b1099sa.bcid = CID
	set b1099sa.bamt1 = AMT(1)/100
	set b1099sa.bamt2 = AMT(2)/100
	set b1099sa.bamt4 = AMT(4)/100
	set b1099sa.bname = NAME
	set b1099sa.baddr1 = FULLADDR.piece("|",1)
	set b1099sa.baddr2 = FULLADDR.piece("|",2)
	set b1099sa.baddr3 = FULLADDR.piece("|",3)
	set b1099sa.bcity = CITY
	set b1099sa.bstate = STATE
	set b1099sa.bzip = FULLADDR.piece("|",6)
	set b1099sa.bdcode = DCODE
	set b1099sa.bhsai = 1

	do b1099sa.save()
	quit


CRECBLD

	// "C" record builder

	// Write the record to tape
	do CRECWRT
	
	quit


%STOPLOD

	// Stop %ZRTNLOD from this point on down
	quit
	

TRECWRT

	// Dummy line reference for GT.M
	quit


ARECWRT

	// Dummy line reference for GT.M
	quit


BRECWRT

	// Dummy line reference for GT.M
	quit


CRECWRT

	// Dummy line reference for GT.M
	quit


STORETOT

	// Dummy line reference for GT.M
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60842^22049^Dhanalakshmi R^9758"	// Signature - LTD^TIME^USER^SIZE
