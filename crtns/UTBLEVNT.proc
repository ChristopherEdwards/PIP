UTBLEVNT	//;PBS -  - V4.0 - Event User Table Maintenance
	/*
	ORIG:  Pete Chenard (6904) 11/13/89
	DESC: "EVENT" user table maintenance

	---- Revision History ------------------------------------------------
	
	10/13/05 - RussellDS - CR17733
		   Modify file section to set mode on UTBLEVENT record to
		   indicate it is a create, not update.
		   
		   Moved save of UTBLEVENT before saving of sub-records.
		   
		   Removed old revision history.
	  
	12/11/04 - RussellDS - CR13642
		   Fix undefined error.  Fix PSL warnings.
	 
	  ---------------------------------------------------------------------	
	*/

	quit


NEW 	//
	do INIT(0)
	quit


UPD 	//
	do INIT(1)
	quit


INQ 	//
	do INIT(2)
	quit


DEL	//
	do INIT(3)
	quit


INIT(%ProcessMode) //

	type Number %PAGE, %PG, %REPEAT, OLNTB
	type String EVENT, QUEUE(), VFMQ

	set %PG=0 
	set %PAGE=1
	type RecordUTBLEVENT fUTBLEVE
	do VPG(.fUTBLEVE)
	quit


VPG(RecordUTBLEVENT fUTBLEVE)	// Page control

	type public Boolean ER

	type public Number %PG
	type public String VFMQ

	type Boolean FINISH

	set FINISH=0
	for  do { quit:FINISH
		if %PG=0 do VPG00 if ER!(VFMQ="Q") set FINISH=1 quit
		if %PG>0 do VPG01(.fUTBLEVE) 
		if "DFQ"[VFMQ do VER(.fUTBLEVE) set FINISH=1 quit
		set %PG=%PG+1
		}
	quit


VPG00	// Set up

	type String %READ, %TAB()

	if '%ProcessMode set %TAB("EVENT")=".EVENT1/HLP=[UTBLEVENT]EVENT/XPP=D PP^UTBLEVNT"
	else  set %TAB("EVENT")=".EVENT1/HLP=[UTBLEVENT]EVENT/TBL=[UTBLEVENT]/XPP=D PP^UTBLEVNT"

	set %READ="@@%FN,,,EVENT/REQ"

	do ^UTLREAD

	quit


VPG01(RecordUTBLEVENT fUTBLEVE)	// Screen

	type public String EVENT

	set fUTBLEVE=Db.getRecord("UTBLEVENT","EVENT=:EVENT",1)	

	do LOAD 

	do DRV^USID(%ProcessMode,"UTBLEVENT",.fUTBLEVE)

	quit


PP	//Event post-processor

	type public Boolean ER
	type public Number %OSAVE
	type public String RM, X

	// ~p1 already exists
	if '%OSAVE,Db.isDefined("UTBLEVENT","EVENT=:X") set ER=1 set RM=$$^MSG(3019,X) quit

	// ~p1 does not exist
	if %OSAVE,'Db.isDefined("UTBLEVENT","EVENT=:X") set ER=1 set RM=$$^MSG(3029,X) quit
	quit


LOAD	//Load QUEUE variable

	type public Number %REPEAT
	type public String QUEUE()

	type Number I, J

	set I=1 
	set J=1 
	set %REPEAT=15 

	type ResultSet rs=Db.select("BATCH","UTBLEBCH","EVENT=:EVENT")
	while rs.next() do {
		set $P(QUEUE(I),"|",J)=rs.getCol("BATCH")
		set J=J+1
		if J>7 set J=1 set I=I+1
		}

	quit


VER(RecordUTBLEVENT fUTBLEVE) 	//

	type public String VFMQ

	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit	
	do FILE(.fUTBLEVE)
	do END
	quit


FILE(RecordUTBLEVENT fUTBLEVE)	// File data

	type public String EVENT, QUEUE()

	type Number I, J
	type String Q

	do Db.delete("UTBLEVENT","EVENT=:EVENT")
	do Db.delete("UTBLEBCH","EVENT=:EVENT")

	if %ProcessMode=3 quit
	
	do fUTBLEVE.setMode(0)		// Set to create mode
	do fUTBLEVE.bypassSave()
	
	set I=""

	for  set I=$O(QUEUE(I)) quit:I=""  do {
		for J=1:1:7 set Q=$P(QUEUE(I),"|",J) do {
			if Q'="" do {
				type RecordUTBLEBCH utblebch=Db.getRecord("UTBLEBCH","EVENT=:EVENT,BATCH=:Q",1)
				type RecordQUEUEB queueb=Db.getRecord("QUEUEB","BCHNUM=:Q")	
				set utblebch.des=queueb.desc
				do utblebch.bypassSave()
				}
			}
		}

	quit


END	//

	type public Boolean ER
	type public String EVENT, RM, VFMQ

	if $G(ER)!(%ProcessMode=2)!(%ProcessMode=4) quit
	set EVENT=$G(EVENT)

	if VFMQ="Q" do {

		// Event ~p1 not created
		if %ProcessMode=0 set RM=$$^MSG(1027,EVENT) quit

		// Event ~p1 not modified
		if %ProcessMode=1 set RM=$$^MSG(1029,EVENT) quit

		// Event ~p1 not deleted
		set RM=$$^MSG(1028,EVENT)
		}
	else  do {

		// Event ~p1 created
		if %ProcessMode=0 set RM=$$^MSG(1023,EVENT) quit

		// Event ~p1 modified
		if %ProcessMode=1 set RM=$$^MSG(1026,EVENT) quit

		// Event ~p1 deleted
		set RM=$$^MSG(1024,EVENT)
		}
	set ER="W"
	quit

vSIG()	quit "60186^56489^Dan Russell^3543"	// Signature - LTD^TIME^USER^SIZE
