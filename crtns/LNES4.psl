LNES4	// ;PBS -  - V2.0 - Escrow type maintenance
	/*
	       ORIG:  Neal E. Gorman (5053) - 04/26/87

	  ---- Revision History ------------------------------------------------
	  
	  05/31/07 - SHARMAR - 25736
	              Modified sectin POTR to allow values P, I,
	              M AND L.Also retain logic for escrow type to 
	              be at least 2 characters .
	  
	   31/01/06 - Srinivar - 19137 
	   	      Cleaned up the warnings.
	   	      Modified the code to conform PSL standards.	
	   
	   07/30/02 - CHHABRIAS - 49451
		      Converted to PSL
		      
	   
	  ---------------------------------------------------------------------

	*/

	do NEW 
	
	quit
	
NEW 	//
	
	do INIT(0)
	quit


UPD 	//
	
	do INIT(1)
	quit


INQ 	//
	
	do INIT(2)
	quit


INIT(%ProcessMode)	//

	
	type Number %PG,%PAGE
	type String %VFMQ
	
	set %PG=0
	set %PAGE=1
	type RecordLNTRS fLNTRS
	do VPG(.fLNTRS)
	quit


VPG(RecordLNTRS fLNTRS)	// Page control

        type public Number %PG
        type public String VFMQ
        
        type Boolean FINISH
        
        set FINISH=0
        for  do { quit:FINISH
		if %PG=0 do VPG00 if VFMQ="Q" set FINISH=1 quit
		if %PG>0 do VPG01(.fLNTRS)
		if "DFQ"[VFMQ do VER(.fLNTRS) set FINISH=1 quit
		set %PG=%PG+1
		}
	quit


VPG00	// Set up

	type public String IO,%READ,%TAB,VFMQ
	
	set %TAB("TRTYPE")=".TRTYPE1/TBL=[LNTRS]TRTYPE,TRDES/XPP=D POTR^LNES4"
	if %ProcessMode=2 set %TAB("IO")=$$IO^SCATAB($I)

	set %READ="@@%FN,,,TRTYPE/REQ"
	if %ProcessMode=2 set %READ=%READ_",IO/REQ"

	do ^UTLREAD

	if VFMQ="Q" quit

	if %ProcessMode=2,IO'=$I do OPEN^SCAIO

	quit


VPG01(RecordLNTRS fLNTRS)	// Screen

	type public String TRTYPE

	set fLNTRS=Db.getRecord("LNTRS","TRTYPE=:TRTYPE",1)

	do DRV^USID(%ProcessMode,"LNTRS",.fLNTRS)

	quit


ERR	//
	
	type public Boolean ER
	type public String VFMQ
	
	set ER=1

	do DSPBP^UTLERR

	set VFMQ="Q"
	quit


VER(RecordLNTRS fLNTRS) 	//
	
	type public String VFMQ,TRTYPE

	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit

	if %ProcessMode=3 do {
		do Db.delete("LNTRS","TRTYPE=:TRTYPE")
		}
	else  do fLNTRS.save()

	do END
	quit


END	//
	
	type public Boolean ER
	type public String RM,%TAB,TRTYPE,VFMQ
	
		
	kill %TAB lock 
	if ER!(%ProcessMode=2)!(%ProcessMode=4) quit

	set TRTYPE=TRTYPE.get()

	if VFMQ="Q" do {

		// Escrow type ~p1 not created
		if %ProcessMode=0 set RM=$$^MSG(1015,TRTYPE) quit

		// Escrow type ~p1 not modified
		if %ProcessMode=1 set RM=$$^MSG(1018,TRTYPE) quit

		// Escrow type ~p1 not deleted
		set RM=$$^MSG(1017,TRTYPE)
		}
	else  do {

		// Escrow type ~p1 created
		if %ProcessMode=0 set RM=$$^MSG(1012,TRTYPE) quit

		// Escrow type ~p1 modified
		if %ProcessMode=1 set RM=$$^MSG(1014,TRTYPE) quit

		// Escrow type ~p1 deleted
		set RM=$$^MSG(1013,TRTYPE)
		}

	set ER="W"
	quit


POTR	//
	
	type public String I(),RM,X
	type public Boolean ER
	type public Number %OSAVE
	
	type String ET
	
	if X.isNull() quit

	// Escrow types must be at least 2 characters
	if X.length()<2,(X'="P"),(X'="I"),(X'="M"),(X'="L") set ER=1 set RM=$$^MSG(1020) quit
	      
	// Escrow types must be all alphabetic characters
	if X'?.A set ER=1 set RM=$$^MSG(1019) quit

	// ESC reserved for system use.
	if %OSAVE=0,$E(X,1,3)="ESC" do { quit
		set ER=1
		set RM=$$^MSG(8507)
		}

	if %OSAVE quit

	set I(3)=""
	if Db.isDefined("LNTRS","TRTYPE=:X") do { 
		do Runtime.setErrSTBLER("LNTRS","RECOF") 
		}
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60787^47246^Richa Sharma^3168"	// Signature - LTD^TIME^USER^SIZE
