public	RFLG3	
	/*

	  ---- Revision History ------------------------------------------------

	   08/02/06 - KELLYP - CR 22048
	   	      Modified LDFNCTN section to eliminate PRECEDENCE warning.
	
	   04/23/05 - TITOVE - CR 15221
	   	      Modified as part of DBI2 project. Removed sections that
	   	      dealed with UX array updates and also section UPD.

	   10/05/02 - CHHABRIAS - 49451
		      Converted to PSL

	  ----------------------------------------------------------------------
        */

	do START
	
	quit

public	START 	// Insert

	do INIT(0)
	
	quit


public	INQ 	// Inquiry

        do INIT(2)
        
        quit


public	DEL	// Delete

	do INIT(3)
	
        quit


INIT(%ProcessMode)

	type Number %PAGE, %PG, ALP, OLNTB, ZNW
	type String FUNCSAV(), FUNCTION(), GRP, RFLG, UCLS, VFMQ
	
	set %PG = 0
	set %PAGE = 1
	set ALP = 9
	
	type RecordUTBLRFLG fUTRFLG
	type RecordUTBLRFLG2 fUTBLRFL

	do VPG(.fUTRFLG,.fUTBLRFL)

	quit


VPG(RecordUTBLRFLG fUTRFLG,RecordUTBLRFLG2 fUTBLRFL)	// Page control

	type public Number %PG, ER
	type public String VFMQ

	type Boolean FINISH = 0
	
        for  do { quit:FINISH
        	
                if (%PG = 0) do VPG00(.fUTRFLG,.fUTBLRFL) if ER!(VFMQ = "Q") set FINISH = 1 quit

                if (%PG > 0) do VPG01(.fUTRFLG,.fUTBLRFL)

                if "DFQ"[VFMQ do VER set FINISH = 1 quit

                set %PG = %PG + 1
                }

	quit


VPG00(RecordUTBLRFLG fUTRFLG,RecordUTBLRFLG2 fUTBLRFL)	// Set up

	type public Number %TAB, ER
	type public String %NOPRMT, %READ, KEYL(), RFLG, VFMQ

	type Number SAVEO = 0

	set %TAB("GRP") = ".GRP1/TBL=[STBLLIBS]NAME"
	set %TAB("RFLG") = ".RFLG1/TBL=[UTBLRFLG]RFLG:DISTINCT:QU ""[UTBLRFLG]GRP=<<GRP>>"""
	set %TAB("UCLS") = ".UCLS4/TBL=[UTBLRFLG3]UCLS:DISTINCT:QU ""[UTBLRFLG3]GRP=<<GRP>> AND [UTBLRFLG3]RFLG=<<RFLG>>""/XPP=D PPUCLSFN^RFLG3"

	if %ProcessMode = 2 set %TAB("IO") = $$IO^SCATAB($I)

	set %READ = "@@%FN,,,GRP/REQ,RFLG/REQ,UCLS/REQ"

	set %NOPRMT = "N"

	if (%ProcessMode = 2) do {
		
		set %READ = %READ_",IO/REQ"
		set SAVEO = %ProcessMode
		}

	if (%ProcessMode = 3) do DEL^UTLREAD quit

	do ^UTLREAD

	if (VFMQ = "Q") set ER = 1 quit

	if SAVEO set %ProcessMode = SAVEO
	
        set KEYL(1) = "RFLG3"
        
        set KEYL(3) = RFLG

	quit


VPG01(RecordUTBLRFLG fUTRFLG,RecordUTBLRFLG2 fUTBLRFL)	// Restrict screen

	type public Number %PG, ALP, ER
	type public String GRP, RFLG, VFMQ

	type Number %MODS, %REPEAT, MPG

	set %REPEAT = ALP
	set MPG = 0
	set %MODS = ((%PG - 1) * ALP) + 1
	
	if (%ProcessMode = 2) do OPEN^SCAIO if ER set VFMQ = "Q" quit

	set fUTRFLG = Db.getRecord("UTBLRFLG", "GRP = :GRP, RFLG = :RFLG", 1)

	do DRV^USID(%ProcessMode,"UTBLRFLG3",.fUTBLRFL,.fUTRFLG)

	quit


PPUCLSFN	// Post processor for Function restriction user class

	type public Number %OSAVE, ZNW
	type public String GRP, I(), KEYL(), RFLG, UCLS

	type ResultSet rs = Db.select("FNUCLSNO","UTBLRFLG3","GRP=:GRP AND RFLG=:RFLG AND UCLS=:X")

	if (%ProcessMode = 0) , 'rs.isEmpty() do {
	
		set %ProcessMode = 1
		set %OSAVE = 1
		}
		
        set KEYL(1) = "RFLG3"
        set KEYL(3) = RFLG

	kill ZNW

	do ALL^UTBLOVR

	if (%ProcessMode = 0) do {
		
		set I(3) = ""
		set ZNW = 1
		}

	// No functions exist for this product group, restriction and userclass
	if rs.isEmpty() quit

	do LDFNCTN

	quit


LDFNCTN	
	/*
	 Load functions that the user class (X) cannot override in the array
	 FUNCTION and array FUNCSAV. FUNCSAV array will be used upon filing
	 the changes, in order to delete the records in UTBLRFLG3 that are
	 being replaced by new function names.
	*/

	type public Number %PAGE, ALP
	type public String FUNCSAV(), FUNCTION(), GRP, RFLG, X
	
	type Number PC, SUB

	set PC = 0
	set SUB = 1

	type ResultSet rs = Db.select("FNUCLSNO","UTBLRFLG3","GRP=:GRP AND RFLG=:RFLG AND UCLS=:X","FNUCLSNO")
	while rs.next() do {
		
		set PC = PC + 1
		
		if (PC > 4) set PC = 1,SUB = SUB + 1
			
		set $P(FUNCTION(SUB),"|",PC) = rs.getCol("FNUCLSNO")
		
		set $P(FUNCSAV(SUB),"|",PC) = rs.getCol("FNUCLSNO")
		}

	if SUB set %PAGE = ('(SUB#ALP=0)) + (SUB\ALP)
	
	quit


VER

	type public String VFMQ

	if (%ProcessMode = 2) ! (%ProcessMode = 4) ! (VFMQ = "Q") do END quit
	
	do FILE
		
	do END
	
	quit


FILE	// File changes

	type public String FUNCSAV(), FUNCTION(), GRP, RFLG, UCLS
	
	type Boolean DONE = 0
	type Number I, SUB
	type String FNORIG, FNUCLSNO

	if (%ProcessMode = 3) do Db.delete("UTBLRFLG3","GRP=:GRP AND RFLG=:RFLG AND UCLS=:UCLS") quit
	
	if (UCLS = "*") do Db.delete("UTBLRFLG3","GRP=:GRP AND RFLG=:RFLG")
	
	set SUB = ""
	for  set SUB = FUNCTION(SUB).order() quit:SUB.isNull()  do {
		
		for I = 1:1:4 set FNUCLSNO = FUNCTION(SUB).piece("|",I)  do { quit:DONE
			
			set FNORIG = FUNCSAV(SUB).get().piece("|",I)

			/*
			  In case there are more entries in FUNCSAV than in
			  FUNCTION, that means some entries have been deleted.
			  In such case we still need to delete the old entry.
			  Only if there are no more entries in both arrays do
			  we need to quit the processing completely.
			*/

			if FNORIG.isNull() , FNUCLSNO.isNull() set DONE = 1 quit

			// Delete entries in UTBLRFLG3 that have been replaced
			if 'FNORIG.isNull() , (FNORIG '= FNUCLSNO) do Db.delete("UTBLRFLG3","GRP=:GRP AND RFLG=:RFLG AND UCLS=:UCLS AND FNUCLSNO=:FNORIG")

			if FNUCLSNO.isNull() quit

			type RecordSCATBL scatbl = Db.getRecord("SCATBL", "FN = :FNUCLSNO", 1)
			type RecordUTBLRFLG3 utblrflg3 = Db.getRecord("UTBLRFLG3", "GRP = :GRP, RFLG = :RFLG, UCLS = :UCLS, FNUCLSNO = :FNUCLSNO", 1)

			// Record already exists and hasn't been changed
			if utblrflg3.getMode() quit

			set utblrflg3.desc = scatbl.desc

			do utblrflg3.save()
			}
		}

	quit


END	//

	type public Number ER, ZNW
	type public String RFLG, RM, UCLS, VFMQ

	if ER ! (%ProcessMode = 2) ! (%ProcessMode = 4) quit
	
	set ZNW = ZNW.get()

	if (VFMQ = "Q") do {

		// User-defined restriction ~p1.  Userclass authorization ~p2 not created.
		if (ZNW = 1) set RM = $$^MSG(2892,RFLG.get(),UCLS.get()) quit

		// User-defined restriction ~p1.  Userclass authorization ~p2 not modified.
		if (%ProcessMode = 1) set RM = $$^MSG(2894,$G(RFLG),$G(UCLS)) quit

		// User-defined restric ion ~p1.  Userclass authorization ~p2 not deleted.
		set RM = $$^MSG(2893,RFLG.get(),UCLS.get())
		}
	else  do {

		// User-defined restriction ~p1.  Userclass authorization ~p2 created.
		if (ZNW) = 1 set RM = $$^MSG(2889,RFLG,UCLS) quit

		// User-defined restriction ~p1.  Userclass authorization ~p2 modified.
		if (%ProcessMode = 1) set RM = $$^MSG(2891,RFLG,UCLS) quit

		// User-defined restriction ~p1.  Userclass authorization ~p2 deleted.
		set RM = $$^MSG(2890,RFLG,UCLS)
		}

	set ER="W"
	
	quit

vSIG()	quit "60479^60360^Pat Kelly^6399"	// Signature - LTD^TIME^USER^SIZE
