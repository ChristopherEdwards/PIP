EFTFEE	//;Private;EFT Fee Plan Creation & Maintenance
	/*

	   Origin: Matt Lessig 6/30/93
	   Description: EFT Fee Plans
	    This routine supports the creation and maintenance of
	    EFT Fee Plans,
	   Inputs:
	         . System        %FN,%LIBS,%UID

	   Returns:
	         . ER    Error indicator         /TYPE=T
	                 Returns ER=1 if an error, otherwise ER="W"

	         . RM    Return message          /TYPE=T
	                 Returns error message if ER=1, otherwise
	                 completion message
	  ---- Revision History -------------------------------------------------

	   08/02/04 - KELLYP - CR 11405
	   	      Cleaned up procedure to conform to PSL standards and 
	   	      corrected logic errors from previous conversion.

	   12/05/03 - CARROLLJ - CR7239
		      Modified to correct parameter passing within procedure.

	   07/29/02 - TELIV - 49451
		      Converting to PSL

	  ----------------------------------------------------------------------

	*/


NEW     //
	do INIT(0) 

	quit

 
UPD     //
	do INIT(1) 

	quit

 
DIS     //
	do INIT(2) 

	quit
 

DEL     //
	do INIT(3) 

	quit
 
 
INIT(%ProcessMode)	//

	type RecordEFTFEE fEFTFEE

	do VPG(.fEFTFEE)

        quit
 
 
VPG(RecordEFTFEE fEFTFEE)     //Page control

	type Public String ER

	type Boolean FINISH
	type Number %PAGE,%PG
	type String CODE,IO,OLNTB,VFMQ

        set %PG=0 
	set %PAGE=1
		
	set FINISH=0
	
	for  do { quit:FINISH

		if %PG=0 do VPG00 if (ER!("Q"[VFMQ)) set FINISH=1 quit
		if %PG>0 do VPG01(.fEFTFEE) if ER set FINISH=1 quit
	
		// process completed/aborted
		if "DFQ"[VFMQ do VER(.fEFTFEE) set FINISH=1 quit
		set %PG=%PG+1
		}

	quit
 
 
VPG00	//Set up

	type Public String IO,VFMQ

        type String %NOPRMT,%READ,%TAB()

        if %ProcessMode=0 set %TAB("CODE")="[EFTFEE]CODE/XPP=D NOSPC^EFTFEE"
        else  set %TAB("CODE")="[EFTFEE]CODE/TBL=[EFTFEE]"

        set %TAB("IO")=$$IO^SCATAB($I)
 
        set %READ="@@%FN,,,CODE/REQ" 
	set %NOPRMT="N"

        if %ProcessMode=2 set %READ=%READ_",IO#1"
        do ^UTLREAD 
	
	if "Q"[VFMQ quit
 
        if %ProcessMode=2,IO'=$I do OPEN^SCAIO

        quit


NOSPC   // No spaces permitted in code

	type Public String ER,RM,X

        // Record already exists
        if Db.isDefined("EFTFEE","CODE=:X") do Runtime.setErrMSG("EFTFEE",2327) quit

	// No spaces allowed
        if X[" " do Runtime.setErrMSG("EFTFEE",7703) quit

        quit
 
 
VPG01(RecordEFTFEE fEFTFEE)   //Flow control

	type Public String CODE
        
        set fEFTFEE=Db.getRecord("EFTFEE","CODE=:CODE",1)

	do DRV^USID(%ProcessMode,"EFTFEE",.fEFTFEE)

        quit
 

VER(RecordEFTFEE fEFTFEE)     //

	type Public String VFMQ

	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit

        do FILE(.fEFTFEE)

	do END

        quit
 

FILE(RecordEFTFEE fEFTFEE)    // File data

	type Public String CODE

        if %ProcessMode=3 do Db.delete("EFTFEE","CODE=:CODE")
	else  do fEFTFEE.bypassSave()

        quit
 

END     //

	type Public String ER,RM,VFMQ

	if (%ProcessMode=2)!(%ProcessMode=4) quit
 
        if VFMQ="Q" do {

                // EFT Fee Plan not created
                if %ProcessMode=0 set RM=$$^MSG(7700) quit

                // EFT Fee Plan not modified
                if %ProcessMode=1 set RM=$$^MSG(7702) quit

                // EFT Fee Plan not deleted
                set RM=$$^MSG(7701)
                }
        else  do {

                // EFT Fee Plan created
                if %ProcessMode=0 set RM=$$^MSG(7697) quit    

                // EFT Fee Plan modified
                if %ProcessMode=1 set RM=$$^MSG(7699) quit    

                // EFT Fee Plan deleted
                set RM=$$^MSG(7698)                   
                }

        set ER="W"

        quit
 
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43487^Sanchez SCM Administrator^3534"	// Signature - LTD^TIME^USER^SIZE
