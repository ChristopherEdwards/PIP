IRSC99Q		/*
	ORIG: titove - 11/03/2005
	DESC: IRS Processing - Form 1099-Q Correction

	---- Comments --------------------------------------------------------
			  THIS PROCEDURE IS TO BE COMPILED
			      DO NOT RUN STANDALONE

	I18N=QUIT: Excluded from I18N standards.
	
	---- Revision History ------------------------------------------------
	
	05/15/06 - DESHPANDE S K - CR 21167
		   Modified BRECBLD section to set address variables to mailing 
		   address instread of permanent address.
	
	05/12/06 - TITOVE - CR 21250
		   Modified BRECBLD to reset AMT(1) to zero for next run.

	05/01/06 - TITOVE - CR 21024
		   Modified BRECBLD section to append RPASEQ when building
		   a value for CID, instead of IRATYP. Also added code
		   to ensure we have pieces in IRA() arrays that directly
		   correspond to columns in IRA table.

	11/03/05 - TITOVE - CR 17287
		   Converted into PSL. Removed CALCAMT and RPASEQ tags and 
		   pre-2005 revision history.
		   
	08/12/05 - SPR - 16770
		   Modified CALCAMT section to update the distribution code 
		   list in the "for" loop to F I=1,5,6,7,18,19,39.

	*/
	
        // Accept "Dead code" warning during runtime of @IRSTAPE
        #ACCEPT Date=10/26/2005;PGM=Eugene Titov;CR=17287
        quit
	
	
MTRCNTRL	// Master control

	type public Number ER
	
	type Number PASSFLG

	do Db.delete("B1099Q")

	set PASSFLG = 1
		
	do ARECBLD quit:ER	// Build and write the "A" record - First pass
	do BRECBLD quit:ER	// Build and write the "B" records - First pass
	do CRECBLD quit:ER	// Build and write the "C" record - First pass
	
	set PASSFLG = 2
		
	do ARECBLD quit:ER	// Build and write the "A" record - Second pass
	do BRECBLD quit:ER	// Build and write the "B" records - Second pass
	do CRECBLD quit:ER	// Build and write the "C" record - Second pass
		
	do STORETOT		// Store the TTR report totals
		
	quit
	

ARECBLD	// "A" record builder

	type public String TAMT()

	type Number AMTIND
	type String FORMCODE

	set FORMCODE = "Q"	// IRS form code
	
	set AMTIND = 123	// IRS amount type indicator
	
	set TAMT(1).piece("|",2) = "Gross distribution"
	set TAMT(2).piece("|",2) = "Earnings"
	set TAMT(3).piece("|",2) = "Basis"
	
	do ARECWRT		// Write the record to tape
	
	quit


BRECBLD	// "B" record builder

	type public Number AMT(), BRAMT(), CUTOFF, IRATYP, PASSFLG, YEAR
	type public String FORM, FORMS()

	type Date JD
	type Number ACN, CID, I, RPASEQ, ZACN
	type String ADDR, CITY, CNTRY, CORRTN, DOCCODE, FORCNTY, FULLADDR, IRA(), LNAME
	type String NAME, NONBNDIS, SPECIAL, STATE, TIN, TINTYPE, TTROLL, VAR, ZIP

	set JD=FORMS(FORM)-1
	
	type DbSet ds=Db.selectDbSet("IRSUPD","TJD>:JD AND FTYPE=17")

	while ds.next() do {
	
		type RecordIRSUPD irsupd=ds.getRecord("IRSUPD")
		
		set ZACN=irsupd.miscseq
		set RPASEQ=irsupd.bseq
	
		// Skip duplicates when writting to tape
		if (irsupd.ccode=1),(PASSFLG=1) quit
		// Skip "New Entry" for first pass
		if (irsupd.ccode=4),(PASSFLG=1) quit
		// Skip "change amount" for second pass
		if (irsupd.ccode=3),(PASSFLG=2) quit

		// Original/New CIF number
		set ACN=$select(PASSFLG=1:irsupd.ocif,PASSFLG=2:ZACN)
		
		set AMT(1) = 0

		type RecordIRA ira=Db.getRecord("IRA","ACN=:ACN,RPASEQ=:RPASEQ,TAXYR=:YEAR", 1)

		// Initialize IRA(0) up to a total number of IRA columns
		for I = 1:1:99 set IRA(0).piece("|",I) = ""
		
		for I = 1:1:10,12:1:29,34:1:39,43:1:89,94:1:99 do {
			
			set VAR = "D"_I
			set IRA(0).piece("|",I) = ira.@VAR
			}		
		
		type RecordIRATYPE iratype = Db.getRecord("IRATYPE", "ACN=:ACN,RPASEQ=:RPASEQ")
	
		set IRATYP = iratype.iratyp		
		set NONBNDIS = iratype.nonbndis
		set TTROLL = $select(IRA(0).piece("|",2):"1",1:" ")
		
		type RecordCIF cif = Db.getRecord("CIF", "ACN = :ACN")
		
		set CID=ACN_"-"_RPASEQ		// Account number (sequence number)
		
		set TIN=$select(PASSFLG=1:irsupd.otin,PASSFLG=2:cif.taxid) // Tax ID Number

		set TINTYPE=$select(TIN?2N1"-"7N:1,TIN?3N1"-"2N1"-"4N:2,1:" ") // Type of TIN

		set CORRTN=$select(PASSFLG=1:"G",PASSFLG=2:"C")
		
		// Payee First and Last Name
		set NAME=$select(PASSFLG=1:irsupd.oname,PASSFLG=2:cif.nam)
		set LNAME=$select(PASSFLG=1:irsupd.olnam,PASSFLG=2:cif.lnm)

		set FULLADDR = cif.mad1_"|"_cif.mad2_"|"_cif.mad3_"|"_cif.mcity_"|"_cif.mstate_"|"_cif.mzip_"|"_cif.mcntry
		set ADDR = FULLADDR.piece("|",1)_" "_FULLADDR.piece("|",2)_" "_FULLADDR.piece("|",3)
		set CITY = FULLADDR.piece("|",4)	// Payee City
		set STATE = FULLADDR.piece("|",5)	// Payee State
		set ZIP = FULLADDR.piece("|",6).piece("-",1)_FULLADDR.piece("|",6).piece("-",2)  // Payee
		set CNTRY = FULLADDR.piece("|",7)	// Foreign country
		set FORCNTY = $select(FULLADDR.piece("|",7)="US":" ",1:1) // Foreign country indicator	

		// Zero amount fields (Counting Records for T-Record)
		if (PASSFLG=1),(irsupd.ccode=2) do SPECIAL,BRECWRT,B1099Q(ACN) quit

		for I = 1,5,6,7,18,19,39 set AMT(1) = AMT(1) + (IRA(0).piece("|",I) * 100)
		
		if AMT(1),(AMT(1) '< (CUTOFF * 100)) do {
		
			if 'NONBNDIS set NONBNDIS = " "
			
			do SPECIAL,B1099Q(ACN)
			
			if (irsupd.ccode '= 1) do BRECWRT
			}

		// Report Trustee Transfer as it's own record
		if 'IRA(0).piece("|",2) quit
		
		set AMT(1) = IRA(0).piece("|",2) * 100
		
		if AMT(1),(AMT(1) '< (CUTOFF * 100)) do {
		
			set NONBNDIS = " "
			
			do SPECIAL,B1099Q(ACN)
			
			if (irsupd.ccode '= 1) do BRECWRT
			}
		}

	quit
	
	
SPECIAL	// Build Special Data

	type public String NONBNDIS, SPECIAL, TTROLL
	
	// Special date position 544-750
	//; Blank |544|546
	set SPECIAL = "   "
	//; Trustee to Trustee Transfer Indicator |547
	set SPECIAL = SPECIAL_TTROLL
	//; Type of Tuition Payment |548
	set SPECIAL = SPECIAL_3
	//; Designated Beneficiary |549
	set SPECIAL = SPECIAL_NONBNDIS
	//; Blank|550|750
	set SPECIAL = SPECIAL_"".justify(201)

	quit


B1099Q(ACN)	// Insert B Records into B1099Q File

	type public Date JD
	type public Number AMT(), BRAMT(), FAMT(), FCNT, TINTYPE
	type public String CID, CITY, FULLADDR, LNAME, NAME, NONBNDIS, STATE, TIN, TTROLL

	type Number BSEQ
	type String NAMCON, X

	if (TINTYPE = 2) set X = LNAME
	else  set X = NAME
	
	do NAMCNTRL^IRSTPMTR
	
	set NAMCON = X
	
	set BSEQ = Db.nextVal("B1099Q","PDATE=:%SystemDate,BACN=:ACN")
		
	type RecordB1099Q b1099q = Db.getRecord("B1099Q","PDATE=:JD,BACN=:ACN,BSEQ=:BSEQ", 1)
	
	set b1099q.bnamcon = NAMCON
	set b1099q.btintype = TINTYPE
	set b1099q.btin = TIN.piece("|",1).translate(" ","-")
	set b1099q.bcid = CID
	set b1099q.bamt1 = AMT(1)/100
	set b1099q.bamt2 = AMT(2)/100
	set b1099q.bamt3 = AMT(3)/100
	set b1099q.bname = NAME
	set b1099q.baddr1 = FULLADDR.piece("|",1)
	set b1099q.baddr2 = FULLADDR.piece("|",2)
	set b1099q.baddr3 = FULLADDR.piece("|",3)
	set b1099q.bcity = CITY
	set b1099q.bstate = STATE
	set b1099q.bzip = FULLADDR.piece("|",6)
	set b1099q.bttrol = TTROLL
	set b1099q.bttpmt = 3
	set b1099q.bdben = NONBNDIS
	
	do b1099q.save()

	quit
	

CRECBLD	// "C" record builder

	do CRECWRT	// Write the record to tape
	
	quit
	
	
%STOPLOD	// Stop %ZRTNLOD from this point on down
	quit
TRECWRT		// Dummy line reference for GT.M
	quit
ARECWRT		// Dummy line reference for GT.M
	quit
BRECWRT		// Dummy line reference for GT.M
	quit
CRECWRT		// Dummy line reference for GT.M
	quit
STORETOT	// Dummy line reference for GT.M
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60403^47725^Shriram Deshpande^6956"	// Signature - LTD^TIME^USER^SIZE
