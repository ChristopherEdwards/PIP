TRNINI	//Public; Release Transaction Code Table
	/*
	   ORIG: ZENGF - 02/23/95

	   KEYWORDS: Transaction Code

	   Examples:
	         D BUILD^TRNINI(opt)     to copy the transaction codes in this
	                                 routine into the global ^TRN

	---- Revision History -------------------------------------------------

	   10/14/05 - KELLYP - CR 17816
	   	      Modified FILE section to call trn.setMode(0) to force
	   	      the processing mode to 0 (Create).  This prevents an 
	   	      issue when rebuilding lower-case transaction codes.

	   01/07/05 - KELLYP - CR 13667
	   	      Added a new section, FILE, which the TRNINI* procedures
	   	      will call to delete then recreate each transaction code.
	   	      Also removed code from BUILD1 that deleted the lower-case
	   	      transaction codes since the FILE section now deletes each
	   	      transaction code prior to recreating it.  Also removed 
	   	      pre-2002 revision history.

	   11/11/02 - Erik Scheetz - 43583
		      Converted routine to PSL DQ Procedure.  Removed CONVERT
		      linetag (and associated sections) as this functionality
		      is no longer used.
	*/

	// I18N=QUIT

	quit

public BUILD(Number case)	//Public; Entry point to build TRN table

	/*
	   ARGUMENT:
	         . case          case of the tran codes to be build
	                         		/TYP=N/MECH=VAL/NOREQ/DFT="0"
	                 0               lower case
	                 1               upper case and lower case

		Creates the transaction table (TRN) by calling 
		other procedures TRNINI0, TRNINI1, TRNINI1A, TRNINI2, 
		TRNINI2A, TRNINI3, TRNINI4 and TRNINI5.

		The transaction codes are converted to lower case
		to avoid possible conflict with the existing customary
		transaction codes, if parameter 'case' is not present or the
		parameter value is 0. If the parameter passed is non-zero
		then the converted codes are in upper case AND lower case.

	*/
	set case=+case.get()
	do BUILD1
	do BUILD2
	quit


BUILD1	// Entry point to build global ^TRN from the codes in text

	// The initial of trancode is equal to "@" to "A"
	do BUILD1^TRNINI0

	// The initial of trancode is equal to "C" to "CL"
	do BUILD1^TRNINI1

	// The initial of trancode is equal to "CM"
	do BUILD1^TRNINI1A

	// The initial of trancode is equal to "D"
	do BUILD1^TRNINI2

	// The initial of trancode is equal to "E" to "H"
	do BUILD1^TRNINI2A

	// The initial of trancode is equal to "I" to "L"
	do BUILD1^TRNINI3

	// The initial of trancode is equal to "M" to "R"
	do BUILD1^TRNINI4

	// The initial of trancode is equal to "S" to "W"
	do BUILD1^TRNINI5

	quit


BUILD2	// Covert old PCF column to corresponding PCFD/PCFL culumns 

	type Number POS
	type String VAL,COL
	type DbSet ds=Db.selectDbSet("TRN")

	if ds.isEmpty() quit
	while ds.next() do {

		type RecordTRN trn=ds.getRecord()
		
		for POS=1:1:trn.pcf.length() do {
			set VAL=trn.pcf.extract(POS)
			if trn.cls="D" do { quit
				set COL="PCFD"_POS
				set trn.@COL=VAL
				}

			if trn.cls="L" do {
				set COL="PCFL"_POS
				set trn.@COL=VAL
				}
			}

		do trn.bypassSave()
		}
	quit
	
	
public FILE(RecordTRN trn)	// Delete old TRN record then add the new one

	/*
	   ARGUMENT:
	         . trn          Transaction code to be recreated
	         		/TYP=RecordTRN/MECH=REF/REQ

		This section recreates the TRN table record for the
		transaction code object passed in by saving any associated
		TRNAUT and TRNSD records, deleting the old transaction code,
		recreating the transaction code from the object passed in, 
		then rebuilding the associated TRNAUT and TRNSD records.

		In a GT.m environment, TRNAUT and TRNSD records will be lost
		when the TRN record is deleted because TRNAUT and TRNSD records
		are saved on sub-nodes of nodes "2" and "3" which TRN uses
		to store other information.  As a result, when nodes 2 and 3 are 
		killed for the primary TRN record deletion, the TRNAUT and TRNSD
		records are also deleted.  It is therefore necessary to cache
		the transaction code's associated TRNAUT and TRNSD records prior
		to the TRN record deletion, and then to restore these records
		after the transaction code has been recreated.
	*/

	type RecordTRNAUT trna()	// Array used to cache TRNAUT records
	type RecordTRNSD trns()		// Array used to cache TRNSD records
	type Number bound,ctr

	set ctr=0

	// Save off any TRNAUT records
	type DbSet trnaut=Db.selectDbSet("TRNAUT","ETC=:trn.etc")
	while trnaut.next() set ctr=ctr+1,trna(ctr)=trnaut.getRecord()
	
	set ctr=0
	
	// Save off any TRNSD records
	type DbSet trnsd=Db.selectDbSet("TRNSD","ETC=:trn.etc")
	while trnsd.next() set ctr=ctr+1,trns(ctr)=trnsd.getRecord()

	// Delete the old TRN record
	if Db.isDefined("TRN","ETC=:trn.etc") do Db.delete("TRN","ETC=:trn.etc")

	// Add the new TRN record
	do trn.setMode(0)
	do trn.bypassSave()

	set bound=trna("").order(-1)

	// Restore the TRNAUT records
	if bound>0 for ctr=1:1:bound do {
		if 'Db.isDefined("TRNAUT","ETC=:trna(ctr).etc,UCLS=:trna(ctr).ucls") do trna(ctr).setMode(0)
		do trna(ctr).bypassSave()
		}
	
	set bound=trns("").order(-1)
	
	// Restore the TRNSD records
	if bound>0 for ctr=1:1:bound do {
		if 'Db.isDefined("TRNSD","ETC=:trns(ctr).etc,SEQ=:trns(ctr).seq") do trns(ctr).setMode(0)
		do trns(ctr).bypassSave()
		}
	
	quit
	

vSIG()	quit "60187^43657^Pat Kelly^5076"	// Signature - LTD^TIME^USER^SIZE
