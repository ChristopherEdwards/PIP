DEALCNFM //;Private;Deal Confirmation Routine

	/*
	   ---- Comments --------------------------------------------------------
	   Origin:       Matt Lessig 04/01/93
	   Description:  Deal Confirmation Procedure

	                 This procedure supports the confirmation of a single
	                 foreign exchange deal record.  Confirmation is the
	    		 external verification of a deal's information.	    		 
	   Inputs:
	         . System        %CRCD,%FN,%LIBS,%UID
	
	   Returns:
	         . ER    Error indicator         /TYPE=T
	                 Returns ER=1 if an error, otherwise ER="W"
	
	         . RM    Return message          /TYPE=T
	                 Returns error message if ER=1, otherwise
	                 completion message
	
	  ---- Revision History -------------------------------------------------
	  
	   03/21/06 - RussellDS - CR20234
	  	      Fixed query syntax for table lookups.

	   01/27/06 - KinI - CR 16664
	   	      DBI related clean-up. Modified VPG section to quit without
	   	      errors if nothing is selected from the prompts and added 
	   	      VPG0 section. Modified VPG01 section to send SID to DRV^USID
	   	      by reference in order to retain its value for further 
	   	      comparison.

	   12/26/02 - TELIV - ARQ 49451	
		      Converted to PSL

	  ---------------------------------------------------------------------------	
	*/

	do VPG
	
	quit
	
VPG
	// Page control
		
	type Public String ER
	
	type Number %PAGE, %PG, pg
	type Boolean FINISH=0
	type String QRY, OLNTB, VFMQ
	
	set %ProcessMode=2
	
	set %PG=0
	set pg=0

	set QRY="([DEAL1]STATUS=1 OR [DEAL1]STATUS=2 OR [DEAL1]STATUS=4 OR [DEAL1]STATUS=6 OR [DEAL1]STATUS=9) AND [DEAL1]CNFRM=0"

	type RecordDEALMSG2 DEALMSG2
	type RecordDEALMSG3 DEALMSG3
	type RecordDEAL1 fDEAL1

	for  do { quit:FINISH
		if %PG=0 do VPG00(.DEALMSG2,.DEALMSG3,.fDEAL1,.VFMQ) do VPG0(.FINISH,.%PG,.pg,VFMQ,.DEALMSG2,.DEALMSG3,.fDEAL1) if FINISH=1 quit

		if %PG>0 do VPG01(.DEALMSG2,.DEALMSG3,.fDEAL1,.VFMQ) do VPG0(.FINISH,.%PG,.pg,VFMQ,.DEALMSG2,.DEALMSG3,.fDEAL1) if FINISH=1 quit
		}

	quit

	
VPG0(Boolean FINISH,		// Completion flag
     Number %PG,		// Page Number system variable
     Number pg,			// Page Number local variable
     String VFMQ,		// User Action Indicator
     RecordDEALMSG2 DEALMSG2,	// Deal Message File 2 record
     RecordDEALMSG3 DEALMSG3,	// Deal Message File 3 record
     RecordDEAL1 fDEAL1)	// DEAL1 record
     
	// Process completed/aborted
	if "DFQAR"[VFMQ do VER(.DEALMSG2,.DEALMSG3,.fDEAL1,.VFMQ) set FINISH=1 quit

	set %PG=pg
	set %PG=%PG+1
	set pg=%PG
		
	quit
	
		
VPG00(RecordDEALMSG2 DEALMSG2,	// Deal Message File 2 record
      RecordDEALMSG3 DEALMSG3,	// Deal Message File 3 record
      RecordDEAL1 fDEAL1,	// DEAL1 record
      String VFMQ)		// Process Indicator
      
	type public Boolean ER
	type public String NOEXT, NOINT, QRY
	
	type String IO, %NOPRMT, %READ, %TAB
	
	set (NOEXT,NOINT)=""

	// User can fill any of 3 prompts to get to the Deal Conf. screen
	set %TAB("NOINT")="[DEAL1]NOINT/TBL=""[DEAL1]NOINT,NOEXT,AMTB,CRCDB,AMTS,CRCDS,STATUS,TYPE/LEN=1:QU """""_QRY_"""""""/XPP=D NOPP^DEALCNFM(.fDEAL1,X)"
	set %TAB("NOEXT")="[DEAL1]NOEXT/TBL=""[DEALMEMO]NOEXT,NOINT,[DEAL1]AMTB,[DEAL1]CRCDB,[DEAL1]AMTS,[DEAL1]CRCDS,[DEAL1]STATUS,[DEAL1]TYPE/LEN=1:QU """""_QRY_"""""""/XPP=D MEMOPP^DEAL(X,NOINT,.fDEAL1)"
	set %TAB("TRREFNO")="[DEAL1]TRREFNO/TBL=""[DEALREFNO]TRREFNO,NOINT,[DEAL1]AMTB,[DEAL1]CRCDB,[DEAL1]AMTS,[DEAL1]CRCDS,[DEAL1]STATUS,[DEAL1]TYPE/LEN=1:QU """""_QRY_"""""""/XPP=D REFPP^DEALCNFM(X,NOINT,NOEXT)"
	set %TAB("IO")=$$IO^SCATAB($I)

	set %READ="@@%FN,,,NOINT#0,NOEXT#0,TRREFNO#0" 
	set %NOPRMT="N"
	
	do ^UTLREAD 

	if "Q"[VFMQ quit
	
	if 'fDEAL1.exists() set fDEAL1=Db.getRecord("DEAL1","NOINT=:NOINT")
	set DEALMSG2=Db.getRecord("DEALMSG2","NOINT=:NOINT,MSG=5")
	set DEALMSG3=Db.getRecord("DEALMSG3","NOINT=:NOINT,MSG=6")

	if %ProcessMode=2,IO'=$I do OPEN^SCAIO

	quit 
	
	
VPG01(RecordDEALMSG2 DEALMSG2,	// Deal Message File 2 record
      RecordDEALMSG3 DEALMSG3,	// Deal Message File 3 record
      RecordDEAL1 fDEAL1,    	// DEAL1 record
      String VFMQ)		// Process Indicator
      	
	type public Boolean ER
	type public String NOINT, DEAL1()
	type public String SID 

	// Select Purchase/Sale screen
	set SID=$S(fDEAL1.dealps="S":"DEAL1SIS",1:"DEAL1SIP")
	
	// Record locked by another user
	lock +DEAL1(NOINT):2 else  set VFMQ="Q" do Runtime.setErrMSG("DEAL1",2333) quit:ER     

	do DRV^USID(%ProcessMode,.SID,.DEALMSG2,.DEALMSG3,.fDEAL1)
	
	quit 
	
	
NOPP(RecordDEAL1 fDEAL1,	// Deal Record	
     String X)			// Contract Number
     
	// Contract Number Post-Processor
	
	quit:X="" 

	type public Boolean ER 
	
	set fDEAL1=Db.getRecord("DEAL1","NOINT=:X",1)
	
	// Invalid contract number
	if 'fDEAL1.getMode() do Runtime.setErrMSG("DEAL1",7607) quit:ER	

	// Entering Contract number is sufficient, no other prompts needed.
	do GOTO^DBSMACRO("END")

	quit 
	
	
REFPP(Number X,		// Transaction Reference Number	
      String NOINT,	// Contract Number
      String NOEXT)	// Trader's Memo
      
	// Transaction Reference Number Post-Processor

	type public Boolean ER

	// Must enter contract number
	if (X="") & (NOINT="") & (NOEXT="") do Runtime.setErrMSG("DEAL1",7603) quit:ER  

	// Must enter transaction reference number
	if (X="") do Runtime.setErrMSG("DEAL1",7604) quit:ER    

	if NOINT=""  do {
		type RecordDEALREFNO dlrfno=Db.getRecord("DEALREFNO",X)
		set NOINT=dlrfno.noint
		}

	// Invalid transaction reference number
	if NOINT="" do Runtime.setErrMSG("DEAL1",7553) quit:ER        

	do CHANGE^DBSMACRO("TBL","")

	quit 
	

UPDATE(RecordDEAL1 fDEAL1)	// DEAL1 record
	
	// Sets "Confirmed" to "Yes" and "Confirmed By" to the current user id.
	
	set fDEAL1.cnfrm=1
	set fDEAL1.cnfrmby=%UserID

	do fDEAL1.save()

	quit 
	
		
VER(RecordDEALMSG2 DEALMSG2,	// Deal Message File 2 record
    RecordDEALMSG3 DEALMSG3, 	// Deal Message File 3 record
    RecordDEAL1 fDEAL1,		// DEAL1 record
    String VFMQ)		// Process Indicator
    
	type public Boolean ER 
	type public String SID
	
	if ER.get() do END quit 
	
	// User must view the last screen of linkage prior to confirmation
	if SID.get()'="DEAL1SI7" do END quit 
	
	// Display a bar menu at the bottom of a screen
	
	// "Confirmed  Unconfirmed  Quit"
	set VFMQ=$$^DBSMBAR(143)  

	if VFMQ=1 do UPDATE(.fDEAL1)  // Confirmed
	
	if (VFMQ=2) ! (VFMQ="") set VFMQ="Q" quit  // Unconfirmed or Quit
	
	quit 
	
	
END     // End of processing

	type public Boolean ER 
	type public String NOINT, DEAL1()
	
	if NOINT.exists() lock -DEAL1(NOINT)

	if ER.get()!(%ProcessMode=2)!(%ProcessMode=4) quit

	set ER="W"

	quit
	
	 
 #OPTION ResultClass ON
Public String vSIG()	quit "60367^44235^Laura Hillanbrand^6377"	// Signature - LTD^TIME^USER^SIZE
