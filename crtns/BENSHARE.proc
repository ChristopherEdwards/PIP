BENSHARE	// Beneficiary Fair Market Value
	/*

	       ORIG:  HILLANBRAND -  9 APR 1993
	       DESC:  Searches IRA global and checks each CIF to see if date
	        of death is within the current year.

	  ----- Revision History -----------------------------------------------

	   12/01/05 - DHANALAKSHMI R - 16851
		      Modified by replacing the variable FMVFLG by FMVELG.

	   09/09/05 - DHANALAKSHMI R - 16851
		      Corrected Warnings - DBI3 clean up.

	   01/29/03 - Spier - 51423
		      Fixed ACN section, compiler reported error.
		      BENSEQ was set to getcol(3) even though only
		      2 columns were retrieved. 
	
	   04/08/02 - GORDONT - 49794
		      Converted to PSL.
		      Removed Pre-2000 revision history.	
     
	   11/15/01 - YENDAPALLIS - 48277
	              Modified the code in DOD section to avoid
	              undefined error BEN(CNT)by removing the counter for
	              counting number of beneficiaries and also changed code
	              to use beneficiary sequence number (BENSEQ) instead of
	              CNT in the calculation of the beneficiary fair market
	              value.

	   09/21/00 - REEDD - 41824
	              Modified the DOD section to check the Fair Market
	              Eligibility flag.  If this flag is not set for a
	              beneficiary then do not calculate a percentage for
	              that beneficiary.

	   09/15/00 - KINI - 36760
	              Modified to check if the DOD is in the current Tax year
	              defined in CUVAR.

	   03/14/00 - TANY - 37587
	              Modified SCADAT calls by passing an additional
	              parameter to the calls to improve performance.
	              The parameter is a "1" which will return a known
	              julian date.

	   03/10/00 - HALPINJ - 37342
	              Fixed insert SQL statment in FILE section.  Also newed the
	              BEN array.  This array was causing bad data to be saved
	              into the IRABEN global.

	*/

	type Number ACN,BEN,BENFAIR,BENPCT,BENSEQ,DODYR,FMV,IACR1
	type Date BOTY,EOTY,DOD
	type Boolean FMVELG
	type Number IBAL,items,NUM,RES,RPASEQ,TAXYR,TOT

	// beginning of tax year
	set BOTY=$$BOTY^SCADAT($$BOTY^SCADAT(%SystemDate,1)-1,1)

	// end of tax year
	set EOTY=$$EOTY^SCADAT(BOTY,1)
	set TAXYR=$$YEAR^SCADAT(EOTY,1)

	type ResultSet rs=Db.select("DISTINCT ACN,RPASEQ","IRABEN")
	if rs.isEmpty() quit

	while rs.next() do {
		set ACN=rs.getCol(1)
		set RPASEQ=rs.getCol(2)
	
        	type RecordCIF cif=Db.getRecord("CIF","ACN=:ACN")

		// Date of Death
		set DOD=cif.dod 
	
		if 'DOD quit
	
		set DODYR=$$YEAR^SCADAT(DOD,1)

		if DODYR>TAXYR quit			 

		/* 
		Get IBAL from D ^IRAYEBAL and divide by the number of 
		beneficiaries.
		*/
		set (IBAL,IACR1,BENFAIR)="" do ^IRAYEBAL
		if 'FMV quit

		type ResultSet rsb=Db.select("BENSEQ,BENPCT,FMVELG","IRABEN","ACN=:ACN and RPASEQ=:RPASEQ")
		if rsb.isEmpty() quit

        	while rsb.next() do {
			set BENSEQ=rsb.getCol("BENSEQ")
			set BENPCT=rsb.getCol("BENPCT")
			set FMVELG=rsb.getCol("FMVELG")
			if (BENPCT="")!(BENPCT=0) quit
			if 'FMVELG quit
			set BENPCT=BENPCT/100
			set BENFAIR=$$^SCARND(FMV*BENPCT)
			set BEN(BENSEQ)=BENFAIR
			}

		}
	
	if '$D(BEN) quit

	set TOT=0 
	set NUM=""

	for  set NUM=$O(BEN(NUM)) quit:NUM=""  set TOT=TOT+BEN(NUM)
	
	set RES=FMV-TOT
	
	set BENSEQ=$O(BEN(""),-1)

	set BEN(BENSEQ)=BEN(BENSEQ)+RES set RES=0

	do FILE

	quit

		
FILE	// Insert new record if none, else update existing record

	type public Number ACN,BEN(),RPASEQ,TAXYR
	type Number BENSEQ

	set BENSEQ=""
	for  set BENSEQ=$O(BEN(BENSEQ)) quit:BENSEQ=""  do {
		type RecordIRABEN1 iraben1=Db.getRecord("IRABEN1","ACN=:ACN,RPASEQ=:RPASEQ,BENSEQ=:BENSEQ,CYR=:TAXYR",1)
		set iraben1.benfair=BEN(BENSEQ)
		do iraben1.bypassSave()
		}
	quit

vSIG()	quit "60236^6822^Dhanalakshmi R^3616"	// Signature - LTD^TIME^USER^SIZE
