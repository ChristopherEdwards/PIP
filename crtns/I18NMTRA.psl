public I18NMTRA //Manual TSet Translation
  /*
	ORIG: kumarb - 10/21/2005
	DESC: Manual TSet Translation

	I18N=QUIT	Does not need to correspond to I18N standards
	 DESC:		This program can be used by the translator to translate
			phrases of a selected Tset. The program startS with     
			prompting the translator for a valid Tset from a
	                UTLREAD screen.
			
			The program then calls the utility GETKEY to 
			get the pointers into the global containing the 
			extracted phrases for this Tset. The navigating 
			within the Tset is based upon standard increments
			or decrements of the %PG variable.
	 
			This program also calls the utility program BLDMTB
			to create the screen content of the I18NMTRA screen.
	 
			This screen is dealing with one normalized phrase.
			It is possible to invoke the MLD Utility for 
			translating/retranslating phrases on the screen,
			viewing the context of appropriate detail phrases and
			navigating between the phrases of the selected Tset.
	 
			When exiting from the screen this program updates
			variables for this translation session, writes out
			the translated normalized and/or detail phrases and
			updates the Tset counters. Locking is used to enforce
			data integrity.
	 
			Based upon translators last keystroke and the value
			of %PG the correct phrase of the Tset is processed.
			
	 CALLED BY:	Menusystem. No other routines.
	 CALL TO:      $$DI^SCATAB
			UTLREAD
			GETKEY^I18NUTL
			BLDMTB^I18NUTL
		 	I18NMTRA Screen program
	 GLOBALS READ:
			I18NTS		- Tsets
	 GLOBALS SET:
			I18NTS		- Tsets
			I18NEXT		- Header level for normalized phrases
					  Detail level for detail phrases
	 ARGUMENTS:
	 INPUTS:
	
	 RETURNS:
	 EXAMPLE:
	 LIBRARY:
	
	---Revision History-------------------------------------------

	03/14/07 - KumarSS - CR 25177
	   	   Removed Invalid Unicode Characters.

	10/21/05 - KUMARB - CR 16921
        	   Converted to PSL.
	
	--------------------------------------------------------------
	*/
	
	do NEW
	
	quit
	
NEW	// Begin Newing

	type public Number %i18nmtra
	
	type Number %PAGE,%PG,DPCNT,DPMT,HPAT,HPMT,NDPMT,NHPMT,TSID
	type String %NOPROMPT,%READ,%TAB(),HDG,HP,HR,OLNT,OP,VFMQ
	
	set %NOPROMPT="C"
	
	set HDG=$$^MSG(5898)
	set HDG=$J("",40-(HDG.length()/2))_HDG
	
	// I18N system variable indicating
	set %i18nmtra=1
	
	set (HPAT,HPMT,DPMT,NDPMT,NHPMT)=0
	
	set %TAB("TSID")=$$DI^SCATAB("[I18NTS2]TSID","Tset ID",12,"[I18NTS2]","","","T")
	
	set %READ="@HDG,,TSID/REQ"
	set %NOPROMPT="C"
	do ^UTLREAD if "Q"[VFMQ quit
	
	do INIT
	
	quit
	
INIT	// Initialize internal variables
	
	type public Number %PAGE,%PG,TSID
	
	type Date CREDT
	type Number %OPG,EFFWORK,T1,WORD
	type String FMTWORK,UID,VFMQ
	
	// Read Tset Header Record
	type RecordI18NTS2 i18nts2=Db.getRecord("I18NTS2","TSID=:TSID",1)
	set CREDT=i18nts2.credt
	set UID=i18nts2.uid
	set EFFWORK=i18nts2.effwork
	// formatting seconds
	set FMTWORK=$$TIME^I18NUTL(EFFWORK)
	set T1=%CurrentTime
	set %PG=i18nts2.lstphr
	set %PAGE=i18nts2.nonorm
	set WORD=(i18nts2.selptype=10)
	
	// Tset ~p1 already in use
	lock +I18NTS2(TSID):1 else  do Runtime.setErrMSG("I18NTS2",5438,TSID) quit
	
	do Main
	
	quit
	
Main	// Main Loop
	
	type public Number %i18nmtra,NDPMT,NHPMT,TSID
	type public String ER,RM,VFMQ
	
	set VFMQ=""
	// Do the screen until Q or F is hit
	for  do Screen quit:("QF"[VFMQ)
	
	// Unlock TSET
	lock -I18NTS2(TSID)
	
	// Create return message(RM) and quit program
	set ER="W"
	// turn flag off
	set %i18nmtra=0
	// Tset ~p1 Manual Translation: ~p2 normalized, ~p3 detail phrases
	if (RM.get()).isNull() set RM=$$^MSG(5460,TSID.get(),NHPMT,NDPMT)
	
	quit
	
Screen // Displays and updates the TSET for each normalized phrase
	
	type public Number %OPG,%PAGE,%PG,NDPMT,NHPMT,TSID
	type public String RM,VFMQ
	
	type String EXTDT,NP,SID,SRC
	
	if %PG<%PAGE set %PG=%PG+1
	
	// Get keys into I18NEXT global and valid detail phrases for this %PG
	type RecordI18NTSETN i18ntsn=Db.getRecord("I18NTSETN","TSID=:TSID,NSEQ=:%PG",1)
	do GETKEY^I18NUTL(.i18ntsn,.EXTDT,.NP,.SRC)
	
	// Not a valid Tset for translation
	if (EXTDT="")!(NP="") set VFMQ="Q",RM=$$^MSG(5461)
	if (EXTDT="")!(NP="") quit
	
	set %OPG=%PG
	
	// Build memory structures for i18NMTRA Screen
	do BLDMTB^I18NUTL
	
	// Invoke the screen progran I18NMTRA
	set SID="I18NMTRA" do DRV^USID(%ProcessMode,SID)
	
	quit
	
HTrans // Update I18NEXT Header record - called by ^I18NCRT

	type public Number HPAT,HPMT,NHPMT
	type public String EXTDT,HP,HR,NP,OHP
	
	set (HPAT,HPMT)=0
	if (HP.exists()),(HP'=OHP) do {
		set NHPMT=NHPMT+1
		if HR.piece("|",6)=2 set (HPAT,HPMT)=1 
		if HR.piece("|",6)=0 set HPMT=1 
		type RecordI18NEXTH i18nexth=Db.getRecord("I18NEXTH","EXTDT=:EXTDT,NORMPHR=:NP",1)
		set i18nexth.targphr=HP
		set i18nexth.trasuid=%UserID
		set i18nexth.trasdt=%CurrentDate
		set i18nexth.ttype=1
		do i18nexth.save()
		}
	
	do UpTset
	
	quit
	
DTrans // Update I18NEXT Detail record - called by ^I18NCRT

	type public Number DPCNT,DPMT,NDPMT
	type public String EXTDT,DP,NP,OP,SRC()
	
	set DPMT=0
	type RecordI18NEXTD i18nextd=Db.getRecord("I18NEXTD","EXTDT=:EXTDT,NORMPHR=:NP,GLOBREF=:SRC(DPCNT)",1)
	if (DP'=OP)!(DP'=i18nextd.origphr)!(i18nextd.targphr'="") do {
		set NDPMT=NDPMT+1
		if i18nextd.ttype'=1 set DPMT=DPMT+1
		set i18nextd.targphr=DP
		set i18nextd.trasuid=%UserID
		set i18nextd.trasdt=%CurrentDate
		set i18nextd.ttype=1
		do i18nextd.save()
		}
	
	do UpTset
	
	quit
	
UpTset // Update timing variables

	type public Number %PG,EFFWORK,DPMT,HPAT,HPMT,T1,T2,TSID
	type public String FMTWORK

	set T2=%CurrentTime,EFFWORK=EFFWORK+(T2-T1),T1=T2
	
	// Update Tset record
	type RecordI18NTS2 i18nts2=Db.getRecord("I18NTS2","TSID=:TSID",1)
	set i18nts2.lstphr=%PG
	if HPMT set i18nts2.nonormman=i18nts2.nonormman+1,HPMT=0
	if HPAT set i18nts2.nonormaut=i18nts2.nonormaut-1,HPAT=0
	set i18nts2.notrans=i18nts2.notrans+DPMT,DPMT=0
	set i18nts2.effwork=EFFWORK
	// formatting seconds for screen
	set FMTWORK=$$TIME^I18NUTL(EFFWORK)
	do i18nts2.save()
	
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60703^23156^Sudanthiran S. Kumar^5738"	// Signature - LTD^TIME^USER^SIZE
