RETUTL		/* 
	ORIG: SCHWARTZC - 12/20/1999
	DESC: Return Item Utilities
	
	---- Comments --------------------------------------------------------
	The following utilies are designed to be used in return item processing.
	
	$$PLHLD - Place a hold for TAMT using the permanent hold code
		defined for the return items source in CTBLINC.
		Returns the hold sequence if succesful and nothing otherwise.
	
	$$RMHLD - Remove a hold for the account using HLDSEQ.
		Returns the success of removing the hold.
	
	$$RETACT - Set the default return item action based on ACTION
		Returns the default action.	
	
	---- Revision History ------------------------------------------------
	
	11/18/05 - HAILEYM - CR18146
		Updated to DBI Standards.
	
	12/29/03 - CARROLLJ - CR7658
		Added #ACCEPT prior to setting %SystemDate.

	01/25/00 - SCHWARTZC - 35136
		Added a quit in section PLHLD if PHC is not defined.

	*/
	
	quit	// quit if called from the top


PLHLD(Number CID,	// Account Number
	Number AMOUNT,	// Amount
	String SRC,	// Inclearing Source
	String TLO,	// Teller Location
	String TCMT) 	// Teller Comment

	// Place hold for return item
	
	type public Cache %CUVAR
	type RecordCUVAR cuvar=%CUVAR.getRecord("CUVAR")
	#ACCEPT DATE=12/29/03;PGM=John Carroll
	if $G(%SystemDate)="" set %SystemDate=cuvar.tjd

	set TLO=TLO.get()
	set TCMT=TCMT.get()

	if TLO.isNull() set TLO=$I
	
	type RecordCTBLINC ctblinc=Db.getRecord("CTBLINC","KEY=:SRC",1)
	quit:'ctblinc.getMode()
	quit:ctblinc.phc="" ""

	quit $$PUTHOLD^PHLD(CID,%SystemDate,"",AMOUNT,ctblinc.phc,%UserID,TLO,TCMT)


RMHLD(Number cid,	// Account Number
	Number seq) 	// Hold Sequence

	// Remove hold for return item.

	quit $$DELHOLD^PHLD(cid,seq)
	

RETACT(Number ACTION,	// Action on Return
	Date TJD,	// Date
	Number BRCD,	// Branch Code
	String UID,	// Teller ID
	Number TSEQ,	// Transaction Sequence
	Number CID)	// Account Number

	// Return default return item action.
	
	if ACTION<2 quit ACTION
	
	type Number retval,BAL,TAMT
	
	type RecordACN acn
	type RecordEXC exc
	
	if ACTION=2 do { quit retval	// Pay on uncollected, return on unavailable
		set acn=Db.getRecord("ACN","CID=:CID",1)
		if 'acn.getMode() set BAL=0
		else  set BAL=acn.bal
		
		set exc=Db.getRecord("EXC","TJD=:TJD,BRCD=:BRCD,UID=:UID,TSEQ=:TSEQ",1)
		if 'exc.getMode() set TAMT=0
		else  set TAMT=exc.tamt
	
		if TAMT>BAL set retval=1 quit
		set retval=0
		}
		
	quit 1		// Invalid Action, use normal default of 1
 #OPTION ResultClass ON
Public String vSIG()	quit "60248^53244^Marie Hailey^2334"	// Signature - LTD^TIME^USER^SIZE
