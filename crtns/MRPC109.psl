MRPC109(return,versn,cid)
        /*
        Procedure ID: MRPC109
        DESC: Cumulative Deposit Account Status
        KEYWORDS: 
 
        ARGUMENTS:
		. cid         Account Number           /TYP=N/REQ/MECH=VAL/
                . versn       ^MRPC109 version number  /TYP=N/REQ/MECH=VAL
 
        RETURNS:
                . NUM 	      Scheduled Deposit Number        /TYP=N/REQ
                . SCHDEPND    Scheduled Deposit Due Date      /TYP=D/REQ
                . EFDDS       Effective Date Scheduled Deposit Satisfied  /TYP=D
                . FEE         Late Fee                        /TYP=N/MECH=VAL
 
                . TOTFEE     Total Fee for Deposits Satisfied    /TYP=N/MECH=VAL
 
                . TOTFEEOV   Total Fee for Deposits not Satisfed /TYP=N/MECH=VAL
 
                . TOTAMTOV   Total Amt Due for Deposits not Satisfied/TYP=N/MECH=VAL
  
        RELATED:
  
        EXAMPLE: S RM=$$^MRPC109(.VAL,1,8536)

	---- Revision History ------------------------------------------------

	12/12/03 - CARROLLJ - CR7239
		   Added #ACCEPT prior to setting %SystemDate.

	09/09/03 - GRAY - 51349
		   Corrected RECNOFL error from occurring at vDb1+6 when
		   account does not exist.

	06/05/03 - CARROLLJ - 51349
		   replace getoneRow with getRecord of dep and passed dep
		   object to FEE^SCHFEE.
	---------------------------------------------------------------------- 
        */

	type Public Cache %CUVAR

	// Version number of client message is not compatible with server
        if $G(versn)'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))

	new ER,FLD,RM,STR,CUMFEE,TOTFEE,TOTFEEOV,TOTAMTOV
	
	set ER=0
        set RM=""

	set CID=cid.get()
	type RecordDEP dep=Db.getRecord("DEP","CID=:CID",1)

	if %SystemDate.get()="" do {
		type RecordCUVAR cuvar=%CUVAR.getRecord("CUVAR")
		#ACCEPT DATE=12/12/03;PGM=John Carroll
		set %SystemDate=cuvar.tjd
		}
	do FEE^SCHFEE(.dep,cid,TJD,.CUMFEE,.TOTFEE,.TOTFEEOV,.TOTAMTOV)
	if ER quit $$ERRMSG^PBSUTL($G(RM))

	set STR=TOTAMTOV_$C(9)_dep.ipl_$C(9)_TOTFEE_$C(9)_TOTFEEOV_$C(9)_dep.grp_$C(9)_dep.type_$C(9)_dep.schdepa_$C(9)_dep.trm_$C(9)_dep.irn_$C(9)_dep.title1_$C(13,10)
	// loop through CUMFEE array to build initial STR value

	set DEPNUM=""
	for  set DEPNUM=$O(CUMFEE(DEPNUM)) quit:DEPNUM=""  do {
		set NXTDT=""
		for  set NXTDT=$O(CUMFEE(DEPNUM,NXTDT)) quit:NXTDT=""  do {
			set STR=STR_DEPNUM_$C(9)_NXTDT_$C(9)_$P(CUMFEE(DEPNUM,NXTDT),"|",1)_$C(9)_$P(CUMFEE(DEPNUM,NXTDT),"|",2)_$C(13,10)
			}
		}

	//Return values
        set FLD(1)=""
        set FLD(2)=""
        set FLD(3)=$G(STR)
        if FLD(3)="" S fld(3)=$C(9)_$C(9)_$C(9)_$C(9)_$C(9)_$C(9)_$C(9)_$C(9)_$C(13,10)
        set FLD(4)=""
        set return=$$V2LV^MSG(.FLD,"",0)
	
	quit ""
 #OPTION ResultClass ON
Public String vSIG()	quit "60122^61080^Chad Smith^2611"	// Signature - LTD^TIME^USER^SIZE
