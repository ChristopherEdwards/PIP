LNAOPL	//User Table Maintenance for Add-on Methods and Payees
	/*
	       ORIG:  Chuck Hardy (6721) 2/19/87

	  ---- Revision History ------------------------------------------------

	   03/14/07 - KumarSS - CR 25177
		      Removed Invalid Unicode Characters.

	   11/16/06 - GIRIDHAL - CR 23280
	   	      Modified FILE section to do fAOAMO.save() instead of
	   	      a bypassSave(). This is because the filer on AOAMO has
	   	      to be executed for edit checks on AOAMO.PLDATE.
	   	      
	   08/01/06 - KELLYP - CR 22234
	   	      Modified VPG00 section to change the lookup table for 
	   	      payees from AOAMO (payees w/ plans defined) to LNAOP
	   	      (which contains all payees).  This allows new plans to
	   	      be defined.

	   01/21/06 - MBUIM - 19700
	   	      Replaced column aoamo.count with column aoamo.cnt. COUNT 
	   	      is an oracle reserve word and was process issues 
	   	      e.g error returned in DBSLOAD when running function 
	   	      @DDPXFR - manual transfers to FEPS.
	   	      
	   09/09/05 - Satyanas - CR16994
		      Modified the Code to PSL standards and removed the
		      unscoped warnings.
	   
	   09/30/02 - TELIV - 49451
		      Converting to PSL

	   04/30/01 - MBUIM - 44605
	              Modified routine to include new Misc. items MSC1 through
	              MSC9. Removed old revision history.
	  ----------------------------------------------------------------------
	*/
	
	do NEW
	quit


NEW 	//
	do INIT(0)
	quit


UPD 	//
	do INIT(1)
	quit


INQ 	//
	do INIT(2)
	quit


DEL 	//
	do INIT(3)
	quit


INIT(%ProcessMode)	//
	
	type public Boolean ER
	type public String VFMQ
	type Number %PAGE,%PG
	
	do OPT 

	if ER set VFMQ="Q" do END quit

	set %PG=0 
	set %PAGE=1

	type RecordAOAMO fAOAMO

	do VPG(.fAOAMO)

	quit


VPG(RecordAOAMO fAOAMO)	// Page control

	type public Number %PG
	type public String VFMQ
	type Boolean FINISH
	
	set FINISH=0

	for  do { quit:FINISH
		
		if %PG=0 do VPG00 if VFMQ="Q" set FINISH=1 quit
		if %PG>0 do VPG01(.fAOAMO) if VFMQ="Q" set FINISH=1 quit
		if "DFQ"[VFMQ do VER(.fAOAMO) set FINISH=1 quit
		set %PG=%PG+1
		}

	quit

VPG00	// Set up

	type public String ET,IO,N,PAYEE,%READ,%TAB(),TYP,UTBL(),VFMQ
	type public String SOP,STBL(,)
		
	if SOP="INS" do {
		set N=""
		type ResultSet rs=Db.select("ATYPE,DESC","STBLAOTYP")
		while rs.next() do {
			set N=rs.getCol("ATYPE")
			if N.extract()="I" set STBL("AOTYP",N)=rs.getCol("DESC")
			}
		}

	if SOP="MSC" do {
		set N=""

		type ResultSet rs=Db.select("ATYPE,DESC","STBLAOTYP")
		while rs.next() do {
                	set N=rs.getCol("ATYPE")
                	if N.extract()="M" set STBL("AOTYP",N)=rs.getCol("DESC")
                	}
		}

	// "Invalid ~p1 type"
	set %TAB("TYP")=".TYP1/TBL=STBL(""AOTYP"",/XPP=I $E(X)'=$E(SOP) S ER=1,RM=$$^MSG(1514,LOP)"
	set %TAB("PAYEE")=".PAYEE1/TBL=[LNAOP]PAYEE:DISTINCT:QU ""[LNAOP]TYP=<<TYP>>"""
	set %TAB("PLAN")=".PLAN1"

	if %ProcessMode=0 set %TAB("PLAN")=%TAB("PLAN")_"/XPP=I $$CHKTAB^LNAOPL(TYP,PAYEE,X) S ET=""RECOF"" D ERR^LNAOPL"

	if %ProcessMode do {

		// I18N=OFF
		set %TAB("PLAN")=%TAB("PLAN")_"/TBL=[AOAMO]PDES:QU ""[AOAMO]TYP=<<TYP>> AND [AOAMO]PAYEE=<<PAYEE>>"""
		if %ProcessMode=2 set %TAB("IO")=$$IO^SCATAB($I)
		}

	// I18N=ON
	set %READ="@@%FN,,,TYP/REQ,PAYEE/REQ,PLAN/REQ"

	if SOP="DLD" set %READ="@@%FN,,,PAYEE/REQ,PLAN/REQ"

	if SOP="PTS" do {
		set TYP="PTS" 
		set PAYEE=1 
		set %READ="@@%FN,,,PLAN/REQ"
		}

	if %ProcessMode=2 set %READ=%READ_",IO/REQ"

	do ^UTLREAD 
	
	if VFMQ="Q" quit

	if %ProcessMode=1 lock +UTBL("AOAMO",TYP,PAYEE):2 else  set ET="RECLOC" do ERR quit
	
	if %ProcessMode=2,IO'=$I do OPEN^SCAIO

	quit


VPG01(RecordAOAMO fAOAMO)	// Screen

	type public String PAYEE,PLAN,SID,SOP,TYP

	set fAOAMO=Db.getRecord("AOAMO","TYP=:TYP,PAYEE=:PAYEE,PLAN=:PLAN",1) 
	set SID="LNAMO"_SOP 

	do DRV^USID(%ProcessMode,SID,.fAOAMO)

	quit

ERR	//

	type public Boolean ER
 	type public String VFMQ
	 
	set ER=1 
	
	do ^UTLERR
	
	set VFMQ="Q"

	quit

VER(RecordAOAMO fAOAMO)	//

	type public String VFMQ

	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit

	do FILE(.fAOAMO)

	do END

	quit

FILE(RecordAOAMO fAOAMO)	// File data

	if %ProcessMode=3 do F(.fAOAMO) quit

	do fAOAMO.save()
	quit


F(RecordAOAMO fAOAMO)	// processing for delete option

	type public String ET,TYP

	if fAOAMO.cnt>0 set ET="TBLINUSE" do ERR do END quit

	do Db.delete("AOAMO","TYP=:TYP AND PAYEE=:PAYEE AND PLAN=:PLAN")

	type ResultSet rs=Db.select("PAYEE","LNAOP","TYP=:TYP")

	if rs.isEmpty() do {
		type RecordLNAOT lnaot=Db.getRecord("LNAOT","TYP=:TYP")
		set lnaot.edt=%SystemDate
		}

	do END

	quit


END	//
	type public String ER,LOP,PLAN,RM,%TAB,VFMQ

	kill %TAB lock

	if ER!(%ProcessMode=2)!(%ProcessMode=4) quit

	set PLAN=PLAN.get()

	if VFMQ="Q" do {

		// ~p1 plan ~p2 not created
		if %ProcessMode=0 set RM=$$^MSG(3068,LOP,PLAN) quit

		// ~p1 plan ~p2 not modified
		if %ProcessMode=1 set RM=$$^MSG(3070,LOP,PLAN) quit

		// ~p1 plan ~p2 not deleted
		set RM=$$^MSG(3069,LOP,PLAN) quit

		}
	else  do {

		// ~p1 plan ~p2 created
		if %ProcessMode=0 set RM=$$^MSG(3065,LOP,PLAN) quit

		// ~p1 plan ~p2 modified
		if %ProcessMode=1 set RM=$$^MSG(3067,LOP,PLAN) quit

		// ~p1 plan ~p2 deleted
		set RM=$$^MSG(3066,LOP,PLAN) quit
		
		}

	set ER="W"

	quit


OPT	// Determine Type Option

	type public Boolean ER
	type public String LOP,SOP
	type public Number OPT

	set ER=0

	// Insurance
	if OPT=1 do { quit
		set SOP="INS" 
		set LOP=$$^MSG(6846)
		}

	// Miscellaneous
	if OPT=2 do { quit
		set SOP="MSC" 
		set LOP=$$^MSG(6847)
		}

	// Dealer
	if OPT=3 do { quit
		set SOP="DLD" 
		set LOP=$$^MSG(6845)
		}

	// Net Fee Income
	if OPT=4 do { quit
		set SOP="PTS" 
		set LOP=$$^MSG(6844)
		}

	set ER=1 
	set (SOP,LOP)=""

	quit


PPTRC	// Check Credit Tran Code For "MISC" & Credit Flag

	type public Boolean ER
	type public String ET,X
	
	if X.isNull() quit
	
	type RecordTRN trn=Db.getRecord("TRN","ETC=:X",1)
	if trn.getMode()=0 do { quit
		set ER=1 
		set ET="INVLDTC" 
		do ^UTLERR
		}

	if trn.cls'="M" do { quit
		set ER=1 
		set ET="INVLDGRP" 
		do ^UTLERR
		}

	if trn.acn.isNull() do { quit
		set ER=1 
		set ET="INVLDACN" 
		do ^UTLERR
		}

	if 'trn.dc do {
		set ER=1 
		set ET="INVDCF" 
		do ^UTLERR
		}
	quit


PPTRD	// Check Debit Tran Code For "MISC" & Debit Flag

	type public Boolean ER
	type public String ET,X
	      
        if X.isNull() quit
 
        type RecordTRN trn=Db.getRecord("TRN","ETC=:X",1)
        if trn.getMode()=0 set ER=1 set ET="INVLDTC" do ^UTLERR quit
 
        if trn.cls'="M" set ER=1 set ET="INVLDGRP" do ^UTLERR quit
        
        if trn.acn.isNull() set ER=1 set ET="INVLDACN" do ^UTLERR quit
        
        if 'trn.dc set ER=1 set ET="INVDCF" do ^UTLERR

	quit


CHKTAB(TYP,PAYEE,PLAN)	// For substituting $D in %TAB

	if Db.isDefined("AOAMO","TYP=:TYP,PAYEE=:PAYEE,PLAN=:PLAN") quit 1
	
	quit 0
 #OPTION ResultClass ON
Public String vSIG()	quit "60703^23208^Sudanthiran S. Kumar^6484"	// Signature - LTD^TIME^USER^SIZE
