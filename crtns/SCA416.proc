public SCA416(String INCD, Number PL)
	/*
	 ORIG: KELLYP - 04/02/2003
	 DESC: Custodial Account Transaction Summary Report Driver
	
	 ---- Comments --------------------------------------------------------

	 This procedure compiles the SRVPLN8 records that will appear on 
	 the SCA416 report.  It is used to drive the collation of the data
	 on the report since the old collation functionality in reports is no 
	 longer accessible.

	 ---- Revision History ------------------------------------------------
	
	 09/29/06 - PUTTASWH - CR 23269
	 	    Modified HIST section to use TMPRPT6 instead of TMPRPT5 
	 	    since more than one transaction is posted to an investor's 
	 	    wash account in the specifed date range and this can be 
	 	    captured with the additonal key in TMPRPT6.
	 	    
	 07/17/06 - KELLYP - CR 22174
		    Initial revision.
	*/

	type public Boolean EWASH,PWASH

	type String WHERE

	if 'EWASH,'PWASH quit

	if INCD="ALL" set WHERE="INCD IS NOT NULL"
	else  set WHERE="INCD=:INCD"
	
	if PL="ALL" set WHERE=WHERE_" AND PL IS NOT NULL"
	else  set WHERE=WHERE_" AND PL=:PL"

	#ACCEPT PGM=Pat Kelly;DATE=07/17/06;CR=22174
	type DbSet ds=Db.selectDbSet("LNLS8",WHERE,"INCD,PL,GRP,CID ASC")
	while ds.next() do {
		type RecordLNLS8 lnls8=ds.getRecord("LNLS8")
		type RecordLNLS2 lnls2=Db.getRecord("LNLS2","INCD=:lnls8.incd,PL=:lnls8.pl")
		if PWASH,lnls2.wash do HIST(.lnls2,.lnls8,lnls2.wash)
		if EWASH,lnls2.escwash do HIST(.lnls2,.lnls8,lnls2.escwash)
		}

	quit

HIST(RecordLNLS2 lnls2, RecordLNLS8 lnls8, Number WASH)

	type public Date FD,TD
	type public String HIST(,)

	type Number SEQ,TAMT,TSEQ
	type String HISTDTL

	set HIST(WASH,-1)=0
	set SEQ=0

	type DbSet ds=Db.selectDbSet("HIST","CID=:WASH AND TJD>=:FD AND TJD<=:TD","TSEQ DESC")
	while ds.next() do {
		type RecordHIST hist=ds.getRecord("HIST")

		if 'HIST(WASH,-1).exists() set HIST(WASH,-1)=hist.endbal

		set TAMT=+hist.tamt
		set TSEQ=hist.tseq

		if 'TAMT quit
		
		set HISTDTL=hist.efd_"|"_hist.etc_"|||"_hist.tcmt_"|"_hist.tjd
		
		// Update credit amount for this HIST record and credit totals
		if hist.itc.extract(1) set HISTDTL.piece("|",3)=TAMT do {
			set HIST(WASH,-1).piece("|",2)=HIST(WASH,-1).piece("|",2)+TAMT
			}	
		// Update debit amount for this HIST record and debit totals
		if 'hist.itc.extract(1) set HISTDTL.piece("|",4)=TAMT do {
			set HIST(WASH,-1).piece("|",3)=HIST(WASH,-1).piece("|",3)+TAMT
			}
		
		set SEQ=SEQ+1
		
		type RecordTMPRPT6 tmprpt6=Class.new("RecordTMPRPT6","PID=:%ProcessID,KEY1=:lnls2.incd,KEY2=:lnls2.pl,KEY3=:lnls8.grp,KEY4=:lnls8.cid,KEY5=:WASH,KEY6=:SEQ")
		
		set tmprpt6.data=HISTDTL
		
		do tmprpt6.bypassSave()
		}

	quit

vSIG()	quit "60537^26838^Hema Puttaswamy^2545"	// Signature - LTD^TIME^USER^SIZE
