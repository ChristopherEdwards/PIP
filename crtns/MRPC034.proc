public MRPC034(RETURN,VERSN,CID,TSEQ,RCODE,BEN,RPAMCZC,RPAMCZYR)	//Public
	/*

	   DESC: Fix IRA History

	   KEYWORDS: MRPC

	   ARGUMENTS:
	   	. RETURN 	RM    			/TYP=T/REQ/MECH=REFNAM:W

	   	. VERSN  	MRPC034 version number	/TYP=N/REQ/MECH=VAL
	                  	current version=1

	   	. CID  		Account Number  	/TYP=N/REQ/MECH=VAL
	   	. TSEQ  	History Sequence 	/TYP=N/REQ/MECH=VAL
	   	. RCODE  	Contr/Dist Reason Code 	/TYP=N/REQ/MECH=VAL
	   	. BEN  		Beneficiary  		/TYP=N/NOREQ/MECH=VAL
	   	. RPAMCZC	RPA Military Combat Zone Code	/TYP=T/NOREQ/MECH=VAL
	   	. RPAMCZYR	RPA Military Year of Service	/TYP=N/NOREQ/MECH=VAL

	   RETURNS:
	    	. $$     	Message   		/TYP=T

	   RELATED:
	         . $$^PBSMRPC - MRPC Service Class Driver

	   EXAMPLE:
	         S RM=$$^MRPC034(.RETURN,1,700000023,10,31,)
	 
         ---- Revision History ------------------------------------------------

         04/07/06 - DHANALAKSHMI R - 20628
                 Modified to set null to RPAMCZC & RPAMCZYR if not defined.
 
         09/09/05 - DHANALAKSHMI R - 16851
                 Implemented "Supporting Military Combat Zone Codes for 
                 Military Contributions" project. Refer CR# 11986 for more
                 Details.

         04/04/02 - GORDONT - 49794
                 Converted to PSL.

	*/

	type public String ET
	new CLS,ER,IRA,RM,VFMQ

	// Version number of client message is not compatible with server
	if $G(VERSN)'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))

	// Data required in Data Item ~p1
	if '$G(TSEQ) quit $$ERRMSG^PBSUTL($$^MSG(7531,"TSEQ"))

	// Data required in Data Item ~p1
	if '$G(RCODE) quit $$ERRMSG^PBSUTL($$^MSG(7531,"IRATSO"))

	// Invalid account ~p1
	if 'Db.isDefined("ACN","CID") quit $$ERRMSG^PBSUTL($$^MSG(1259,CID))

	type RecordDEP dep=Db.getRecord("DEP","CID")

	set CLS=dep.cls

	// Invalid Account Class
	if CLS'="D" set ET="INVLDCLS" do ^UTLERR quit $$ERRMSG^PBSUTL($G(RM))

	set IRA=dep.ira

	// Not a retirement plan account
	if 'IRA quit $$ERRMSG^PBSUTL($$^MSG(2030))

	set VFMQ="F"
	set BEN=$G(BEN)
	if 'RPAMCZC.exists() set RPAMCZC=""
	if 'RPAMCZYR.exists() set RPAMCZYR=""

	do MRPC034^IRAFIX(CID,TSEQ,RCODE,BEN,RPAMCZC,RPAMCZYR)
	if $G(ER) quit $$ERRMSG^PBSUTL($G(RM))

	set RETURN=$$V2LV^MSG(1)
	quit ""

vSIG()	quit "60362^10734^Dhanalakshmi R^2113"	// Signature - LTD^TIME^USER^SIZE
