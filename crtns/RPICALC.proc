RPICALC ; Procedure RPICALC - RPI premium calculation.
 ;;Copyright(c)2005 Sanchez Computer Associates, Inc.  All Rights Reserved - 08/09/05 10:35 - spatola
 ;
 ; **** This is a DATA-QWIK generated routine (level 25) ****
 ;
  /* 
    ORIG: OYEGUNZ - 11/10/1999
    DESC: RPI premium calculation.

    ---- Comments -------------------------------------------------------------
	LDWI calculates the Repayment Insurance Premium (to be used as a fee
	which is added to the loan principal at teller disbursement) on a loan
	that has RPI.

    ---- Revision History -----------------------------------------------------
	05/20/03 - CARROLLJ - 51349
		   Modified procedure to cleanup %A and ETC array along with
		   other miscelleanous cleaup for the database independence 
		   project.

    ---------------------------------------------------------------------------
	
	dummy quit for the compiler
 */
        quit
 
public LDWI(RecordLN ln,RecordTTX ttx,RecordTRN trn,CID,EFD,BIL,AMT,ELMNT)

	new PRIETC,PCFL,change,fee
	set PRIETC=ttx.etc
	if PRIETC="" quit 0

	new PCFL38,PCFL39
	//Get Calculate Premium processing control flag
	set PCFL38=trn.pcfl38

	//Get Calculate Rebate processing control glag
	set PCFL39=trn.pcfl39

	// If pcfl39 is set calculate RPI rebate and generate promissory
	// number if aplicatble (ln.genprn=1)
	// change is an internal flag to signal a change to either rpirebate
	// or rpi premium.
	set (change,fee)=0
	if PCFL39=1 do rebate(.ln,.change,.ttx)

	// If pcfl38 is set calculate RPI primium for the Balance-rpirebate
	// ! of the loan for loans with RPI premium.
	if PCFL38=1 do rpi(.ln,.change,.ttx)


	if 'PCFL38 do {
		if ln.rpiprem="" quit
		do HSEQ^LNPTSU(.ttx)
		set TCMT=CID_"["_LN_"]"_RPIPREM_":"_ln.rpiprem_":"_""
		do HISTBLD^ACNFUNCS(TCMT)
	}
	if 'PCFL39 do {
		if ln.rpireb="" quit
		do HSEQ^LNPTSU(.ttx)
		set TCMT=CID_"["_LN_"]"_RPIREB_":"_ln.rpireb_":"_""
		do HISTBLD^ACNFUNCS(TCMT)
		}

	// Fee amount is a difference of premium and rebate
	// As this could be a secondary disbursement - a fee for the amount of
	// rebate was already applied to the loan - no need to double deep.
	if change>0 set fee=ln.rpiprem-ln.rpireb if ln.cntdr=1 set fee=ln.rpiprem
	quit fee


public rebate(RecordLN ln,change,RecordTTX ttx)	
	// Line tag to calculate rebate amounts and generate promissory
	// number for secondary disbursements.
	// Rebate is only calculated if there is an existing amount on the
	// loan.

	new genprn
	set genprn=ln.genprn
	// If this is not the first disbursement (after loan creation or file
	// maintanace) need to generate a new promissory number.
	if genprn=1 do {
		new utblncidt,nnum,XTYP,HSEQ,oldperno
		set XTYP="PRN"
		type RecordUTBLNCIDT utblncidt
		set utblncidt=Db.getRecord("UTBLNCIDT","TYPE=:XTYP")
		set nnum=utblncidt.nnum
		set oldperno=ln.perno
		set ln.perno=nnum
		set utblncidt.nnum=nnum+1
		do utblncidt.bypassSave()

		do HSEQ^LNPTSU(.ttx)
		set TCMT=CID_"["_LN_"]"_PERNO_":"_oldperno_":"_ln.perno
		do HISTBLD^ACNFUNCS(TCMT)


		}
	if genprn=0 do {
		set ln.genprn=1
		do HSEQ^LNPTSU(.ttx)
		set TCMT=CID_"["_LN_"]"_GENPRN_":"_0_":"_1
		do HISTBLD^ACNFUNCS(TCMT)
	
		}
	

	// If there is no existing premium - no rebate necessary
	// Also if first time disbursement no rebate calculation
	if (ln.cntdr=1)!(ln.rpiprem'>0) quit

	// Use RPI rate defined on the account if non get from UTBLINSPC
	new rpirate 
	set rpirate=ln.rpirate 
	if rpirate="" do {
		new PCM,PFRE,TRM
		set PCM=ln.pcm
		set PFRE=ln.dist1fre
		set TRM=ln.ptrm 
		if TRM="" set TRM=ln.trm
		set rpirate=Db.getOneRow("INRATE","UTBLINSPC","PCM,TRM,PFRE")
		// RPI rate is not defined for PCM, Term and Payment Frequency.
		if rpirate="" set OVR(CID,"OVR","INVRPIRT")=""
	}

	new AOB,AOC,AOT,AOTD,AO,AOI,CALC,AF,FRE,NEG,NJD,MET,LT
	set AOI=rpirate
	set CALC=1,NEG=0
	set AOC=ln.cntcr
	set AOT=ln.onp
	set AOTD=0
	set AO=ln.rpiprem
	set AOB=AO-AOTD
	set FRE=ln.dist1fre
	if AO<0 set NEG=1,AO=-AO
	set NJD=$$NJD^UFRE(TJD,FRE,.AF)
	set MET=ln.rcalcm
	if MET="" quit
	do START^LNCYCMET(.ln,MET)
	if AOC>AOT set AOC=AOT

	if ln.rpireb'=%AMT do {
		do HSEQ^LNPTSU(.ttx)
		set TCMT=CID_"["_LN_"]"_RPIREB_":"_ln.rpireb_":"_%AMT
		do HISTBLD^ACNFUNCS(TCMT)
		}
 
	set ln.rpireb=%AMT          ; Rebate amount
	set change=change+1
	quit

public rpi(RecordLN ln,change,RecordTTX ttx)
	// Calculate a new RPI premium on the new balance of the account.

	// Use RPI rate defined on the account if non get from UTBLINSPC
	new rpirate set rpirate=ln.rpirate 
	if rpirate="" do {
		new PCM,PFRE,TRM
		set PCM=ln.pcm
		set PFRE=ln.dist1fre
		set TRM=ln.ptrm if TRM="" set TRM=ln.trm
		set rpirate=Db.getOneRow("INRATE","UTBLINSPC","PCM,TRM,PFRE")
		// RPI rate is not defined for PCM, Term and Payment Frequency.
                if rpirate="" set OVR(CID,"OVR","INVRPIRT")=""
	}

	new BASE,rpiprem,rebate
	if ln.cntdr=1 set rebate=0
	else  set rebate=ln.rpireb
	// Premium is calculated on the balance of the loan - rebate.
	// for the secondary disb so not to charge premium on premium.
	set BASE=ln.balcmp-rebate
	set rpiprem=$$^SCARND(((BASE*rpirate)/100),0,,,2)

	if ln.rpiprem'=rpiprem do {
		do HSEQ^LNPTSU(.ttx)
		set TCMT=CID_"["_LN_"]"_RPIPREM_":"_ln.rpiprem_":"_rpiprem
		do HISTBLD^ACNFUNCS(TCMT)
		}
	set ln.rpiprem=rpiprem,change=change+1
	quit
 

vSIG()	quit "59886^43602^Sanchez SCM Administrator^4895"	// Signature - LTD^TIME^USER^SIZE
