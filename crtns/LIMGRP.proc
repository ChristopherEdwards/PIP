LIMGRP	//Private;Product Group Limits Summary Calculations
	/*
	   ORIG: SMITHC - 09/24/97
	   DESC: Product Group Limits Summary Calculations

	   RETURNS:
	   . XX desc of return  /TYP=T

	   EXAMPLE:
	   D ^LIMGRP

	  ---- Revision History ------------------------------------------------

	   08/17/05 - KumarSS - CR16667
	   	      Removed the #WARN directive.
	   	      
	   03/31/03 - THONIYILM - 51351
	              Conversion to PSL

	*/

	do START
	quit


	//---------------------------------------------------------------------
START	//
	//---------------------------------------------------------------------
	
	do TTL
	do EXPOS
	quit


	//---------------------------------------------------------------------
TTL	// Accumulate teller total data for outstanding exposure
	//---------------------------------------------------------------------
	
	type String CLS,CRCD,GRP,YJD
	type Number TYPE

	set YJD=%SystemDate-1
	type ResultSet rs=Db.select("CRCD,CLS,GRP,TYPE","TTL","TJD=:YJD")
	while rs.next() do {
		set CRCD=rs.getCol("CRCD")
		set CLS=rs.getCol("CLS")

		if CLS="L" do {
			set GRP=rs.getCol("GRP")
			set TYPE=rs.getCol("TYPE") 
			do TTLEXEC(CRCD,CLS,GRP,TYPE)
		}
	}
	quit


	//---------------------------------------------------------------------
TTLEXEC(String CRCD,		// Currency Code
        String CLS,		// Product Class
        String GRP,		// Product Group
        Number TYPE)		// Product Type
	//---------------------------------------------------------------------
	
	// Type level

	type String SYS,GRPLIM
	type Number PERCENT,BALCMP

	set BALCMP=0

	type ResultSet rs=Db.select("GRPLIM","UTBLPLANTLIM","TYPELIM=:TYPE AND SYS IS NOT NULL")
	while rs.next() do {
		set GRPLIM=rs.getCol("GRPLIM")

		if (TYPE>1) do TALLY(CRCD,CLS,GRP,TYPE,.BALCMP)
		if (GRPLIM="") quit
		// Institution limit not defined
		type RecordUTBLINSTLIM utblinstlim=Db.getRecord("UTBLINSTLIM","GRPLIM=:GRPLIM",1)
		set PERCENT=utblinstlim.percent
		if (PERCENT="") quit

		type RecordLIMGRP limgrp=Db.getRecord("LIMGRP","PID=:%ProcessID,GRPLIM=:GRPLIM",1)
		set limgrp.outexp=limgrp.outexp+BALCMP
	}
	quit


	//---------------------------------------------------------------------
TALLY(String CRCD,		// Currency Code
      String CLS,		// Product Class
      String GRP,		// Product Group
      Number TYPE,		// Product Type
      Number BALCMP)		// Balance
	//---------------------------------------------------------------------
	
	// Tally for type

	type Number BAL,BALADJ

	type DbSet ttlds=Db.selectDbSet("TTL","TJD=:YJD AND CRCD=:CRCD AND CLS=:CLS AND GRP=:GRP AND TYPE=:TYPE")
	while ttlds.next() do {
		type RecordTTL ttlrec=ttlds.getRecord("TTL")
		set BAL=+ttlrec.bal
		set BALADJ=ttlrec.baladj
		set BALCMP=BALCMP+(BAL-BALADJ)
	}
	quit


	//---------------------------------------------------------------------
EXPOS	// Add exposure table data
	//---------------------------------------------------------------------
	type String ACN,INTPROD,RECID,SYS

	type ResultSet rs=Db.select("ACN,SYS,INTPROD,RECID","EXPOS","")
	while rs.next() do {
		set ACN=rs.getCol("ACN")
		set SYS=rs.getCol("SYS")
		set INTPROD=rs.getCol("INTPROD")
		set RECID=rs.getCol("RECID")
		do EXPEXEC(ACN)
	}
	quit


	//---------------------------------------------------------------------
EXPEXEC(Number ACN)		// Customer Number
	//---------------------------------------------------------------------

	// Calculate exposure table data for one account
	
	type String GRPLIM,PLAN
	type Number PERCENT

	type RecordCIF cif=Db.getRecord("CIF","ACN=:ACN",1)
	set PLAN=cif.plan
	if PLAN="" quit
	
	type ResultSet rs=Db.select("GRPLIM","UTBLPLANGLIM","PLAN=:PLAN")
	if (rs.next()) set GRPLIM=rs.getCol("GRPLIM")

	// Institution Limit not defined
	type RecordUTBLINSTLIM utblinstlim=Db.getRecord("UTBLINSTLIM","GRPLIM=:GRPLIM",1)
	set PERCENT=utblinstlim.percent
	if (PERCENT="") quit

	type RecordLIMGRP limgrp=Db.getRecord("LIMGRP","PID=:%ProcessID,GRPLIM=:GRPLIM",1)
	set limgrp.outexp=limgrp.outexp+$P($$EXPOS^PROCLIM(ACN),"|",2)

	quit

vSIG()	quit "60173^25524^Sudanthiran S. Kumar^3826"	// Signature - LTD^TIME^USER^SIZE
