BTTHANG	//Allow hang for backups in EOD or BOD
	/*
	ORIG:  Dan S. Russell (2417) - 12/24/86

	DESC:  This routine (linked to function QUE038) may be placed
	       anywhere within the EOD or BOD job queue if the institution
	       desires to pause at that
	       point to perform a backup or other function (e.g., run a
	       trial balance).

	       MUMPS cannot be taken down to perform a backup since this
	       job just acts to "pause" the processing.  Therefore, if a
	       backup is to be done it must be done with /IGNORE=INTERLOC.

	--------- Revision History ------------------------------------------
	   
	01/06/05 - RussellDS - CR13817
	  	   Remove extraneous tag (HANG) since entry is from top.
	  	   
	  	   Correct looping logic in REPLY.
	   	      
	   	   Fix scope errors.  Do some PSL clean-up.
	   	     
	   	   Remove old revision history.
	  
        --------------------------------------------------------------------  
	*/

	//Temporarily stop queue processing

	type String MSG, USR, USERS

	type RecordCUVAR cuvar=Db.getRecord("CUVAR")
	quit:cuvar.hang=3
	set cuvar.hang=1
	do cuvar.save()

	set USERS=cuvar.reply

	set USR=$$USERNAM^%ZFUNC

	if '(USERS[USR) set USERS=USR_","_USERS

	// Processing suspended. Run RCH002 to acknowledge, or RCH003 to continue.
	set MSG=$$^MSG(2245)
	
	do REPLY
	
	quit


REPLY   // Send message to operator - OK to do backup

	type public String MSG, USERS

	type Number HANG, I
	type String USER, Z
	
	for  do { quit:((+HANG = 0) ! (HANG '< 3))
	
	type RecordCUVAR cuvar=Db.getRecord("CUVAR")
	
		set HANG = cuvar.hang
		if HANG = 1 for I=1:1 set USER=$P(USERS,",",I) quit:USER=""  set Z=$$SYS^%ZFUNC("REPLY/NONOTIFY/BELL/USERNAME="_USER_" "_$C(34)_MSG_$C(34))
		if HANG, (HANG < 3) hang 120
	}
	
	quit


ACK	//Operator acknowledges Hang reply

	type public String VFMQ

	quit:VFMQ="Q"
	
	type RecordCUVAR cuvar=Db.getRecord("CUVAR")
	set cuvar.hang=2
        do cuvar.save()
	
	quit


CONT	//Backup complete...continue queue processing
	
	type public String VFMQ
	
	type RecordCUVAR cuvar=Db.getRecord("CUVAR")
	quit:VFMQ="Q"!(cuvar.hang=3)
	set cuvar.hang=0
        do cuvar.save()
	
	quit


PP	//Post processor on screen RCH003

	type public Number TTR
	type public String ER, RM, VFMQ, YN

	set ER="W"

	// Day-end processing not continuing
	if VFMQ="Q" set RM=$$^MSG(760) quit

	// Day-end processing continuing
	set RM=$$^MSG(759)

	type RecordTTRPA ttrpa=Db.getRecord("TTRPA","PA='PA'") 
        set TTR=ttrpa.qntty 

	type RecordCUVAR cuvar=Db.getRecord("CUVAR")
	if 'YN,TTR>1 set cuvar.hang=3
 	else  set cuvar.hang=0
        do cuvar.save() 
	
	quit

vSIG()	quit "59920^49621^Dan Russell^2470"	// Signature - LTD^TIME^USER^SIZE
