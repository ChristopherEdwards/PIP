KITE1	// Kite suspect global purge
	/*
	       ORIG:  Bill Greene (1674)
	       DESC:

	---- Revision History ------------------------------------------------
	11/07/06 - chhabris - CR23259
		   Modified VPG section to quit:ER. Modified VPG00 section to
		   make ER variable public and made use of Runtime.setErrMSG
		   to throw error if there are less than 3 kite periods on file.
		   Removed the setting of %ProcessMode.

        07/26/05 - SkariahV- CR16679
	           Removed #WARN and #OPTIMIZE directives.
	              
	12/08/03 - CARROLLJ - CR7239
		   Corrected precedence error in POST section.

	05/21/03 - GRAY - 51351
		   Converted to PSL.

	*/

	type Number %PAGE,%PG
	type String OLNTB,VFMQ

	set %PG=1 set %PAGE=1 

	do VPG
	quit

VPG	// Page control
	type public Boolean ER

	do VPG00 

	quit:ER

	do VPG0

	quit


VPG0 	//

	type Public Number %PG
	type Public String VFMQ

	if "DFQ"[VFMQ do VER quit
	if VFMQ="M" do VPG quit
	
	set %PG=%PG+1

	do VPG

	quit


VPG00	// Set up

	type public String %MSKD,RM,VFMQ
	type public Boolean ER
	type Date BDAT,BDAT1,EDAT,EDAT1,SAVE
	type Number I=0
	type String %READ,%TAB,HDG1,HDG2
	
	set (BDAT1,EDAT1)=""

	type ResultSet rs2=Db.select("DISTINCT ADAT","KITE")
	if rs2.next() set BDAT=rs2.getCol("ADAT") set SAVE=2 set BDAT1=$$DAT^%ZM(BDAT,%MSKD.get())

	set EDAT=""

	type ResultSet rs=Db.select("DISTINCT ADAT","KITE",,"ADAT DESC")
	while rs.next() do { quit:I=3
		set I=I+1
		if I=3 set EDAT=rs.getCol("ADAT")
		}

	// Three or more kite periods must be on file in order to purge
	if 'EDAT do Runtime.setErrMSG("KITE",2642) set VFMQ="Q" quit:ER

	set EDAT1=$$DAT^%ZM(EDAT,%MSKD.get())

	// This function will purge all kite suspect period
	set HDG1=$$^MSG(3806)

	// entries except for those periods specified below
	set HDG2=$$^MSG(3804)

	set %TAB("SAVE")=".SAVE1/TBL=PER(/XPP=D POST^KITE1/XPR=D PRE^KITE1/MIN=2/MAX=99"

	// Purge from ~p1 through ~p2
	set RM=$$^MSG(2289,BDAT1,EDAT1)

	set %READ="@@%FN,,,@HDG1,@HDG2,,SAVE,,@MSG"

	do ^UTLREAD

	quit


PRE	// PROCESS TO SET LOOK UP
	
	type Number CNT=0
	type Public String %MSKD,I(),PER()

	type ResultSet rs=Db.select("ADAT","KITE",,"ADAT DESC")
	if rs.isEmpty() quit

	while rs.next() do {
		set CNT=CNT+1
		set PER(CNT)=$$DAT^%ZM(rs.getCol("ADAT"),%MSKD.get())
		}

	// SET MINIMUM TO ONE ONLY FOR TABLE LOOK-UP
	set I(7)=1

	quit


POST	// PROCESSOR TO RE-DEFINE MINIMUM, DISPLAY NEW DATE RANGE

	type Number CT=0
	type Public Date BDAT1,EDAT,EDAT1
	type Public String %MSKD,I(),RM,X

	set I(7)=2

	set EDAT=""

	type ResultSet rs=Db.select("DISTINCT ADAT","KITE",,"ADAT DESC")
	while rs.next() do { quit:CT=(X+1)
		set CT=CT+1
		set EDAT=rs.getCol("ADAT")
		}

	if 'EDAT do {
		// Save all ~p1 periods - do not purge
		set RM=$$^MSG(2449,(CT-1))_"|8001"
		}

	else  do {
		set EDAT1=$$DAT^%ZM(EDAT,%MSKD.get())
		// Purge from ~p1 through ~p2
		set RM=$$^MSG(2289,BDAT1,EDAT1)_"|8001"
		}

	quit


VER	//
	
	type Public Date EDAT
	type Public String VFMQ

	if 'EDAT!(VFMQ="Q") quit

	do FILE

	quit

FILE	// File data
	
	type Date I
	type Public Date BDAT,EDAT
	
	for I=BDAT:1:EDAT do Db.fastDelete("KITE","ADAT=:I") do Db.fastDelete("KITE0","TJD=:I")

	quit

vSIG()	quit "60583^7586^Sanjay Chhabria^2986"	// Signature - LTD^TIME^USER^SIZE
