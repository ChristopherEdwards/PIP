LNPRI1	// Edit Check Payment Application String
	/*
	       ORIG:
	       DESC:  Post processor verifies payment string elements

	---- Revision History--------------------------------------------------

	06/08/07 - DHANALAKSHMI R - 27536
		   Modified the section PRI1 to include a condition to check
		   the existence of INFEEP record.

	02/06/06 - KumarSS - 19309
		   Rewritten the PRI1 secton to confirm the way  the logic was
		   structured in P01.
		   Also done the general clean-up in the Procedure.

	03/23/05 - JERUCHIMC - 14057
	           Add a line of code to check if the LNTRS record is defined
	           before fetching the record.	           

	05/02/02 - SHVACHKINAD - 49794
		   Converted to PSL.
	*/
	quit


	//-----------------------------------------------------------------------
PRI1	// Verify priority elements
	//-----------------------------------------------------------------------

	type public Boolean ER
	type public String ET,X

	type String FOUNDI,FOUNDP,PC,PRI()
	
	// Invalid priority item
	if X.translate("-","")="" set ET="LNPRI2" do ERR quit
	
	for PC=1:1:X.length("-") do { quit:ER

		set PRI=X.piece("-",PC)
		if PRI(PRI).exists() set ET="LNPRI2" do ERR quit

		set PRI(X.piece("-",PC))=""
		if (PRI="ESC")!(PRI="I")!(PRI="P") quit

		type RecordLNTRS lntrs=Db.getRecord("LNTRS","TRTYPE=:PRI",1)

		if lntrs.nepf quit

		type RecordLNFEEP lnfeep=Db.getRecord("LNFEEP","FEETYP=:PRI",1)

		if lnfeep.getMode(),'lnfeep.lfee quit

		set ET="LNPRI2" do ERR quit
		}

	// Verify that both Interest and Principal are in the payment string.
        for PC=1:1:X.length("-") do {
		if X.piece("-",PC)="I" set FOUNDI=1
		if X.piece("-",PC)="P" set FOUNDP=1
	        }

	if (FOUNDI.get()="")!(FOUNDP.get()="") set ET="LNPRI3" do ERR quit

	quit


	//-----------------------------------------------------------------------
ERR	// Error handling
	//-----------------------------------------------------------------------

	type public Number ER

	set ER=1
	do ^UTLERR

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60789^24454^Dhanalakshmi R^1883"	// Signature - LTD^TIME^USER^SIZE
