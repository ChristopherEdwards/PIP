CTFGRP	 
 /* ORIG: STATTOND - 02/02/2001 - ARQ 43390
    DESC: CTF Group Plan Maintenance Screen Driver

    ---- Comments --------------------------------------------------------

    ---- Revision History ------------------------------------------------

	10/20/05 - SPR - CR17008
		Agent/Commission - General DBI3 system area cleanup.	
		  
 */
 
	quit


NEW     //
        type Boolean tvar
	set tvar=1
	do INIT(0) quit


UPD     //
	do INIT(1) quit


INQ     //
	do INIT(2) quit


DEL     // 
	do INIT(3) quit

	
INIT(%ProcessMode)    // Page Initilization
	
	type public String OLNTB,VFMQ
	type Number CNT,DUP(),ENDDATE(),MAX,%PAGE,%PG
	
	set %PG=0 
	set %PAGE=1
	set MAX=13
	kill OLNTB,VFMQ

	do VPG

	quit 
	

VPG     // Page control

	type public Number %PG,%PAGE 	
	type public String VFMQ
	type Boolean FINISH
	type String DESCRPT
	
	set DESCRPT=""
	set FINISH=0

	for  do { quit:FINISH

		if %PG=0 do VPG00 if VFMQ="Q" set FINISH=1 quit
		if "DFQ"[VFMQ set FINISH=1 quit

		if %PG>0 do VPG01 if VFMQ="Q" set FINISH=1 quit
		if "DFQ"[VFMQ do VER set FINISH=1 quit
		set %PG=%PG+1
		}

	quit 

	
VPG00	// Set up screen for Group Name
     
	type public Number CNT,MAX,%PAGE  
	type public String VFMQ
	type String I,IO,%NOPRMT,%READ,%TAB()
	
	set %TAB("GRPNAME")="[UTBLCTFGRP]GRPNAME/TBL=[UTBLCTFGRP]:NOVAL/XPP=D POSGP^CTFGRP"
	
	if %ProcessMode=2 set %TAB("IO")=$$IO^SCATAB($I)
        set %READ="@@%FN,,,GRPNAME/REQ",%NOPRMT="F"
	if %ProcessMode=2 set %READ=%READ_",IO/REQ"
	do ^UTLREAD 
	if VFMQ="Q" quit
        
	if %ProcessMode=2,IO'=$I do OPEN^SCAIO
	
	do LOAD
	if (%ProcessMode=2)!(%ProcessMode=3) do {
		set %PAGE=CNT\MAX
		if CNT#MAX set %PAGE=%PAGE+1
		}
 	quit
        

VPG01	// Screen Set Up for data entry

	type public Number MAX,%PG
	type public String GRPNAME
	type Number %MODS,%REPEAT 
	type String SID

	// Always a full page in modify or insert mode
	set %REPEAT=MAX            
	set %MODS=(MAX*(%PG-1))+1
        
	type RecordUTBLCTFGRP fCTFGRP=Db.getRecord("UTBLCTFGRP","GRPNAME=:GRPNAME",1)

	set SID="UTBLCTFGRP"

	do DRV^USID(%ProcessMode,SID,.fCTFGRP)
	quit
	

LOAD    // Loop through UTBLCTFGRP1 file to create array for screen display
	type public Number CNT
	type public String DESCRPT,DUP(),ENDDATE(),GRPNAME,PLN()
	type Number CALCORD,cnt,X
	type String CTFPLN,ODESCRPT
	
	set X="",cnt=0
	
	type ResultSet rs=Db.select("GRPNAME,CTFPLN,CALCORD","UTBLCTFGRP1","GRPNAME=:GRPNAME")   
		while rs.next() do {
		
		set GRPNAME=rs.getCol("GRPNAME")
		set CTFPLN=rs.getCol("CTFPLN")          
		set CALCORD=rs.getCol("CALCORD")
		set cnt=cnt+1
		
		if CALCORD="" do Db.delete("UTBLCTFGRP1","CTFPLN=:CTFPLN AND GRPNAME=:GRPNAME") quit
		set PLN(CALCORD)=CALCORD_"|"_CTFPLN
		set DUP(CTFPLN)=""                     //check for duplicates
	 	
	 	type RecordUTBLCTFPLN ctfpln=Db.getRecord("UTBLCTFPLN","CTFPLN=:CTFPLN",1)
	 	set ENDDATE(CTFPLN)=ctfpln.enddate
		
		}

	if (%ProcessMode=2)!(%ProcessMode=3) set CNT=cnt
	 
	type RecordUTBLCTFGRP ctfgrp=Db.getRecord("UTBLCTFGRP","GRPNAME=:GRPNAME",1)
	set DESCRPT=ctfgrp.descr
	set ODESCRPT=DESCRPT
	
	quit


POSGP	//Post Processor for Group Name prompt
	
	type public Boolean tvar
	type public Number X
	type Number I()
	
	// Group ~p1 already exists
        if tvar.exists()&(Db.isDefined("UTBLCTFGRP","X")) do Runtime.setErrMSG("UTBLCTFGRP",1171,X) quit 
	
	if tvar.exists()&'(Db.isDefined("UTBLCTFGRP","X")) set I(3)=""
	
	quit
	
	
ERR	
	
	type public Boolean ER
	type public String VFMQ
	
	set ER=1 do ^UTLERR
	set VFMQ="Q"
	
	quit
        
        
VER 	
	type public String VFMQ
	
	if (%ProcessMode=2)!(%ProcessMode=4)!(VFMQ="Q") do END quit
	
	if %ProcessMode=3 do DELETE do END quit
	
	do FILE
	
	do END

	quit
        
        
FILE	//  File data
	
	type public Number CALCORD,cnt
	type public String CTFPLN,DESCRPT,GRPNAME,ODESCRPT,PLN()
	
	do PREFILE
	
	// Loop through array and set the calculation order

	set cnt=0

	for  set cnt=PLN(cnt).order() quit:cnt=""  do {
		
		set CALCORD=+PLN(cnt)
		set CTFPLN=PLN(cnt).piece("|",2)
		if CTFPLN="" quit
		
		type RecordUTBLCTFGRP1 ctfgrp1=Db.getRecord("UTBLCTFGRP1","GRPNAME=:GRPNAME,CTFPLN=:CTFPLN",1)
		set ctfgrp1.grpname=GRPNAME
		set ctfgrp1.ctfpln=CTFPLN
		set ctfgrp1.calcord=CALCORD
		
		do ctfgrp1.save()
			
		}
		
	if DESCRPT'=ODESCRPT.get() do {
	
		type RecordUTBLCTFGRP ctfgrp=Db.getRecord("UTBLCTFGRP","GRPNAME=:GRPNAME",1)
		set ctfgrp.grpname=GRPNAME
		set ctfgrp.descr=DESCRPT
		do ctfgrp.save()
		
		}
		
	do REMOVE
	
	quit


PREFILE	// Check for multiple plans with same calculation order
	
	type public Number MAX
	type public String ORDER(),PLN(),%REPEAT
	type Number cnt,lseq,NCALCORD,num,repaint
	
	set repaint=0
	set cnt=0
	set num=""
	
	for  set num=PLN(num).order() Q:num=""  set cnt=cnt+1
	for lseq=1:1:cnt D BORDER

	set NCALCORD=0

	if repaint do {

		 set %REPEAT=MAX 
		 do DISPLAY^DBSMACRO("ALL","","0")

		}

	quit

	
BORDER	// Sort calculation orders (Build ORDER array) 

	type public Number lseq,repaint
	type public String ORDER(),PLN()
	type Number CALCORD
	
	set CALCORD=+PLN(lseq)
	if 'CALCORD quit

	if 'ORDER(CALCORD).exists() set ORDER(CALCORD)=lseq
	else  do {

		set ORDER(ORDER(CALCORD+1).order(-1)+.001)=lseq
		set repaint=1

		}

	quit


REMOVE	// Delete any plans still in the user table that are not in the array
	
	type public Boolean ER 
	type public Number PLN()
	type public String CTFPLN,GRPNAME,ZPLN()
	type Number CALCORD,cnt

	set cnt=""

	for  set cnt=PLN(cnt).order() quit:cnt=""  do {
		set CALCORD=+PLN(cnt)
		if (CALCORD=0)!(CALCORD="") quit
		set CTFPLN=PLN(cnt).piece("|",2)
		if CTFPLN="" quit
		set ZPLN(CTFPLN)=CALCORD
		}

	set CTFPLN=""

	type ResultSet rs=Db.select("CTFPLN,CALCORD","UTBLCTFGRP1","GRPNAME=:GRPNAME") 
		if rs.isEmpty() quit
		while rs.next() do {
			set CTFPLN=rs.getCol(1)
			set CALCORD=rs.getCol(2)
			if 'ZPLN(CTFPLN).exists() do {
				do Db.delete("UTBLCTFGRP1","GRPNAME=:GRPNAME AND CTFPLN=:CTFPLN AND CALCORD=:CALCORD") quit:ER
				}
			}
	quit 


DELETE	// Delete a Group and its associated plans from UTBLCTFGRP1 table.

	type public String CTFPLN,GRPNAME

	if %ProcessMode=3 do {
		set CTFPLN=""
        	type ResultSet rs=Db.select("GRPNAME,CTFPLN","UTBLCTFGRP1","GRPNAME=:GRPNAME")
			if rs.isEmpty() quit
			while rs.next() do {
				set GRPNAME=rs.getCol(1)
				set CTFPLN=rs.getCol(2)
				do Db.delete("UTBLCTFGRP1","GRPNAME=:GRPNAME AND CTFPLN=:CTFPLN")
				}
		}
	do Db.delete("UTBLCTFGRP","GRPNAME=:GRPNAME")
		
	quit
	
	
END     //  Display return Messages
	
	type public String ER,GRPNAME,VFMQ
	
        quit:ER.get()!(%ProcessMode=2)!(%ProcessMode=4)
        
	set GRPNAME=GRPNAME.get()
        
        if VFMQ="Q" do {
		//Table ~p1 not created
		if %ProcessMode=0 do Runtime.setErrMSG("UTBLCTFGRP",2593,GRPNAME) quit
		//Table ~p1 not modified
		if %ProcessMode=1 do Runtime.setErrMSG("UTBLCTFGRP",2596,GRPNAME) quit
		//Table ~p1 not deleted
        	do Runtime.setErrMSG("UTBLCTFGRP",2595,GRPNAME)
		}
	else  do {
		//Table ~p1 created
		if %ProcessMode=0 do Runtime.setErrMSG("UTBLCTFGRP",2590,GRPNAME) quit
		//Table ~p1 modified
		if %ProcessMode=1 do Runtime.setErrMSG("UTBLCTFGRP",2592,GRPNAME) quit
		//Table ~p1 deleted
		do Runtime.setErrMSG("UTBLCTFGRP",2591,GRPNAME)
		}
	set ER="W"
	
	quit

vSIG()	quit "60201^38444^Renga SP^6895"	// Signature - LTD^TIME^USER^SIZE
