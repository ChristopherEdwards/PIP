TTXPOS	// Universal Batch Posting Driver
	
	/*
		ORIG:  Frank R. Sanchez (2497)
	Procedure ID:  TTXPOS     
		DESC:  Universal Batch Posting Driver	
	       CALLS:  ^SCADAT1,^TTXP1

	    GLOBALS -  ^SCAU,^T,^TTX
		READ:  ^TTX,^SCAU
		 SET:  ^T

	    EXT ENTRY:  %EXT
			SRC


	---- Revision History ------------------------------------------------
	
	03/14/05 - SmithCD - CR 13782
		   . Replaced TEMPTTXPOS references (table is now obsolete) 
		     with generic TMPRPT2 references, and made use of 
		     ADDTLR^TTXSRTPS function
		   . Streamlined %EXT section to handle external calls better
		   . Added /HLP attribute in %TAB fields
		   . Replaced cuvar (cache) with CUVAR references
		   . Modified to comply with current PSL standards
	
	02/01/05 - SmithCD - CR 13661
		   Removed the TTX part of the to ^TTXP1 to allow for the 
		   removal of that line tag in ^TTXP1 (this was the only call 
		   to it in core).
	
	11/10/04 - JERUCHIMC - 13035
	           Database Indepedence project - Replace isDefined/getRecord
	           with getRecord (w/ 3rd argument) and getMode.
	
	12/29/03 - CARROLLJ - CR7658
		   PSL conversion cleanup.	
		   
	07/26/02 - YENDAPALLIS - 43583 
		   Converted to PSL
		   
	----------------------------------------------------------------------
	*/

	type String %READ, %TAB, I(), TOPT, VFMQ
	type Boolean REQ()
	type Number BOPT, OLNTB, TBRCD()
	type Date TPD
	
	set TPD = %SystemDate
	
	set %TAB("BOPT") = ".BOPT1/HLP=[TTX]BRCD/TBL=[TTX]BRCD:DISTINCT:QUERY ""[TTX]TJD=<<TPD>>""/XPP=D BRCDPP^TTXPOS"
	set %TAB("TOPT") = ".TOPT1/HLP=[TTX]UID/TBL=[TTX]UID:DISTINCT:QUERY ""[TTX]TJD=<<TPD>> AND [TTX]BRCD=<<BOPT>>""/XPP=D TLRPP^TTXPOS"

	set %READ="@@%FN,,,BOPT/REQ,TOPT/REQ"

	do ^UTLREAD quit:VFMQ = "Q"
	
	// Posting began
	write !,$$^MSG(5480) do ^%T
		
	do %EXT(%ProcessID, BOPT, TOPT, TPD)

	quit


public %EXT(Number PROCID, Number BOPT, String TOPT, Date TPD)

	/*
	 External entry point for multiple batch posting.
	*/

	type Number %ODP, %RET, BRCD
	type String %UserID, SRC
	
	quit:PROCID.get().isNull()
	quit:BOPT.get().isNull()
	quit:TOPT.get().isNull()

	set:TPD.get().isNull() TPD = %SystemDate
	
	do Db.fastDelete("TMPRPT2", "PID=:PROCID")
	
	do BRCDT
	do TLRT
		
        type DbSet ds=Db.selectDbSet("TMPRPT2", "PID=:PROCID")
        while ds.next() do {
        	type RecordTMPRPT2 tmprpt2 = ds.getRecord("TMPRPT2")

        	set BRCD = tmprpt2.key1
        	set %UserID = tmprpt2.key2

		do SRC

		do ^TTXP1
		}
	
	do Db.fastDelete("TMPRPT2", "PID=:PROCID") 
	
	quit


public SRC	// Posting source (Also called by ^JRNLPST)

	type public Number %ODP, %RET, BRCD
	type public String SRC
	
	type RecordSCAU scau = Db.getRecord("SCAU", "UID=:%UserID", 1)
	if scau.getMode() = 1 do {
	
		//Userclass
		set %UserClass = scau.%ucls

		// Overdraft Protection
		set %ODP = scau.odp
		}
	
	else  set (%UserClass, %ODP)=""
	
	type RecordTTXUID ttxuid = Db.getRecord("TTXUID", "TJD=:%SystemDate,BRCD=:BRCD,UID=:%UserID", 1)
	if ttxuid.getMode() = 1 set SRC = ttxuid.src
	else  set SRC = ""

	// Posting Source
	if SRC.isNull() set %RET = 0 quit

	type RecordCTBLINC ctblinc = Db.getRecord("CTBLINC", "SRC")

	// Return processing
	set %RET = ctblinc.ret

	// If the return flag is on, set it to the return option (default=1).
	#IF CUVAR.RETOPT
		if %RET set %RET = CUVAR.RETOPT
	#ELSE
		if %RET set %RET = 1
	#ENDIF

	quit


BRCDT	// Create list of branches to post

	type public Number BOPT
	type public String TBRCD()
	
	if BOPT'="ALL" set TBRCD(BOPT)="" quit

	type ResultSet rs = Db.select("DISTINCT BRCD","TTX","TJD=:%SystemDate")
	while rs.next() set TBRCD(rs.getCol("BRCD")) = ""

	quit


public	BRCDPP	// Select branches to post

	type public String I(), REQ(), TOPT, X
	type public Number NI
	
	if X = "ALL" do { 
		
		set I(3) = ""
		set NI = NI + 1
		set TOPT = "ALL"
		kill REQ(NI)
		}
	quit


TLRT	// Create list of tellers to post

	type public Number BOPT
	type public String TOPT, TBRCD()
	
	type Number BRCD
	type Date DAT
	
	if TOPT'="ALL" do ADDTLR^TTXSRTPS(BOPT, TOPT) quit

	set DAT = %SystemDate
	set BRCD = ""
	
	for  set BRCD = TBRCD(BRCD).order() quit:BRCD.isNull()  do {
		type ResultSet rs = Db.select("DISTINCT UID", "TTX", "TJD=:DAT AND BRCD=:BRCD")
		while rs.next() do ADDTLR^TTXSRTPS(BRCD, rs.getCol("UID"))
		}

	quit


public	TLRPP	// Select teller to post
	
	type public String I(), X

	if X = "ALL" set I(3) = ""

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59987^38444^Chad Smith^4166"	// Signature - LTD^TIME^USER^SIZE
