MRPC086(return,versn,cid)
        /*
        Procedure ID: MRPC086
        DESC:Generates the sequence number, budget and company code
             descriptions for a look-up on the teller screen.

        KEYWORDS: RPC,budget
 
        ARGUMENTS:
                . return         		/TYP=N/REQ/MECH=REFNAM
 
                . versn          /TYP=N/REQ/MECH=VAL
        	                 current version=1
 		. cid            current account number
        RETURNS:
                . $$      Error Message                 /TYP=T
                          Null= No Error

                . RETURN        sequence number,          /TYP=N
                                budget code description,   /TYP=T
                                company code description   /TYP=T 
        RELATED:
                 . $$^PBSMRPC - MRPC Service Class Driver
 
        EXAMPLE:
                S RM=$$^MRPC086(.return,versn,cid)

	---- Revision History------------------------------------------------- 
	31/08/2005 - Srinivasan Rajesh - CR 1688
		     Replaced the deprecated method Db.getoneRow() with
		     Db.getRecord(). 	
	
	03/23/00 - ROYTENBERGE - 37767
		   Switched the order between BGTDESC and COMPDESC in calcseq 
		   section.
        //----------------------------------------------------------------------
	*/
	// Version number of client message is not compatible with server
	if versn.get()'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))

	type String str ,ER ,RM

	if cid.get().isNull() set return=""
	else  set str=$$calcseq(cid)
	
	quit:RM.get()!ER.get() $$ERRMSG^PBSUTL($G(RM))

	set return=$$V2LV^MSG(str)
	quit ""

        //-----------------------------------------------------------------

calcseq(CID)
	
	type Date BGTYRSD
	type Number BGTSEQ,BGTCD
	type String seqstr,bgtdesc,compdesc,COCD
	
	type RecordDEP dep=Db.getRecord("DEP","CID")
	
	set BGTYRSD=dep.BGTYRSD
	set seqstr = ""
	type ResultSet rsseq
	set rsseq=Db.select("BGTSEQ,BGTCD,COCD","BUDGET","CID=:CID AND BGTYRSD=:BGTYRSD")
	if rsseq.isEmpty() quit ""
	for  quit:'rsseq.next()  do {
	
			set BGTSEQ=rsseq.getCol(1)
			set BGTCD=rsseq.getCol(2)
			set COCD=rsseq.getCol(3)
			
			type RecordUTBLBGTCD utblbgtcd=Db.getRecord("UTBLBGTCD","BGTCD=:BGTCD",1)
			set bgtdesc=utblbgtcd.des
			
			type RecordUTBLBGTCOCD utblbgtcocd=Db.getRecord("UTBLBGTCOCD","BGTCD=:BGTCD,COCD=:COCD",1)
			set compdesc=utblbgtcocd.des
			
			if seqstr.get().isNull() set seqstr=BGTSEQ_$C(9)_compdesc_$C(9)_bgtdesc
			else  set seqstr=seqstr_$C(13)_$C(10)_BGTSEQ_$C(9)_compdesc_$C(9)_bgtdesc
			}
	quit seqstr

vSIG()	quit "60159^29453^Srinivasan, Rajesh^2446"	// Signature - LTD^TIME^USER^SIZE
