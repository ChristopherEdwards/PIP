PROCTRTP		/*
	  ---- Revision History-----------------------------------------------
	  01/20/2000 - DOUGANM - 31126
		Removed trtype object from argument list, since calling program
		has not yet been PSL'd.  Instead intantiated it within SELFPAY
		linetag.
	  
	  8/23/99 - DOUGANM - 33890
		Converted M code into PSL

	  --------------------------------------------------------------------
	*/
	quit

public SELFPAY(CID,TYP,CREMND,SATFP,RAMT)	// Public -- Self pay remittances.
	/*
		.cid 	Escrow account number
	   
		.typ  	Escrpw transfer Types
	   
		.cremnd Current remittance date
	   
		.satfp 	Satisfies Full pmt.
	    
			Value	Description
	      
			1  	True
	      
			0 	False
	   
		.ramt 	Amount being remitted
	  
	trtype,lntrs1 AND ramt table column variables

	*/

	new CREMFRE,PAYID,SPF,trtype
	new ESCTYP	//non-numeric escrow type variable (TRTYPE)
	new XRAMT	//current + and previous remit amounts.
	new XSATFP	//XSATFP - "A" = Actual E=Estimate
	
	/*
	Determine if the escrow account provided is self pay and therefore
	valid
	*/
	set SPF=Db.getOneRow("SPF","TRTYPE","CID,TYP")
	if SPF="" set ER=1 quit 
	
	type RecordTRTYPE trtype=Db.getRecord("TRTYPE","CID,TYP")

	set XRAMT=trtype.remptp+RAMT
	
	//Establish remit amount as an estimate until redefined.
	set XSATFP="E"
	
	//Increase the current YTD Remittance with the current remittance.
	set trtype.remytd.journal=0,trtype.remytd=trtype.remytd+RAMT
	
	/*
	Set Last remittance made on date to system date. This will record that
	a remittance was made as of the entry date.
	*/
	set trtype.ldrem.journal=0,trtype.ldrem=%SystemDate
	
	/*
	If the remittance amount does not satisfy a full payment the trtype
	table needs to reflect the total amount of partial payments which
	is the current remittance plus any previous partial remittance.
	*/
	if 'SATFP do {
		set trtype.remptp.journal=0,trtype.remptp=trtype.remptp+RAMT
		set DTE=CREMND	//Date for current remittance.
		}
	// Payment satisfies full amount.
	else  do { quit:ER 
		//Try to locate frequency in TRTYPE table.
		set CREMFRE=trtype.cremfre
	
		//If unable to locate obtain the frequency from the LNTRS1 table
		if CREMFRE="" do {
			set ESCTYP=$$TT^LNU(TYP)	//remove trailing #'s
			set PAYID=trtype.payid
			set CREMFRE=Db.getOneRow("CREMFRE","LNTRS1","ESCTYP,PAYID")
			}
	
		/*
		Set next remittance date based on the current
		remittance date and the frequency.
		*/
		set REMND=$$NJD^UFRE(CREMND,CREMFRE) quit:ER 
	
		/*
		Try to select record from the RAMT table based on next remit.
		If unsucessful, insert a new record into the RAMT table with
		the remittance amount as an estimated value.
		*/
		if 'Db.isDefined("RAMT","CID,TYP,REMND") do INS(REMND) quit:ER 
	
		/*
		Utilize the next remittance date determined earlier to update
		the remittance next date in the trtype table.
		*/
		set trtype.remnd.journal=0,trtype.remnd=REMND
	
		/*
		Set remit last paid in full date to entered (current)
		remittance date in the trtype table.
		*/
		set trtype.remld.journal=0,trtype.remld=CREMND
	
		/*
		Set the remittance partial payment to 0 since a full payment
		was remitted in this case.
		*/
		set trtype.remptp.journal=0,trtype.remptp=0
	
		//Reflect the current remittance as an Actual full pmt.
		set XSATFP="A"
		//Change date back to the current remittance date for RAMT.
	
		}
	/*
	First check current remit(CREMND) for existance. If it does not
	exist insert both for full and partial remittances. However,
	only insert for partial remittances, otherwise REMAMT in the RAMT
	table will be unable to determine any remainder.
	The next remit date was already inserted as an estimate in RAMT
	if it did not previously exist. If it did - do not update since
	the number is an estimate based on this routine.
	Any records in RAMT > REMND should not be affected.
	*/
	if 'Db.isDefined("RAMT","CID,TYP,CREMND") do INS(CREMND) quit:ER 
	else  do UPDT(CREMND)
	
	// Update UX array set up for the TRTYPE table.
	do trtype.save()

	quit 

	
UPDT(REMDT)	// Private -- Update RAMT table
	/*
		.REMDT  	Date variable
	
			variable	Description
	
			CREMND	- Current remittance date variable
			REMND	- Next remittance date variable
	*/
	set REMDT=$G(REMDT)
		
	do Db.update("RAMT","REMAMT=:XRAMT,ESTFLG=:XSATFP","CID=:CID AND TYP=:TYP AND REMDT=:REMDT") 

	quit 

	
INS(REMDT)	// Private --  Insert into RAMT table.
	/*
		.REMDT		Date variable
			CREMND	- Current remittance date variable
			REMND	- Next remittance date variable
	*/
	do Db.insert("RAMT","CID,TYP,REMDT,REMAMT,ESTFLG",":CID,:TYP,:REMDT,:XRAMT,:XSATFP")

	quit 
	

vSIG()	quit "59886^43619^Sanchez SCM Administrator^4396"	// Signature - LTD^TIME^USER^SIZE
