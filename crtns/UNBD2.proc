UNBD2		/*
	ORIG: alagarss - 08/26/2005
	DESC: Add/Delete Non-Business Dates to Calenda

	---- Comments --------------------------------------------------------

	---- Revision History ------------------------------------------------

	06/09/06 - KELLYP - CR 21711
		   Modified FILE section to only delete UTBLNBD records b/w 
		   the dates represented by the BY and EY variables.  The
		   functions used by this procedure are for setting NBD's
		   for a specificied calendar year and the Db.delete in the
		   FILE section was wiping out NBD's for other calendar
		   years.

	01/19/06 - TITOVE - CR 19135
		   Modified FILE section to reset record mode for updates.
		   Brought up to standards.

	08/26/05 - ALAGARSS - CR16677
		   Copied from Bilbo V70DEV.
		   Also removed the revision history prior to 2004.

	----------------------------------------------------------------------
	*/
	quit
	
 
public UPD
        do INIT(1)
        quit
        

public INQ

        do INIT(2)
        quit
        
 
INIT(Number %ProcessMode)
        
        type Date BY,EY
	type Number %PG, %PAGE, OLNTB, YEAR
	type String NBDC, VFMQ
	
        set %PG = 0
        set %PAGE = 1
        
        type RecordUTBLNBD1 UTBLNBD1()
        type RecordUTBLNBD fUTBLNBD
        
        do VPG(.UTBLNBD1(),.fUTBLNBD)
        
        quit
        

VPG(RecordUTBLNBD1 UTBLNBD1(),RecordUTBLNBD fUTBLNBD)   //
        
	type public Number %PG, ER
        type public String VFMQ

        type Boolean FINISH = 0
        
	for  do { quit:FINISH
                
		if (%PG = 0) do VPG00 if ER!(VFMQ = "Q") set FINISH = 1 quit
		
                if (%PG > 0) do VPG01(.UTBLNBD1(),.fUTBLNBD) if ER set FINISH = 1 quit
                
                if "DFQ"[VFMQ do VER(.UTBLNBD1(),.fUTBLNBD) set FINISH = 1 quit
                
                set %PG = %PG + 1
	        }
	        
	quit
	

VPG00   // Set up
	
	type public String %READ, %TAB, VFMQ
	type public Number IO
	
        set %TAB("NBDC") = ".NBDC1/HLP=[UTBLNBD]NBDC/TBL=[UTBLNBD]"
        set %TAB("YEAR") = ".YEAR1/MIN=1950/MAX=2050"
        set %TAB("IO") = $$IO^SCATAB($I)
        
        set %READ = "@@%FN,,,NBDC/REQ,YEAR/REQ"
        
        if (%ProcessMode = 2) set %READ = %READ_",,IO/REQ"

        do ^UTLREAD
        
        if VFMQ = "Q" quit
        
	if (%ProcessMode = 2),(IO '= $I) do OPEN^SCAIO
	
        quit

VPG01(RecordUTBLNBD1 UTBLNBD1(),RecordUTBLNBD fUTBLNBD) // Screen
        
	type public Date BY, EY
	type public String %DS, NBDC
        type public Number YEAR
        
        type Number SEQ = 0

        set %DS = "12/31/"_(YEAR - 1)
        set BY = $$^SCAJD(%DS)
        set %DS = "12/31/"_YEAR
        set EY = $$^SCAJD(%DS)
        
        type DbSet ds = Db.selectDbSet("UTBLNBD1","NBDC=:NBDC AND NBD>:BY AND NBD<=:EY")
        
	while ds.next() do {
                
		set SEQ = SEQ + 1
		
                set UTBLNBD1(SEQ) = ds.getRecord("UTBLNBD1")                 
		} 
       
	set fUTBLNBD = Db.getRecord("UTBLNBD", "NBDC = :NBDC")
	
        do DRV^USID(%ProcessMode,"UTBLNBD1",.UTBLNBD1(),.fUTBLNBD)
        
        quit
        

VER(RecordUTBLNBD1 UTBLNBD1(),RecordUTBLNBD fUTBLNBD)     //
	
	type public String VFMQ
	
        if (%ProcessMode = 2)!(%ProcessMode = 4)!(VFMQ = "Q") do END quit
        
        do FILE(.UTBLNBD1(),.fUTBLNBD)
        
        do END
        
        quit
        

FILE(RecordUTBLNBD1 UTBLNBD1(),RecordUTBLNBD fUTBLNBD)  // File data

        type public Date BY,EY
	type public String NBDC
	
	type Number SEQ = 0
	
        do fUTBLNBD.save()
        
        do Db.delete("UTBLNBD1","NBDC=:NBDC AND NBD>:BY AND NBD<=:EY")
        
	for  set SEQ = UTBLNBD1(SEQ).order() quit:SEQ.isNull()  do {
                
		set UTBLNBD1(SEQ).nbdc = NBDC
		
                if UTBLNBD1(SEQ).nbd.isNull() quit
                
		// Update of type "Delete then Insert", need to reset mode
		if (%ProcessMode = 1) do UTBLNBD1(SEQ).setMode(0)
		
                do UTBLNBD1(SEQ).save()                
		}		

        quit
        

END     //
	
	type public String ER, NBDC, RM, VFMQ
	
        if ER!(%ProcessMode = 2)!(%ProcessMode = 4) quit
        
        if (VFMQ = "Q") do {
                
		if NBDC.exists() set RM = "" quit

                // Calendar ~p1 not modified
                set RM = $$^MSG(407,NBDC)               
		}
        // Calendar ~p1 modified
        else  set RM = $$^MSG(405,NBDC)
        
        set ER = "W"
        
        quit
        

vSIG()	quit "60425^37776^Pat Kelly^4297"	// Signature - LTD^TIME^USER^SIZE
