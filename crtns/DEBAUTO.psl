DEBAUTO  //
	  /*
	
	   ORIG: ZWITKOWITSM - 04/10/00
	
	   DESC: Debit Authorization Override Setup
	
	   This routine when used with screen DEBAUTO will create, modify,
	   and delete entries in the UTBLDEBAUTO table.
	
	  ---- Revision History ------------------------------------------------
	   04/08/02 - CHHABRIAS - 49451
		      Converted to PSL
	
	   04/10/00 - ZWITKOWITSM - 38035
	              Brought up this routine from V446.  Made several
	              modifications to use ^DBSFILER instead of direct global
	              sets and to correct hard coded return messages.
	
	  ---------------------------------------------------------------------
	
	*/


INIT	//

	new OLNTB,VFMQ
	set %PG=1
	set %PAGE=100
	set %ProcessMode=1
	set pg=0
	set upd=0
	do SET

	do VPG

	quit


VPG     // Page control

	new FINISH
        set FINISH=0
        for  do { quit:FINISH
                do VPG01

                if "DFQ"[VFMQ do VER set FINISH=1 quit

                set %PG=%PG+1
		set pg=%PG-1
                }
        quit


VPG01	// Screen

	// Load data
	do LOAD     

	do DRV^USID(%ProcessMode,"DEBAUTO")

	// Save data
	do SAVE     

	quit 
	

LOAD	//

	set %REPEAT=10
	set start=pg*10+1
	set end=pg*10+10
	set cnt=0
	for I=start:1:end do {
		set cnt=cnt+1
		set debit(cnt)=$G(master2(I))
		}
	quit 

	
SAVE	//

	set start=pg*10+1
	set end=pg*10+10
	set cnt=0
	for I=start:1:end do {
		set cnt=cnt+1
		set master2(I)=$G(debit(cnt))
		}
	quit 

	
SET	// Set values into master array

	new ovr
	set cnt=0

	type DbSet rs=Db.selectDbSet("UTBLDEBAUTO")
	while rs.next() do {
		type RecordUTBLDEBAUTO fUTBLDEB=rs.getRecord("UTBLDEBAUTO")
		set ovr=fUTBLDEB.prenum
		set cnt=cnt+1
		set master(ovr)=fUTBLDEB.des
		set master2(cnt)=$P(ovr,"-",1)_"|"_$P(ovr,"-",2)_"|"_fUTBLDEB.des
		}
	quit
	

VER 	//

	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit

	do FILE

	do END
	quit
	

FILE	// File data

	new ovr,x,z
	new %ProcessMode
	set (ovr,x,z)=""

	// Move master2 to master3
	for  set ovr=$O(master2(ovr)) quit:ovr=""  do {
		set x=master2(ovr)
		set z=$P(x,"|",1)_"-"_$P(x,"|",2)
		if ($P(z,"-",1)=""),($P(z,"-",2)="") quit
		set master3(z)=$P(x,"|",3)
		}
	
	// Compare master to master3 for deletions or modifications
	for  set ovr=$O(master(ovr)) quit:ovr=""  do {

		// Delete
		if '$D(master3(ovr)) do { quit
			set %ProcessMode=3
			set upd=1
			do Db.delete("UTBLDEBAUTO","PRENUM=:ovr")
			}
		
		// Modify
		if master(ovr)'=master3(ovr) do { quit
			set %ProcessMode=1
			type RecordUTBLDEBAUTO fUTBLDEB=Db.getRecord("UTBLDEBAUTO","PRENUM=:ovr")
			set fUTBLDEB.des=master3(ovr)
			set upd=1
			do fUTBLDEB.save()
			}
		}
	
	// Compare master3 to master for creations
	for  set ovr=$O(master3(ovr)) quit:ovr=""  do {
		if $D(master(ovr)) quit 
		set %ProcessMode=0
		set upd=1

		// Create
		type RecordUTBLDEBAUTO fUTBLDEB=Class.new("RecordUTBLDEBAUTO")
		set fUTBLDEB.prenum=ovr
		set fUTBLDEB.des=master3(ovr)
		do fUTBLDEB.save()
		}
	quit 
	

END	//

	kill %TAB lock 
	if $G(ER)!(%ProcessMode=2)!(%ProcessMode=4) quit

	set ER="W"
	if (%ProcessMode=0)!(%ProcessMode=1) do {
		if %ProcessMode=0 set RM=$$^MSG($S(upd:2590,1:2593),"UTBLDEBAUTO") quit
		if %ProcessMode=1 set RM=$$^MSG($S(upd:2592,1:2596),"UTBLDEBAUTO")
		}
	else  do {
		set RM=$$^MSG($S(upd:2591,1:2595),"UTBLDEBAUTO")  
		}

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43506^Sanchez SCM Administrator^3140"	// Signature - LTD^TIME^USER^SIZE
