public	IRAYEBAL	// IRA Balance at Year-End
	/*

	   INPUT:
	       . ACN 		Customer number		/TYP=N/REQ
	       . RPASEQ 	IRA plan type   	/TYP=N/REQ
	       . TAXYR 		Tax year   		/TYP=N/REQ

	   OUTPUT:
	       . IBAL 		IRA year-end balance
	       . FMV 		Fair Market Value 
			  (IBAL + accrued interest at end of year)

	---- Comments --------------------------------------------------------

	I18N=QUIT: Excluded from I18N standards.

	---- Revision History ------------------------------------------------

	11/01/05 - TITOVE - CR 17287
		   Cleaned up and corrected logic. Removed CID and CURRYR
		   sections and old revision history.

	*/
		
	type public Number ACN, FMV, IBAL, RPASEQ, TAXYR

	set (FMV,IBAL) = ""

	type Boolean CURRYR = 0
	type Date BOTY, EOTY
	type Number CID

	// If TAXYR passed equals to the end of Tax year as of system date,
	// this is current year	
	if TAXYR = $$YEAR^SCADAT($$EOTY^SCADAT(%SystemDate,1),1) set CURRYR = 1
	
	else  do {
	
		// Beginning of reporting tax year
		set BOTY = $$BOTY^SCADAT($$BOTY^SCADAT(%SystemDate,1)-1,1)
	
		// End of reporting tax year
		set EOTY = $$EOTY^SCADAT(BOTY,1)
		}

	// Find each RPA account for the customer	
	type ResultSet rs = Db.select("CID", "RELCIF", "ACN = :ACN")
 
	if rs.isEmpty() quit
 
        while rs.next() do {
        	
        	set CID = rs.getCol("CID")
        	
		type RecordDEP dep = Db.getRecord("DEP", "CID = :CID", 1)
		
		if 'dep.getMode() quit		// Not a deposit account
		
		if (dep.rpaseq '= RPASEQ) quit
		
                if (dep.acn '= ACN) quit
	
                if CURRYR set IBAL = IBAL + dep.bal quit
	
                do PREVYR(CID,EOTY)
		} 
 

	// Round to the cents
	set FMV = FMV.roundDec()

	quit


PREVYR(Number CID,
       Date EOTY)
       
       	// Previous Year's Balance and Fair Market Value
	
	type public Number FMV, IBAL

	type RecordHIST0 hist0 = Db.getRecord("HIST0", "CID = :CID,JD = :EOTY", 1)
	
	if 'hist0.getMode() quit 

	// Add in ACR at EOTY
	set FMV = FMV + hist0.bal + hist0.acrcf

	// Account balance at EOTY
	set IBAL = IBAL + hist0.bal

	quit

vSIG()	quit "60204^55670^Eugene Titov^1953"	// Signature - LTD^TIME^USER^SIZE
