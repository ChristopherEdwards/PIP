SRVPLN2A	//Copy a current Service Fee Transaction Code Group to a New Transaction Code
	/*
	---- Revision History ------------------------------------------------
	
	08/23/05 - RussellDS - CR16911
		   		   
		   Rewrite to get it to work and clean up code.
		   
		   Removed old revision history.
	*/
	
	quit


CPY	// Copy

	type public String ER, RM

	type Date TRNGRPDN, TRNGRPDT
	type String %READ, %TAB(), ET, TRNGRP, TRNGRPNW, UTBL(,), VFMQ

	set %ProcessMode = 0
	
	set %TAB("TRNGRP")=".TRNGRP1/HLP=[UTBLTRNGRPT]TRNGRP/TBL=[UTBLTRNGRPT]TRNGRP:DISTINCT"
	set %TAB("TRNGRPDT")=".EFD1/TBL=[UTBLTRNGRPT]TRNGRPDT:QU ""[UTBLTRNGRPT]TRNGRP=<<TRNGRP>>"""
	set %TAB("TRNGRPNW")=".TRNGRPNW1/HLP=[UTBLTRNGRPT]TRNGRP/XPP=D POSPLN^SRVPLN2A"
	set %TAB("TRNGRPDN")=".EFD1/XPP=D CHKDT^SRVPLN2A"

	set %READ="@@%FN,,,TRNGRP/REQ,TRNGRPDT/REQ,,TRNGRPNW/REQ,TRNGRPDN/REQ"
	do ^UTLREAD quit:VFMQ = "Q"

	// Record locked by another user
	lock +UTBL("FEE","TRNGRP",TRNGRP):2 else  set ET="RECLOC" do ^UTLERR quit
	
	// Record locked by another user
	lock +UTBL("FEE","TRNGRP",TRNGRPNW):2 else  do { quit
		
		lock -UTBL("FEE","TRNGRP",TRNGRP)
		set ET="RECLOC"
		do ^UTLERR
	}

	do FILE quit:ER
	
	// Tran Group ~p1 copied to Tran Group ~p2
	set RM = $$^MSG(4544,TRNGRP,TRNGRPNW)
	set ER="W"

	quit


POSPLN	//

	type public Boolean ER
	type public String RM, X

	if 'X.isNull(), X?.E1P.E do {
	
		set ER = 1
		// Punctuation characters not allowed
		set RM=$$^MSG(2281)
	}

	quit


CHKDT	//

	type public Boolean ER
	type public String RM, X

	quit:X.isNull()
	
	if (X < %SystemDate) do {
		
		set ER=1
		// Effective date must be the same as or after the system date
		set RM=$$^MSG(879)
	}
	
	else  if Db.isDefined("UTBLTRNGRPT","TRNGRP=:TRNGRPNW,TRNGRPDT=:X") do {
	
		set ER = 1
		// Tran Group already on file
		set RM=$$^MSG(4548)
	}

	quit


FILE	// File data

	type public Date TRNGRPDN, TRNGRPDT
	type public String TRNGRP, TRNGRPNW
	
	type RecordUTBLTRNGRPT utbltrngrpt = Db.getRecord("UTBLTRNGRPT","TRNGRP=:TRNGRP,TRNGRPDT=:TRNGRPDT")

	type RecordUTBLTRNGRPT newtrngrpt = utbltrngrpt.copy()
	set newtrngrpt.trngrp = TRNGRPNW
	set newtrngrpt.trngrpdt = TRNGRPDN
	do newtrngrpt.setMode(0)
	do newtrngrpt.save()

	type ResultSet rs = Db.select("TRNCD","UTBLTRNGRP","TRNGRP=:TRNGRP AND TRNGRPDT=:TRNGRPDT")
	while rs.next() do {
		
		type String TRNCD
		
		set TRNCD = rs.getCol("TRNCD")

		type RecordUTBLTRNGRP utbltrngrp = Db.getRecord("UTBLTRNGRP","TRNGRP=:TRNGRP,TRNGRPDT=:TRNGRPDT,TRNCD=:TRNCD")

		type RecordUTBLTRNGRP newtrngrp = utbltrngrp.copy()
		set newtrngrp.trngrp = TRNGRPNW
		set newtrngrp.trngrpdt = TRNGRPDN
		set newtrngrp.trncd = TRNCD
		do newtrngrp.setMode(0)
		do newtrngrp.save()
	}
	
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60139^42764^Dan Russell^2504"	// Signature - LTD^TIME^USER^SIZE
