public LNCALG(RecordLN ln)	// CALCULATE GEM & IEM MORTGAGE PAYMENTS
	/*
	       ORIG:  Chuck Hardy (6721) - 04/12/86

	---- Revision History ------------------------------------------------
	
        10/19/05 - S.Krishnan - CR16885
		   Converted the ^UFRE calls to .nextFreqDate() method.
		   Made top of the procedure as public.
		   
        07/26/05 - SkariahV- CR16679
	           Removed #WARN and #OPTIMIZE directives.
	              
	06/20/03 - GRAY - 51351
		   Corrected error on PSL warning error, unscoped variable,I.
 
	03/27/03 - GRAY - 51351
		   Converted to PSL.


	*/


	type Public Boolean STOP
	type Public Number CID,ER,PMTPI
	type Date JD,NJD
	type Number AF,AMBAS,BAL,CAF,I,IRN,LAF,NUMCH,PPINC,PRIN,X
	type String CLS,DIST1FRE,FRE,LNGEM(),MTY,PCFRE

	if STOP.data() quit

	set CLS="L"

	// Payment Change Method
	set X=ln.pchm 
	set MTY=$S(X=2:"GPM",X=3:"IEM",1:"")

	// Amortization Base
	set AMBAS=+ln.ambas

	// Original Number of Payments
	if '$L(ln.onp) set ln.onp=AMBAS

	// Number of Payment Changes
	set NUMCH=ln.numch

	// Payment Change Frequency
	set PCFRE=ln.pcfre

	// Distribution 1 Frequency
	set DIST1FRE=ln.dist1fre

	// Payment Percentage Increase
	set PPINC=ln.ppinc
	
	set IRN=+ln.irn

	set (PRIN,BAL)=+ln.bal

	// Distribution 1 Annual Factor
	set (LAF,AF)=ln.dist1af

	// Distribution 1 Annual Factor
	if 'AF do { quit:ER
		set FRE=DIST1FRE 
		set JD=%SystemDate
		set NJD=JD.nextFreqDate(FRE)
		set LAF=AF 
		set ln.dist1af=AF
		}

	// Number of Payments Per Change
	set FRE=PCFRE 
	set JD=%SystemDate 
	set NJD=JD.nextFreqDate(FRE)
	
	set CAF=LAF/AF 
	set ln.caf=CAF

	if ln.pchm=2 do ^LNGEM(.ln) quit

	do CALC1^LNCALP(.ln)

	do ^LNIEM(.ln)

	for I=1:1:NUMCH+1 do {
		set LNGEM(I)=I_"|"_PMTPI
		set PMTPI=$$^SCARND(PMTPI*(1+PPINC),0,CID.get())
		}

	// Principal and Interest Payment
	set ln.pmtpi=LNGEM(1).piece("|",2)

	quit

vSIG()	quit "60199^27495^S. Krishnan^1716"	// Signature - LTD^TIME^USER^SIZE
