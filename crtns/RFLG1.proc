public	RFLG1
	/*

	---- Revision History ------------------------------------------------

	   02/02/06 - TITOVE - CR 19274
	   	      Modified to correct prompt data selection and data
	   	      insert issues.

	   04/28/05 - TITOVE - CR 15221
	   	      Modified as part of DBI2 project.

	   10/04/02 - SRIVASTAVAN - 49451
		      Converted to PSL
	-----------------------------------------------------------------------
	*/

	// Insert/Update

	do INIT(1)
	
	quit


public	INQ	// Inquiry

	do INIT(2)
	
	quit


public	DEL	// Delete

	do INIT(3)
	
	quit


INIT(%ProcessMode)
	new OLNTB,VFMQ

	type RecordUTBLRFL1 fUTRFL1

	set %PG=0 
	set %PAGE=1

	do VPG(.fUTRFL1)

	quit


VPG(RecordUTBLRFL1 fUTRFL1)	// Page control

	type public Number %PG, ER
	type public String VFMQ

	type Boolean FINISH = 0

	for  do { quit:FINISH
		
		if (%PG = 0) do VPG00 if ER!(VFMQ = "Q") set FINISH = 1 quit
			
		if (%PG > 0) do VPG01(.fUTRFL1)
		
 		if "DFQ"[VFMQ do VER(.fUTRFL1) set FINISH = 1 quit
 		
		set %PG = %PG + 1
		}
		
	quit


VPG00	// Set up

	set %TAB("GRP") = ".GRP1/TBL=[STBLLIBS]NAME,DESC:DISTINCT:QU"
	set %TAB("RFLG") = ".RFLG1/TBL=RFLDESC(/XPR=D PR^RFLG"
	set %TAB("UCLS") = ".UCLS4/TBL=[UTBLRFL1]UCLS:DISTINCT:QU ""[UTBLRFL1]GRP=<<GRP>> AND [UTBLRFL1]RFLG=<<RFLG>>""/XPP=D PP^RFLG1" 

	if (%ProcessMode = 2) do {
		
		set %TAB("UCLS") = ".UCLS4/TBL=[UTBLRFL1]UCLS:DISTINCT:QU ""[UTBLRFL1]GRP=<<GRP>> AND [UTBLRFL1]RFLG=<<RFLG>>""/XPP=D PP^RFLG1"
		set %TAB("IO") = $$IO^SCATAB($I)
		}

	set %READ = "@@%FN,,,GRP/REQ,RFLG/REQ,UCLS/REQ" 

	set %NOPRMT = "N"

	if (%ProcessMode = 2) set %READ=%READ_",,IO/REQ"

	do ^UTLREAD 
	
	if (VFMQ = "Q") set ER = 1 quit
	
        do PPG00

	quit


PP	// UTLREAD post processor

	if X.isNull() quit

	set KEYL(1) = "RFLG"
	set KEYL(2) = X
	set KEYL(3) = RFLG

	do ALL^UTBLOVR 
	
	if (%OSAVE.get() > 1) quit

	if Db.isDefined("UTBLRFL1","GRP=:GRP,RFLG=:RFLG,UCLS=:X") quit

	set I(3) = ""
	
	// Reset mode indicator to insert
	set %ProcessMode = 0
	
	quit


VPG01(RecordUTBLRFL1 fUTRFL1)	 // Restrict screen

	if %ProcessMode=2,IO'=$I do OPEN^SCAIO if ER set VFMQ="Q" quit

	set fUTRFL1=Db.getRecord("UTBLRFL1","GRP=:GRP,RFLG=:RFLG,UCLS=:UCLS",1)

	do DRV^USID(%ProcessMode,"UTBLRFL1",.fUTRFL1)

	kill SAVTR

	quit
	
	
PPG00   // Post-processor

	type public String KEYL(), RFLG

	// These are used in ^UTBLOVR
        set KEYL(1) = "RFLG"
        set KEYL(3) = RFLG
        
        quit


VER(RecordUTBLRFL1 fUTRFL1)	//

	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit
	
	do FILE(.fUTRFL1)
	
	do END
	
	quit


FILE(RecordUTBLRFL1 fUTRFL1)	// File data

	/*
	   If all user classes are to be authorized, then a single entry will
	   be created with a * in the userclass. All other user classes will be
	   deleted.
	*/

	if (%ProcessMode = 0),(UCLS = "*") do Db.delete("UTBLRFL1","GRP=:GRP AND RFLG=:RFLG")

	if (%ProcessMode = 0) ! (%ProcessMode = 1) do fUTRFL1.save() quit
	
	do Db.delete("UTBLRFL1","GRP=:GRP AND RFLG=:RFLG AND UCLS=:UCLS")

	quit


END	//

	if ER!(%ProcessMode=2)!(%ProcessMode=4) quit

	if VFMQ="Q" do {

		// User-defined restriction ~p1.  Userclass authorization ~p2 not created.
		if (%ProcessMode = 0) set RM=$$^MSG(2892,RFLG,UCLS) quit

		// User-defined restriction ~p1.  Userclass authorization ~p2 not modified.
		if %ProcessMode=1 set RM=$$^MSG(2894,RFLG,UCLS) quit

		// User-defined restriction ~p1.  Userclass authorization ~p2 not deleted.
		set RM=$$^MSG(2893,RFLG,UCLS)
		}
	else  do {

		// User-defined restriction ~p1.  Userclass authorization ~p2 created.
		if (%ProcessMode = 0) set RM=$$^MSG(2889,RFLG,UCLS) quit

		// User-defined restriction ~p1.  Userclass authorization ~p2 modified.
		if %ProcessMode=1 set RM=$$^MSG(2891,RFLG,UCLS) quit

		// User-defined restriction ~p1.  Userclass authorization ~p2 deleted.
		set RM=$$^MSG(2890,RFLG,UCLS)
		}

	set ER="W"
	
	quit

vSIG()	quit "60298^52724^Eugene Titov^3658"	// Signature - LTD^TIME^USER^SIZE
