LNSTS	 /* 
    ORIG: GRAY - 02/09/2000
    DESC: Loan Inquiry
    
    ---- Comments --------------------------------------------------------
    
    ---- Revision History ------------------------------------------------
    
	10/26/05 - RussellDS - CR17834
 		   Removed call to DBSFILER in FILE section.  The screen
 		   LIS, which is the only one now called, does not update
 		   CIF records, so references to CIF removed.  If this
 		   changes in the future, should be done using standard
 		   object references.
 		   
	02/08/05 - MBUIM - 13273
		   Modified to restructure INQ section due to errors related
		   to the structure of code in the screen driver CHKPRT.proc.
		   PSL compiler was dropping lines of code that it was 
		   optimizing based on the fact that %PG was set right before 
		   it was referenced and PSL uses that value, ignoring other
		   values that it cannot be.
		   Also removed sections VPG and VPG0
    
	05/19/03 - CARROLLJ - 51349
		   Modified VPG01 section to remove use of A array.

	03/12/03 - Dan Russell - 51351
		   Removed call to obsolete ^LNEXEC, replaced with call to
		   VERIFY^LNIC.
		   
		   Removed old change history.
 */
	
	quit
		
Public INQ	//  Called by screen EFTREV (EFT Payment Review)
	
	type String OLNTB,VFMQ
	
	set %O=2,EFD=""	
	set %PG=0,%PAGE=1
	
	do VPG00 
	if "DFQ"[$G(VFMQ)!$G(ER) do VER quit
	set %PG=%PG+1
	do VPG01
	do VER
	
	quit

VPG00	// Set up
	
	set %TAB("CID")=".CID3/XPP=set %EXT=1 do ^UACN"
	set %TAB("IO")=$$IO^SCATAB($I)
	set (CLS,ZCLS)="L"
	
	set %READ="@@%FN,,,CID/REQ,IO/REQ"
	do ^UTLREAD if VFMQ="Q" quit
	if IO'=$I do OPEN^SCAIO quit:ER
	
	quit
	
VPG01	// Screen
	
	kill %PAGE

	Type RecordACN acn=Db.getRecord("ACN","CID=:CID")
	set ACN=acn.cid
	set TYPE=acn.type
	set GRP=acn.grp
	set CRCD=acn.crcd
	
	/*
	Forced to use screen LIS, Loan Inquiry - General (Short), since all
	native screen columns have been removed from the product type (#32507).
	*/
	
	set SID="LIS"				// Inquiry
	
	do ^USID if PGM="" set ET="INVLDSCR" do ERR quit
	do ^@PGM
	
	quit
	
Public CHKLN	// Called by LNCONDIL.M
	
	set ER=0
	
	new ln,AFLG,BAL,INDEX,IRN,PPF,PVAR,TYPE
	type RecordLN ln=Db.getRecord("LN","CID")
	
	set PPF=ln.ppf			// loading of node 49
	set TYPE=ln.type		// loading of node 50
	set BAL=ln.bal			// loading of node 51
	set IRN=ln.irn			// loading of node 57
	set PVAR=ln.pvar		// loading of node 58
	set INDEX=ln.index		// loading of node 60
	set AFLG=ln.aflg		// loading of node 85

	do ln.fromArray("LN(")
	
	if $D(UX("LN","IRN")) set X=ln.index if X'="" do { quit:ER
		set X=$P(X," ",1)
		if Db.getOneRow("BASREL","INDEX","X")=1 set ER=1,ET="IRNINDX"
		}
	
	// Verify that a PCHND has a freq and offset
	
	set FRE=ln.pcfre
	if FRE'="" set JD=%SystemDate do NJD^UFRE(JD,FRE) if AF>364 set ER=1,ET="PCFRED" quit
	if FRE="",ln.pchnd set ER=1,ET="PCFRE" quit
	if FRE'="",ln.pchnd,ln.pcoff="" set ER=1,ET="PCOFF" quit
	
	// Verify that an ICHND fas a freq
	
	set DLY=0,FRE=ln.intfre
	if FRE'="" set JD=%SystemDate do NJD^UFRE(JD,FRE) if AF>364 set DLY=1
	if FRE="",ln.ichnd set ER=1,ET="INTFRE" quit
	
	// Verify that escrow analysis has a freq and offset
	
	set FRE=ln.anfre
	if FRE="",ln.apcnd set ER=1,ET="ANFRE" quit
	if FRE'="",ln.apcnd,'ln.anoff set ER=1,ET="ANOFF" quit
	
	/* 
	Verify that capitalized deferred interest is not less than neg am
	reduction.
	*/
	
	if ln.dic<ln.narl set ER=1,ET="DIMEX" quit
	
	// Verify MINDR and ODSMTR
	
	new PRODCTL
	set PRODCTL=Db.getOneRow("ODSITR,ODSMTR","PRODCTL","TYPE")
	
	set X=ln.mindr
	if X set ODSMTR=$P(PRODCTL,$C(9),2) if ODSMTR,ODSMTR<X set ER=1,ET="ODSMTR" quit
	
	// Verify INCDR and ODSITR
	set X=ln.incdr
	if X set ODSITR=$P(PRODCTL,$C(9),1) if ODSITR,ODSITR#X set ER=1,ET="ODSITR" quit
	
	// Verify the integrity of Escrow To Round
	if $D(UX("LN","RND")) do RND if ER quit
	
	quit
	
ERR	//
	
	set ER=1 do ^UTLERR
	set VFMQ="Q"
	
	quit
	
VER	//
	
	if %O=2!(%O=4)!(VFMQ="Q") do END quit
	
	do CHKLN if ER do ^UTLERR do END quit
	
	do FILE,END
	
	quit
	
FILE	// File data
	
	do VERIFY^LNIC(CID)				
	
	if $G(%LOGID) quit
	
	if ER,$G(ET)'="" do ^UTLERR
	
	quit
	
END	//
	
	kill %TAB
	quit:ER!(%O=2)!(%O=4)
	
	set CID=$G(CID)
	
	// Account ~p1 not modified
	if VFMQ="Q" set RM=$$^MSG(135,CID)
	
	// Account ~p1 modified
	else  set RM=$$^MSG(122,CID)
	
	set ER="W"
	
	quit
	
RND	// Verify the integrity of Escrow To Round
	
	new E
	set E=$P(UX("LN","RND"),"|",2) if E="" quit
	
	if E?1E.E1N.N set E=$TR(E,"1234567890","")
	if Db.getOneRow("RND","LNTRS","E") quit
	
	set ER=1,ET="ESC2RND"
	
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60205^55300^Dan Russell^4323"	// Signature - LTD^TIME^USER^SIZE
