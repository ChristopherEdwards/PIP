UTLASTK
	/*
	ORIG: ROTELLA - 07/05/94
	DESC: Library of "wildcard" utilities

	KEYWORDS: Utility, Formatting

	LIBRARY:
	   . TRN	- Returns list of tran codes based
			  upon wildcard input.
	   . ALLDRCR	- Verify DR*, CR*

	-------- Revision History --------------------------------------------
	  
	07/14/06 - RussellDS - CR22121
		   Eliminate use of $A and $C to make Unicode compliant.
	  	     
		   Removed unused code.  Clean-up code.
	  	     
		   Removed old revision history.
	*/

	quit
	

public TRN(String ITEM,		// Wildcarded (*) tran code
	   String ETC,		// Tran code to match		/NOREQ
	   Boolean FOUND)	// Found flag for ETC match	/NOREQ/MECH=REFNAM:W
	   
	/*
	Build tran list from ETC with wildcard
	
	RETURNS:
	    . $$  Comma separated list of tran codes	/TYP=T 
		  matching selected item (with *)

	EXAMPLE:
	    . set x = $$TRN^UTLASTK("DW*")
	      write x
	      "DW,DWAD,DWIC,DWZH"
	*/
	
	type String LONGLIST, XETC, WHERE

	set LONGLIST = "" 
	set FOUND = 0
	set ETC = ETC.get()

	set XETC = ITEM.piece("*", 1)
	
	if XETC.isNull() set WHERE = ""
	else  set WHERE = "ETC LIKE '"_XETC_"%'"

	// Accept dynamic select
	#ACCEPT Date=07/14/06; Pgm=RussellDS; CR=22121
	type ResultSet rs = Db.select("ETC", "TRN", WHERE)

	while rs.next() quit:FOUND  do {
		
		set XETC = rs.getCol("ETC")
		set LONGLIST=LONGLIST_","_XETC
		
		if 'ETC.isNull(), (ETC = XETC) set FOUND = 1
	}

	quit LONGLIST.extract(2, LONGLIST.length())
	

public ALLDRCR(String ITEM,		// DR* or CR*
	       String ETC,		// Current tran code
	       Boolean OPTION)		// [*]			/NOREQ

	/*
	Verify ETC as DR or CR

	ARGUMENT NOTES
	
	    . OPTION	If = 1 then verify only that item contains DR* or CR*

	   RETURNS:
	    . $$	0 = not verified			/TYP=L
	    		1 = verified

	   EXAMPLE:
	    . if $$ALLDRCR^UTLASTK("DR*",.ETC, 1) quit

	    . if $$ALLDRCR^UTLASTK("DR*",.ETC) quit
	*/

	type Boolean RETURN
	type Number DRCR
	
	set RETURN = 0

	set OPTION = OPTION.get() 
	set ITEM = ITEM.get()
	
	if OPTION do { quit RETURN
		
		if ITEM.isLike("%DR*%") set RETURN = 1
		if ITEM.isLike("%CR*%") set RETURN = 1
	}
	
	type RecordTRN trn = Db.getRecord("TRN","ETC")
	
	set DRCR = trn.itc.extract(1)

	if (DRCR = 0), ITEM.isLike("%DR*%") set RETURN = 1
	if (DRCR = 1), ITEM.isLike("%CR*%") set RETURN = 1

	quit RETURN

vSIG()	quit "60471^63247^Dan Russell^2183"	// Signature - LTD^TIME^USER^SIZE
