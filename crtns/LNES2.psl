LNES2

	/*
	Loan Escrow Maintenance
	       ORIG:  Neal E. Gorman (5053) - 11/24/86
	  CALLED BY:
	      CALLS:
	       DESC:
	
	      INPUT:
	     OUTPUT:
	
	---- Revision History ------------------------------------------------
	
	08/11/06 - SmithCD - CR 22489 (20748)
		   Modified TT section to use ORIGTM instead of %TRNMODE.
	
	02/10/06 - Srinivar - CR19137 
		   Cleaned up the warnings
		   Modified the code to conform to PSL standards.
	
	01/14/04 - RussellDS - CR 7514
		   Modified section TT to fix error.  Quiting if 'ESCEA,
		   changed to quit if null.

	----------------------------------------------------------------------
	*/

	do UPD
	
	quit

UPD	
	type Number EFDF

	set %ProcessMode=1
	set EFDF=0
	do VPG0

	quit
	
INQ	
	type Number EFDF
	
	set %ProcessMode=2
	set EFDF=0
	do VPG0
	
	quit
	
EFD	
	type Number EFDF
	
	set %ProcessMode=1
	set EFDF=1
	
	do VPG0 quit


VPG0	
	type public Number OLNTB,PID
	type public String IO,PR(),%TAB(),%READ(),VFMQ,ESCACT,ESCT,ESC,QTT
	
	type String EC
	type Number %PG,SAVCID,CID
	
	kill OLNTB
	set %PG=0
	
	set %TAB("CID")=".CID3/XPP=S %EXT=1 D ^UACN/XPR=S (CLS,ZCLS)=""L"""
	set %TAB("EC")=".TRTYPE1/TBL=PR(#10/XPP=D TTP^LNES2/XPR=I CID K PR D TT^LNES2"
	if %ProcessMode=2 set %TAB("IO")=$$IO^SCATAB($I)
	set EC="ESC1"
	
	set %READ="@@%FN,,,CID/REQ,EC/REQ"
	if %ProcessMode=2 set %READ=%READ_",IO/REQ"
	do ^UTLREAD if VFMQ="Q" quit 
	
	if %ProcessMode=2,IO'=$I do OPEN^SCAIO
	set SAVCID=CID
	set %PG=1
	set CID=PR($P(ESCACT,$C(9),2))
	do VPG^DEPSTS
	do ESCA(PR,ESCT,PID,QTT,ESC)	
	quit


ESCA(PR,ESCT,PID,QTT,ESC)	// Escrow type lookup table

	do TT

	quit

	
public	TT 
	
	type public String PR(),ESCACT,ESCEA,D,DATA,PAYEE,TRTYPE,TT,RM,QTT,ESCT
	type public Number PID,CID,ELENUM,ORIGTM,%NET
	type public Boolean ER
		
	type Number ECID
	
	kill ESCT,PR
	
	set TT=""
	set PID=$G(PID)

	type RecordLNBIL0 lnbil0=Db.getRecord("LNBIL0","CID")
	set ELENUM=0
	for  set ESCACT=$$ESCACT^BILFUNCS(.lnbil0,.ELENUM) quit:ESCACT=""  do {
		set ECID=$P(ESCACT,$C(9),1)
		set ESCEA=$P(ESCACT,$C(9),2)
		if ESCEA="" quit
		
		// escrow account lookup
		set PR($P(ESCACT,$C(9),2))="||"_ECID

		if (ORIGTM.get()=1)!(ORIGTM.get()=3)!($G(%NET)) do { quit:ER 
			set ER=0 kill RM
			new X 
			set X=ESCEA 
			do TTP(ECID)
			if ER kill PR($P(ESCACT,$C(9),2))
			}
		Type ResultSet rs=Db.select("TYP,PAYID","TRTYPE","CID=:ECID")
		if 'rs.isEmpty() while rs.next() do {
			set TT=rs.getCol(1)
			set PAYEE=rs.getCol(2)
			if PID'="",PID'=PAYEE quit 
			set TRTYPE=$$TT^LNU(TT)
			if $G(QTT)'="",QTT'=TRTYPE quit 
			Type RecordLNTRS lntrs=Db.getRecord("LNTRS","TRTYPE")
			Type RecordLNTRS1 lntrs1=Db.getRecord("LNTRS1","TRTYPE,PAYID=:PAYEE")
			set D=lntrs.trdes_" - "_lntrs1.paydes
			// escrow type lookup
			set ESCT(TT)=D
			}
		}
	if ER,$D(PR) set ER=0 set RM=""
	quit 

	
TTP(CID)

	// Restrict posting to closed escrow accounts

	type public String PR(),RM,X
	type public Boolean ER
	type public Number %OSAVE,ECID
	
	type String ESC="ESC1"
	
	// No account linked to escrow ~p1
	if CID="" set ER=1 set RM=$$^MSG(1886,X) quit
	
	// Invalid escrow account number
	if 'Db.isDefined("ACN","CID") set ER=1 set RM=$$^MSG(1325) quit

	// Escrow account ~p1 is closed
	if $G(%OSAVE)'=2 do {
		Type RecordDEP dep=Db.getRecord("DEP","CID")
		if dep.stat=4 set ER=1 set RM=$$^MSG(1003,CID)
		}

	quit 
 #OPTION ResultClass ON
Public String vSIG()	quit "60488^52349^Chad Smith^3170"	// Signature - LTD^TIME^USER^SIZE
