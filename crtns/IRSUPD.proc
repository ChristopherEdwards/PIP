IRSUPD		/*
	ORIG: titove - 10/17/2005
	DESC: IRS Correction Reporting

	---- Comments --------------------------------------------------------

	I18N=QUIT: Excluded from I18N standards.
	
	---- Revision History ------------------------------------------------
	
	09/27/06 - KinI - CR 22447
		   Modified VPG01 section to correct M1099 table name in 
		   look-up syntax.

	10/18/05 - TITOVE - CR 17287
		   Converted into PSL. Added a look-up on IRSUPD table for
		   modify/inquiry/delete modes. Removed VPG0 and ERR sections
		   and also the old revision history.
	
	03/02/05 - DHANALAKSHMI R - 14638
	           Modified the sections VPG01, VPG02, TMPLD, CIDPP to 
		   include the code for the option FTYPE=16.
	
	12/08/04 - SIVAKUMAR - 13439
	           Modified sections VPG01,VPG02, TMPBLD to add processing
	           for form 1099-Q (form type 18).
 
	*/

NEW     //
        do INIT(0)
        quit
 
 
UPD     //
        do INIT(1)
        quit
 
INQ     //
        do INIT(2)
        quit
 
 
DEL     //
        do INIT(3)
        quit
        
	
INIT(%ProcessMode)
	
	type Number %PAGE, %PG, OLNTB
	type String VFMQ

	set %PG = 0
	set %PAGE = 2
	
	type RecordIRSUPD IRSUPD
	
	do VPG(.IRSUPD)
	
	quit
	
	
VPG(RecordIRSUPD IRSUPD)	// Page control

	type public Number %PG, ER
	type public String VFMQ

	type Boolean FINISH = 0
	
	for  do { quit:FINISH
		
		if (%PG = 0) do VPG00 if 1
		
		else  if (%PG = 1) do VPG01(.IRSUPD) if 1
		
		else  if (%PG = 2) do VPG02(.IRSUPD)
		
		if "DFQ"[VFMQ do VER(.IRSUPD) set FINISH = 1 quit
		
		set %PG = %PG + 1
		}
	quit

	
VPG00	// Form type screen
	
	type public Number ER
	type public String %NOPRMT, %READ, %TAB, FTYPE, VFMQ

	set %TAB("FTYPE") = ".FTYPE1/TBL=[STBLIRSFORM2]"
	set %READ = "@@%FN,,,FTYPE/REQ"
	set %NOPRMT = "N"

	do ^UTLREAD 
	
	if (VFMQ = "Q") set ER = 1 quit

	quit
	
VPG01(RecordIRSUPD IRSUPD)	// Account info screen	

	type public Number ER
	type public String %READ, %TAB, FTYPE, VFMQ
	
	if '%ProcessMode set %TAB("CID") = ".CID1/XPP=D CIDPP^IRSUPD"
	else  set %TAB("CID") = ".CID1/TBL=[IRSUPD]MISCSEQ:DISTINCT:QU ""[IRSUPD]FTYPE=<<FTYPE>>""/XPP=D CIDPP^IRSUPD"
	set %TAB("ACN") = ".ACN1/TBL=[IRA]ACN:DISTINCT:QU/XPP=D ACNPP^IRSUPD"
	set %TAB("IRATYP") = ".IRATYP2/TBL=[IRA]RPASEQ:DISTINCT:QU ""[IRA]ACN=<<ACN>>"""
	set %TAB("MISC") = ".TAXID1/TBL=[M1099]TAXID:DISTINCT:QU/XPP=D MISCPP^IRSUPD"
	set %TAB("BSEQ") = ".SEQ1/TBL=[M1099]SEQ:DISTINCT:QU ""[M1099]TAXID=<<MISC>>""/XPP=D MISCPPP^IRSUPD"
	set %TAB("TJD") = ".TJD1/TBL=TMP(/XPR=D TMPBLD^IRSUPD"
	
	if (FTYPE = 1)!(FTYPE = 4)!(FTYPE = 5)!(FTYPE = 13)!(FTYPE = 15)!(FTYPE = 16) set %READ = "@@%FN,,,CID/REQ"
	if (FTYPE = 3)!(FTYPE = 6)!(FTYPE = 7)!(FTYPE = 17)!(FTYPE = 18) set %READ = "@@%FN,,,ACN/REQ,IRATYP/REQ"
	if (FTYPE = 2)!(FTYPE = 9)!(FTYPE = 10)!(FTYPE = 11)!(FTYPE = 12)!(FTYPE = 14) set %READ = "@@%FN,,,MISC/REQ,BSEQ/REQ"

	if %ProcessMode set %READ = %READ_",TJD/REQ"

	do ^UTLREAD 
	
	if (VFMQ = "Q") set ER = 1 quit

	quit
	
	
VPG02(RecordIRSUPD IRSUPD)	// Screen

	type public Number ER
	type public String ET, FTYPE

	if (FTYPE = 3)!(FTYPE = 6)!(FTYPE = 7)!(FTYPE = 17)!(FTYPE = 18) do ACN(.IRSUPD)
	if (FTYPE = 1)!(FTYPE = 4)!(FTYPE = 5)!(FTYPE = 13)!(FTYPE = 15)!(FTYPE = 16) do CID(.IRSUPD)
	if (FTYPE = 2)!(FTYPE = 9)!(FTYPE = 10)!(FTYPE = 11)!(FTYPE = 12)!(FTYPE = 14) do MISC(.IRSUPD)
	
	do DRV^USID(%ProcessMode,"IRSUPD",.IRSUPD) 
	
	quit
	
	
ACN(RecordIRSUPD IRSUPD)
	
	type public Date TJD
	type public Number BSEQ, FTYPE, IRATYP
	type public String ACN, CURCIF, CURLNM, CURNAM, CURTIN, MISCSEQ

	type RecordCIF cif = Db.getRecord("CIF", "ACN = :ACN", 1)
	
	set IRSUPD = Db.getRecord("IRSUPD", "TJD=:TJD,FTYPE=:FTYPE,MISCSEQ=:ACN,BSEQ=:IRATYP", 1)
	
	if '%ProcessMode do {
	
		set IRSUPD.ocif = ACN
		set IRSUPD.otin = cif.taxid
		set IRSUPD.obseq = IRATYP
		set IRSUPD.oname = cif.nam
		set IRSUPD.olnam = cif.lnm
		}

	set CURCIF = ACN
	set CURTIN = cif.taxid
	set CURNAM = cif.nam
	set CURLNM = cif.lnm	
	set MISCSEQ = ACN
	set BSEQ = IRATYP
	
	quit
	
	
CID(RecordIRSUPD IRSUPD)
	
	type public Date TJD
	type public Number BSEQ, CID, FTYPE
	type public String ACN, CURCIF, CURLNM, CURNAM, CURTIN, MISCSEQ
	
	type RecordACN acn = Db.getRecord("ACN", "CID = :CID", 1)
	
	set ACN = +acn.acn

	type RecordCIF cif = Db.getRecord("CIF", "ACN = :ACN", 1)
	
	set IRSUPD = Db.getRecord("IRSUPD", "TJD=:TJD,FTYPE=:FTYPE,MISCSEQ=:CID,BSEQ=:0", 1)
	
	if '%ProcessMode do {
	
		set IRSUPD.ocif = ACN
		set IRSUPD.otin = cif.taxid
		set IRSUPD.obseq = 0
		set IRSUPD.oname = cif.nam
		set IRSUPD.olnam = cif.lnm
		}

	set CURCIF = ACN
	set CURTIN = cif.taxid
	set CURNAM = cif.nam
	set CURLNM = cif.lnm
	set MISCSEQ = CID
	set BSEQ = 0
	
   	quit
   	
	
MISC(RecordIRSUPD IRSUPD)

	type public Date TJD
	type public Number BSEQ, FTYPE
	type public String MISC, CURCIF, CURLNM, CURNAM, CURTIN, MISCSEQ

	type RecordM1099 m1099 = Db.getRecord("M1099", "TAXID = :MISC,SEQ = :BSEQ", 1)
	
	set IRSUPD = Db.getRecord("IRSUPD", "TJD=:TJD,FTYPE=:FTYPE,MISCSEQ=:MISC,BSEQ=:BSEQ", 1)
	
	if '%ProcessMode do {
	
		set IRSUPD.ocif = m1099.relacn
		set IRSUPD.otin = MISC
		set IRSUPD.obseq = BSEQ
		set IRSUPD.oname = m1099.nam
		set IRSUPD.olnam = m1099.lnam
		}

	set CURCIF = m1099.relacn
	set CURTIN = MISC
	set CURNAM = m1099.nam
	set CURLNM = m1099.lnam
	set MISCSEQ = MISC

	quit


VER(RecordIRSUPD IRSUPD)

	type public Date TJD
	type public Number BSEQ, ER, FTYPE
	type public String MISC, RM, VFMQ

	if ER!(%ProcessMode = 2)!(%ProcessMode = 4)!(VFMQ = "Q") do END quit

	if (%ProcessMode = 3) do Db.delete("IRSUPD","TJD=:IRSUPD.tjd AND FTYPE=:IRSUPD.ftype AND MISCSEQ=:IRSUPD.miscseq AND BSEQ=:IRSUPD.bseq")
	
	if (%ProcessMode < 2) do IRSUPD.save() 
	
	do END
	
	quit

	
END	
	type public Number CID, ER
	type public String RM, VFMQ

	if ER!(%ProcessMode = 2)!(%ProcessMode = 4) quit
	
	set CID = CID.get()
	
	if (VFMQ = "Q") do {
		
		if (%ProcessMode = 0) set RM = "Correction for account "_CID_" not created" quit
		if (%ProcessMode = 1) set RM = "Correction for account "_CID_" not modified" quit
		set RM = "Correction for account "_CID_" not deleted"
		}
	else  do {
		
		if (%ProcessMode = 0) set RM = "Correction for account "_CID_" created" quit
		if (%ProcessMode = 1) set RM = "Correction for account "_CID_" modified" quit
		set RM = "Correction for account "_CID_" deleted"
		}
		
	set ER = "W"
	
	quit
	
	
TMPBLD	// Build a temporary global for date help

	type public Number BSEQ, IRATYP
	type public String ACN, CID, FTYPE, MISC, TMP()
	
	type Date ZDATE
	type Number ZC
	type String Z
	
	if (FTYPE = 1)!(FTYPE = 4)!(FTYPE = 5)!(FTYPE = 13)!(FTYPE = 15)!(FTYPE = 16) set Z = CID,ZC = 0
	if (FTYPE = 3)!(FTYPE = 6)!(FTYPE = 7)!(FTYPE = 17)!(FTYPE = 18) set Z = ACN,ZC = IRATYP
	if (FTYPE = 2)!(FTYPE = 9)!(FTYPE = 10)!(FTYPE = 11)!(FTYPE = 12)!(FTYPE = 14) set Z = MISC,ZC = BSEQ
	
	type ResultSet rs = Db.select("TJD","IRSUPD","FTYPE=:FTYPE AND MISCSEQ=:Z AND BSEQ=:ZC")

	while rs.next() do {
		
		set ZDATE = rs.getCol("TJD")
		
		type RecordIRSUPD irsupd = Db.getRecord("IRSUPD","TJD=:ZDATE,FTYPE=:FTYPE,MISCSEQ=:Z,BSEQ=:ZC")
		
		type RecordSTBLIRSCHG irschg = Db.getRecord("STBLIRSCHG", "OPT = :irsupd.ccode")
		
		set TMP(ZDATE) = irschg.desc
		}

	quit
	
	
CIDPP	// Post processor for CID

	type public Date TJD
	type public Number %OSAVE, ER
	type public String FTYPE, RM, X
	
	type Number %EXT
	type String %DUP(), ZCLS
	
	if (%OSAVE.get() = 0) do { quit:ER
		
		type ResultSet rs = Db.select("BSEQ","IRSUPD","TJD=:TJD AND FTYPE=:FTYPE AND MISCSEQ=:X") 
		
		if 'rs.isEmpty() set ER = 1,RM = "Correction already created for account today"
		}

	if (FTYPE = 1) kill ZCLS	
	else  set ZCLS = $S(FTYPE=5:"L",FTYPE=16:"L",1:"D")
	
	set %EXT = 1 do ^UACN quit:ER  quit:X.isNull()
	
	type RecordACN acn = Db.getRecord("ACN", "CID = :X")
	
	if 'ZCLS.exists() set ZCLS = acn.cls
	
	if (ZCLS = "D"),((FTYPE '= 5)!(FTYPE '= 11)) do { quit:ER
		
		type RecordDEP dep = {RecordDEP}acn
		
		if dep.ira set ER = 1,RM = "Incorrect form for an IRA account"
		}
	
	quit
	
	
ACNPP	// Post processor for ACN

	type public Date TJD
	type public Number %OSAVE, ER
	type public String FTYPE, RM, X
	
	type Number %EXT
	type String %DUP()
	
	if (%OSAVE.get() = 0) do { quit:ER
		
		type ResultSet rs = Db.select("BSEQ","IRSUPD","TJD=:TJD AND FTYPE=:FTYPE AND MISCSEQ=:X") 
		
		if 'rs.isEmpty() set ER = 1,RM = "Correction already created for account today"
		}

	set %EXT = 1 do ^UCIF quit:ER  quit:X.isNull()

	type ResultSet rs1 = Db.select("RPASEQ","IRA","ACN=:X") 
	
	if rs1.isEmpty() set ER = 1,RM = "CIF number not associated with IRA plan" quit
	
	quit
	
	
MISCPP	// Post processor for M1099

	type public Number ER
	type public String RM, X
	
	if X'?3N1"-"2N1"-"4N&(X'?2N1"-"7N) set ER = 1,RM = "Invalid Tax ID Number" quit
	
	quit
	
	
MISCPPP	// Post processor for M1099 Sequence

	type public Date TJD
	type public Number %OSAVE, ER
	type public String FTYPE, MISC, RM, X
	
	type String TYPE
	
	if (%OSAVE.get() = 0) do { quit:ER
		
		type RecordIRSUPD irsupd = Db.getRecord("IRSUPD","TJD=:TJD,FTYPE=:FTYPE,MISCSEQ=:MISC,BSEQ=:X", 1) 
		
		if irsupd.getMode() set ER = 1,RM = "Correction already created for account today"
		}

	type RecordM1099 m1099 = Db.getRecord("M1099", "TAXID = :MISC,SEQ = :X", 1)
	
	// Record ~p1 is not on file
	if 'm1099.getMode() set ER = 1,RM = $$^MSG(2341,X) quit

	type RecordSTBLM1099T stblm1099t = Db.getRecord("STBLM1099T", "SEQ=:m1099.formtyp")

	set TYPE = stblm1099t.desc.piece(" ",1)
	
	type RecordSTBLIRSFORM2 stblform2 = Db.getRecord("STBLIRSFORM2","FORMNAME=:FTYPE")

	if (stblform2.desc.piece("-",1,2) '= TYPE) set ER = 1

	if ER set RM = "Correction form type does not match record"

	quit
	

vSIG()	quit "60535^58248^Irina Kin^9281"	// Signature - LTD^TIME^USER^SIZE
