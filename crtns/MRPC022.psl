MRPC022(RETURN,VERSN,CID,FD,TD)	//Public;MRPC call for Computed Balance Inquiry
	/*

	   KEYWORDS: RPC

	   ARGUMENTS:
	         . RETURN        Computed balance details  /TYP=T/REQ/MECH=REFNAM:W

	         . VERSN         ^MRPC022 version number   /TYP=N/REQ/MECH=VAL
	                         current version=1

	         . CID           Account Number            /TYP=T/REQ/MECH=VAL

	         . FD            From Date                 /TYP=T/REQ/MECH=VAL

	         . TD            To Date                   /TYP=T/REQ/MECH=VAL

	   RETURNS:
	         . $$    Error Message                     /TYP=T
	                 Null= No Error

	   RELATED:
	         . $$^PBSMRPC - MRPC Service Class Driver

	   EXAMPLE:
	         S RM=$$^MRPC022(.VAL,1,CID,FD,TD)

	  ---- Revision History ------------------------------------------------


	   08/04/03 - CARROLLJ - 51629
		      Modified call to INIT^UAVB to remove second parameter
		      passed.

           04/10/02 - TELIV - 49794
                      Converted to PSL
 
	*/

	new STR,TAB,UAVB,X

	// Version number of client message is not compatible with server
	if $G(VERSN)'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))

	// Invalid account ~p1
	if 'Db.isDefined("ACN","CID=:CID") quit $$ERRMSG^PBSUTL($$^MSG(1259,CID))
	set X=FD
	do FDPP^UAVB2

	if $G(ER) quit $$ERRMSG^PBSUTL($G(RM))

	type RecordACN acn=Db.getRecord("ACN","CID=:CID")

	do INIT^UAVB(.acn,CID,FD,TD,"1111110111011")

	if $G(ER) quit $$ERRMSG^PBSUTL($G(RM),$G(ET))

	// Create formatted output string
	set TAB=$C(9)

	// Average Balance, Credit Count, Credit Amount
	set STR=+$P(UAVB(1),"|",1)_TAB_+$P(UAVB(1),"|",10)_TAB_+$P(UAVB(1),"|",8)

	// Beginning Balance, Debit Count
	set STR=STR_TAB_+$P(UAVB(1),"|",19)_TAB_+$P(UAVB(1),"|",11)

	// Debit Amount, Ending Balance
	set STR=STR_TAB_+$P(UAVB(1),"|",9)_TAB_+$P(UAVB(1),"|",12)

	// Payment Count, Payment Amount, Advance Count
	set STR=STR_TAB_+$P(UAVB(2),"|",5)_TAB_+$P(UAVB(2),"|",4)_TAB_+$P(UAVB(2),"|",7)

	// Advance Amount, Ending Collected
	set STR=STR_TAB_+$P(UAVB(2),"|",6)_TAB_+$P(UAVB(1),"|",13)

	// Reg Z Changes, Average Collected
	set STR=STR_TAB_+$P(UAVB(2),"|",2)_TAB_+$P(UAVB(2),"|",3)

	// Fee Trans, Int Trans
	set STR=STR_TAB_+$P(UAVB(1),"|",17)_TAB_+$P(UAVB(1),"|",18)

	// End of Day High, Reached On
	set STR=STR_TAB_+$P(UAVB(1),"|",3)_TAB_$P(UAVB(1),"|",4)

	// Average Rate, Intra Day High
	set STR=STR_TAB_+$P(UAVB(2),"|",1)_TAB_+$P(UAVB(1),"|",2)

	// Enclosures, Days Out of Debt
	set STR=STR_TAB_+$P(UAVB(1),"|",16)_TAB_+$P(UAVB(2),"|",8)

	// End of Day Low, Consecutive Days
	set STR=STR_TAB_+$P(UAVB(1),"|",6)_TAB_+$P(UAVB(2),"|",9)

	// Reached On, Beginning History Sequence
	set STR=STR_TAB_$P(UAVB(1),"|",7)_TAB_$P(UAVB(1),"|",14)

	// Intra Day Low, Ending History Sequence
	set STR=STR_TAB_+$P(UAVB(1),"|",5)_TAB_$P(UAVB(1),"|",15)_$C(13)_$C(10)
	set RETURN=$$V2LV^MSG(STR)
	quit ""
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43582^Sanchez SCM Administrator^2717"	// Signature - LTD^TIME^USER^SIZE
