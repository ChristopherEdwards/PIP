SW942GEN	 /* 
  ORIG: SCHWARTZC - 11/05/1999
  DESC: SWIFT MT942 Message Generation
 
  ---- Comments --------------------------------------------------------

  This routine will build a SWIFT MT942 Interim Transaction Report message.
  Once the message has been build, it will be filed to the SWIFT global and
  the ready to transmit queue SWIFTQ("RTT").
  
  ---- Revision History ------------------------------------------------
  01/29/04 - CARROLLJ - CR7997
  	     Corrected more parameter mismatch errors. 
 
  01/06/04 - CARROLLJ - CR7658
	     Fix parameter mismatch error.

  05/07/03 - CARROLLJ - 51349
	     Modified tag61 linetag to remove call to DTL^TTXP2.


 */ 

 	quit	// Quit if called from the top.


ext(cid,gendate,gentime,crth,drth,mt920ref)	// External Entry Point
	/*
	This routine is the external entry point to build an MT942 SWIFT
	message.


	ARGUMENTS:
	
		cid		account number
		gendate		MT942 message generation date
		gentime		MT942 message generation time
		crth            credit threshold
		drth            debit threshold
		mt942ref	Related Reference (from incoming MT920 Request)
	

	RETURNS:
		ER		Error
		
	*/


	type Public Cache %CUVAR

	quit:($G(cid)="")!($G(gendate)="")!($G(gentime)="") ""

 	new dep,SW61,page,swiftseq,TRREFNO,SEQ,swstmt

	new sumdr,numdr,sumcr,numcr
	set (sumdr,numdr,sumcr,numcr)=0

	type RecordDEP dep=Db.getRecord("DEP","cid") 

	//!!  Get Correct Message Number
	if 'dep.mt942 set ER=1,RM=$$^MSG(1234) quit ER	//Generate MT942 must="Y"

	set acn=dep.acn
	set val=Db.getOneRow("MT942CRTH,MT942DRTH,MT942A,CRCD","CIF","acn")
	set cifcrt=$P(val,$C(9),1)
	set cifdrt=$P(val,$C(9),2)
	set cifswad=$P(val,$C(9),3)
	set cifcrcd=$P(val,$C(9),4)
 
	//Set the receiver address to the MT942 address defined on the account,
	//or if that is not defined, use the MT942 address defined on the
	//customer.  If neither of those are defined, use CUVAR.SWIFTADD.
		
	if dep.mt942a'="" set receive=dep.mt942a
 	else  if $G(cifswad)'="" set receive=cifswad
	else  do {
		type RecordCUVAR cuvar=%CUVAR.getRecord("CUVAR")
		set receive=cuvar.swiftadd
		}
	// if receiver address is not defined, error "SWIFT Address must be defined"
	if $G(receive)="" set ER=1,RM=$$^MSG(4029) quit ER 


	// Determine the debit and credit threshold.  Starting with the values
	// passed in, then go back to the account level, then the customer
	// level until a defined threshold is found.
	if $G(drth)="" d {
		If (dep.mt942crth'=""),(dep.mt942drth'="") set crth=dep.mt942crth,drth=dep.mt942drth
		else  if cifcrt'="",cifdrt'="" do {
			set crth=cifcrt,drth=cifdrt
			if cifcrcd'=dep.crcd d {
				do EXC^CRCDUTL(cifcrcd,dep.crcd,drth,1,2,11)
				set drth=EXCAMT
				do EXC^CRCDUTL(cifcrcd,dep.crcd,crth,1,2,11)
				set crth=EXCAMT
				}
			}
		}

	if $G(drth)'="",$G(crth)="" set crth=drth
		
	//if neither are defined then quit and return error
	if $G(drth)="",$G(crth)="" set ER=1 quit ER	//return error message!!

	new swift
	type RecordSWIFT swift=Class.new("RecordSWIFT")

	// Build the local array containing the MT942 Statement details
	do detail(.dep) quit:ER ER
	
	if page>0 set swstmt=dep.mt942stmt+1

	do BLDGEN(.swift,.dep) quit:ER ER

	for  quit:(page<1)!($G(ER))  do {
		do set61(.swift)		// set the details into the swift message
		do file942(.swift)		// file the message to SWIFT

		if page>0 do KSWIFT(.swift)
		}
	quit:ER ER

	do swiftq(.swift)
	do updatedep(.dep)

	quit ER

detail(RecordDEP dep)	// build MT942 message

	new starttm,startdt

	//!! NOTE: not sure about the MT940 data item names.

	//if (dep.mt942lrd>dep.SW940LSDT)!((dep.mt942lrd=dep.sw940lsdt),(dep.mt942lrt>dep.sw940lstm)) do {
		set starttm=dep.mt942lrt
		set startdt=dep.mt942lrd
	//	}
	//else  do {
	//	set starttm=dep.sw940lstm
	//	set startdt=dep.SW940LSDT
	//	}
	do SELECT^UHFETCH(.Q,startdt,1,0)
	set vsql=$$OPEN^UHFETCH(.exe,.cid,"",1,.Q)

	new overthr,stmtnum,SEQ
	set (stmtnum)=0

 	if vsql for  S vsql=$$FETCH^UHFETCH(.exe,"",.SEQ,.fhist) do { Q:vsql=0 
		quit:($P(fhist,"|",1)=startdt)&($P(fhist,"|",10)'>starttm)
		quit:$P(fhist,"|",2)=""		;not a financial transaction
		quit:$E($P(fhist,"|",2),6)	;error corrected transaction
	
		set overthr=0
		if $E($P(fhist,"|",2),1) do {  //increment number and sum of credits
			set numcr=numcr+1
			set sumcr=sumcr+$P(fhist,"|",4)
			if crth'<$P(fhist,"|",4) set overthr=1
			}
		else  do {		//increment number and sum of debits
			set numdr=numdr+1
			set sumdr=sumdr+$P(fhist,"|",4)
			if drth'<$P(fhist,"|",4) set overthr=1
			}
		quit:overthr

		// Call tag61 to build the details of the message.
		do tag61(.dep,fhist)
		set stmtnum=stmtnum+1
		}

	set page=stmtnum\15
	if stmtnum#15>0 set page=page+1

	if page=0,stmtnum=0 set page=1

   	quit

BLDGEN(RecordSWIFT swift,RecordDEP dep) // Build the general MT942 message

	new refno

	set swift.swdirect="OUT"
	set swift.msg=942

	set swift.rcvr=receive
	set swift.relref=$G(mt920ref)
	set swift.acctid=cid
	set swift.stnum=dep.mt942stmt+1
       	set swift.drthr=drth
	set swift.crthr=crth
	
	new time,date,tamt,crcd
	set time=$$TIM^%ZM(gentime,"24:60")
	set date=$$DATE^SWIFTCDI(gendate,"YYMMDD")
	set swift.datetime=date_time

	set crcd=dep.crcd if crcd="" set crcd=dep.crcd
	set sumdr=$$AMTCNV^SWIFTCDI(.dep,sumdr,crcd)
	set swift.drsum=numdr_crcd_sumdr

	set sumcr=$$AMTCNV^SWIFTCDI(.dep,sumcr,crcd)
	set swift.crsum=numcr_crcd_sumcr

	set swift.status=0		// status = "RTT"

	quit

tag61(RecordDEP dep,fhist)	// Build tag 61, statement line

	/*
          For financial transactions format field 61 of the SWIFT 942 message 
	  Tag 61 consists of 9 subfields:
 
                1. transaction date (YYMMDD)                    /LEN=6/REQ
                2. Entry date (MMDD)                            /LEN=4/NOREQ
                3. Debit/Credit Indicator                       /LEN=2/REQ
                4. Funds code (last char. of currency code)     /LEN=1/NOREQ
                5. Amount                                       /LEN=15/REQ
                6. Transaction Type Code                        /LEN=4/REQ
                7. Ref for account owner                        /LEN=16/REQ
                8. Account servicing institution reference      /LEN=16/NOREQ
                9. Supplimental data                            /LEN=34x/NOREQ
 
          These subfields are fixed length and there are no delimiters
          with the exception of the supplimental data.  A CR/LF will be
          inserted berfore subfield 9 (only if there is supplimental data)
          and after each line of data.
        */

	new vdt,edt,dc,fcode,amtcnv,ttype,ref,asir,supp

	set vdt=$$DATE^SWIFTCDI($P(fhist,"|",1),"YYMMDD")
	set edt=$$DATE^SWIFTCDI($p(fhist,"|",1),"MMDD")
	set dc=$$DC^SWIFTCDI($P(fhist,"|",7))
	set fcode=$$FCODE^SWIFTCDI($P(fhist,"|",17))

	set tamt=+$P(fhist,"|",4)
	set crcd=$P(fhist,"|",17) if crcd="" set crcd=dep.crcd
	set amtcnv=$$AMTCNV^SWIFTCDI(.dep,tamt,crcd)

	set ttype=$$TTYPE^SWIFTCDI($P(fhist,"|",7),$P(fhist,"|",3))
	set ref=$$REF^SWIFTCDI($P(fhist,"|",7),$P(fhist,"|",3))
	set asir=$$ASIR^SWIFTCDI(fhist)
	set supp=$$SUPP^SWIFTCDI($P(fhist,"|",7))

	set seq="",seq=$O(SW61(seq),-1)+1

	if supp='"" set SW61(seq)=vdt_edt_dc_fcode_amtcnv_ttype_ref_asir_$C(11,13)_supp
	if supp="" set SW61(seq)=vdt_edt_dc_fcode_amtcnv_ttype_ref_asir

	quit



set61(RecordSWIFT swift)	//

	if swift.seqnumber="" set sseq=""
	else  set sseq=swift.seqnumber*15
	set I=0
	set page=page-1
	for I=1:1:15  set sseq=$O(SW61(sseq)) quit:sseq=""  do { 
		if I=1 set swift.f61data1=SW61(sseq)
		if I=2 set swift.f61data2=SW61(sseq)
		if I=3 set swift.f61data3=SW61(sseq)
		if I=4 set swift.f61data4=SW61(sseq)
		if I=5 set swift.f61data5=SW61(sseq)
		if I=6 set swift.f61data6=SW61(sseq)
		if I=7 set swift.f61data7=SW61(sseq)
		if I=8 set swift.f61data8=SW61(sseq)
		if I=9 set swift.f61data9=SW61(sseq)
		if I=10 set swift.f61data10=SW61(sseq)
		if I=11 set swift.f61data11=SW61(sseq)
		if I=12 set swift.f61data12=SW61(sseq)
		if I=13 set swift.f61data13=SW61(sseq)
		if I=14 set swift.f61data14=SW61(sseq)
		if I=15 set swift.f61data15=SW61(sseq)
		
                }
	quit

file942(RecordSWIFT swift) //
 
	set refno=$$950^TRREFNO()	//SWIFT category 9 transaction reference number
	set swift.trrefno=refno
	set swift.trnofmt=refno

	set:$G(swiftseq)="" swiftseq=0
	set swiftseq=swiftseq+1
	set swift.seqnumber=swiftseq
	set swift.stnum=swstmt_"/"_swiftseq
	
	do swift.save()
 
	quit


swiftq(RecordSWIFT swift)	//

	set TRREFNO=swift.trrefno
	set MSG=swift.msg

	do QUEUE^SWIFTGEN("RTT",%SystemDate)

	quit


updatedep(RecordDEP dep)	//

	set dep.mt942lrd=gendate
	set dep.mt942lrt=gentime
	if dep.feepln'="" set dep.mt942prd=dep.mt942prd+1
	set dep.mt942stmt=dep.mt942stmt+1

	do dep.bypassSave()
	
	quit


KSWIFT(RecordSWIFT swift)      //
 
        /*
           This section will remove transactions from fSWIFT(61-61.014) for
           an account that has more than 15 transactions so that transactions
           on the first sequence are not repeated on the sectond ysequence.
        */
 
        set swift.f61data1=""
        set swift.f61data2=""
        set swift.f61data3=""
        set swift.f61data4=""
        set swift.f61data5=""
        set swift.f61data6=""
        set swift.f61data7=""
        set swift.f61data8=""
        set swift.f61data9=""
        set swift.f61data10=""
        set swift.f61data11=""
        set swift.f61data12=""
        set swift.f61data13=""
        set swift.f61data14=""
        set swift.f61data15="" 
 
        quit

vSIG()	quit "59886^43613^Sanchez SCM Administrator^9158"	// Signature - LTD^TIME^USER^SIZE
