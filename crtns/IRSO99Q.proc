IRSO99Q		/*
	ORIG: titove - 11/03/2005
	DESC: IRS Processing - Form 1099-Q Original

	---- Comments --------------------------------------------------------
			  THIS PROCEDURE IS TO BE COMPILED
			      DO NOT RUN STANDALONE

	I18N=QUIT: Excluded from I18N standards.
	
	---- Revision History ------------------------------------------------

	05/15/06 - DESHPANDE S K - CR 21167
		   Modified Section BRECBLD to set address vars to mailing 
		   address instead of perm address.
	
	04/24/06 - DHANALAKSHMI R - CR 20879
		   Modified the section BRECBLD by replacing the setting of
		   CID to include RPASEQ instead of IRATYP. Also added a 
		   condition ('ira.d2) to check "no distribution" to report.

	04/12/06 - DHANALAKSHMI R - CR 20700
		   Modified the section BRECBLD by replacing IRATYP=12 by 
		   IRATYP'=12 to consider only Coverdell ESA and modified the
		   logic that sets AMT(1).

	11/03/05 - TITOVE - CR 17287
		   Converted into PSL. Removed CALCAMT and RPASEQ tags and 
		   pre-2005 revision history.
		   
	08/12/05 - SPR - 16770
		   Modified CALCAMT section to update the distribution code
		   list in the "for" loop to F I=1,5,6,7,18,19,39.

	*/

        // Accept "Dead code" warning during runtime of @IRSTAPE
        #ACCEPT Date=10/26/2005;PGM=Eugene Titov
        quit
	
	
MTRCNTRL	// Master control
	
	type public Number ER

	do Db.delete("B1099Q")

	do ARECBLD quit:ER	// Build and write the "A" record
	
	do BRECBLD quit:ER	// Build and write the "B" record
	
	do CRECBLD quit:ER	// Build and write the "C" record
	
	do STORETOT		// Store the TTR report totals
	
	quit
	

ARECBLD	// "A" record builder

	type public String TAMT()

	type Number AMTIND
	type String FORMCODE

	set FORMCODE = "Q"	// IRS form code
	
	set AMTIND = 123	// IRS amount type indicator
	
	set TAMT(1).piece("|",2) = "Gross distribution"
	set TAMT(2).piece("|",2) = "Earnings"
	set TAMT(3).piece("|",2) = "Basis"
	
	do ARECWRT		// Write the record to tape
	
	quit


BRECBLD	// "B" record builder

	type public Number AMT(), CUTOFF, YEAR

	type Number ACN, CID, I, IRATYP, RPASEQ
	type String ADDR, CITY, CNTRY, CORRTN, FORCNTY, FULLADDR, LNAME, NAME, NONBNDIS
	type String SPECIAL, STATE, TIN, TINTYPE, TTROLL, ZIP
	
	type DbSet ds = Db.selectDbSet("IRA","TAXYR=:YEAR")
	
	while ds.next() do {
		
		type RecordIRA ira = ds.getRecord("IRA")
		
		set ACN = ira.acn
		set RPASEQ = ira.rpaseq
	
		type RecordIRATYPE iratype = Db.getRecord("IRATYPE", "ACN=:ACN,RPASEQ=:RPASEQ")
	
		set IRATYP = iratype.iratyp
		
		// Quit if not a Coverdell ESA
		if IRATYP '= 12 quit
		
		set NONBNDIS = iratype.nonbndis
		
		set AMT(1) = (ira.d1 + ira.d5 + ira.d6 + ira.d7 + ira.d18 + ira.d19 + ira.d39) * 100
			
		// No distributions
		if 'AMT(1),('ira.d2) quit

		if ira.d2 set TTROLL = "1"
		else  set TTROLL = " "

		type RecordCIF cif = Db.getRecord("CIF", "ACN = :ACN")
		
		// Account number (RPA sequence number)
		set CID=ACN_"-"_RPASEQ
		
		set TIN=cif.taxid 		// Taxpayers identification number
		
		set TINTYPE=$S(TIN?2N1"-"7N:1,TIN?3N1"-"2N1"-"4N:2,1:" ") // Type of TIN

		set CORRTN=" "			// Corrected return indicator
		set NAME = cif.nam	 	// Payee First Name
		set LNAME = cif.lnm  		// Payee Last Name
		
		set FULLADDR = cif.mad1_"|"_cif.mad2_"|"_cif.mad3_"|"_cif.mcity_"|"_cif.mstate_"|"_cif.mzip_"|"_cif.mcntry
		set ADDR = FULLADDR.piece("|",1)_" "_FULLADDR.piece("|",2)_" "_FULLADDR.piece("|",3)
		set CITY = FULLADDR.piece("|",4)	// Payee City
		set STATE = FULLADDR.piece("|",5)	// Payee State
		set ZIP = FULLADDR.piece("|",6).piece("-",1)_FULLADDR.piece("|",6).piece("-",2)  // Payee
		set CNTRY = FULLADDR.piece("|",7)	// Foreign country
		set FORCNTY = $S(FULLADDR.piece("|",7)="US":" ",1:1) // Foreign country indicator	
		
		if AMT(1),(AMT(1) '< (CUTOFF * 100)) do {
		
			if 'NONBNDIS set NONBNDIS = " "
			
			do SPECIAL,BRECWRT,B1099Q(ACN)
			}

		// Report Trustee Transfer as it's own record
		if 'ira.d2 quit
		
		set AMT(1) = ira.d2 * 100
		
		if AMT(1),(AMT(1) '< (CUTOFF * 100)) do {
		
			set NONBNDIS = " "
			
			do SPECIAL,BRECWRT,B1099Q(ACN)
			}
		}
		
	quit


SPECIAL	// Build Special Data

	type public String NONBNDIS, SPECIAL, TTROLL
	
	// Special date position 544-750
	//; Blank |544|546
	set SPECIAL = "   "
	//; Trustee to Trustee Transfer Indicator |547
	set SPECIAL = SPECIAL_TTROLL
	//; Type of Tuition Payment |548
	set SPECIAL = SPECIAL_3
	//; Designated Beneficiary |549
	set SPECIAL = SPECIAL_NONBNDIS
	//; Blank|550|750
	set SPECIAL = SPECIAL_"".justify(201)

	quit


B1099Q(ACN)	// Insert "B" Records into B1099Q File  

	type public Number AMT(), BRAMT(), FAMT(), FCNT, TINTYPE
	type public String CID, CITY, FULLADDR, LNAME, NAME, NONBNDIS, STATE, TIN, TTROLL

	type Number BSEQ
	type String NAMCON, X

	if (TINTYPE = 2) set X = LNAME
	else  set X = NAME
	
	do NAMCNTRL^IRSTPMTR
	
	set NAMCON = X
	
	set BSEQ = Db.nextVal("B1099Q","PDATE=:%SystemDate,BACN=:ACN")
		
	type RecordB1099Q b1099q = Db.getRecord("B1099Q","PDATE=:%SystemDate,BACN=:ACN,BSEQ=:BSEQ", 1)
	
	set b1099q.bnamcon = NAMCON
	set b1099q.btintype = TINTYPE
	set b1099q.btin = TIN.translate(" ","-")
	set b1099q.bcid = CID
	set b1099q.bamt1 = AMT(1)/100
	set b1099q.bamt2 = AMT(2)/100
	set b1099q.bamt3 = AMT(3)/100
	set b1099q.bname = NAME
	set b1099q.baddr1 = FULLADDR.piece("|",1)
	set b1099q.baddr2 = FULLADDR.piece("|",2)
	set b1099q.baddr3 = FULLADDR.piece("|",3)
	set b1099q.bcity = CITY
	set b1099q.bstate = STATE
	set b1099q.bzip = FULLADDR.piece("|",6)
	set b1099q.bttrol = TTROLL
	set b1099q.bttpmt = 3
	set b1099q.bdben = NONBNDIS
	
	do b1099q.save()

	// Calculate number of records and tax form report totals
	set FCNT = FCNT + 1
	set FAMT(1) = FAMT(1) + (AMT(1)/100)

	quit


CRECBLD	// "C" record builder

	do CRECWRT	// Write the record to tape
	
	quit
	
	
%STOPLOD	// Stop %ZRTNLOD from this point on down
	quit
TRECWRT		// Dummy line reference for GT.M
	quit
ARECWRT		// Dummy line reference for GT.M
	quit
BRECWRT		// Dummy line reference for GT.M
	quit
CRECWRT		// Dummy line reference for GT.M
	quit
STORETOT	// Dummy line reference for GT.M
	quit
	

vSIG()	quit "60403^47803^Shriram Deshpande^5843"	// Signature - LTD^TIME^USER^SIZE
