MLTRM	/*
		REMOVE MLT CHANGES FROM E-O-D QUEUE
	      	ORIG:  Allan R. Mattson (6915) - 08/29/86
	      	DESC:

	  ---- Revision History ------------------------------------------------

	  01/14/05 - ARPAVC 13861
		   Fixed compile errors and miscellaneous issues.  
		   - Properly scoped all variables
		   - Address issue in DELETE section if the user tries
		     to delete a record that doesn't exist
		   - Fixed logic to that end message displays properly
		   
	  11/13/02 - TELIV - 49451
		     Converted to PSL

	  ------------------------------------------------------------------------

	*/

	quit

INIT	//
	new VFMQ
	
	type Public Boolean ER
	type Public String ET,RM
	type String HLD(,),MLTRM()
	
	type Number %PAGE,%PG,%PROCESSMODE,%REPEAT

	set %PG=1
	set %PAGE=1
	set %ProcessMode=1 
	set %REPEAT=15

	type ResultSet rs=Db.select("TJD","DAYENDBFM","TJD=:%SystemDate")
	if rs.isEmpty() do { quit
		set ET="NOMLT" 
		set ER="W" 
		do DSPBP^UTLERR
		}
	
	do INI
	do VPG
	quit


VPG	// Page control
	
	type Boolean FINISH
	type Public String VFMQ,%LINK,%PGM
	type Public Number %PG
	type Public String HLD(,),MLTRM()
	
	set FINISH=0
	for  do { quit:FINISH
		if $D(VFMQ),"DFQ"[VFMQ set FINISH=1 do VER quit
		if %PG>0 do {
			do VPG01
			if VFMQ="Q" set FINISH=1 do VER quit
			if $D(%PGM) do { quit 
				set %PGM=%PGM_$S($D(%LINK):"VPG0^",1:"VPG^")_$T(+0)_","
				set FINISH=1
				}

			// Process completed/aborted
			if "DFQ"[VFMQ do VER set FINISH=1 quit
			set %PG=%PG+1
			}
		}

	quit



VPG01	// Screen

	type Public Number %PG,%REPEAT
	type Public String HLD(,),MLTRM(),VFMQ 
	type Number HLDP,I
	
	set HLDP=%PG 
	for I=1:1:%REPEAT do {
		if '$D(HLD(HLDP,I)) quit
		set $P(MLTRM(I),"|",1)=$P(HLD(HLDP,I),"|",1)
		set $P(MLTRM(I),"|",2)=$P(HLD(HLDP,I),"|",2)
		set $P(MLTRM(I),"|",3)=$P(HLD(HLDP,I),"|",3)
		set $P(MLTRM(I),"|",4)=$P(HLD(HLDP,I),"|",4)
		}
	
	do DRV^USID(%ProcessMode,"MLTRM")

	if VFMQ="Q" quit

	for I=1:1:%REPEAT do {
		set $P(HLD(HLDP,I),"|",1)=$P(MLTRM(I),"|",1)
		set $P(HLD(HLDP,I),"|",2)=$P(MLTRM(I),"|",2)
		set $P(HLD(HLDP,I),"|",3)=$P(MLTRM(I),"|",3)
		set $P(HLD(HLDP,I),"|",4)=$P(MLTRM(I),"|",4)
		}

	quit


ERR	//

	type Public Boolean ER
	type Public String VFMQ
	
	set ER=1 
	do ^UTLERR

	set VFMQ="Q"

	quit


VER	//

	type Public String VFMQ
	type Number HLDP
	
	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit
	set HLDP=0
	do DELETE
	do END

	quit


DELETE	//

	type Number HLDP,N
	type Public String ABFM(),HLD(,),MLTRM()
	
	set HLDP=0
	set N=0
	for   set HLDP=$O(HLD(HLDP)) quit:HLDP=""  do {
		set N=0
		for  set N=$O(HLD(HLDP,N)) quit:N=""  do {
			if ($P(MLTRM(N),"|",1)=1),(ABFM(HLDP,N).data()) do {
				type Date TJD
				type Number CHGNUM,SEQ,TYPE
				Type String CLS
				
				set TJD=$P(ABFM(HLDP,N),"|",1)
				set CLS=$P(ABFM(HLDP,N),"|",2)
				set TYPE=$P(ABFM(HLDP,N),"|",3)
				set CHGNUM=$P(ABFM(HLDP,N),"|",4)
				set SEQ=$P(ABFM(HLDP,N),"|",5)
	
				do Db.delete("DAYENDBFM","TJD=:TJD AND CLS=:CLS AND TYPE=:TYPE AND CHGNUM=:CHGNUM AND SEQ=:SEQ")
				}
			}

		}


	quit


INI	//

	type Number %N,%TYPE
	type Public Number %PAGE,%REPEAT
	type Public String ABFM(,),HLD(,)
	
	set (%TYPE,%N)=""
	type DbSet rs=Db.selectDbSet("DAYENDBFM","TJD=:%SystemDate")
	while rs.next() do {
		type RecordDAYENDBFM dbfm=rs.getRecord("DAYENDBFM")
		set %N=%N+1
		if %N>%REPEAT set %PAGE=%PAGE+1 set %N=1
		set HLD(%PAGE,%N)="N|"_dbfm.qry_"|"_dbfm.uid_"|"_dbfm.type
		set ABFM(%PAGE,%N)=dbfm.tjd_"|"_dbfm.cls_"|"_dbfm.type_"|"_dbfm.chgnum_"|"_dbfm.seq
		}
	
	quit


END	

	type Public Boolean ER
	type Public String ET,M,RM,VFMQ
	type Public Number %TAB
	
	kill %TAB,M lock 
	if ER!(%ProcessMode=2)!(%ProcessMode=4) quit
	set ER="W"

	// MLT queue entries not deleted
	if VFMQ="Q" set RM=$$^MSG("5633")

	// MLT queue entries~p1deleted
	else  set RM=$$^MSG("1772")

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59932^39217^Vincent Arpa^3632"	// Signature - LTD^TIME^USER^SIZE
