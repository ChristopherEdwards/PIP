CIFHPRG // DO NOT MODIFY  Customer History Purge|CIFHPRG|||||||1
  /*
ORIG: satyanas - 12/06/2005
DESC: Customer History Purge

---- Comments --------------------------------------------------------

---- Revision History ------------------------------------------------

	12/06/2005 - SATYANAS - 18155
		     General DBI3 System area clean up. 

 	*/
 
 	do INIT
	quit
	

INIT 	//

	type public Number %REPEAT
	type public String HEADER(),VFMQ
	type String PPPGM,TYPE
	type Number ELEM,I

	set PPPGM="CIFHPRG"

	type RecordPURHIS fPURHIS()

	// CUSTOMER INFORMATION
	set HEADER(1)=$$^MSG(6739)

	// CUSTOMER INFORMATION
	set HEADER(2)=$$^MSG(6739)

	set %ProcessMode=0
	set %REPEAT=5
	
	for I=1:1:%REPEAT do {
		set fPURHIS(I)=Class.new("RecordPURHIS")
		}
		
	set TYPE=HEADER(2)

	do DRV^USID(%ProcessMode,"PURHIS",.fPURHIS())

	if VFMQ="Q" quit 

	kill TYPE
	set ELEM=0

	for I=1:1:%REPEAT do SELCOL(fPURHIS(I))

	kill fPURHIS

 	if ELEM=0 quit
	
	do VER
	quit


VER	// Verification

	type public Boolean ER
	type public Number ELEM
	type public String FILE,%MSKD,%NOPRMT,OUT,%READ,RM,%TAB,TEXT(),TYPE(),X
	type String DIR
	type Number CONT,I

	// CIF History Purge Verification
	set TEXT(1)=$$^MSG(6737)

	// CONTINUING AT THIS POINT WILL PURGE CIF HISTORY
	set TEXT(2)=$$^MSG(6738)               

	set %TAB("CONT")=".CONT1"
	set CONT=0

	set %TAB("DIR")=".DIR3"
	set DIR=CUVAR.spldir	
	set %TAB("FILE")=".FILE3"
	set FILE="SAVE.CIFH"
	set %READ="@TEXT(1),,@TEXT(2),,,CONT/REQ,DIR/REQ,FILE/REQ"
	set %NOPRMT="F"

	do ^UTLREAD

	if 'CONT.exists() quit 
 
	if 'CONT quit 
	
	kill TEXT

	set OUT=DIR_FILE
	if ELEM=1 set OUT=OUT_TYPE(1).piece("|",1)
	// History purge for CIF type ~p1 to cutoff date ~p2
	for I=1:1:ELEM write !!,$$^MSG(6741,$P(TYPE(I),"|",1),$$DAT^%ZM($P(TYPE(I),"|",2),$G(%MSKD)))

	// Data will be archived in the RMS file ~p1
	write !,$$^MSG(6740,OUT),!
		
	type IO io=Class.new("IO")
	set io.fileName=OUT 	
	set io.directory=DIR
	set io.openParams="WRITE/NEWV" 

	do io.open()
	
	do NACN(.io)
	quit

 
NACN( IO io)	//

	type public Number ELEM
	type public String TYPE()
	type Number ACN
	
	type ResultSet rs=Db.select("ACN","CIF")
	
	while rs.next() do {
		
		set ACN=rs.getCol("ACN")
		for TYPE=1:1:ELEM do {
			type RecordCIF cif=Db.getRecord("CIF","ACN=:ACN")
			if cif.type=TYPE(TYPE).piece("|",1) do PURGE(.io)
			}
		}
		
	else  do END(.io)

	quit	


PURGE( IO io)	// Output record to RMS and delete from database
	
	do NSEQ(.io)
	quit


NSEQ( IO io) 	//

	type public Number ACN
	type public String TYPE(),TYPE
	type Boolean FLAG
	type Number SEQ
	type String DATA
	
	set FLAG=0
	type ResultSet rs=Db.select("TJD,EFD,TLO,TSO,TCMT,HDATE,HTIME,UID,IDENT,SEQ","CIFH","ACN=:ACN")
	if rs.isEmpty() quit	
	while rs.next() do { quit:FLAG
		set DATA=rs.getRow()
		set DATA=DATA.translate($CHAR(9),"|")
		set SEQ=rs.getCol("SEQ")
		set DATA=DATA.piece("|",1,12)
		if DATA>TYPE(TYPE).piece("|",2) set FLAG=1
		}
		
	do SAVE(.io)

	quit


SAVE( IO io)	// Save Record

	type public Number ACN,SEQ
	type public String DATA
	type String OUT1

	// Purge CIF #~p1
	set OUT1=$$^MSG(3141,ACN)
	do io.write(.OUT1)
	do io.write("")
	do io.write("^CIFH("_ACN_","_SEQ_")="""_DATA_"""")
	do io.write("")
	
	do KILL(.io)
	quit


KILL(IO io)	// Purge Record
	
	type public Number ACN,SEQ
	
	do Db.delete("CIFH","ACN=:ACN AND SEQ=:SEQ")
	
	do NSEQ(.io)
	quit	


END( IO io)	// Close file and exit
	
	type String ER
	
	do io.write("$$EOF")
	do io.write("")
	do io.close() 

	// CIF history purge complete
	set ER=$$^MSG(7427) 
	set ER="W"

	quit 

	//v v v v v v v Post processor section for screen PURGHIS v v v v v v v


CALCDAT( RecordPURHIS fPURHIS())	// Calculate the default purge dates
	
	type public Boolean ER
	type public Date ZZ
	type public Number I(),%MOD,%MSKD,NI,%REPEAT
	type public String DES,DI,RM(),X
	type Number COL,CRCD,II,JJ,ROW
	type String GRP
	
	set DI=""
	set GRP=""
	// CIF's have no CRCD defined
	set CRCD=0
	
	if X.isNull() quit

	if NI=1 & (X="ALL") do ALLTYPE(.fPURHIS()) quit

	for II=1:1:%REPEAT do {
		for JJ=1,3,5 if I(1)'=II!(((NI-1)#%MOD)+1'=JJ) do {
			if JJ=1,(X'=fPURHIS(II).type1) quit
			if JJ=3,(X'=fPURHIS(II).type2) quit
			if JJ=5,(X'=fPURHIS(II).type3) quit
			
			// Duplicate CIF type already entered
			do Runtime.setErrMSG("PURHIS",867)
			}
		}

	if ER quit
	
	// Check to see if type exists

	type ResultSet rsprodd=Db.select("GRP","PRODDFTC","TYPE=:X")
	if rsprodd.next() set GRP=rsprodd.getCol("GRP")
	
	// If type doesn't exist, send error message
	if GRP.isNull() do { quit

		// Type ~p1 is not a valid CIF type
		do Runtime.setErrMSG("PRODDFTC",2771)
		}

 	if ER quit

	type ResultSet rs=Db.select("MINHIST","PRODCTL","TYPE=:X")
	if rs.next() set ZZ=rs.getCol("MINHIST")

 	set ZZ=%SystemDate-ZZ
	set ROW=+I(1)
	set COL=((NI-1)#%MOD)+2
	if COL=2 set DI="DATE1"
	if COL=4 set DI="DATE2"
	if COL=6 set DI="DATE3"
	
	do SETVAL(.fPURHIS(ROW),DI,ZZ)

	set RM(1)=DES.get()

 	set RM(2)=ZZ.toString($G(%MSKD))_"|"_(NI+1)

	quit


ALLTYPE( RecordPURHIS fPURHIS())	//

	type public Number I(),NI
	type public String RM(),X
	type Number N,TYPE
	
	set N=0
	set NI=1
	set I(3)=""
	
	type ResultSet rs=Db.select("TYPE","PRODDFTC")
	
	while rs.next() do {
		set TYPE=rs.getCol("TYPE")
		set N=TYPE
		do ALL(.fPURHIS())
		}

	set NI=0
	set RM(1)=X_"|1"

	quit


ALL( RecordPURHIS fPURHIS())	//

	type public Date ZZ
	type public Number COL,%MOD,%MSKD,N,NI,ROW
	type public String DI,RM(),X

	if X="ALL" set X=N

	set ROW=((NI-1)\%MOD)+1
	set COL=((NI-1)#%MOD)+1
	if COL=1 set DI="TYPE1"
	if COL=3 set DI="TYPE2"
	if COL=5 set DI="TYPE3"
	do SETVAL(.fPURHIS(ROW),DI,N)

 	type ResultSet rs=Db.select("MINHIST","PRODCTL","TYPE=:N")
	if rs.next() set ZZ=rs.getCol("MINHIST")

	set ZZ=%SystemDate-ZZ
	set ROW=((NI-1)\%MOD)+1
	set COL=((NI-1)#%MOD)+2
	if COL=2 set DI="DATE1"
        if COL=4 set DI="DATE2"
        if COL=6 set DI="DATE3"
        
	do SETVAL(.fPURHIS(ROW),DI,ZZ)

	set RM(NI)=N_"|"_NI

	set RM(NI+1)=ZZ.toString($G(%MSKD))_"|"_(NI+1)

	set NI=NI+2
	
	quit 


CHKDATE( RecordPURHIS fPURHIS())	// Check for valid purge date range

	type public Number COL,I(),NI,%MOD,ROW
	type public String VAL,X

	if X.isNull() quit

	set I(7)=21550
	set ROW=+I(1)
	set COL=((NI-1)#%MOD)+1
	if COL=2 set VAL=fPURHIS(ROW).date1
        if COL=4 set VAL=fPURHIS(ROW).date2
        if COL=6 set VAL=fPURHIS(ROW).date3

	set I(8)=VAL

	quit 
	

SELCOL( RecordPURHIS fPURHIS)

        type public Number ELEM
        type public String TYPE()
        type Number COL1,COL2,J
        
        for J=1:1:3 set COL1="TYPE"_J do {
        	
		if 'fPURHIS.@COL1.isNull() do {
                        set ELEM=ELEM+1
                        set COL2="DATE"_J
                        set TYPE(ELEM)=fPURHIS.@COL1_"|"_fPURHIS.@COL2
                        }
                        
                }
                
	quit
	

SETVAL( RecordPURHIS fPURHIS,DI,VAL)

	set fPURHIS.@DI=VAL
	quit

	

vSIG()	quit "60255^23352^Sethy, Satyanarayan^6487"	// Signature - LTD^TIME^USER^SIZE
