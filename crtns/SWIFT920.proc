SWIFT920	
  /*
ORIG: VETSENM - 12/07/1999
DESC: Incoming SWIFT 920 Message Server
 
------ Comments --------------------------------------------------------
 
------ Revision History ------------------------------------------------
        11/22/2005 - SAHOOU - CR 17050
        	 Modified swiftq section by removing ORDER BY parameter
        	 from the parameter list.
        
        11/17/2005 - SAHOOU - CR 17050
        	 Removed the new commands which are corresponding to the
        	 PSL objects .
        	 
	11/15/2005 - SAHOOU - CR 17050
	         The $$^CUVAR reference is replaced with a cuvar literal
	         CUVAR.swiftadd,as the CUVAR procedure is obsoleted.
	         
-------------------------------------------------------------------------
*/

 new stopflg
 set stopflg=0  // initialize to run

 new VARIN set VARIN="IN"

 new KEY set KEY=1 	 // Define literal key for SWINTSRV file 

 for  quit:stopflg="STOP"  do {
	set stopflg=Db.getOneRow("MT920STP","SWINTSRV","KEY")
        quit:stopflg="STOP"
 
        type ResultSet rs=Db.select("QUE,DT,SEQ,TRREFNO,TYPE","SWIFTQ1","TYPE=920 AND QUE='REC'")
        if rs.isEmpty() hang 10 quit

	for  quit:'rs.next()  do {
                new QUE,DT,SEQ,TRREFNO,TYPE

                set QUE=rs.getCol(1)
		set DT=rs.getCol(2)
                set SEQ=rs.getCol(3)
                set TRREFNO=rs.getCol(4)
                set TYPE=rs.getCol(5)
 
                quit:'Db.isDefined("SWIFT","VARIN,TRREFNO,TYPE")
                type RecordSWIFT swift=Db.getRecord("SWIFT","VARIN,TRREFNO,TYPE")

                if swift.status'=8 quit
		if swift.msgreq'=942 do ERR(.swift) quit

                new XCID,SNDR,crth,drth
		set crth=swift.crthr
		set drth=swift.drthr
                set XCID=swift.acctid
                set SNDR=swift.sndr
		
                if 'Db.isDefined("DEP","XCID") do ERR(.swift) quit
		
		type RecordDEP dep=Db.getRecord("DEP","XCID")
		set XACN=dep.acn

                if dep.mt942a="" do {
			set mt942a=Db.getOneRow("MT942A","CIF","XACN")
			if mt942a="" set mt942a=CUVAR.swiftadd
			}
		else  set mt942a=dep.mt942a
               
		if (SNDR="")!(mt942a'=SNDR) do ERR(.swift) quit
                
		// Update swift record to status In Process
		set swift.status=4
           	do swift.bypassSave()

                // Generate outgoing MT942 message
                set gentime=%CurrentTime
                set gendate=%SystemDate
                set mt920ref=TRREFNO
                set ER=$$ext^SW942GEN(XCID,gendate,gentime,crth,drth,mt920ref)
		if ER do ERR(.swift)
 
                // Update SWIFTQ1 from "REC" to "CMP"
		do swiftq()

		// Update swift record to status Completed
                set swift.status=3
                do swift.bypassSave()	
		}
 	hang 10
 	}
 quit
  
 
ERR(RecordSWIFT swift)     // Update the SWIFT record to show that the 
			       //request was not processed.

	set swift.status=9		// set status to Request not Processed
	do swift.bypassSave()
	set ER=0,RM=""
	quit

ZT	// Error Trap
	set junk="junk"
	Q
 	
swiftq()
	/*----------------------------------------------------------------------

	Create a new swiftq1 record for the completed incoming 920.
	Delete the old swiftq1 record for the incoming received 920.
	
	------------------------------------------------------------------------
	*/

	new QUE

	set QUE="REC"
	type RecordSWIFTQ1 swq1=Db.getRecord("SWIFTQ1","QUE,DT,SEQ,TRREFNO,TYPE")

	do Db.delete("SWIFTQ1","QUE=:QUE AND DT=:DT AND SEQ=:SEQ AND TRREFNO=:TRREFNO AND TYPE=:TYPE")

	set QUE="CMP"
	set swq1.que=QUE

	type ResultSet rs=Db.select("SEQ","SWIFTQ1","QUE=:QUE AND DT=:DT","SEQ DESC")
	
	if 'rs.next() set swq1.seq=1
	else  set swq1.seq=rs.getCol(1)+1

	do swq1.save()

	quit 

vSIG()	quit "60226^20468^Upendra Sahoo^3593"	// Signature - LTD^TIME^USER^SIZE
