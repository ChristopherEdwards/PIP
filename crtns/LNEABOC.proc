LNEABOC	//;Run Escrow Analysis Off Cycle
	/*
	   ORIG:  Chuck Hardy (6721)
	   DESC:  Run Escrow Analysis Off Cycle
	
	--- Revision History --------------------------------------------------
	03/30/06 - Srinivar -CR 20188
		   Modified the FILE section to call LNS and inturn 
		   LNS will call MRPC inorder to process more than 
		   one loan account or option "ALL" is choosen.Removed the
		   section N1 and code calling section N1 is replaced with MRPC.
		   Modified the FILE section to call MRPC instead of 
		   LNS for selective loan accounts. In this way the 
		   system will process only the specified loan account 
		   and not all the loan accounts.
		
	11/28/05 - Srinivar -CR 16890
		   
		   Modified the section MRPC as public.
		   Modified to conform to current standards.
	           Replaced the Q() references with dynamic select.
		   
		   Code for the screen driver procedure has been 
	           restructured to include the  code present in 
	           VPG and VPG0 section to be in the INIT section .
	           Section VPG and VPG0 has been 
	           removed.    	
	                    	          
		   Modified the sections L1,P1,D1,MRPC to call the line
		   tag OC of the batch  BCHLNEAB.
		   		   
	03/18/04 - GIRIDHARANB - 8466
		   Modified section DE to use table DAYENDAPCND instead of 
		   DAYEND (per CR7501 - Mark Spier)
		   
	01/14/04 - CARROLLJ - CR7901
		   Added #ACCEPT prior to xecute command to correct compile
		   errors.

	12/16/03 - RussellDS - CR7514
		   Replace use of table XREFLNTRS (copy of index LNTRS for
		   table TRTYPE -- and going to be obsoleted), with use of
		   table TRTYPE.


	12/23/02 - Varsha Malhotra - ARQ 47547
		   Modified section MRPC to quit if offcycle analysis has
		   already been posted or its already conducted when MRPC=1.
		   Also modified section EXC to log the new error message
		   5195 in the dayend exceptions global if LNAPCHG.EPOST=1.

	07/17/02 - SHVACHKINAD/GRAY - ARQ 49794
		   Converted to PSL. 	

	06/05/00 - BECKEYW - ARQ 35870
	           Modified section MRPC to move the Global Check for MPRC
	           to the end of MRPC section so that data is saved to the
	           LN file.

	05/17/00 - BECKEYW - ARQ 35870
	           Modified section MRPC to check for MRPC=1 and if it is
	           it quits.  MRPC is set to 1 in MRPC094.

	03/10/00 - BECKEYW - ARQ 35870
	           Need to add section MRPC.

	01/24/00 - BECKEYW - ARQ 35870
	           Modified section N1 to check for MRPC=1 and if it is
	           it quits.  MRPC is set to 1 in MRPC094.

	*/
	
	
	do INIT 
	quit

INIT	

	type public Number DTYPS,LNS,LTYPS,OLNTB,OPT,PIDS,CTL
	type public Boolean ER
	type public String VFMQ
	type Number %PG,%PAGE
	
	set %PG=0 
	set (CTL,%PAGE)=1
	set %EffectiveDate=%SystemDate
	set (OPT,LNS,LTYPS,DTYPS,PIDS)=""
	do VPG00 if "DFQ"[$G(VFMQ)!$G(ER) do VER quit
	set %PG = %PG + 1
        do VPG01
        do VER

	kill VFMQ,OLNTB
	
	quit	

VPG00	// Set up

	type public String %TAB,%READ
	type Number OLNTB
	
	set %TAB("OPT")=".OPT5/TBL=OPT(/XPR=D OPT^LNEABOC"
	set OLNTB=35

	set %READ="@@%FN,,,OPT/REQ"
	do ^UTLREAD
	
	quit

VPG01	//
	type public String %READ,%TAB,VFMQ
	type public Number OPT
	type Number OLNTB
	
	set %TAB("EFD")=".EFD3/HLP=[SYSDEV,LN]APCND/MIN=1/MAX=80000"
	if OPT=1 set %TAB("LTYPS")=".LTYPS1/HLP=[PRODDFTL]TYPE/TBL=[PRODDFTL]"
	if OPT=2 set %TAB("DTYPS")=".DTYPS1/HLP=[PRODDFTD]TYPE"
	if OPT=3 do {
		set %TAB("TRT")=".TRT1/HLP=[DEP]TRTYPE/TBL=[LNTRS]"
		set %TAB("PIDS")=".PIDS1/HLP=[LNTRS1]PAYID"
		}
	if OPT=4 do {
		set %TAB("LNS")=".LNS1/HLP=[LN]CID"
		set %TAB("PTY")=".TYPE2/HLP=[PRODDFTL]TYPE/TBL=[PRODDFTL]"
		}
	set OLNTB=4035

	set %READ="EFD/REQ,"
	set %READ=%READ_$S(OPT=1:"LTYPS/REQ",OPT=2:"DTYPS/REQ",OPT=3:"TRT/REQ,PIDS/REQ",OPT=4:"PTY/REQ,LNS/REQ")
	do ^UTLREAD 
	if VFMQ="Q" quit
	
	quit


VER	
	
	type public String VFMQ
	type Number CNTESC
	
	set CNTESC=0
	if VFMQ="Q" do END quit
	do FILE
	do END 
	quit

FILE	// File data

	type public Number DTYPS,LNS,LTYPS
	type public String VFMQ,PIDS
	
	// Sort By Loan Types
	if 'LTYPS.get().isNull() do LTYPS quit
	// Sort By Escrow Types
	if 'DTYPS.get().isNull() do DTYPS quit
	// Sort by Payee ID
	if 'PIDS.get().isNull() do PIDS quit
	// Selective Loan Accounts
	if 'LNS.get().isNull() do LNS quit

	set VFMQ="Q"
	quit

LTYPS	// Sort By Loan Types

	type public Boolean ER
	type public Number TYPE,LTYPS
	type public String GRP
	type String DQQRY(), WHERE
		
	set DQQRY(1) = "[PRODDFTL]TYPE "_LTYPS
	set WHERE = $$WHERE^SQLCONV(.DQQRY(),"PRODDFTL") quit:ER
			
	#ACCEPT DATE=01/09/06;PGM=Srinivar
	type ResultSet rs = Db.select("TYPE,GRP", "PRODDFTL",WHERE)
	if rs.next() do {
			set TYPE = rs.getCol("TYPE")
			set GRP=rs.getCol("GRP")
			do L1						
	}
	else  quit
	
	quit
	

L1	//
	type public Number CID,CNTESC,LCID,TYPE,TYP
	type public String CLS,CRCD,GRP
	type public Date APCND
	type public Boolean ER
	
	type Number ANOFF,BR
	
	set CLS="L"
	type ResultSet xclsrs
	set xclsrs=Db.select("CID","LN","CLS=:CLS AND GRP=:GRP AND TYPE=:TYPE")
	if xclsrs.isEmpty() quit 
	while xclsrs.next() do {
		set LCID=xclsrs.getCol(1)
		type RecordLN ln=Db.getRecord("LN","CID=:LCID")
		if ln.eacip do EXC(.ln) quit
		set CID=LCID
		set APCND=%EffectiveDate

		// Analysis already has been posted for this loan
		if $$POST(LCID,APCND) do { quit
			do EXC(.ln) 
			do MRPC
			}

		set ANOFF=APCND-ln.anoff

		set TYP=ln.type
		set BR=ln.boo
		set CRCD=ln.crcd

		do OC^BCHLNEAB(.ln)
				
		set ER=0
		set LCID=CID 
		set CNTESC=CNTESC+1 
		do DE(.ln)
		}
	quit

DTYPS	// Sort By Escrow Types

 	type public String DTYPS
 	type public Boolean ER
 	type public Number TYPE
	type String DQQRY(), WHERE
	
	set DQQRY(1) = "[DEP]TYPE "_DTYPS
	set WHERE = " AND "_$$WHERE^SQLCONV(.DQQRY(),"PRODDFTL") quit:ER

		
	#ACCEPT DATE=01/09/06;PGM=Srinivar
	type ResultSet rs = Db.select("TYPE", "DEP","CLS='D' AND GRP='ESC' "_WHERE)
	if rs.next() do {
	set TYPE = rs.getCol("TYPE")
	do D1
	}
	else  do END quit
	do END
	quit

D1
	
	type public Boolean ER
	type Public Number CNTESC,CID,TYPE
	type Number ANOFF,BR
	type Date APCND,LCID
	type String CRCD,TYP
	
	type ResultSet xcls2
	set xcls2=Db.select("CID","DEP","CLS='D' AND GRP='ESC' AND TYPE=:TYPE")
	if xcls2.isEmpty() quit
	while xcls2.next() do {
		set CID=xcls2.getCol(1)
		type RecordDEP dep=Db.getRecord("DEP","CID=:CID")
		set LCID=dep.aref

		type RecordLN ln=Db.getRecord("LN","CID=:LCID")
		 // Escrow Analysis Change In Process
		if ln.eacip do EXC(.ln) quit

		set APCND=%EffectiveDate

		// Analysis already has been posted for this loan
		if $$POST(LCID,APCND) do { quit
			do EXC(.ln) 
			do MRPC
			}

		set ANOFF=APCND-ln.anoff
		set TYP=ln.type
		set BR=ln.boo
		set CRCD=ln.crcd
		
		do OC^BCHLNEAB(.ln)
			
		set ER=0
		set CNTESC=CNTESC+1 
		do DE(.ln)
		}
	quit

PIDS	// Sort By Payee IDs
	
	type public String PIDS,PAYID,TRT
	type public Boolean ER
	type Number TYPE
	type String DQQRY(),TRTN,WHERE
	
	type ResultSet xrefrs=Db.select("DISTINCT TYP","TRTYPE","TRTYPE=:TRT")
	if xrefrs.isEmpty() do END quit
	while xrefrs.next() do {
		set TRTN=xrefrs.getCol(1)
		
	set DQQRY(1) = "[LNTRS1]PAYID "_PIDS
	set WHERE = " AND "_$$WHERE^SQLCONV(.DQQRY(),"LNTRS1") quit:ER
	
	#ACCEPT DATE=01/09/06;PGM=Sriivar
	type ResultSet rs = Db.select("PAYID", "LNTRS1","TRTYPE=:TRT"_WHERE)
	if rs.next() do {
		set PAYID = rs.getCol("PAYID")
		do P1
		}	
	}

	do END
	quit

P1	
	
	type public Boolean ER
	type public Number ANOFF,BR,CID,CNTESC,LCID,TYP
	type public String CRCD,PAYID,TRTN
	type Date APCND
	
	
	type ResultSet xrefrs2
	set xrefrs2=Db.select("CID","TRTYPE","TRTYPE=:TRT AND TYP=:TRTN AND PAYID=:PAYID")
	if xrefrs2.isEmpty() quit
	while xrefrs2.next() do {
		set CID=xrefrs2.getCol(1)
		type RecordDEP dep=Db.getRecord("DEP","CID=:CID")
		set LCID=dep.aref

		type RecordLN ln=Db.getRecord("LN","CID=:LCID")
		// Escrow Analysis Change In Process
		if ln.eacip do EXC(.ln) quit

		set APCND=%EffectiveDate

		// Analysis already has been posted for this loan
		if $$POST(LCID,APCND) do { quit
			do EXC(.ln) 
			do MRPC
			}

		set ANOFF=APCND-ln.anoff
		set TYP=ln.type
		set BR=ln.boo
		set CRCD=ln.crcd
	
		do OC^BCHLNEAB(.ln)
		set ER=0
		set CNTESC=CNTESC+1
		do DE(.ln)
		}

	quit

LNS	// Selective Loan Accounts

	
	type public Number CID
	type public Boolean ER
	type public String GRP,LNS
	type String DQQRY(), WHERE
	
	set DQQRY(1) = "[LN]CID "_LNS
	set WHERE = " AND "_$$WHERE^SQLCONV(.DQQRY(),"LN") quit:ER
	
		
	#ACCEPT DATE=01/09/06;PGM=Sriivar
	type ResultSet rs = Db.select("CID,GRP", "LN","CID=:LNS"_WHERE)
	if rs.next() do {
	set CID = rs.getCol("CID")
	set GRP=rs.getCol("GRP")
	do MRPC
	}	
		
	quit


public MRPC	// This one also called from MRPC094 (MRPC flag set to 1)

	type public Number CID,CNTESC,LCID,MRPC,TYP,FLG,EPOST
	type public Boolean ER
	type public Date APCND
	
	type Number ANOFF,BR
	type String CRCD
		
	set LCID=CID
		
	type RecordLN ln=Db.getRecord("LN","CID=:LCID")

	// Escrow Analysis Change In Process
	if $G(MRPC)=1,ln.eacip set ER=1 quit
	else  if ln.eacip do EXC(.ln) quit

	set APCND=%EffectiveDate

	// Analysis has already been posted for this loan
	
	set EPOST=$$POST(LCID,APCND)
	
	if $G(MRPC)=1,EPOST set FLG=1 quit
	else  if EPOST do EXC(.ln) quit

	set ANOFF=APCND-ln.anoff
	set TYP=ln.type
	set BR=ln.boo
	set CRCD=ln.crcd
		
	do OC^BCHLNEAB(.ln)
	
	set LCID=CID
		
	set ER=0
	set CNTESC=CNTESC+1 

	do DE(.ln)

	quit

EXC(RecordLN ln)	// File Dayend exceptions

	 /*
 
        ARGUMENTS:
                . ln            Loan Account Object     TYP=RecordLN/REQ
        */
	
	type public Number EPOST,ET,LCID
	type public String DESC,ZE	
		
	// Analysis has already been posted
	if $G(EPOST) set ET=$$^MSG(5195)
	// Acct in Analysis, Off-cycle Analysis Not Conducted
	else  set ET=$$^MSG(4070)

	// Off-cycle Escrow Analysis
	set ZE="" set DESC=$$^MSG(4075)
	do LOG^UTLEXC($T(+0),"*",DESC,LCID,ZE,ET,+ln.bal)
	quit

DE(RecordLN ln)	// File DAYEND entries for EFD
	
	type public Number LCID
	
	/*
 
        ARGUMENTS:
                . ln            Loan Account Object     TYP=RecordLN/REQ
        */
	
	do ln.setAuditFlag(1)

	
	type RecordDAYENDAPCND dayend=Db.getRecord("DAYENDAPCND","APCND=:%EffectiveDate,CID=:LCID",1)
	set dayend.apcnd=%EffectiveDate
	set dayend.cid=LCID
	do dayend.bypassSave()

	// Off-cycle analysis flag and date.
	set ln.ocaf=1
	set ln.ocadt=%EffectiveDate
	do ln.save()
	quit

END	
	type public Number CNTESC
	type public String RM,VFMQ
	type public Boolean ER
		
	quit:ER

	// Analysis has not been run.  ~p1 loan(s) analyzed.
	if VFMQ="Q" set RM=$$^MSG(294,CNTESC)

	// Analysis has been run.  ~p1 loan(s) analyzed.
	else  set RM=$$^MSG(292,CNTESC)
	set ER="W"
	quit

OPT	// Pre-processor for Analysis Option prompt
	
	type public String OPT(),I()

	set I(3)="OPT("
	// Analyze by Loan Product Type
	set OPT(1)=$$^MSG(4074)

	// Analyze by Escrow Product Type
	set OPT(2)=$$^MSG(4071)

	// Analyze by Escrow Transfer Payee
	set OPT(3)=$$^MSG(4072)

	// Analyze by Loan Account
	set OPT(4)=$$^MSG(4073)

	quit


POST(CID,ND)
	/*
	   Determine if analysis already has been posted for this loan.

        ARGUMENTS:
		. CID	Loan Account Number		    TYP=N/MECH=VAL/REQ
		. ND	Analysis Payment Next Change Date   TYP=D/MECH=VAL/REQ

	RETURNS:
		. $$	0 - Analysis Posted
			1 - Analysis Not Posted
	*/
	

	type RecordLNAPCHG lnapchg=Db.getRecord("LNAPCHG","APCND=:ND,CID=:CID",1)
	
	if 'lnapchg.getMode() quit 0	
	else  quit +lnapchg.epost
	quit	

vSIG()	quit "60355^29363^Srinivasan, Rajesh^10925"	// Signature - LTD^TIME^USER^SIZE
