C5498SA		/*
	---- Comments --------------------------------------------------------
			  THIS PROCEDURE IS TO BE COMPILED
			      DO NOT RUN STANDALONE

	I18N=QUIT: Excluded from I18N standards.

	---- Revision History ------------------------------------------------
	
	05/30/07 - DHANALAKSHMI R - CR 25413
		Added for IRS processing of Health Savings Account 
		(Form 5498-SA Correction).

	*/
	
        quit
        
	
MTRCNTRL	// Master control

	type public Number ER
	
	type Number PASSFLG

	do Db.delete("B5498SA")

	set PASSFLG = 1
		
	// Build and write the "A" record - First pass
	do ARECBLD quit:ER

	// Build and write the "B" record - First pass
	do BRECBLD quit:ER

	// Build and write the "C" record - First pass
	do CRECBLD quit:ER
	
	set PASSFLG = 2
		
	// Build and write the "A" record - Second pass
	do ARECBLD quit:ER

	// Build and write the "B" records - Second pass
	do BRECBLD quit:ER

	// Build and write the "C" record - Second pass
	do CRECBLD quit:ER

	// Store the TTR report totals
	do STORETOT

	quit
	

ARECBLD

	// "A" record builder

	type public String TAMT()

	type Number AMTIND, FORMCODE

	// IRS form code
	set FORMCODE = "K"
	
	// IRS amount type indicators
	set AMTIND = "12345"
	
	set TAMT(2).piece("|",2) = "Current Year Contributions"
	set TAMT(3).piece("|",2) = "Prior Year Contributions"
	set TAMT(4).piece("|",2) = "Rollover Contributions"
	set TAMT(5).piece("|",2) = "Fair Market Value"

	// Write the record to tape
	do ARECWRT
	
	quit
	

BRECBLD

	// "B" record builder

	type public Number AMT(), BRAMT(), IRATYP, PASSFLG, YEAR
	type public String FORM, FORMS()

	type Date JD
	type Number BSEQ, CID, FMV, I, IBAL, PYBAL, RPASEQ, STATEID, TAXYR, TAXYR1, ZACN
	type String ACN, ADDR, CITY, CNTRY, CORRTN, FORCNTY, FULLADDR
	type String LNAME, MCZC, NAME, SPECIAL, STATE, TIN, TINTYPE, X, ZIP

	set JD = FORMS(FORM)-1

	type DbSet ds = Db.selectDbSet("IRSUPD","TJD>:JD AND FTYPE=20")

	while ds.next() do {

		type RecordIRSUPD irsupd = ds.getRecord("IRSUPD")

		set ZACN = irsupd.miscseq
		set RPASEQ = irsupd.bseq

		type RecordIRATYPE iratype = Db.getRecord("IRATYPE", "ACN = :ZACN,RPASEQ = :RPASEQ", 1)

		set IRATYP = iratype.iratyp

		if (IRATYP '= 18),(IRATYP '= 19) quit

		// Skip duplicates when writting to tape
		if (irsupd.ccode = 1),(PASSFLG = 1) quit

		// Skip "New Entry" for first pass
		if (irsupd.ccode = 4),(PASSFLG = 1) quit

		// Skip "change amount" for second pass
		if (irsupd.ccode = 3),(PASSFLG = 2) quit

		set FMV = ""

		// Original/New CIF number,Tax ID Number, 
		// Corrected return indicator, Payee First Name & Payee Last Name
		if (PASSFLG=1) do {
			set ACN = irsupd.ocif
			set TIN = irsupd.otin
			set CORRTN="G"
			set NAME = irsupd.oname
			set LNAME = irsupd.olnam
			}
		else  if (PASSFLG=2) do {
			set ACN = ZACN
			type RecordCIF cif = Db.getRecord("CIF", "ACN = :ACN", 1)
			set TIN = cif.taxid
			set CORRTN="C"
			set NAME = cif.nam
			set LNAME = cif.lnm
		}

		// Type of TIN
		if (TIN?2N1"-"7N) set TINTYPE=1
		else  if (TIN?3N1"-"2N1"-"4N) set TINTYPE=2
		else  set TINTYPE=" "

		set TAXYR1 = YEAR + 1
		
		type RecordIRA ira = Db.getRecord("IRA","ACN = :ACN,RPASEQ = :RPASEQ,TAXYR = :YEAR", 1)

		type RecordIRA ira1 = Db.getRecord("IRA", "ACN = :ACN,RPASEQ = :RPASEQ,TAXYR = :TAXYR1", 1)

		// Calculate IRA Balance at Year-End
		set TAXYR = YEAR	
		do ^IRAYEBAL

		type RecordCIF cif = Db.getRecord("CIF", "ACN = :ACN", 1)

		// Account number (sequence number)
		set CID = ACN_"-"_RPASEQ

		set FULLADDR = $$FULLADDR^RSPADD(ACN,RPASEQ)

		set ADDR = FULLADDR.piece("|",1)_" "_FULLADDR.piece("|",2)_" "_FULLADDR.piece("|",3)

		// Payee City
		set CITY = FULLADDR.piece("|",4)

		// Payee State
		set STATE = FULLADDR.piece("|",5)

		// Payee
		set ZIP = FULLADDR.piece("|",6).piece("-",1)_FULLADDR.piece("|",6).piece("-",2)

		// country
		set CNTRY = FULLADDR.piece("|",7)

	 	// Foreign country indicator
	 	set FORCNTY = " "
	 	if (FULLADDR.piece("|",7) '= "US") set FORCNTY = 1

		// Special date position 544-750
		// Blank |544|547
		set SPECIAL = "    "

		// HSA Indicator |548
		set SPECIAL = SPECIAL_1

		// Blank|549|750
		set SPECIAL = SPECIAL_"".justify(202)

		if (irsupd.ccode = 2),(PASSFLG = 1) do BRECWRT,B5498SA quit

		// Current Year Contribution
		set AMT(2) = ira.c1 * 100

		// Prior Year Contribution
		set AMT(3) = ira1.c2 * 100

		// Rollover Contribution
		set AMT(4) = ira.c4 * 100

		// Fair market value
		set AMT(5) = 0
		if (PASSFLG '= 1) ! (irsupd.ccode '= 2) set AMT(5) = FMV * 100

		// No contribution to report
		if (AMT(2) + AMT(3) + AMT(4) + AMT(5)) = 0 quit

		// Skip duplicates
		if (irsupd.ccode '=  1) do BRECWRT

		do B5498SA
		}

	quit


B5498SA

	// Insert B Record data into B5498SA table

	// If change in taxid, check the entry with new taxid.
	// If zero amounts, skip the entry.

	type public Number ACN, AMT(), CID, TINTYPE
	type public String ADDR, CITY, FULLADDR, LNAME, NAME, STATE, TIN
	type public Date JD

	type Number BSEQ
	type String X

	if (TINTYPE = 2) set X = LNAME
	else  set X = NAME

	// Format name
	do NAMCNTRL^IRSTPMTR

	// Get next BSEQ
	type ResultSet rs = Db.select("BSEQ","B5498SA","PDATE = :JD AND BACN = :ACN","BSEQ DESC")
	if rs.next() set BSEQ = rs.getCol("BSEQ") + 1
	else  set BSEQ = 1

	// B Record-IRS 5498SA Form Data
	type RecordB5498SA b5498sa = Db.getRecord("B5498SA", "PDATE = :JD, BACN = :ACN, BSEQ = :BSEQ", 1)

	set b5498sa.bnamcon = X
	set b5498sa.btintype = TINTYPE
	set b5498sa.btin = TIN.translate(" ","-")
	set b5498sa.bcid = CID
	set b5498sa.bamt2 = AMT(2) / 100
	set b5498sa.bamt3 = AMT(3) / 100
	set b5498sa.bamt4 = AMT(4) / 100
	set b5498sa.bamt5 = AMT(5) / 100
	set b5498sa.bname = NAME
	set b5498sa.baddr1 = ADDR.piece(" ",1)
	set b5498sa.baddr2 = ADDR.piece(" ",2)
	set b5498sa.baddr3 = ADDR.piece(" ",3)
	set b5498sa.bcity = CITY
	set b5498sa.bstate = STATE
	set b5498sa.bzip = FULLADDR.piece("|",6)
	set b5498sa.bhsai = 1

	do b5498sa.save()
	quit

CRECBLD

	// "C" record builder

	// Write the record to tape
	do CRECWRT
	
	quit


%STOPLOD

	// Stop %ZRTNLOD from this point on down
	quit


TRECWRT

	// Dummy line reference for GT.M
	quit


ARECWRT

	// Dummy line reference for GT.M
	quit


BRECWRT

	// Dummy line reference for GT.M
	quit


CRECWRT

	// Dummy line reference for GT.M
	quit


STORETOT

	// Dummy line reference for GT.M
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60787^19706^Dhanalakshmi R^6046"	// Signature - LTD^TIME^USER^SIZE
