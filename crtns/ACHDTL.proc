ACHDTL  //;PBS -  - V2.0 - ACH Detail Record Maintenance

	/*
	ORIG: CHENARDP - 11/30/2001
	DESC: ACH Detail Record Maintenance

	   ORIG: Allan Mattson
	
	   I18N=QUIT: Excluded from I18N standards.

	---- Revision History ------------------------------------------------

	   10/19/2005 - Satyanas
	   		Removed the Unscoped Warnings.
	   
	   02/18/2002 - TELIV - 49451
			Converted to PSL

       ------------------------------------------------------------------------	
	*/
	
	quit

NEW	//
	do INIT(0)

	quit

 
UPD	//
	do INIT(1)

	quit 


INQ	//
	do INIT(2)

	quit 


DEL	//
	do INIT(3)

	quit 
	
	
INIT(%ProcessMode) // ------------------------
	
	type public String OLNTB,VFMQ
	type Number %PAGE,%PG
	
	set %PG=0 
	set %PAGE=1
	kill OLNTB,VFMQ

	do VPG

	quit 

	
VPG	//
	
	type public String VFMQ
	type public Number %PG
	type Boolean FINISH
	
	type RecordACH1 fACH1
	type RecordACH2 fACH2
	
	set FINISH=0

	for  do { quit:FINISH

		if %PG=0 do VPG00 if VFMQ="Q" set FINISH=1 quit
		if "DFQ"[VFMQ set FINISH=1 quit

		if %PG>0 do VPG01(.fACH2,.fACH1) if VFMQ="Q" set FINISH=1 quit
		if "DFQ"[VFMQ do VER(.fACH2,.fACH1) set FINISH=1 quit
		set %PG=%PG+1
		}

	quit 

	
VPG00   //
	
	type public Boolean ER
	type public String %NOPRMT,%READ,%TAB(),VFMQ
	
	set %TAB("COID")=".COID1/HLP=[ACH2]COID/TBL=[ACH]"
	set %TAB("PTYPE")=".PTYPE1/HLP=[ACH2]PTYPE/TBL=[ACH1]"
	if %ProcessMode set %TAB("SEQ")=".SEQ2/HLP=[ACH2]SEQ/TBL=[ACH2]"
	if %ProcessMode=2 set %TAB("IO")=$$IO^SCATAB($I)
	
	set %READ="@@%FN,,,COID/REQ,PTYPE/REQ" 
	set %NOPRMT="N"

	if %ProcessMode set %READ=%READ_",SEQ/REQ"
	if %ProcessMode=2 set %READ=%READ_",,IO/REQ"

	do ^UTLREAD 

	if VFMQ="Q" set ER=1 quit 
	quit 

	
VPG01(RecordACH2 fACH2,RecordACH1 fACH1)	//

	type public Boolean LFLG
	type public Number SEQ
	type public String ACH(),COID,PTYPE
	type String ET
	
	set fACH1=Db.getRecord("ACH1","COID=:COID,PTYPE=:PTYPE")
	if %ProcessMode=0 do {
		set SEQ=Db.nextVal("ACH2","COID=:COID,PTYPE=:PTYPE")
		for SEQ=SEQ:1 do { quit:'LFLG
			set LFLG=0
			lock +ACH(COID,PTYPE,SEQ):2 else  set LFLG=1 quit
			}
		}

	//Lock on Update and Delete or return "Record locked by another user"
	if (%ProcessMode=1)!(%ProcessMode=3) lock +ACH(COID,PTYPE,SEQ):2 else  set ET="RECLOC" do ERR quit

	set fACH2=Db.getRecord("ACH2","COID=:COID,PTYPE=:PTYPE,SEQ=:SEQ",1) 		

	do DRV^USID(%ProcessMode,"ACHDTL",.fACH2,.fACH1)

	lock -ACH(COID,PTYPE,SEQ)

	quit 


ERR     //
	
	type public Boolean ER
	type public String VFMQ
	
	set ER=1 

	do ^UTLERR

        set VFMQ="Q"

        quit
	

VER(RecordACH2 fACH2,RecordACH1 fACH1)	//

	type public Number SEQ
	type public String COID,PTYPE,VFMQ

	if %ProcessMode=2!(VFMQ="Q") do END quit

	if %ProcessMode=3 do Db.delete("ACH2","COID=:COID AND PTYPE=:PTYPE AND SEQ=:SEQ")	
 	if (%ProcessMode=0)!(%ProcessMode=1) do fACH2.save()
 	 	
	do END
	quit

	
END	//

	type public Number SEQ
	type public String COID,ER,PTYPE,RM,VFMQ
	
	quit:ER!(%ProcessMode=2)!(%ProcessMode=4) 
	
	if VFMQ="Q" do {
		if %ProcessMode=0 set RM=COID_" "_PTYPE_" detail record sequence "_SEQ_" not created" quit 
		if %ProcessMode=1 set RM=COID_" "_PTYPE_" detail record sequence "_SEQ_" not modified" quit 
		set RM=COID_" "_PTYPE_" detail record sequence "_SEQ_" not deleted"
		}
	else  do {
		if %ProcessMode=0 set RM=COID_" "_PTYPE_" detail record sequence "_SEQ_" created" quit 
		if %ProcessMode=1 set RM=COID_" "_PTYPE_" detail record sequence "_SEQ_" modified" quit 
		set RM=COID_" "_PTYPE_" detail record sequence "_SEQ_" deleted"
		}
	set ER="W"
	quit 

vSIG()	quit "60206^25804^Sethy, Satyanarayan^3303"	// Signature - LTD^TIME^USER^SIZE
