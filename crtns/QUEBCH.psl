QUEBCH	// Queuing Batch File Maintenance
	/*
 
 	ORIG:  Pete Chenard 4/11/91
 	DESC:  Allows modification of fields at batch and job levels for
 	       all batches in an event with one call.
 	       
 	       Allows modification to "fake" batch and job (number = 0),
 	       and then applies all changes to all batches and all jobs
 	       within a selected event.

	---- Revision History ------------------------------------------------
	
	04/05/07 - VanithaK - CR 26477
	  	   
	  	   Replication of the CR 26314 in Profile04.
		   Modified END section to protect the EVENT variable to 
		   prevent it from being undefined when the user F11's out
		   of the query screen without entering an event name.
	
	01/04/05 - RussellDS - CR13817
		   Eliminate PSL warnings, remove dead code, clean-up and get
		   to work.
		   
		   Remove old revision history.
 	*/

	type public String ER = 0

	type Boolean FINISH
	type Number %PAGE, %PG, OLNTB
	type String EVENT, VFMQ

 	set %ProcessMode = 1

 	set %PG = 0
	set %PAGE = 2
	set FINISH = 0
	
	type RecordQUEUEB fQUEUEB = Class.new("RecordQUEUEB", "BCHNUM=0")
	type RecordQUEUEJ fQUEUEJ = Class.new("RecordQUEUEJ", "BCHNUM=:0,JOBNUM=0")
	
	for  do { quit:(FINISH ! ER)
 		if (%PG.get() = 0) do VPG00
 		if (%PG.get() = 1) do VPG01(.fQUEUEB) quit:ER
 		if (%PG.get() > 1) do VPG02(.fQUEUEJ) quit:ER
 		
 		if (VFMQ = "Q") do {
 			do END
 			set FINISH = 1
 		}
 		else  if (VFMQ = "F") do {
 			do FILE(.fQUEUEB, .fQUEUEJ)
 			set FINISH = 1
 			do END
 		}
		else  set %PG=%PG+1
	}
	
	quit
	
 
VPG00	// Get event

	type public String EVENT

	type String %NOPRMT, %READ, %TAB()
	
 	set %TAB("EVENT") = ".EVENT1/TBL=[UTBLEVENT]"
 	set %READ = "@@%FN,,,EVENT/REQ" 
	set %NOPRMT = "C"
 	
 	do ^UTLREAD 

	quit
		
 	
VPG01(RecordQUEUEB fQUEUEB)	// Batch File Maintenance Screen
	
	type public String ER
	
	type Boolean BATCH = 1		// Signal to screen for post-processors
	type Number BCHNUM
	
 	set BCHNUM = 0			// Fake batch
 	
	do DRV^USID(%ProcessMode, "QUEUEB", .fQUEUEB)
	
	if ER do ERR
	
 	quit
 	
 	
VPG02(RecordQUEUEJ fQUEUEJ)	// Job Maintenance Screen
	
	type public String ER
	
	type Boolean BATCH = 1		// Signal to screen for post-processors
	type Number BCHNUM, JOBNUM
	
 	set (BCHNUM, JOBNUM) = 0	// Fake batch and job
 	
	do DRV^USID(%ProcessMode, "QUEUEJ", .fQUEUEJ)
	
	if ER do ERR
	
 	quit

 	
ERR	// Log error and quit

	type public String ER = 1
 	
 	do ^UTLERR

 	quit 


FILE(RecordQUEUEB fQUEUEB,RecordQUEUEJ fQUEUEJ)	// Isolate changes from screens and apply to all batches/jobs

	type public String EVENT

	type Boolean BCHCHG, JOBCHG
	type Number BCHNUM
	
	if '(fQUEUEB.fre.isNull() & fQUEUEB.nrd.isNull() & fQUEUEB.strt.isNull() & fQUEUEB.aoe.isNull() & fQUEUEB.rest.isNull() & fQUEUEB.mail.isNull() & fQUEUEB.mailf.isNull() & fQUEUEB.brcd.isNull() & fQUEUEB.brcdf.isNull()) set BCHCHG = 1
	else  set BCHCHG = 0
	
	if '(fQUEUEJ.reti.isNull() & fQUEUEJ.maxr.isNull() & fQUEUEJ.aof.isNull() & fQUEUEJ.ttr.isNull() & fQUEUEJ.diz.isNull() & fQUEUEJ.skip.isNull()) set JOBCHG = 1
	else  set JOBCHG = 0
	
	quit:'(BCHCHG ! JOBCHG)

	type ResultSet rs = Db.select("BATCH", "UTBLEBCH", "EVENT=:EVENT")
	while rs.next() do {
		
		set BCHNUM = rs.getCol("BATCH")
		
		if BCHCHG do {
			type RecordQUEUEB queueb = Db.getRecord("QUEUEB", "BCHNUM=:BCHNUM")
			if 'fQUEUEB.fre.isNull() set queueb.fre = fQUEUEB.fre
			if 'fQUEUEB.nrd.isNull() set queueb.nrd = fQUEUEB.nrd
			if 'fQUEUEB.strt.isNull() set queueb.strt = fQUEUEB.strt
			if 'fQUEUEB.aoe.isNull() set queueb.aoe = fQUEUEB.aoe
			if 'fQUEUEB.rest.isNull() set queueb.rest = fQUEUEB.rest
			if 'fQUEUEB.mail.isNull() set queueb.mail = fQUEUEB.mail
			if 'fQUEUEB.mailf.isNull() set queueb.mailf = fQUEUEB.mailf
			if 'fQUEUEB.brcd.isNull() set queueb.brcd = fQUEUEB.brcd
			if 'fQUEUEB.brcdf.isNull() set queueb.brcdf = fQUEUEB.brcdf
			
			do queueb.save()
		}
		
		if JOBCHG do {
			
			type DbSet ds = Db.selectDbSet("QUEUEJ", "BCHNUM=:BCHNUM")
			while ds.next() do {
				type RecordQUEUEJ queuej = ds.getRecord("QUEUEJ")
				if 'fQUEUEJ.reti.isNull() set queuej.reti = fQUEUEJ.reti
				if 'fQUEUEJ.maxr.isNull() set queuej.maxr = fQUEUEJ.maxr
				if 'fQUEUEJ.aof.isNull() set queuej.aof = fQUEUEJ.aof
				if 'fQUEUEJ.ttr.isNull() set queuej.ttr = fQUEUEJ.ttr
				if 'fQUEUEJ.diz.isNull() set queuej.diz = fQUEUEJ.diz
				if 'fQUEUEJ.skip.isNull() set queuej.skip = fQUEUEJ.skip
				
				do queuej.save()
			}
		}
	}

	quit
	
 	
END	// Exit

	type public String ER, EVENT, VFMQ, RM
	
	set EVENT = EVENT.get("")
	
	if (ER = 0) do {
 	
	 	// Event ~p1 not modified
 		if (VFMQ = "Q") set RM = $$^MSG(1029, EVENT)  

	 	// Event ~p1 modified
		else  set RM = $$^MSG(1026,EVENT)      

		set ER = "W"
	}
 	
 	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60730^34274^Vanitha Krishnasamy^4503"	// Signature - LTD^TIME^USER^SIZE
