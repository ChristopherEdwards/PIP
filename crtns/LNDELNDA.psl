LNDELNDA	//Public;Dayend Delinquent Loan Notice
	/*
	   ORIG: HILLANBRAND - 04/14/97
	   DESC: Dayend Delinquent Loan Notice

	   KEYWORDS:

	  ---- Revision History ------------------------------------------------
		09/19/02 - CHHABRIAS - 49451
			   Converted to PSL			
	  ---------------------------------------------------------------------
	*/


NEW 	//
	do INIT(0)
	quit


UPD 	//
	do INIT(1)
	quit


INQ 	//
	do INIT(2)
	quit


DEL	//
	do INIT(3)
	quit


INIT(%ProcessMode)	//

	new OLNTB,VFMQ
	type RecordLNDELNDA LNDELNDA
	set %PG=0 
	set %PAGE=1
	do VPG(.LNDELNDA)
	quit


VPG(RecordLNDELNDA LNDELNDA)	// Page control

	new FINISH
	set FINISH=0
	for  do { quit:FINISH
		if %PG=0 do VPG00 if VFMQ="Q" set FINISH=1 quit 
		if %PG>0 do VPG01(.LNDELNDA)
		if "DFQ"[VFMQ do VER(.LNDELNDA) set FINISH=1 quit
		set %PG=%PG+1
		}
	quit


VPG00	// Set-up

	set %TAB("CID")=".CID1/XPP=S %EXT=1 D ^UACN/XPR=S (CLS,ZCLS)=""L"""
	//set %TAB("CID")=".CID1/XPP=S %EXT=1/XPR=S (CLS,ZCLS)=""L"""
	if %ProcessMode do {
		set %TAB("EFD")=".EFD1/XPR=D NPDT^LNDELNDA/TBL=ZNPDT("
		set %TAB("KEY")=".KEY1/TBL=ZKEY(EFD,"
		}
	else  do {
		set %TAB("EFD")=".EFD1"
		set %TAB("KEY")=".KEY1/XPR=D KEY^LNDELNDA/TBL=KEY("
		}

	if %ProcessMode=2 set %TAB("IO")=$$IO^SCATAB($I)

	set %READ="@@%FN,,,CID/REQ,EFD/REQ,KEY/REQ" 
	set %NOPRMT="N"
	if %ProcessMode=2 set %READ=%READ_",,IO/REQ"
	
	do ^UTLREAD 
	
	if VFMQ="Q" quit

	quit


VPG01(RecordLNDELNDA LNDELNDA)	// Data entry screen

	if %ProcessMode=2,IO'=$I do OPEN^SCAIO if ER set VFMQ="Q" quit

	set LNDELNDA=Db.getRecord("LNDELNDA","EFD=:EFD,KEY=:KEY,CID=:CID",1)

	do DRV^USID(%ProcessMode,"LNDELNDA",.LNDELNDA)
	
	quit


VER(RecordLNDELNDA LNDELNDA)	//

	 if VFMQ="Q"!(%ProcessMode=2)!(%ProcessMode=4) do END quit
	 do FILE(.LNDELNDA)
	 do END 
	 quit


FILE(RecordLNDELNDA LNDELNDA)	// File data -

	set LND="LND"_KEY

	if '%ProcessMode do {
		set LNDELNDA=Db.getRecord("LNDELNDA","EFD=:EFD,KEY=:LND,CID=:CID",1)
		do LNDELNDA.save()
		}
	else  if %ProcessMode=3 do {
 		do Db.delete("LNDELNDA","CID=:CID AND EFD=:EFD AND KEY=:LND")
		}
		quit


KEY	/* Create table for KEY entry.  Key matches late charge category
	   dayend file definitions.
	*/

	for I=1:1:5 set KEY(I)=""
	quit


NPDT	// Get existing entries in dayend notice files.

	type ResultSet rs=Db.select("EFD,KEY","LNDELNDA","CID=:CID AND KEY IN ('LND1','LND2','LND3','LND4','LND5')")
	while rs.next() do {
		set ZNPDT(rs.getCol("EFD"))=""
		set ZKEY(rs.getCol("EFD"),$E(rs.getCol("KEY"),4,99))=""
		}
	quit


END	// Return message to user

	if (%ProcessMode=2)!(%ProcessMode=4) quit
	if ER quit
	if VFMQ="Q" do {

		// Account ~p1 not created
		if %ProcessMode=0 set RM=$$^MSG(129,CID) quit

		// Account ~p1 not modified
		if %ProcessMode=1 set RM=$$^MSG(135,CID) quit

		// Account ~p1 not deleted
		set RM=$$^MSG(130,CID)
		}
	else  do {

		// Account ~p1 created
		if %ProcessMode=0 set RM=$$^MSG(107,CID) quit

		// Account ~p1 modified
		if %ProcessMode=1 set RM=$$^MSG(122,CID) quit

		// Account ~p1 deleted
		set RM=$$^MSG(109,CID)
		}
	set ER="W"
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43532^Sanchez SCM Administrator^2859"	// Signature - LTD^TIME^USER^SIZE
