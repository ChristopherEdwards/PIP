MRPC083(return,versn,ACN,RPASEQ,EFD,PEN,FEE)
	/*
	Procedure ID: MRPC083
	DESC: Retirement Plan Closeout Inquiry
	KEYWORDS: RPC IRA
 
	ARGUMENTS:
		. return  Retirement Plan Closeout Info
							/TYP=T/REQ/MECH=REFNAM:W
 
		. versn   version number		/TYP=N/REQ/MECH=VAL
			  current version=1
 
		. ACN     Customer Number		/TYP=N/REQ/MECH=VAL
 
		. RPASEQ  Retirement Plan Sequence	/TYP=N/REQ/MECH=VAL
		
		. EFD     Closeout Effective Date	/TYP=D/REQ/MECH=VAL
	
		. PEN	  Consider Penalties		/TYP=L/NOREQ/MECH=VAL

		. FEE 	  Consider Service Fees		/TYP=L/NOREQ/MECH=VAL
 
	RETURNS:
		. $$      Error Message                 /TYP=T
			  Null= No Error
 
	EXAMPLE:
		S RM=$$^MRPC083(.RETURN,1,CID,EFD,1,1)

	----------------------------------------------------------------------
	;---- Revision History ------------------------------------------------
        ;
        ; 10/01/99 - SCHWARTZC/LEVINTOLR - 31427
	;            This new MRPC will calculate the values needed for the 
	;	     Retirement Plan Closeout Inquiry Report.	
	;----------------------------------------------------------------------
	;
	*/
	//Version number of client message is not compatible with server
	if $G(versn)'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))
	
	//quit:($G(ACN)="")!($G(RPASEQ)="")!($G(EFD)="") ""

	if ($G(PEN)="")!($G(PEN)="No") set PEN=0
	else  set PEN=1
	if ($G(FEE)="")!($G(FEE)="No") set FEE=0
	else  set FEE=1

	new rs,dep,CID,ret19,rm,str

	type ResultSet rs=Db.select("CID","RELCIF","ACN=:ACN")

	for  quit:(('rs.next())!($G(rm)'=""))  do {
		set CID=rs.getCol(1)
		new acn,dep
		type RecordACN acn=Db.getRecord("ACN","CID")
		quit:acn.cls="L" 
		type RecordDEP dep=Db.getRecord("DEP","CID")

		if dep.rpaseq=RPASEQ do {
			set rm=$$^MRPC019(.ret19,1,CID,EFD,,FEE)
			if $G(rm)="" do BLDRTN(.dep)
			}
		}

	quit:$G(rm)!$G(ER) $$ERRMSG^PBSUTL($G(rm))

	set return=$$V2LV^MSG($G(str))
	quit ""

        //---------------------------------------------------------------------
BLDRTN(RecordDEP dep)	// Build the return message
	
	new prin,int,pen,feetot,with,clamt,tab,rs,fld

	set tab=$C(9)
	set return=$$LV2V^MSG($G(ret19),.fld)

	set prin=$P(fld(1),$C(9),1)  
	set int=$P(fld(1),$C(9),3)
	if PEN set pen=$P(fld(1),$C(9),4)
	if FEE set feetot=+$P(fld(1),$C(9),15)
	set with=$P(fld(1),$C(9),10)

	if $G(prin)="" set prin=0
	if $G(int)="" set int=0
	if $G(pen)="" set pen=0
	if $G(feetot)="" set feetot=0
	if $G(with)="" set with=0

	//closout amount= principal+interest-penalties-service fees-withholding
	set clamt=prin+int-pen-feetot-with

	set grp=dep.grp
	set typ=dep.type
	
	// CID, Product Group, Product Type, Principal, Interest, Penalties
	set str=$G(str)_CID_tab_grp_tab_typ_tab_prin_tab_int_tab_pen_tab
	// Service Fees, Withholding, Closeout Amount
	set str=$G(str)_feetot_tab_with_tab_clamt_$C(13,10)
	
	quit

vSIG()	quit "59886^43588^Sanchez SCM Administrator^2668"	// Signature - LTD^TIME^USER^SIZE
