FIDMINQ		/* ---- Revision History -----------------------------------------------
	
	11/30/04 - Frans S.C. Witte CR12808
		   Changed IO.read() to IO.read(.ETYP) to maintain compatibility
		   with new implementation of class IO.

	04/05/01 - KINI - 43115
		   Converted to PSL format.
		   Modified to change to one device prompt instead of "Import
		   File Name" and "File Location" to consider other then VMX
		   platform syntax for the device input.
		   Added $$^MSG(4882) to confirm successful import completion.
	*/


	/* FIDNINQ - FIDM Inquiry File Import Procedure
 
	ORIG: KINI - 03/30/01
	DESC: FIDM Inquiry File Import

	This procedure accepts an incoming inquiry data file that the financial
	institution receives from the State. This file contains a list of
	persons that are delinquent in the payment of child support and will
	be accessed when the execution of the match process and production of
	the extract file is initiated.

	-----------------------------------------------------------------------

	INPUTS:
		. Inquiry File

		POSITION   FIELD SIZE         DESCRIPTION
		--------   ----------        --------------------------------
		"D" Record
		001        1                  Record Type
		002-007    6                  Year and Month File Generated
		008        1                  Data Match File Indicator
		009-099    91                 Blanks

		"I" Record
		001        1                  Record Type
		002-010    9                  Inquiry Social Security Number
		011-020    10                 State Pass Back Information
		021-040    20                 Inquiry Last Name
		041-056    16                 Inquiry First Name
		057-071    15                 Case Pass Back Information
		072-076    5                  FIPS Code Pass Back Information
		077-099    23                 Additional State Pass Back Info

		"T" Record
		001        1                   Record Type
		002-011    10                  Number of Inquiry Records on this file                
		012-099    88                  Blanks

	-----------------------------------------------------------------------
	*/


	new %READ,%TAB,IMPNAME,VFMQ
	new REC,RTYPE,DT,TAXID,SPBINF,INQLNAM,INQFNAM,CPBINF,FIPSCPBINF,ADSPBINF

	set %TAB("IMPNAME")=".IMPNAME"
	set %READ="@@%FN,,,IMPNAME/REQ"
	do ^UTLREAD quit:VFMQ="Q"

	// catching a GT.M error
	catch vERROR {
		new ET,RM
		set ET=vERROR.type
		if ET["%GTM-" do ZE^UTLERR quit
		set ET=ET_"-"_vERROR.thrownAt
		set RM=vERROR.description
		do ^UTLERR
		}

	set %PAGE=1 set %PG=1

	type IO io=Class.new("IO")
	set io.fileName=IMPNAME
	set io.openParams="READ"
	set io.recordSize=198

	do io.open()

	for  set REC=io.read(.ETYP)  quit:ETYP'=0  do {
 
		set RTYPE=$E(REC,1,1)
		if RTYPE="D" set DT=$E(REC,2,7)
		if RTYPE="I" do {
			set TAXID=$E(REC,2,10)
			set SPBINF=$$RTB^%ZFUNC($E(REC,11,20))
			set INQLNAM=$$RTB^%ZFUNC($E(REC,21,40))
			set INQFNAM=$$RTB^%ZFUNC($E(REC,41,56))
			set CPBINF=$$RTB^%ZFUNC($E(REC,57,71))
			set FIPSCPBINF=$$RTB^%ZFUNC($E(REC,72,76))
			set ADSPBINF=$$RTB^%ZFUNC($E(REC,77,99))

			type RecordFIDMINQ fidminq=Class.new("RecordFIDMINQ")

			set fidminq.dt=DT
			set fidminq.taxid=TAXID
			set fidminq.spbinf=SPBINF
			set fidminq.inqlnam=INQLNAM
			set fidminq.inqfnam=INQFNAM
			set fidminq.cpbinf=CPBINF
			set fidminq.fipscpbinf=FIPSCPBINF
			set fidminq.adspbinf=ADSPBINF

			do fidminq.bypassSave()
			if ER do EXC
	 
			}
		}

	do io.close()

	// "FIDM inquiry file import complete"
	if 'ER set ER="W" set RM=$$^MSG(4882)

	quit

EXC	// Log error to DAYENDEXC

	// "FIDM inquiry file"
	set RM=$$^MSG(4798)
	do LOG^UTLEXC("FIDMINQ","*",RM,$G(TAXID),$G(%ZTSEQ),$G(ET))
	kill ET,%ZTSEQ

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43517^Sanchez SCM Administrator^3457"	// Signature - LTD^TIME^USER^SIZE
