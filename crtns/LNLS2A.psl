LNLS2A	 /* 
    ORIG: GRAY - 01/14/2000
    DESC: Transfer all accounts of one pool to 2nd
    
    ---- Comments --------------------------------------------------------
    
    ---- Revision History ------------------------------------------------

         07/26/05 - SkariahV- CR16679
	           Removed #WARN directive.
	              
	01/08/03 - GRAY - 51351
		   Replaced Db.nextKey methods with Db.select.

	12/21/99 - GRAY - 32507
		   Removed code no longer needed due to the Elimination
		   of Teller/Branch Character Interface and placed remaining
		   code into PSL'd procedure definition.
 */

	quit
	

Public FILE(INV,OLD,NEW)	// File data
		
	// Called internally and by routine MRPC049

	type String GRP
	type Number CID,PSEQ,SEQ
	type Date DATE

	type Public Number ER
	type Public String ET,RM

	// Resolve UTBL("INCD" table
	if 'Db.isDefined("LNLS2","INV,NEW") do {
		new lnls2o,lnls2n
		type RecordLNLS2 lnls2o=Db.getRecord("LNLS2","INV,OLD")
		type RecordLNLS2 lnls2n=lnls2o.copy(0)
		set lnls2n.incd=INV
		set lnls2n.pl=NEW
		do lnls2n.bypassSave()
		}

	// Modify each account
	type ResultSet rs1=Db.select("DISTINCT GRP","LNLS8","INCD=:INV AND PL=:OLD")
	while rs1.next() do {
		set GRP=rs1.getCol(1)

		if 'Db.isDefined("LNLS3","INV,NEW,GRP") do {
			type RecordLNLS3 lnls3o=Db.getRecord("LNLS3","INV,OLD,GRP")
			type RecordLNLS3 lnls3n=lnls3o.copy(0)
			set lnls3n.incd=INV
			set lnls3n.pl=NEW
			set lnls3n.grp=GRP
			do lnls3n.bypassSave()
			}

		type ResultSet rs2=Db.select("CID,PSEQ","LNLS8","INCD=:INV AND PL=:OLD AND GRP=:GRP")
		while rs2.next() do {
			set CID=rs2.getCol(1)
			set PSEQ=rs2.getCol(2)

			type RecordLNLS8 lnls8=Class.new("RecordLNLS8")
			set lnls8.incd=INV
			set lnls8.pl=NEW
			set lnls8.grp=GRP
			set lnls8.cid=CID
			set lnls8.pseq=PSEQ
			do lnls8.bypassSave()
			
			do Db.delete("LNLS8","INCD=:INV AND PL=:OLD AND GRP=:GRP AND CID=:CID")
			
			type RecordLNLS6 lnls6=Db.getRecord("LNLS6","CID,PSEQ")
			do lnls6.setAuditFlag(1)
			set lnls6.pl=NEW
			do lnls6.save()
			}
		}
	
	// Move all daily transaction information to new pool
	
	type ResultSet rs3=Db.select("EFD,CID,TSEQ","LNLSDT","INCD=:INV AND PL=:OLD")
	while rs3.next() do {
		set DATE=rs3.getCol(1)
		set CID=rs3.getCol(2)
		set SEQ=rs3.getCol(3)

		type RecordLNLSDT lnlsdto=Db.getRecord("LNLSDT","DATE,INV,OLD,CID,SEQ")
		type RecordLNLSDT lnlsdtn=lnlsdto.copy(0)
		set lnlsdtn.efd=DATE
		set lnlsdtn.incd=INV
		set lnlsdtn.pl=NEW
		set lnlsdtn.cid=CID
		set lnlsdtn.tseq=SEQ
		do lnlsdtn.bypassSave()
				
		do Db.delete("LNLSDT","EFD=:DATE AND INCD=:INV AND PL=:OLD AND CID=:CID AND TSEQ=:SEQ")
		}

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60107^17753^Viji Skariah^2531"	// Signature - LTD^TIME^USER^SIZE
