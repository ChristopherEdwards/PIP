IRSO99A		/*
	ORIG: Michael Winigrad (6969)
	DESC: IRS Processing - Form 1099-A Original
	I18N=QUIT: Excluded from I18N standards.

	---- Comments --------------------------------------------------------
	======================================================================
		*** THIS ROUTINE IS TO BE COMPILED WITH IRSTPMTR ***
		     *** DO NOT RUN THIS ROUTINE STANDALONE ***
	======================================================================
	
	---- Revision History ------------------------------------------------
	
	09/22/05 - HAILEYM - CR17143
		Converted to PSL.
		
	*/
	
	// Accept "Dead code" warning during runtime of @IRSTAPE.
	#ACCEPT Date=10/14/2005;PGM=Marie Hailey
	quit
	
	
MTRCNTRL	// Master control
	
	type public Boolean ER
	type public Number FAMT(),FCNT
	
	type Number I
	
	set FCNT=""
	for I=1:1:8 set FAMT(I)=""
	
	do Db.fastDelete("B1099A")
	do ARECBLD quit:ER	// Build and write the "A" record
	do BRECBLD quit:ER	// Build and write the "B" records
	do CRECBLD quit:ER	// Build and write the "C" record
	do STORETOT		// Store the TTR report totals
	quit
	

ARECBLD	// "A" record builder

	type public String TAMT()
	
	type String AMTIND,FORMCODE
	
	set FORMCODE=4		// IRS form code
	set AMTIND=24		// IRS amount type indicator
	set TAMT(2).piece("|",2)="Balance of Principal Outstanding"
	set TAMT(4).piece("|",2)="Fair Market Value of Property"
	do ARECWRT		// Write the record to tape
	quit
	

BRECBLD	// "B" record builder

	type public Date BEGYR,ENDYR
	type public Number AMT(),FAMT(),FCNT
	
	type Date DATE
	type Number AMT2,AMT4,CID,I
	type String ADDR,CITY,CNTRY,CORRTN,DOCCODE,FORCNTY,FULLADDR,LIND,LNAME
	type String NAME,SPECIAL,STATE,TIN,TINTYPE,X,XTIN,ZIP
	
	type RecordB1099A b1099a
	type RecordM1099 m1099
	type ResultSet rs
	
	set DOCCODE="  "	// Document specific code
	set CORRTN=" "		// Corrected return indicator

	set b1099a=Class.new("RecordB1099A")
	set b1099a.pdate=CUVAR.TJD
	
	set rs=Db.select("DISTINCT TAXID","M1099","FORMTYP=2")
	while rs.next() do {
		set TIN=rs.getCol(1)
	
		for I=1:1:9,"A","B","C" set AMT(I)=0
	
		set TINTYPE=$S(TIN?2N1"-"7N:1,TIN?3N1"-"2N1"-"4N:2,1:" ")
		set XTIN=TIN.piece("|",1).translate(" ","-")
		
		set b1099a.btaxid=XTIN
		set b1099a.btintype=TINTYPE
		set b1099a.btin=XTIN
		
		type DbSet ds=Db.selectDbSet("M1099","TAXID=:TIN AND FORMTYP=2")
		while ds.next() do {
			set m1099=ds.getRecord("M1099")
			
			if m1099.td<BEGYR!(m1099.td>ENDYR) quit	// Wrong year
			if (m1099.debtout+m1099.debtsat+m1099.fmktv)=0 quit	// Nothing to report
			
			set AMT(2)=m1099.debtout*100	// Debt Outstanding
			set AMT(4)=m1099.fmktv*100	// Fair Market Value

			set CID=m1099.seq	// Account number (sequence number)
			set NAME=m1099.nam
			set LNAME=m1099.lnam
			set FULLADDR=m1099.ad1_"|"_m1099.ad2_"|"_m1099.ad3_"|"_m1099.city_"|"_m1099.state_"|"_m1099.mzip
			set ADDR=m1099.ad1_" "_m1099.ad2_" "_m1099.ad3
			set CITY=m1099.city
			set STATE=m1099.state
			set ZIP=m1099.mzip.piece("-",1)_m1099.mzip.piece("-",2)
			set CNTRY=m1099.cntry
			set FORCNTY=$S(CNTRY="US":" ",1:1)	// Foreign country indicator
			set LIND=$S(m1099.bliab:1,1:" ")
			set DATE=m1099.td
			// ---------  Special Data Entry Section  --------
	
			//; Blank|544|546
			set SPECIAL="".justify(3)
			//; Liability Indicator|547
			set SPECIAL=SPECIAL_LIND
			//; Lender's Date of Acquisition|548|555
			set SPECIAL=SPECIAL_DATE.toString("YEARMMDD")
			//; Description of property|556|594
			set X=m1099.desc_" "_m1099.desc2_" "_m1099.desc3
			set SPECIAL=SPECIAL_X.justify(39,-1,,1)
			//; Blank|595|662
			set SPECIAL=SPECIAL_"".justify(68)
			//; Special Data Entries|663|722
			set SPECIAL=SPECIAL_"".justify(60)
			//; Blank|723|750
			set SPECIAL=SPECIAL_"".justify(28)
			
			do BRECWRT	// Write the record to tape
		
			if TINTYPE=2 set X=LNAME
			else  set X=NAME
			do NAMCNTRL^IRSTPMTR set b1099a.bnamcon=X
			
			set AMT2=AMT(2)/100
			set AMT4=AMT(4)/100
	
			// Insert B Records into file B1099A
			/*
			B1099A.BLDA will be in MM/DD/YY format because 1099PRO 
			accept only this xx/xx/xx date format.
			*/
			if b1099a.getMode() do b1099a.setMode(0)
			set b1099a.bseq=m1099.seq
			set b1099a.bcid=CID
			set b1099a.bamt2=AMT2
			set b1099a.bamt4=AMT4
			set b1099a.bname=NAME
			set b1099a.baddr1=m1099.ad1
			set b1099a.baddr2=m1099.ad2
			set b1099a.baddr3=m1099.ad3
			set b1099a.bcity=CITY
			set b1099a.bstate=STATE
			set b1099a.bzip=m1099.mzip
			set b1099a.bli=LIND
			set b1099a.blda=DATE.toString("MM/DD/YY")
			set b1099a.bdp1=m1099.desc
			set b1099a.bdp2=m1099.desc2
			set b1099a.bdp3=m1099.desc3
			do b1099a.bypassSave()
	
			// Calculate number of records and tax form report totals.
			set FCNT=FCNT+1
			set FAMT(1)=FAMT(1)+AMT2	// Balance
			set FAMT(2)=FAMT(2)+AMT4	// Value
			}
		}
	quit
	
	
CRECBLD	// "C" record builder
	
	do CRECWRT	// Write the record to tape
	quit


%STOPLOD	// Stop %ZRTNLOD from this point on down

	quit
	
	
TRECWRT		// Dummy line reference for GT.M

	quit
	

ARECWRT		// Dummy line reference for GT.M

	quit
	
	
BRECWRT		// Dummy line reference for GT.M

	quit
	
	
CRECWRT		// Dummy line reference for GT.M

	quit
	
	
STORETOT	// Dummy line reference for GT.M

	quit

vSIG()	quit "60215^50576^Marie Hailey^4976"	// Signature - LTD^TIME^USER^SIZE
