MRPC017(RETURN,VERSN,CID,FD,TD)	//Public;MRPC call for Interest Change Summary
	/*

	   DESC: Interest Change Summary Report.
	   KEYWORDS: RPC

	   ARGUMENTS:
	         . RETURN        Date, interest rate & balance details /TYP=T/REQ/MECH=REFNAM:W

	         . VERSN         ^MRPC017 version number          /TYP=N/REQ/MECH=VAL
	                         current version=1

	         . CID           Account Number                 /TYP=N/REQ/MECH=VAL

	         . FD            From Date                      /TYP=D/REQ/MECH=VAL

	         . TD            To Date                        /TYP=D/REQ/MECH=VAL

	   RETURNS:
	         . $$    Error Message                   /TYP=T
	                 Null= No Error

	   RELATED:
	         . $$^PBSMRPC - MRPC Service Class Driver

	   EXAMPLE:
	         S RM=$$^MRPC017(.VAL,1,CID,FD,TD)
	  -------Revision History------------------------------------------
	
	   16/05/06 - SANTHUMS - 21179
		      Added WIRN array as parameter while calling 
		      INIT^UAVB.
		       
	   08/04/03 - CARROLLJ - 51629
		      Modified call to INIT^UAVB to pass acn object as first
		      parameter.

           04/10/02 - TELIV - 49794
           Converted to PSL
 
	*/

	new CLS,CNT,CRLF,I,N,RCS,STR,TMPSTR,UAVB,X,Y
	type String WIRN()

	// Version number of client message is not compatible with server
	if $G(VERSN)'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))

	// Invalid account ~p1
	if 'Db.isDefined("ACN","CID=:CID") quit $$ERRMSG^PBSUTL($$^MSG(1259,CID))
	type RecordACN acn=Db.getRecord("ACN","CID=:CID")
	set CLS=acn.cls

	// Invalid Account Class
	if (CLS'="L"),(CLS'="D") set ET="INVLDCLS" do ^UTLERR quit $$ERRMSG^PBSUTL($G(RM))

	// From Date Error Checking
	do FDERR^UAVB3

	if $G(ER) quit $$ERRMSG^PBSUTL($G(RM))

	// To Date Error Checking
	do TDERR^UAVB3

	if $G(ER) quit $$ERRMSG^PBSUTL($G(RM))
	do INIT^UAVB(.acn,CID,FD,TD,1,,,,.WIRN())

	if $G(ER) quit $$ERRMSG^PBSUTL($G(RM),$G(ET))

	// Remove any duplicate entries from WIRN.  They may have been added
	// back in by ^UAVB0 when it processed the bill record entries.
	set X=""
	set X=$O(WIRN(X))
	set Y=X
	for  set X=$O(WIRN(X)) quit:X=""  do {
		if $P(WIRN(X),"|",1)=$P(WIRN(Y),"|",1),$P(WIRN(X),"|",2)=$P(WIRN(Y),"|",2) kill WIRN(X)
		else  set Y=X
		}

	set CNT=0

	// DONE TO OBTAIN FIRST REQUIRED VALUE OF WIRN()
	set N=""

	for  set N=$O(WIRN(N)) quit:(N="")!(N>TD)  do {
		set CNT=CNT+1
		set RCS(CNT)=N_"|"_WIRN(N)
		}

	// No rate change activity
	if 'CNT set ER=1 set RM=$$^MSG(1977) quit $$ERRMSG^PBSUTL($G(RM))

	// Create formatted output string
	set (TMPSTR,STR)=""
	set CRLF=$C(13,10)
	set N=""

	// Change Date, Interest Rate, Principal Balance
	if CLS="L" for  set N=$O(RCS(N)) quit:N=""  set STR=STR_$P(RCS(N),"|",1)_$C(9)_$P(RCS(N),"|",2)_$C(9)_$P(RCS(N),"|",6)_CRLF

	// Change Date, Interest Rate, Principal Balance
	else  for  set N=$O(RCS(N)) quit:N=""  set STR=STR_$TR($P(RCS(N),"|",1,3),"|",$C(9))_CRLF
	set RETURN=$$V2LV^MSG(STR)
	quit ""

vSIG()	quit "60402^6692^Sunitha Santhumayor^2785"	// Signature - LTD^TIME^USER^SIZE
