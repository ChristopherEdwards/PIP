MASDEPCL // Mass Deposit Closeout
	/*

		 ORIG: RATMANSKYD - 08/11/98
		 DESC: Mass Deposit Closeout

	   KEYWORDS:     Closeout
	  ------------ Revision History -----------------------------------------
	01/21/04 - CARROLLJ - CR7997
		   Changed call from EXEC^TRNDRV to TRNSINGL^TRNDRV.

	12/11/03 - CARROLLJ - CR7239
		   Modified call to EXEC^TRNDRV.

	10/10/02 - DATTAR - 49451
		   Converted to PSL

          -------------------------------------------------------------------------
	*/


	new CID,CONT,FROM,ETC,ETC2,ITC,ITC2,MASDEP,OFFCID,SEQ,TOTAMT,WHERE

	set %ProcessMode=0

	// Kill Temp File
	do Db.delete("TMPMADCL","JOB=:%ProcessID")
	
	// Prompt Screen
	set CID="" 
	set SEQ=0
	type RecordMASDEPCL fMASDCL=Db.getRecord("MASDEPCL","JOB=:%ProcessID",1)

	do DRV^USID(%ProcessMode,"MASDEPCL",.fMASDCL)

	// If Account Number was Entered
	if CID'="" do {
		if 'Db.isDefined("ACN","CID=:CID") quit
		type RecordTMPMADCL fTMPMADC=Db.getRecord("TMPMADCL","JOB=:%ProcessID,CID=:CID")
		do fTMPMADC.bypassSave()
		}
	else  do {
		new FROM,SELECT,WHERE

		// Build FROM and WHERE for SQL
		set FROM=$$FROM(.Q) 
		set WHERE=$$Q2SQL^SQLCONV(.Q)

		// DEP or CIF only
		if FROM=""!(WHERE="") set ER=1 set RM=$$^MSG(2292,"DEP","CIF") quit

		set SELECT="CID"

		//I18N=OFF
		if FROM["DEP",FROM["CIF" do {
			set WHERE=WHERE_" AND CIF.ACN=DEP.ACN"
			set SELECT="ACN.CID"
			}
	
		//I18N=ON

		#ACCEPT DATE=12/10/03;PGM=John Carroll
		type ResultSet rs=Db.select(SELECT,FROM,WHERE)
		while rs.next() do {
			type RecordTMPMADCL fTMPMADC=Db.getRecord("TMPMADCL","JOB=:%ProcessID,CID=:rs.getCol(1)")
			do fTMPMADC.bypassSave()
			}
		}
	if $G(ER) quit

	// Closeout Report	
	set RID="SCA622" 
		
	do ^URID 
	
	// Continue Prompt
	set %TAB("CONT")=".CONT2"
	set %READ="@@%FN,,,,CONT/REQ"
		
	do ^UTLREAD 
		
	if VFMQ="Q" quit
	if 'CONT set VFMQ="Q" quit

	// Build Offest transaction
	set ETC="MCR"
	
	type RecordTRN trn=Db.getRecord("TRN","ETC=:ETC")
	
	// Internal Transaction Code
	set ITC2=trn.itc
	set ETC2=ETC
	
	type ResultSet rs=Db.select("CID","TMPMADCL","JOB=:%ProcessID")
	while rs.next() do CLOSE(rs.getCol("CID"))
	quit


CLOSE(CID) //This section is closing accounts

	// INPUT: CID - Account Number
	new CRCD,DFT,ETC,TAMT,TYPE

	type RecordACN acn=Db.getRecord("ACN","CID=:CID")
	if acn.cls'="D" quit
	set TYPE=acn.type 
	set CRCD=acn.crcd

	type RecordPRODCTL fCTL=Db.getRecord("PRODCTL","TYPE=:TYPE")
	set ETC=fCTL.drtrci

	// Transaction code missing in product ~p1, node ~p2, piece ~p3
	if ETC="" set ER=1 set RM=$$^MSG(2694,TYPE,14,8) do EXC quit

	type RecordTRN trn=Db.getRecord("TRN","ETC=:ETC")

	// Internal Transaction Code
	set ITC=trn.itc

	set SEQ=SEQ+1

	type RecordTTX ttx=Db.getRecord("TTX","TJD=:%SystemDate,BRCD=0,UID=:%UserID,TSEQ=:SEQ",1)
	
	type RecordDEP dep=Db.getRecord("DEP","CID=:CID")

	do CWIP^DEPDBS(.dep,.ttx,.trn,%SystemDate)

	set TAMT=DFT("AMT")

	// Mass deposit closeout transaction
	set ttx.cid=CID
	set ttx.itc=ITC
	set ttx.etc=ETC
	set ttx.tamt=TAMT
	set ttx.tlo=%UserStation
	set ttx.tcmt=$$^MSG(3573)

	// Currency Code
	set ttx.crcd=CRCD

	do TRNSINGL^TRNDRV(.ttx,.acn,%SystemDate,BRCD,4)

	// Mass deposit closeout offset transaction
	set SEQ=SEQ+1
	type RecordTTX ttx=Db.getRecord("TTX","TJD=:%SystemDate,BRCD=0,UID=%UserId,TSEQ=:SEQ",1)
        set ttx.cid=OFFCID
	set ttx.itc=ITC2
	set ttx.etc=ETC2
	set ttx.tamt=TAMT
	set ttx.tlo=%UserStation
	set ttx.tcmt=$$^MSG(3577)

	// Currency Code
	set ttx.crcd=CRCD

	do TRNSINGL^TRNDRV(.ttx,.acn,%SystemDate,BRCD,4)
	quit


FROM(Q)	// This function builds FROM statement based on Q array

	new COUNT,EXIT,NUM,RETURN
	set NUM=""
	set COUNT=0
	for  set NUM=$O(Q(1,NUM)) quit:NUM=""  set TMP($P(Q(1,NUM),".",2,2))=""
	if $D(TMP("DEP"))=0 set TMP("DEP")=""
	set EXIT=0
	for  set NUM=$O(TMP(NUM)) quit:NUM=""!(EXIT=1)  do {
		if NUM'="DEP",(NUM'="CIF") set EXIT=1 quit
		if COUNT=0 set RETURN=NUM set COUNT=1
		else  set RETURN=RETURN_","_NUM
		}

	if EXIT=0 quit RETURN
	quit ""


EXC	// Log error to DAYEND

	if $G(ET)="" set ET=$G(RM)
	set CID=$G(CID)
	do LOG^UTLEXC($T(+0),"*",$G(RM),CID,$G(%ZTSEQ),$G(ET),BAL)

	kill ET,%ZTSEQ
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43576^Sanchez SCM Administrator^3919"	// Signature - LTD^TIME^USER^SIZE
