LNPTS16(RecordLN ln, RecordTTX ttx)	

	/*

	  Escrow only paid in and out via mtg acc

	       ORIG:  David Caliendo (5527) - 07/03/86
	       DESC:  Generate a secondary transaction to the escrow account 
	              to post a withdrawal or deposit to the escrow.
	
	
	---- Revision History ------------------------------------------------
	
	01/26/06 - SmithCD - CR 19343 (16890)
		   . Modified to properly set TRTYPE in TB() to prevent error 
		     in LNPTS2
		   . Cleaned up code
		   . Removed old revision history and cleaned up code

	*/

	do INIT(.ln, .ttx, 0)

	quit

	
INIT(RecordLN ln,		// Loan account				/REF:RW
     RecordTTX ttx,		// Primary transaction			/REF:RW
     Boolean ERS)		// Escrow remittance shortcut

	type public Cache %CACHE()
	type public Number ZAMT
	type public String LTC, TB()
	type public Boolean ER
	type public RecordDEP dep, dep()

	type Number AMT, ESCCID
	type String ESCACT, ETC, TRTYPE
	
	// Find transfer type
	set TRTYPE = $$FIELD^UTSO(ttx.tso, "ESC").piece(":", 1)

	type RecordLNBIL0 lnbil0 = Db.getRecord("LNBIL0", "CID=:ln.cid")
	set ESCACT = $$BIL0^BILFUNCS(.lnbil0, TRTYPE)
	set ESCCID = ESCACT.piece("#", 2)

	// Escrow type ~p1 not defined for this account
	if ESCCID.isNull() do Runtime.setErrMSG("LNBIL0", 1016, TRTYPE) quit

	// Update transfer balance array (used in LNPTS1)

	// Transaction amount
	set TB(TRTYPE).piece("," ,1) = TB(TRTYPE).get().piece(",", 1) + ZAMT
	// Transfer account number
	set TB(TRTYPE).piece(",", 2) = ESCCID
	// Transaction amount remaining
	set TB(TRTYPE).piece(",", 3) = TB(TRTYPE).piece(",", 3) + ZAMT
	
	type RecordTRN trn = Db.getRecord("TRN", "ETC=:LTC")
	
	// Ensure we're using the correct dep object
	if 'dep.exists() ! (dep.exists() & (ESCCID '= dep.cid)) do {
		if dep(ESCCID).exists() set dep = dep(ESCCID)
		else  set dep = Db.getRecord("DEP", "CID=:ESCCID")
		}
	
	type RecordPRODCTL prodctl = %CACHE("PRODCTL").getRecord("PRODCTL", "TYPE=:dep.type")

	if 'trn.itc.extract(1) do {
		// Affects Escrow Remittances - DR Final Remittance Tran Code
		if trn.pcfl11 set ETC = prodctl.drtrfr
		// DR Escrow Remit Shortcut Tran Code
		else  if ERS set ETC = prodctl.drters
		// DR Transfer Suspense Tran Code
		else  set ETC = prodctl.drtrts
		}
	else  do {
		// Affects Escrow Remittances - CR Final Remittance Tran Code
		if trn.pcfl11 set ETC = prodctl.crtrfr
		// CR Principal Variance Tran Code
		else  if ERS set ETC = prodctl.crtrpv
		// CR Transfer Suspense Tran Code
		else  set ETC = prodctl.crtrts
		}

	do POST^LNTRB(.ttx, ESCCID, ETC, ZAMT, %EffectiveDate, %UserStation, ttx.tso, , dep.crcd) quit:ER

	// Escrow payment suspense
	do GL^LNPTSU(.ttx, ZAMT, 7)	
	
	// Update the History Support record (lower level of HIST)
	do %HSEQ^LNPTSU(.ttx, "*#"_TRTYPE_"#"_ZAMT)
	
	set AMT = $select(ttx.itc1:ZAMT, 1:ZAMT * (-1))
	set ln.teb = ln.teb + AMT

	type RecordUTBLICPA utblicpa = Db.getRecord("UTBLICPA", "KEY=:ln.icpa")

	// Subtract Total Escrow Balances from Adjusted Balance for Accr Calc
	if utblicpa.pc4 set ln.balint = ln.balint - AMT

	set ZAMT = 0
	
	// Total Secondary Balance
	set ttx.tsb = dep.bal
		
	quit
	

public	ERS(RecordLN ln,	// Loan account				/REF:R
    	    RecordTTX ttx)	// Primary transaction			/REF:RW
    
	// Entry point for Escrow Remittance Shortcut transactions
	
	do INIT(.ln, .ttx, 1)

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60297^70600^Chad Smith^3181"	// Signature - LTD^TIME^USER^SIZE
