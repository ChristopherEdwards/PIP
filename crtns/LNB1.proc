LNB1		/*
	ORIG: kini - 04/25/2005
	DESC: Standard Loan Bill Print Design (Non-Rev/Cred)

	---- Comments --------------------------------------------------------
	This procedure is called indirectly from LNBLPNT procedure.
	
	I18N=QUIT: Excluded from I18N standards.
	---- Revision History ------------------------------------------------

	04/26/05 - KinI - 15524
		Created procedure based on LNB1.m routine.
	*/
 
	type public String ER
	type public Number BLSQ, CID, PG, TYPE, ZSUP
	type public Date TD

	type RecordLN ln = Db.getRecord("LN","CID=:CID",1)

	// Initialize data
	do INIT^LNB(.ln) if ER.get() quit
	
	type RecordLNBIL1 lnbil1 = Db.getRecord("LNBIL1","CID=:CID,SCHSEQ=:BLSQ",1)
	
	type RecordLNBLP lnblp = Db.getRecord("LNBLP","CID=:CID,BILDT=:TD,TYPE=:TYPE",1)
	
	// Initialize custom data
	set ZSUP = 0  // Do not suppress bank name and address print
	
	// Advance to new page
	do PNT1^LNB()
	
	// Print billing information
	do S1^LNB(ZSUP,,.ln)
	do S2(.ln,.lnbil1,.lnblp)
	do S3H^LNB
	do S3D^LNB("S5^LNB1(.lnbil1,.lnblp),S6(1,.ln),S7(0)","S1^LNB(ZSUP,,.ln),S3H",$$MSG())
	
	if (PG = 1) do {
		do S4 
		do S5(.lnbil1,.lnblp) 
		do S6^LNB(1,.ln)
		}
	
	do S7^LNB(ZSUP)
	
	quit

	
S2(RecordLN ln,
   RecordLNBIL1 lnbil1,
   RecordLNBLP lnblp)	
   
	// Section 2 - Static Bill Date Data

	type public String LINE, WIRN()
	type public Number APD, CHG, IDP, LCHG, MCHG

	type String COLL, ELMNT, ROW
	type Number CDUE, CUES, CUIN, CUPR, PC, PIDUE, TOTINT, TOTPRN
	type Boolean TESC = 0		// Escrow flag
	
	set (TOTINT, TOTPRN) = 0 
	set ELMNT = "" 
	set COLL = ""
	
	kill LINE	 	

	set CDUE = +lnbil1.casd             // Cur due
        set PIDUE = +lnbil1.ctab            // P&I Total 
	
	type RecordLNBIL0 lnbil0 = Db.getRecord("LNBIL0","CID=:ln.cid")

	set ROW = $$ELEMENT^BILFUNCS(.lnbil0)
	for PC = 1:1:20 quit:ROW.piece($C(9),PC) = ""  do {
		set ELMNT = ROW.piece($C(9),PC).piece("#",1)
		if (ELMNT.extract(1,3) = "ESC") set TESC = 1
		if (ELMNT.extract(1,3) = "I") set TOTINT = TOTINT + $$AMTDUE^BILFUNCS(.lnbil1,"I")
		if (ELMNT.extract(1,3) = "P") set TOTPRN = TOTPRN + $$AMTDUE^BILFUNCS(.lnbil1,"P")
		}
		
	set CUIN = +TOTINT           // Cur int
	set CUPR = +TOTPRN           // Cur prl
	
	// Current Due (CDUE) includes principal, interest, escrow, and loan 
	// fees (MCHG).  Subtract all extra elements to calculate escrow due 
        set CUES = 0
        						
        if 'IDP set CUES = CDUE - CUIN - CUPR - MCHG	// Cur esc	
        else  if TESC set CUES = CDUE - PIDUE - MCHG
	
	set CHG = APD + CDUE + LCHG                     // Tot due

	// Collateral Record
	if 'ln.coll.isNull() do {
		type RecordLNCOL lncol=Db.getRecord("LNCOL","CID=:ln.cid,COLL=:ln.coll",1)

		set COLL = lncol.phldseq_"|"_lncol.id_"|"_lncol.fpldg_"|"_lncol.pctpldg
		set COLL = COLL_"|"_lncol.pldgacn_"|"_lncol.maxpldg_"|"_lncol.coltyp
		set COLL = COLL_lncol.desc_"|"_lncol.pldgamt_"|"_lncol.sltvc
		set COLL = $$FULL^LNB(COLL,17)
		}
		
	do S2A^LNB("Past Due",lnblp.apd,1,"$")
	do S2A^LNB("Payment(s) Due",lnblp.schdue,2,"D")

        if IDP do S2A^LNB("Prin. & Int. Due",PIDUE,1,"$")          
        else  do S2A^LNB("Interest Due",CUIN,1,"$")              

	do S2A^LNB("Collateral",COLL,2,"T")

        if 'IDP do S2A^LNB("Principal Due",CUPR,1,"$")             

	do S2A^LNB("Current Rate",$$^SCARND(WIRN(WIRN("").order(-1)),8,"","",5)_"%",2,"T")
	do S2A^LNB("Escrow Due",CUES,1,"$")
	do S2A^LNB("Maturity Date",ln.mdt,2,"D")
	do S2A^LNB("Late Charge Due",+LCHG,1,"$",1)
	do S2A^LNB("Miscellaneous Due",+MCHG,1,"$")
	do S2A^LNB("Interest Paid "_lnblp.yob,+lnblp.iytd,2,"$")
	do S2A^LNB("TOTAL DUE",CHG,1,"$")
	do S2A^LNB("Interest Paid "_(lnblp.yob-1),+lnblp.ipy,2,"$")

	quit

	
S4	// Marketing Messages

	do S4^LNB($$MSG())

	quit

	
S5(RecordLNBIL1 lnbil1,
   RecordLNBLP lnblp)	// Top section of remittance advice

	type public String LINE
	type public Number CHG, LCHG, MCHG, %SCHDUE

	do BLN^LNB(45)

	set LINE=""

	do S2A^LNB("Due "_%SCHDUE,+lnbil1.casd,1,"$")
	do S2A^LNB("Total Due",CHG,2,"$")
	do S2A^LNB("Previously Billed",lnblp.apd,1,"$")
	do S2A^LNB("Extra Principal","_____________",2)		
	do S2A^LNB("Charges Due",+LCHG+MCHG,1,"$")
	do S2A^LNB("Extra Tax Escrow","_____________",2)		
	do S2A^LNB("Extra Insurance","_____________",2)		

	do PNT^LNB("",2)

	quit
	
	
MSG()	// Define fixed message to print after variable marketing message

	quit "Please detach bottom portion and return with your payment"
	
	

vSIG()	quit "60079^60581^Irina Kin^4270"	// Signature - LTD^TIME^USER^SIZE
