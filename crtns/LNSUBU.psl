LNSUBU
	/*
		Loan subsidy processing utility routine

	  CALLED BY:
	      CALLS:
	       DESC:  Loan subsidy library utility.
	
	   GLOBALS -
	       READ:
	        SET:
	
	      INPUT:
	     OUTPUT:
	
	  EXT ENTRY:
	
	    CALCPGM: Custom program to calculate subsidy information.
	
	        Must contain 2 line tags:
	      SUBRAT - extrinsic function returns [LN]SUBRAT
	      SUBAMT - extrinsic function returns [LN]SUBAMT
	
	    In each of the 3 cases, the variables pre-defined are:
	      CID - loan account number
	      EFD - effective date of calculations (usually =TJD)
	      RecordLN - Loan object


	---- Revision History ------------------------------------------------

	07/11/07 - DHANALAKSHMI R - CR28045
		   Modified the section AMT to check for Invalid routine name
		   to avoid the system hanging while running @REP543.
		   Modified the sections AMT and SUBPLN to correct warnings.

	10/20/06 - chhabris - CR23631
		   Modified AMT section to set SUBRAT as RATE if SUBRAT is null.
		   This was causing undefined errors while executing @REP543.
	
	03/13/06 - chhabris - CR19871
		   Modified INIT section to remove setting of variable SUBRAT.
		   Modified RATE section to initialize SUBRAT="" as it was 
		   throwing undefined errors for the same.
			
	02/22/06 - chhabris - CR19558
		   Modified section RATE to check for SUBRAT.isNull and retrive
		   the value from UTBLLNSUB.
		   Modified section CALC to change variable SUBRAT to RATE and
		   SUBAMT to AMT.
	
	12/30/05 - chhabris - CR18154
		   Modified SUBDT section to remove "ORDER" from the last
		   parameter of Db.select() statement. 
		  

	07/12/05 - BHOLT - CR18154
		   Removed the warings and GTM error.
	
	12/10/03 - CARROLLJ - CR7239
		   Added #ACCEPT prior to xecute command to correct compile
		   errors.

	01/09/03 - CARROLLJ - 51349
		   Modified the RATE and AMT sections to correctly set the
		   program to be executed.

	06/27/02 - CARROLLJ - 43583
		   PSL conversion cleanup.

	10/31/01 - CARROLLJ - 43583
		   Convert to PSL. The UX and UXA sections were removed. The
		   setAuditFlag must now be used.

	  -----------------------------------------------------------------------
	*/

	quit


public RATE(RecordLN ln,CID,%EffectiveDate)	//Private; Return subsidy rate

	/*
	
	   ARGUMENTS:
	       . ln  Loan Object		/TYP=RecordLN/REQ/REF:RW
	       . CID Account Number		/REQ/TYP=N
	       . EFD Effective date for rate	/REQ/TYP=D
	
	   RETURNS:
	       . $$ Subsidy rate
	
	*/
	type public Number RATE
	type public String SUBPLN

	type Number CALCPGM,SUBRAT
	set SUBRAT=""

	do INIT(.ln)
	
	set SUBPLN=ln.subpln
	if 'SUBPLN.isNull() do {
		type RecordUTBLLNSUB utbllnsub=Db.getRecord("UTBLLNSUB","SUBPLN",1)
		if utbllnsub.getMode() set SUBRAT=utbllnsub.prcntg
		}
		
	if 'CALCPGM.isNull() set CALCPGM=CALCPGM.translate("$")

	quit SUBRAT

	
public AMT(RecordLN ln,CID,%EffectiveDate)	// Return subsidy amount


	/*
	
	   ARGUMENTS:
	       . ln  Loan Object		/TYP=RecordLN/REQ/REF:RW
	       . CID Account Number   /REQ/TYP=N
	       . EFD Effective date for amt  /REQ/TYP=D
	
	   RETURNS:
	       . $$ Subsidy amount
	
	*/
	type public Number INT,RATE,SUBRAT
	type public String CALCPGM
	
	type Number SUBAMT = 0	
        type String X
	
	do INIT(.ln)
	
	/*
	RATE variable will be always defined for Subsidized loan with 
	subsidy plan attached to the account, the same is calulated in
	the RATE section.
	*/
	if SUBRAT.get().isNull() set SUBRAT=RATE

	if CALCPGM="" do {
		set SUBAMT=INT*SUBRAT/100
		set SUBAMT=$$^SCARND(SUBAMT,,CID)
		}
	else  do {

		// Invalid routine name ~p1
		if '$$VALID^%ZRTNS(CALCPGM) do Runtime.setErrMSG("UTBLSLCO",1454,CALCPGM) quit

		#ACCEPT DATE=12/10/03;PGM=Tanuja Bhol;CR=18154
		set X="S SUBAMT=$$SUBAMT"_CALCPGM_"(.ln,CID,INT)" xecute X
		}
	quit SUBAMT
	

public CALC(RecordLN ln,CID,%EffectiveDate,RATE,AMT,INT)

	/*
	
	   ARGUMENTS:
	       . ln  Loan Object		/TYP=RecordLN/REQ/REF:RW
	       . CID Account Number		/REQ/TYP=N
	       . EFD Effective date for rate	/REQ/TYP=D
	       . RATE Subsidy Rate		/NOREQ/MECH=REF/RW
	       . AMT Subsidy amount		/NOREQ/MECH=REF/RW
	       . INT Billed Interest		/NOREQ/MECH=VAL/TYP=$
	
	*/

	type public Number SUBAMT,SUBRAT

	set INT=INT.get()
	set RATE=RATE.get()
	
	set RATE=$$RATE(.ln,CID,%EffectiveDate)
	if 'RATE.isNull() set ln.subrat=RATE
	set AMT=$$AMT(.ln,CID,%EffectiveDate)
	set ln.subamt=AMT
	quit 
	
	
	
Public INIT(RecordLN ln) //Public; Account initialization

	/*
	
	   INPUTS:
	       . ln  Loan Object		/TYP=RecordLN/REQ/REF:RW

	   OUTPUTS:
	       . SUBPLN  Subsidy Plan
	       . CALCOPT  Calc. Option
	       . CALCPGM  Calc. Program
	
	*/
	
	type public String CALCPGM

	type Number CALCOPT
	type String SUBPLN

	set SUBPLN=ln.subpln,CALCOPT=""
	if 'SUBPLN.isNull() do {
		type RecordUTBLLNSUB utbllnsub=Db.getRecord("UTBLLNSUB","SUBPLN",1)
		if utbllnsub.getMode() set CALCOPT=utbllnsub.calcopt
		}
	if CALCOPT="" set CALCPGM=""
	else  do {
		type RecordUTBLSLCO utblslco=Db.getRecord("UTBLSLCO","CALCOPT",1)
		if utblslco.getMode() set CALCPGM=utblslco.pgm
		}
	quit 

	
PPSLCO	// Post processor for UTBLSLCO.PGM

	type public Boolean ER
	type public String RM, X
	
	if X="" quit 
	if X.extract()'="^" set ER=1 set RM=$$^MSG(5424) quit
	if '$$VALID^%ZRTNS(X) set ER=1 set RM=$$^MSG(5424) quit
	quit 


public SUBDT(ARRAY)
	
	type public Number CID
	type public String %MSKD

	type Number BLDUE
	
	type ResultSet rs=Db.select("CDPD","LNBIL1","CID=:CID","SCHSEQ DESC")
	while rs.next() do {
		set BLDUE=rs.getCol(1)		
		set ARRAY($$DAT^%ZM(BLDUE,$G(%MSKD)))=""
		}
	
	quit 


public SUBPLN()

	// Subsidy Plan
	type public String CID, TSO()

	type RecordLN ln=Db.getRecord("LN","CID=:CID",1)
	if ('ln.getMode())!(ln.subpln="") quit 0

	set TSO("SUBPMT")=ln.subpln
	if ln.schseq set TSO("SUBDT")=ln.schld.toString("MM-DD-YEAR")

	quit 1
 #OPTION ResultClass ON
Public String vSIG()	quit "60824^31519^Dhanalakshmi R^5597"	// Signature - LTD^TIME^USER^SIZE
