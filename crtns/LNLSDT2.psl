public LNLSDT2	// Loan Sale Daily Transaction File Integrity Check
	/*
	       
	DESC: Ensures that all transactions which should be in ^LSDTJ
	      are in ^LSDTJ.  Reports discrepancies using report SCA432.

	---- Revision History ------------------------------------------------

	07/14/06 - KELLYP - CR 22174
		   Rewrote entire procedure to eliminate VPG* logic and to
		   correct various UNDEFINED errors.

        07/26/05 - SkariahV- CR16679
	           Removed #WARN and #OPTIMIZE directives.
	              
	03/20/05 - JERUCHIMC - 13430
	           change reference from XDTJ to DTJ.

	05/28/03 - GRAY - 51351
		   Converted to PSL.
	*/

	type public Number ER
	type public String RM

	type Boolean REVCUS
	type Date FEFD,FRDT,TODT,XEFD
	type Number CID,EXIST,LGRP,PL,TSEQ
	type String CLS,INCD,IO,OLNTB,%READ,RID,STAMP,%TAB(),VFMQ

	do Db.fastDelete("LNLSDT2","PID=:%ProcessID")

	set CLS="L"

	set %TAB("FRDT")=".FRDT1"
	set %TAB("TODT")=".TODT1"
	set %TAB("LGRP")=".GRP1/TBL=[STBLGRP]"
	set %TAB("IO")=$$IO^SCATAB($I)

	set %READ="@@%FN,,,FRDT/REQ,TODT/REQ,LGRP/REQ,IO/REQ"
	
	do ^UTLREAD 
	
	if VFMQ="Q" quit

	if IO'=$I do OPEN^SCAIO quit:ER

	set EXIST=0

	// Create TMP file of transaction discrepancies
	set FEFD=FRDT-1

	type ResultSet rs=Db.select("DISTINCT TJD,CID","DTJ","TJD>:FEFD AND TJD NOT >:TODT","TJD,CID")
	while rs.next() do {
		set XEFD=rs.getCol("TJD")
		set CID=rs.getCol("CID")

		type RecordACN acn=Db.getRecord("ACN","CID=:CID",1)
		if (acn.cls'="L")!(acn.grp'=LGRP) quit

		type DbSet lsds=Db.selectDbSet("LNLS6","CID=:CID")
		while lsds.next() do {
			type RecordLNLS6 lnls6=lsds.getRecord("LNLS6")
			set INCD=lnls6.incd
			set PL=lnls6.pl
			
			type RecordLNLS1 lnls1=Db.getRecord("LNLS1","INCD=:INCD")
			set REVCUS=lnls1.revcus

			type ResultSet xsrs=Db.select("TSEQ,DATETIME","DTJ","TJD=:XEFD AND CID=:CID AND ITC7 NOT>0")
			while xsrs.next() do {
				set TSEQ=xsrs.getCol("TSEQ")
				set STAMP=xsrs.getCol("DATETIME")
				type RecordDTJ dtj=Db.getRecord("DTJ","TJD=:XEFD,DATETIME=:STAMP,CID=:CID,TSEQ=:TSEQ")
 					
				// Eliminate reversals and error corrects if
				// no Reverse from Custodial flag LNLS1.REVCUS
				if 'REVCUS,dtj.itc6 quit

				if Db.isDefined("LNLSDT","EFD=:XEFD,INCD=:INCD,PL=:PL,CID=:CID,TSEQ=:TSEQ") quit

				type RecordLNLSDT2 tmp=Class.new("RecordLNLSDT2")
				set tmp.pid=%ProcessID
				set tmp.date=XEFD
				set tmp.inv=INCD
				set tmp.pl=PL
				set tmp.cid=CID
				set tmp.tseq=TSEQ
				do tmp.bypassSave()
				
				set EXIST=EXIST+1
				}
			}
		}

	// No discrepancies found
	if 'EXIST set ER="W",RM=$$^MSG(1927) quit

	// Invalid report ~p1
	set RID="SCA432" 
	do DRV^URID

	do Db.fastDelete("LNLSDT2","PID=:%ProcessID")

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60460^51953^Pat Kelly^2542"	// Signature - LTD^TIME^USER^SIZE
