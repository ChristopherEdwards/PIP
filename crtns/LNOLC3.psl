LNOLC3	// Loan On-Line Collection - Reindex User
	/*
	       DESC:
	 ---- Revision History ------------------------------------------------

	  12/28/05 - MbuiM
	  	     Added public linetag NAM to return cif.nam to computed 
	  	     column lnolc0.nam 
	  	     
	  11/09/05 - Satyanas
	  	     Converted to PSL Standards.
	 	  
	  04/17/02 - KRISHNAG - ARQ# 49794
                      Converted to PSL
	*/
	
	do INIT
	quit
	

INIT	//

	type public Boolean ER
	type public String NUID,%READ,%TAB(),UID,VFMQ
		
	set ER=0

	// /DES=Borrower's Customer Number
	set %TAB("ACN")=".ACN8/TBL=[LNOLC0]/XPP=D ACN^LNOLC3"

	set %TAB("UID")=".F3/TBL=VUID(/XPR=do TBLUID^LNOLC3"

	// Invalid entry ~p1
	set %TAB("NUID")=".T3/TBL=[SCAU]/XPP=I X=UID S ER=1,RM=$$^MSG(5791)"

	set NUID=%UserID 
	set UID=""

	set %READ="@@%FN,,,ACN/REQ,UID/REQ,NUID/REQ"

	do ^UTLREAD

	if VFMQ="Q" quit

	do FILE

	do END 
	quit
	

TBLUID	// Create lookup table for valid UID for this CIF account
	
	type public String UID,VUID()
	
	type ResultSet rs=Db.select("DISTINCT UID","LNOLC2","ACN=:ACN")
	if 'rs.isEmpty() while rs.next() do {

			set UID=rs.getCol(1)
			set VUID(UID)=""
			}
	quit


FILE	// File data

	type public Number ACN,LSEQ
	type public String NUID,UID
	type RecordLNOLC2 lnolc3
	
	//   Copy UID records to new UID
	type ResultSet rs=Db.select("LSEQ","LNOLC2","ACN=:ACN AND UID=:UID")
	while rs.next() do {
		set LSEQ=rs.getCol("LSEQ")
	
		type RecordLNOLC2 lnolc2=Db.getRecord("LNOLC2","ACN=:ACN,UID=:UID,LSEQ=:LSEQ",1)
		if lnolc2.getMode() do {
			set lnolc3=lnolc2.copy()
			set lnolc3.acn=lnolc2.acn
			set lnolc3.uid=NUID
			set lnolc3.lseq=lnolc2.lseq	
			do lnolc3.bypassSave()
			}
		
		// Remove old records
		do Db.delete("LNOLC2","ACN=:ACN AND UID=:UID AND LSEQ=:LSEQ")

		// Remove DAYEND entries in the future
        	do Db.delete("DAYENDLNOLC","EFD>:%SystemDate AND UID=:UID AND ACN=:ACN AND LSEQ=:LSEQ")
		}

	quit
	

END	//

	type public Boolean ER
	type public String NUID,RM,%TAB,UID,VFMQ

	kill %TAB
	quit:ER
	
	set UID=UID.get()
	set NUID=NUID.get()

	// Collection records for user ~p1 not transferred to user ~p2
	if VFMQ="Q" set RM=$$^MSG(580,UID,NUID)

	// Collection records for user ~p1 transferred to user ~p2
	else  set RM=$$^MSG(581,UID,NUID)
	quit


ACN	// Post processor for customer number
	
	type public Boolean ER
	type public Number %EXT
	type public String X
	
	if X.isNull() quit
	set %EXT=1 

	do ^UCIF quit:ER

	quit
	
public NAM(Number ACN)	// Computed column lnolco.nam

	type RecordCIF cif=Db.getRecord("CIF","ACN=:ACN")
	
	quit cif.nam
	
 #OPTION ResultClass ON
Public String vSIG()	quit "60262^51511^Marie Mbui^2393"	// Signature - LTD^TIME^USER^SIZE
