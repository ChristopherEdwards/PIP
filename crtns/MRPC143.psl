MRPC143(RETURN,VERSN,ORG,IACM,TRM,TRATE,TREXD,MDT,ODT,IRN,ICF,IRCB)	//
	/*
	 ORIG: NATRAJAH - 12/06/2003
	 DESC: This routine used to calculate the Sales Price.

	   KEYWORDS: RPC

	   ARGUMENTS:

		•	RETURN	- Activity detail		/TYP=T/REQ/REF:W
		•	VERSN	- ^MRPC143 version 
			number (current version=1)              /TYP=N/REQ/REF:R
		•	ORG	- Redemption Value		/TYP=N/REQ/REF:R
		•	IACM 	- Accrual calculation Method	/TYP=N/REQ/REF:R
		•	TRM 	- Account Term			/TYP=T/REQ/REF:R
		•	TRATE 	- Promotional Rate		/TYP=N/NOREQ/REF:R
		•	TREXD 	- Promotional Expiration Date	/TYP=D/NOREQ/REF:R
		•	MDT	- Maturity Date			/TYP=D/REQ/REF:R
		•	ODT 	- Account Opened Date		/TYP=D/REQ/REF:R
		•	IRN 	- Interest Rate			/TYP=N/REQ/REF:R
		•	ICF 	- Compound Frequency		/TYP=N/REQ/REF:R
		•	IRCB 	- Int/Div Rate Calculation Base	/TYP=N/REQ/REF:R

	   RETURNS:
	         . $$    	Error Message                   /TYP=T
	                 	Null=No Error
	                 	
	 	 . SALESPRC	Sales Price
	   RELATED:
	         . $$^PBSMRPC -MRPC Service Class Driver

	   EXAMPLE:
	         S RM=$$^MRPC143(.RETURN,1,ORG,IACM,TRM,TRATE,TREXD,MDT,ODT,IRN,ICF,IRCB)

	----Revision History --------------------------------------------

	07/09/07 - NATRAJAH - CR 27921
		   Created to calculate the Sales Price for Original Issue
		   Discount account.

	-----------------------------------------------------------------

	*/
	type Boolean ER
	type String RM,ET
	type Number CMP,FSTTRM,FV,PV,SALESPRC,SECTRM
	type Date AMODT
	
	// Version number of client message is not compatible with server
	if VERSN.get()'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))
	
	set ORG=ORG.get()
	set IACM=IACM.get()
	set TRM=TRM.get()
	set TRATE=TRATE.get()
	set TREXD=TREXD.get()
	set MDT=MDT.get()
	set ODT=ODT.get()
	set IRN=IRN.get()
	set IRCB=IRCB.get()
	
	// Verify input received from client
	set ER=0 do VERINP(ORG,IACM,TRM,TRATE,.TREXD,.MDT,.ODT,IRN,IRCB) quit:ER $$ERRMSG^PBSUTL($G(RM),$G(ET))
	
	//If teaser rate, split term into two periods and Calculate the second period first"
	if TRATE.get() do {       
		set SECTRM=MDT-TREXD
		set TRM=SECTRM_"D"
		set FV=ORG
		set AMODT=TREXD+1
		if ICF.get() set CMP=1
		else  set CMP=0
		set PV=$$PV^DEPDBS(AMODT,FV,IACM,IRN,TRM,CMP)
		//Calculate the first period"
		set IRN=TRATE
		set FSTTRM=TREXD-ODT
		set TRM=FSTTRM_"D"
		set FV=PV
		set AMODT=ODT
		set PV=$$PV^DEPDBS(AMODT,FV,IACM,IRN,TRM,CMP)
		}
	else  {
		//If not teaser rate, calculate the whole term
		set TRM=MDT-ODT
		set TRM=TRM_"D"
		set FV=ORG
		set AMODT=ODT
		if ICF.get() set CMP=1
		else  set CMP=0
		set PV=$$PV^DEPDBS(AMODT,FV,IACM,IRN,TRM,CMP)
		}
	
	set SALESPRC=$$^SCARND(PV,,,"USD")
	if ER quit $$ERRMSG^PBSUTL($G(RM),$G(ET))
	set RETURN=$$V2LV^MSG(SALESPRC,"",0)
	quit ""


VERINP(Number ORG,	//ORG	- Redemption Value		/TYP=N/REQ/REF:R
       Number IACM,	//IACM 	- Accrual calculation Method	/TYP=N/REQ/REF:R
       String TRM,	//TRM 	- Account Term			/TYP=T/REQ/REF:R
       Number TRATE,	//TRATE - Promotional Rate		/TYP=N/REQ/REF:R
       Date TREXD,	//TREXD - Promotional Expiration Date	/TYP=D/REQ/REF:RW
       Date MDT,	//MDT	- Maturity Date			/TYP=D/REQ/REF:RW
       Date ODT,	//ODT 	- Account Opened Date		/TYP=D/REQ/REF:RW
       Date IRN,	//IRN 	- Interest Rate			/TYP=N/REQ/REF:R       
       Number IRCB)	//IRCB 	- Int/Div Rate Calculation Base	/TYP=N/REQ/REF:R
        
        //Verify input data

	type public Boolean ER
	set ER=0
	if ODT.isNull() set ODT=%SystemDate
	else  set ODT=$$DSJD^SCADAT(ODT)
	
	//Data required in Data Item ~p1
	if ORG.isNull() do Runtime.setErrMSG("DEP",7531,"DEP.ORG") quit:ER
	if IRN.isNull() do Runtime.setErrMSG("DEP",7531,"DEP.IRN") quit:ER
	if TRM.isNull() do Runtime.setErrMSG("DEP",7531,"DEP.TRM") quit:ER
	if MDT.isNull() do Runtime.setErrMSG("DEP",7531,"DEP.MDT") quit:ER	
	if IACM.isNull() do Runtime.setErrMSG("DEP",7531,"DEP.IACM") quit:ER
	if IRCB.isNull() do Runtime.setErrMSG("DEP",7531,"DEP.IRCB") quit:ER
	
	set MDT=$$DSJD^SCADAT(MDT)
	
	if 'TREXD.isNull() set TREXD=$$DSJD^SCADAT(TREXD)
	//Teaser rate requires term or expiration date
	if 'TRATE.isNull(),TREXD.isNull() do Runtime.setErrMSG("DEP",2614,"DEP.MDT") quit:ER
	
	//Teaser expiration not valid without teaser rate
	if TRATE.isNull(),'TREXD.isNull() do Runtime.setErrMSG("DEP",2612,"DEP.MDT") quit:ER
	
	//Interest Rate Calculation Base must equal 4 for discount certificates
	if IRCB'=4 do Runtime.setErrMSG("DEP",6929) quit:ER
	
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60831^19959^Hari Natrajan^4239"	// Signature - LTD^TIME^USER^SIZE
