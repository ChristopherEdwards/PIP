SRVPLN3A	//Copy a Current Fee Schedule to new Fee Schedule
	/*
	---- Revision History ------------------------------------------------
	
	08/23/05 - RussellDS - CR16911
		   		   
		   Rewrite to get it to work and clean up code.
		   
		   Removed old revision history.
	*/
	
	quit


CPY	// Copy

	type public String ER, RM
	
	type Date FEESCHDN, FEESCHDT
	type String %READ, %TAB(), ET, FEESCH, FEESCHNW, UTBL(,), VFMQ

	set %ProcessMode = 0
	
	set %TAB("FEESCH")=".FEESCH1/HLP=[UTBLFEESCH]FEESCH/TBL=[UTBLFEESCH]FEESCH:DISTINCT"
	set %TAB("FEESCHDT")=".EFD1/TBL=[UTBLFEESCH]FEESCHDT:QU ""[UTBLFEESCH]FEESCH=<<FEESCH>>"""
	set %TAB("FEESCHNW")=".FEESCHNW1/HLP=[UTBLFEESCH]UTBLFEESCH/XPP=D POSPLN^SRVPLN3A"
	set %TAB("FEESCHDN")=".EFD1/XPP=D CHKDT^SRVPLN3A"

	set %READ="@@%FN,,,FEESCH/REQ,FEESCHDT/REQ,,FEESCHNW/REQ,FEESCHDN/REQ"
	do ^UTLREAD quit:VFMQ = "Q"

	// Record locked by another user
	lock +UTBL("FEE","FEESCH",FEESCH):2 else  set ET="RECLOC" do ^UTLERR quit

	// Record locked by another user
	lock +UTBL("FEE","FEESCH",FEESCHNW):2 else  do { quit
		
		lock -UTBL("FEE","FEESCH",FEESCH)
		set ET="RECLOC"
		do ^UTLERR
	}

	do FILE quit:ER
	
	// Fee Schedule ~p1 copied to Fee Schedule ~p2
	set RM=$$^MSG(4546, FEESCH, FEESCHNW)
	set ER="W"

	quit


POSPLN	//

	type public Boolean ER
	type public String RM, X

	if 'X.isNull(), X?.E1P.E do {
	
		set ER = 1
		// Punctuation characters not allowed
		set RM=$$^MSG(2281)
	}
	
	quit


CHKDT	//

	type public Boolean ER
	type public String RM, X

	quit:X.isNull()
	
	if (X < %SystemDate) do {
		
		set ER=1
		// Effective date must be the same as or after the system date
		set RM=$$^MSG(879)
	}

	else  if Db.isDefined("UTBLFEESCH","FEESCH=:FEESCHNW,FEESCHDT=:X") do {
	
		set ER = 1
		// Fee Schedule already on file
		set RM=$$^MSG(4549)
	}
	quit


FILE	// File data

	type public Date FEESCHDN, FEESCHDT
	type public String FEESCH, FEESCHNW


        type RecordUTBLFEESCH utblfeesch = Db.getRecord("UTBLFEESCH","FEESCH=:FEESCH,FEESCHDT=:FEESCHDT")
	
	type RecordUTBLFEESCH newfeesch=utblfeesch.copy()

	set newfeesch.feesch = FEESCHNW
	set newfeesch.feeschdt = FEESCHDN
	do newfeesch.setMode(0)
	do newfeesch.save()
	
	type ResultSet rs=Db.select("TL","UTBLFEESCH1","FEESCH=:FEESCH AND FEESCHDT=:FEESCHDT")
	while rs.next() do {
		
		type String TL
		
		set TL=rs.getCol("TL")
		
		type RecordUTBLFEESCH1 utblfeesch1 = Db.getRecord("UTBLFEESCH1","FEESCH=:FEESCH,FEESCHDT=:FEESCHDT,TL=:TL")

		type RecordUTBLFEESCH1 newfeesch1=utblfeesch1.copy()
		set newfeesch1.feesch = FEESCHNW
		set newfeesch1.feeschdt = FEESCHDN
		set newfeesch1.tl = TL
		do newfeesch1.setMode(0)
		do newfeesch1.save()
	}

	quit

vSIG()	quit "60139^42764^Dan Russell^2487"	// Signature - LTD^TIME^USER^SIZE
