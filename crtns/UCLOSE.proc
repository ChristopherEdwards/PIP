UCLOSE	//Private;UTILITY TO CLOSE AN ACCOUNT
	/*

	   Utility to close an account

	  ---- Revision History ------------------------------------------------

	   09/08/05 - NATRAJAH - 16674
	   	Added the "setAuditFlag(1) line of code in FILE section.
	   
	   04/17/2002 - TELIV - 49794
		Converted from M to PSL

	   12/21/00 - TANY - 42685
	        Check account closed status ^ACN(CID,STAT) on CID.
	        If the account number is closed, display "Account
	        already closed" message.
	        Also chang set =Db.getOneRow("O A GLOBAL REFERENCE SINCE IT","","")
	        is not as efficient as global access.


	*/

INIT	//
	type Number %PG,%PAGE
	type String VFMQ,OLNTB
	set %PG=1 
	set %PAGE=1 
	set %EffectiveDate="" 
	set %ProcessMode=1

	kill OLNTB,VFMQ
	do VPG 

	quit

VPG	// Page control
	type Public Number %PG
	if %PG>0 do VPG1 
	do VPG0

	quit


VPG0 	//
	type Public String VFMQ,%READ,%TAB()
	type Public Number %PG
	if "DFQ"[VFMQ do VER quit

	set %PG=%PG+1
	do VPG

	quit

VPG1	// Get account number
	type Public String %READ,%TAB,VFMQ
	type Public Number CID
	set %TAB("CID")=".CID1/XPP=D POSCID^UCLOSE"

	set %READ="@@%FN,,,CID/REQ"
	do ^UTLREAD if VFMQ="Q" quit

	quit



POSCID	// Post-processor to account number
	type Public String X,ER,RM
	type Public Number %EXT,CID
	if X="" quit
	set %EXT=1 do ^UACN quit:ER  quit:X=""
	
	// Account already closed
	type RecordACN acn=Db.getRecord("ACN","CID=:CID")
	if acn.stat=4 set ER=1 set RM=$$^MSG(51,CID)
	
	quit


VER	//
	type Public String VFMQ
	if VFMQ'="Q" do FILE

	do END

	quit


END	// Exit
	type Public Number CID
	type Public String ER,RM,VFMQ
	set CID=$G(CID)
	if ER quit

	// Account ~p1 not closed
	if VFMQ="Q" set RM=$$^MSG(125,CID)

	// Account ~p1 is closed
	else  set RM=$$^MSG(6050,CID)

	set ER="W"
	quit


FILE	// File changes to account record and close.

	new XCID
	type Public Number CID,CLS
	type Public String ER
	set ER=0
	set XCID=CID
	type RecordACN acn=Db.getRecord("ACN","CID=:CID")
	set CLS=acn.cls

	if ER quit

	if CLS="D" do {
		type RecordDEP dep=Db.getRecord("DEP","CID=:XCID")
		do dep.setAuditFlag(1)
		set dep.stat=4
		do dep.save()
		}
	else  do {
                type RecordLN ln=Db.getRecord("LN","CID=:XCID")
		do ln.setAuditFlag(1)                
                set ln.stat=4
                do ln.save()
		}

	quit

ERR	//
	type Public String ER,VFMQ
	set ER=1 do ^UTLERR
	set VFMQ="Q"
	quit

vSIG()	quit "60152^20632^Hari Natrajan^2234"	// Signature - LTD^TIME^USER^SIZE
