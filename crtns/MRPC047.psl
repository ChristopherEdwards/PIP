MRPC047(String return,Number versn,Number CID,String cls)	//Public;Account Integrity Checks
	/*
	ORIG: Neal Gorman - 02/18/97

	Returns list of file integrity errors found for specified customer
	deposit, or loan account.

	[SCATBL5]RPCID=47

	KEYWORDS: Client/Server, Customer Utilities, Deposit Utilities,
	          Loan Utilities

	ARGUMENTS:
		. return    					/TYP=T/REQ
	        						/MECH=REFNAM:W

		. versn 	^MRPC047 version number  	/TYP=N/REQ
	    			Current version = 1 		/MECH=VAL

		. CID 		Account Number  		/TYP=N/REQ
								/MECH=VAL

		. cls 		Account Class			/TYP=N/REQ
	    			TABLE: STBLCLSGRP		/MECH=VAL


	RETURNS:
		. $$ 		Error message  		 	/TYP=T
	    			Null = No error

	RELATED:
		. $$^PBSMRPC - MRPC Service Class Driver

	---- Revision History -------------------------------------------------
	
	12/19/06 - SHRIRAM - CR 24547
		   Modified MRPC047 by adding new verrors statement. 	
	
	10/26/05 - RussellDS - CR17834
 		   Replaced calls to EXT^DBSFILER with direct calls to the
 		   filers.
 		   
 		   Cleaned up PSL scope warnings.
	  
	03/10/03 - Dan Russell - 51351
	  	   Changed call to obsoleted ^CIFEXEC to VERIFY^CIFVER, call
	  	   to obsoleted ^DEPEXEC to VERIFY^DEPVER0, and call to
	  	   obsoleted ^LNEXEC to VERIFY^LNIC, plus general clean-up.
  
	  	   Removed old change history.

	30/05/02 - SRIVASTAVAN - 49794
		   Converted To PSL
	
	*/
	
	type String verrors()
	type Number ACN,ER
	type String ET,key1,key2,RM,table
	

	// Version number of client message is not compatible with server
	if $G(versn)'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))

	set %ProcessMode=2
	set ER=0

	// Ensure that account is valid
	if "D|L"[cls,'Db.isDefined("ACN","CID=:CID") set ER=1

	if cls="*",'Db.isDefined("CIF","ACN=:CID") set ER=1

	// Invalid account ~p1
	if ER quit $$ERRMSG^PBSUTL($$^MSG(1259,CID))

	if cls="*" do CIF(CID,.table)
	if cls="L" do LN(CID,.table)
	if cls="D" do DEP(CID,.table)

	if ER quit $$ERRMSG^PBSUTL($G(RM),$G(ET))

	// Structure output array
	set (key1,key2,return)=""
	for  set key1=$O(verrors(key1)) quit:key1=""  do {

		for  set key2=$O(verrors(key1,key2)) quit:key2=""  do {

			type String coltyp,column,out,rec

			set (coltyp,out)=""
			set rec=verrors(key1,key2)

			// Determine column data type.  Needed so that client
			// system can properly format data returned.
			set column=$P(rec,"|",5)
			if column'="" do {

				if '$F(column,".") set column=table_column

				set coltyp=$$TYP^DBSDD(column)

				}

			// error description
			set out(1)=$P(rec,"|",8)
			
			// current value
			set out(2)=$P(rec,"|",6)

			// alternate value
			set out(3)=$P(rec,"|",7)

			// field type
			set out(4)=coltyp

			// error code
			set out(5)=$P(rec,"|",3)

			// File/Table Name
			set out(6)=$P(verrors(key1),"|",1)

			// fatal error indicator
			set out(7)=$P(rec,"|",1)

			set out=out(1)_$C(9)_out(2)_$C(9)_out(3)_$C(9)_out(4)_$C(9)_out(5)_$C(9)_out(6)_$C(9)_out(7)
			set return=return_out_$C(13,10)

			}
		}

	set return=$$V2LV^MSG(return)

	quit ""


CIF(Number ACN,		// Private - Customer account
    String table)

	do VERIFY^CIFVER(ACN)

	set table="CIF."

	quit


DEP(Number CID,		// Private - Deposit account
    String table)

	// Account closed
	if $$CLOSED(CID) do { quit
		
		type RecordDEP dep = Db.getRecord("DEP", "CID=:CID")
		
		do dep.setMode(2)
		do ^DEPFILE(.dep, "NOVALDD/NOINDEX/NOVALREQ")
	}
	
	// Account open
	do VERIFY^DEPVER0(CID)

	set table="DEP."

	quit


LN(Number CID,		// Private - Loan account
   String table)

	// Account closed
	if $$CLOSED(CID) do { quit
		
		type RecordLN ln = Db.getRecord("LN", "CID=:CID")
		
		do ln.setMode(2)
		do ^LNFILE(.ln, "NOVALDD/NOINDEX/NOVALREQ")
	}

	// Account open
	do VERIFY^LNIC(CID)

	set table="LN."

	quit


CLOSED(Number CID)	// Private - Is account closed?
	
	type RecordACN acn=Db.getRecord("ACN","CID=:CID")
	quit acn.stat=4
 #OPTION ResultClass ON
Public String vSIG()	quit "60634^34405^Shriram Deshpande^3602"	// Signature - LTD^TIME^USER^SIZE
