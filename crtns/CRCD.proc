CRCD	//;Currency Code Maintenance
	/*

	  ---- Revision History ------------------------------------------------

	   11/29/06 - KumarSS - 23383
	   	      Modified VPG section to default the Company Name while executing
	   	      the functions CRCDC, CRCDD and CRCDM by declaring the CO as public.
	   
	   03/24/06 - KinI - 20108
	   	      Modified to restore XREF section as UTBLCRCDNO and 
	   	      UTBLCRCDX tables have been restored as well and moved
	   	      to the Domain file type.

	   03/07/06 - KinI - CR 19961
	              Modified FILE section to prevent DBSFILER error when 
	              deleting the currency code. Modified PP01 pre-processor
	              to use %OSAVE instead of %ProcessMode as it is redefined 
	              by UTLREAD. Removed unused ERR section.

	   12/05/05 - KinI - CR18478
	   	      Removed XREF section as UTBLCRCDNO and UTBLCRCDX index 
	   	      files are getting obsoleted.

	   08/11/04 - KELLYP - CR 11477
	   	      Re-converted to PSL to conform to current standards and
	   	      added procedure to source control.

	   03/25/03 - SRIVASTAVAN - 49451
		      Converted to PSL

	*/
	
	quit


NEW     //

        do INIT(0)
        quit
 
 
UPD     //

        do INIT(1)
        quit

 
INQ     //

        do INIT(2)
        quit
 
 
DEL     //

        do INIT(3)
        quit
 

INIT(Number %ProcessMode)      //

	type public String ER,ET

        type String CO
        
        type RecordCUVAR cuvar=Db.getRecord("CUVAR")
        
        if 'cuvar.%mcp set ER=1,ET="MCPNE" do ^UTLERR quit
        
        set CO=cuvar.co
        
        type RecordCRCD fCRCD
        
	do VPG(.fCRCD)
        
	quit
 

VPG(RecordCRCD fCRCD)   // Page control

	type public String CO,ER

	type Boolean FINISH
	type Number %PAGE,%PG
	type String CRCD,IO,OLNTB,VFMQ

	set %PG=0
        set %PAGE=1

        set FINISH=0
        for  do { quit:FINISH
	        if %PG=0 do VPG00 if ER set FINISH=1 quit
	        if %PG>0 do VPG01(.fCRCD)
                if "DFQ"[VFMQ do VER(.fCRCD) set FINISH=1 quit
	        set %PG=%PG+1
                }
        quit
 

VPG00	// Set up

	type public String CO,ER,OLNTB,VFMQ
	
	type String %NOPRMT,%PGM,%READ,%TAB()
	
	set OLNTB="0039"
	
	set %TAB("CO")=".CO2/HLP=[CRCD]CO/TBL=[CRCD]CO:DISTINCT"
	if %ProcessMode=2 set %TAB("IO")=$$IO^SCATAB($I)

	if %ProcessMode=0 set %TAB("CRCD")=".CRCD1/HLP=[CRCD]CRCD/XPP=D PP00^CRCD"
	else  set %TAB("CRCD")=".CRCD1/HLP=[CRCD]CRCD/TBL=[CRCD]CRCD,DESC:QU ""[CRCD]CO=<<CO>>"",#1/XPP=D PP01^CRCD"

	set %READ="@@%FN,,,"
	set %NOPRMT="N"
	
	set %READ=%READ_"CO/REQ,CRCD/REQ"
	if %ProcessMode=2 set %READ=%READ_",IO/REQ"

	do ^UTLREAD 

	if %PGM.data() quit

	if VFMQ="Q" set ER=1
	
	quit


VPG01(RecordCRCD fCRCD)	// Screen

	type public String CO,CRCD,ER,IO,VFMQ

	if %ProcessMode=2,IO'=$I do OPEN^SCAIO if ER set VFMQ="Q" quit

	set fCRCD=Db.getRecord("CRCD","CO=:CO,CRCD=:CRCD",1)
	do DRV^USID(%ProcessMode,"CRCDDRV",.fCRCD) quit:ER

	quit


PP00	// Currency code post-processor (%O=0)

	type public String ER,CO,RM,X

	if X="" quit

	// Record already exists
	if Db.isDefined("CRCD","CO=:CO,CRCD=:X") set ER=1,RM=$$^MSG(2327)

	quit


PP01	// Currency code post-processor (%O=1,2,3)

	type public String CO,ER,RM,X
	type public Number %OSAVE
	
	if X="" quit

	// Inquiry or Modify
	if (%OSAVE=1)!(%OSAVE=2) quit
	
	type DbSet db=Db.selectDbSet("CRCD0","CO=:CO")
        while db.next() do { quit:ER
     		type RecordCRCD0 crcd0=db.getRecord("CRCD0")

		// ~p1 is a direct relationship currency for currency code ~p2
		if crcd0.dircrcd=X do Runtime.setErrMSG("CRCD0",3043,X_"~"_crcd0.crcd) quit
		}

	type ResultSet rs=Db.select("RELCRCD","CRCD0","CO=:CO AND CRCD=:X")
	
	// no special relationships
	if rs.isEmpty() quit

	// ~p1 special relationship(s) must be deleted first
	do Runtime.setErrMSG("CRCD0",3081,X)

	quit


VER(RecordCRCD fCRCD)	//

	type public String VFMQ

	if (%ProcessMode=2)!(%ProcessMode=4)!(VFMQ="Q") do END quit

	do FILE(.fCRCD)

	do END

	quit


FILE(RecordCRCD fCRCD)	// File data

	type public String CO,CRCD

	if %ProcessMode=3 do Db.delete("CRCD","CO=:CO AND CRCD=:CRCD") quit

	do fCRCD.save()
	
	quit


END	//

	type public String CRCD,ER,RM,VFMQ

	if (ER)!(%ProcessMode=2)!(%ProcessMode=4) quit

	if VFMQ="Q" do {

		//Currency code ~p1 not created
		if %ProcessMode=0 set RM=$$^MSG(659,CRCD) quit

		//Currency code ~p1 not modified
		if %ProcessMode=1 set RM=$$^MSG(661,CRCD) quit

		//Currency code ~p1 not deleted
		set RM=$$^MSG(660,CRCD)
		}

	else  do {

		//Currency code ~p1 created
		if %ProcessMode=0 set RM=$$^MSG(656,CRCD) quit

		//Currency code ~p1 modified
		if %ProcessMode=1 set RM=$$^MSG(658,CRCD) quit

		// Currency code ~p1 deleted
		set RM=$$^MSG(657,CRCD)
		}

	set ER="W"

	quit


public	XREF	// Rebuild cash currency and currency number cross-reference files

	type public String ER,RM

	type String CO

	set CO=CUVAR.co

	do Db.fastDelete("UTBLCRCDNO")
	do Db.fastDelete("UTBLCRCDX")
	
	do XREFBLD^PROCRCD  // Rebuild files deleted above

	// Currency cross-reference files maintained
	set ER="W" 
	do Runtime.setErrMSG("CRCD",674)
	
	quit


	

vSIG()	quit "60598^5876^Sudanthiran S. Kumar^4840"	// Signature - LTD^TIME^USER^SIZE
