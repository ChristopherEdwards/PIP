MRPC144(RETURN,VERSN,CID,ETC)	//Public; Populate dropdown lists for contribution & distribution codes.

	/*

	   DESC: This routine determines and returns the drop down lists for 
	   contribution and distribution codes of Retirement/Savings Plan transactions.

	   KEYWORDS: RPC

	   ARGUMENTS:

	         . RETURN Activity detail		 /TYP=T/REQ/MECH=REFNAM:W

	         . VERSN  ^MRPC144 version number        /TYP=N/REQ/MECH=VAL
	                  current version=1

	         . CID    Account Number                 /TYP=N/REQ/MECH=VAL

	         . ETC    External Transaction Code      /TYP=T/REQ/MECH=VAL

	   RETURNS:
	         . $$    Error Message                   /TYP=T
	                 Null=No Error

	   RELATED:
	         . $$^PBSMRPC -MRPC Service Class Driver

	   EXAMPLE:
	         S RM=$$^MRPC144(.VAL,1,CID,ETC)

	----Revision History --------------------------------------------

	05/30/07 - DHANALAKSHMI R - CR 25413
		   Created to list the contribution and distribution codes
		   from the newly created table UTBLCONTYP/UTBLIRACON and 
		   UTBLDISTYP/UTBLIRADIS respectively.

	-----------------------------------------------------------------

	*/

	type public Cache %CACHE()

        // Protect Variables
	type Number ER,IRATYP
	type String CRLF, RM, STR

	set STR = ""
	set CRLF=$C(13)_$C(10)

	// Validate Input

	// Version number of client message is not compatible with server
	if VERSN.get()'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))

	type RecordDEP dep=Db.getRecord("DEP","CID",1)

	// Invalid account ~p1
	if dep.getMode()=0 quit $$ERRMSG^PBSUTL($$^MSG(1259,CID))

	type RecordTRN trn=%CACHE("TRN").getRecord("TRN", "ETC = :ETC")

	if ER.get() quit $$ERRMSG^PBSUTL(RM.get())

	set IRATYP = dep.ira

	if trn.itc.extract() do {

		type ResultSet rs=Db.select("CON,FMDESC","UTBLCONTYP","IRATYP=:IRATYP")

		if rs.isEmpty() do {

			set rs=Db.select("KEY,DESC","UTBLIRACON")

			while rs.next() set STR = STR_rs.getCol("KEY")_$C(9)_rs.getCol("DESC")_CRLF
	
		}
		else  while rs.next() set STR = STR_rs.getCol("CON")_$C(9)_rs.getCol("FMDESC")_CRLF

	}
	else  do {

		type ResultSet rs=Db.select("DIS,FMDESC","UTBLDISTYP","IRATYP=:IRATYP")

		if rs.isEmpty() do {

			set rs=Db.select("KEY,DESC","UTBLIRADIS")

			while rs.next() set STR = STR_rs.getCol("KEY")_$C(9)_rs.getCol("DESC")_CRLF
	
		}
		else  while rs.next() set STR = STR_rs.getCol("DIS")_$C(9)_rs.getCol("FMDESC")_CRLF
	}

	set RETURN=$$V2LV^MSG(STR)

	quit ""
 #OPTION ResultClass ON
Public String vSIG()	quit "60793^22621^Dhanalakshmi R^2253"	// Signature - LTD^TIME^USER^SIZE
