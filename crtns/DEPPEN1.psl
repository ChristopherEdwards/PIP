DEPPEN1(RecordDEP dep,RecordTTX ttx,RecordTRN trn)	// Increase Penalty YTD
	/*
	  ---- Revision History ------------------------------------------------
	
            07/27/05 - SkariahV- CR16679
	               Removed #WARN and #OPTIMIZE directives.
	
	  
	    1/21/03 - Erik Scheetz - 51349
   	    	    Made general changes for the following:
		    	1. Cleaned up parameter mismatches
		    	2. Cleaned up code by using new PSL methods/properties.
		    	3. Added #WARN/#OPTIMIZE compiler switches

	    9/28/02 - Erik Scheetz - 43583
		      PSL conversion clenup

	    6/05/02 - Erik Scheetz - 43583
		      Convert to PSL, moved from routine to DQ Procedure 
		      Definition.
		      Remove pre-2001 revision history
	
	*/
	
	
	type Public Number ER
	type Number TAMT
	
	set TAMT=ttx.tamt+0
	
	// Amount exceeds balance
	if dep.bal-TAMT<0 do Runtime.setErrMSG("DEP",262) quit:ER
	if dep.ira do CYRPEN^IRA(.dep,.ttx) quit:ER

	do AUT(.dep)
	if ER quit

	do FILE(.dep,.ttx,.trn)
	quit
	
FILE(RecordDEP dep,RecordTTX ttx,RecordTRN trn) 	// File Data

	type Public Number TAMT,TYR,YR
	
	// Update Pr.Fin.Year indicator
	set YR=$$PYFLAG^DEPINT(.dep,.ttx)

	// Update Pr.Tax Year indicator
	set TYR=$$PTYFLAG^DEPINT(.dep,.ttx)
	
	// Transaction Amount
	set ttx.tamt=TAMT
	set dep.bal=dep.bal-TAMT
	set dep.tld=%SystemDate	
	set dep.balcol=dep.balcol-TAMT
	
	if YR set dep.pytd=dep.pytd+TAMT
	else  set dep.ppy=dep.ppy+TAMT

	if TYR set dep.pentytd=dep.pentytd+TAMT	

	// don't update pr.tax year
	if 'TYR,'trn.pcfd35 set dep.penpty=dep.penpty+TAMT
	
	set dep.ppl=dep.ppl+TAMT
	
	do ^UPDSTAT(.dep,.trn)
	quit 
	
	
public	EC(RecordDEP dep,RecordTTX ttx,RecordTRN trn) // Error Correct

	type Public Number ER,TAMT
		
	if ttx.tso["REV#" do ^UPDCCLD(.dep,.ttx)

	set TAMT=ttx.tamt+0
	if dep.ira do CYRPEN^IRA(.dep,.ttx) quit:ER

	do FILE(.dep,.ttx,.trn)
	
	quit
	
AUT(RecordDEP dep) // Authorization Verification

	type Public Number CID,OVR(,,)
	
	// Restriction Flag
	if dep.rflg do ^UFLG(.dep)					

	// Account Status
	if dep.stat set OVR(CID,"OVR","STAT"_dep.stat)=""	
	quit 
 #OPTION ResultClass ON
Public String vSIG()	quit "60108^20734^Viji Skariah^1914"	// Signature - LTD^TIME^USER^SIZE
