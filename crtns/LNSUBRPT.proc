public LNSUBRPT(plan,CID,fromdt,thrudt)	//Public;Subsidized Loan Report Preprocessor
	/*
	   ORIG: 	LOCKE - 06/09/96
	   DESC: 	Subsidized Loan Report Preprocessor

	   This procedure is called from SCA546.rpt for the subsidized loans
	   to get the values for the data items (SUBAMT, SUBDT, BILINT) from
	   the history table.

	   KEYWORDS: Subsidized Loans

	   ARGUMENTS:

		   . plan 	Plan list  		/TYP=T/REQ/MECH=VAL
		   . CID 	Account Number	 	/TYP=N/REQ/MECH=VAL
		   . fromdt 	From date  		/TYP=D/REQ/MECH=VAL
		   . thrudt 	Thru date  		/TYP=D/REQ/MECH=VAL

	   INPUTS:

	   RETURNS:

	   RELATED:

	   EXAMPLE:

	 ---- Revision History ------------------------------------------------
	04/28/06 - chhabris - CR21762
		   Replaced parameter cid with CID as errors were thrown while 
		   running @REP546.

	04/27/06 - chhabris - CR20966
		   Modified HIST section to replace '~' with '|' while saving
		   record in TMPRPT5. Also removed the 'quit:BILINT' to process
		   all the record as it was quitting without saving the records
		   in TMPRPT5.

	03/20/06 - chhabris - CR20163
		   Modified HIST section to use Db.getRecord(,,1) for TMPRPT5
		   instead of Class.new.		   
	 -----------------------------------------------------------------------
	*/

	type public String SUBPLN
	type String CRCD
	
	type RecordLN ln=Db.getRecord("LN","CID=:CID")
	set CRCD=ln.crcd
	set SUBPLN=ln.subpln
	if CRCD="" set CRCD=" "

	do HIST
	quit


HIST 	//
	type public Date fromdt,thrudt
	type public Number CID,TSEQ
	type public String CRCD,SUBPLN
	type Date fromdt1,todt

	set fromdt1=fromdt-1
	set todt=thrudt+1

	type ResultSet histt=Db.select("TSEQ","HIST","CID=:CID and TJD>:fromdt1 and TJD<:todt","TSEQ DESC")
	if histt.isEmpty() quit

	while histt.next() do { 

		set TSEQ=histt.getCol(1)
		type RecordHIST hist=Db.getRecord("HIST","CID=:CID,TSEQ=:TSEQ")

		new BILINT,BLSEQ,SUBDT,SUBAMT,VAR1

		if hist.tjd>thrudt quit
		if hist.tjd<fromdt quit

		if $$FIELD^UTSO(hist.tso,"SUBPMT")="" quit

		set SUBDT=$$FIELD^UTSO(hist.tso,"SUBDT")

		if SUBDT'="" set SUBDT=$$DSJD^SCADAT(SUBDT)

		else  set SUBDT=hist.efd

		if SUBDT="" set SUBDT=hist.tjd

		set SUBAMT=+hist.tamt

		set BLSEQ=$$SUBSEQ^LNPTS15(SUBDT)

		set BILINT=""
		
		if BLSEQ do {
			type RecordLNBIL1 lnbil1=Db.getRecord("LNBIL1","CID=:CID,SCHSEQ=:BLSEQ")
			set BILINT=$$AMTDUE^BILFUNCS(.lnbil1,"I")
			}

		set VAR1="SUBPLN"

		type RecordTMPRPT5 tmprpt5=Db.getRecord("TMPRPT5","PID=:%ProcessID,KEY1=:SUBPLN,KEY2=:VAR1,KEY3=:CRCD,KEY4=:CID,KEY5=:TSEQ",1)
		set tmprpt5.data=SUBAMT_"|"_SUBDT_"|"_BILINT
		do tmprpt5.bypassSave()
		}
	quit

vSIG()	quit "60446^50433^Sanjay Chhabria^2421"	// Signature - LTD^TIME^USER^SIZE
