UTBLCNTC	// Copy closeout codes from group to group
	/*
	       ORIG:  LOCKE - 15 NOV 95
	       DESC:  Copy closeout notification entries between groups


	---- Revision History -------------------------------------------------
	
	06/06/06 - MBUIM - CR 21607
		   Modified FILE section as undefined error on TCLS was 
		   returned. Modified code not to reference TCLS as it may not
		   be defined for the destination group if new.

	12/01/05 - ALAGARSS - 16677
		   Modified the scope for the variables FGRP,TGRP,VFMQ 
		   to avoid undefined error.In the FILE section removed
		   the lines which check in the ACN table.Because the only 
		   condition needs to check is the target group should not
		   be existing already. 
		
	05/22/03 - GRAY - 51351
		   Converted to PSL.

	*/
	
	type Number %PAGE,%PG
	type Number FINISH=0
	type String OLNTB,VFMQ
		
	set %PG=1 set %PAGE=1

	// Page control
	for  do { quit:FINISH
		if %PG>0 do VPG01
		if "DFQ"[VFMQ do VER set FINISH=1 quit
		set %PG=%PG+1
		}

	quit	


VPG01	// Set up the heading and prompts.
	/*	   
	The heading contains the following:
	Copy closeout notification entries to another group.
	Pre-existing entries within destination group will not be copied.
	Function DDPXFR must be run afterwards to copy entries to frontend(s).
	*/

	type String %READ,%TAB,HDG1,HDG2,HDG3
	type public String FGRP,TGRP,VFMQ

	// Copy closeout notification entries to another group
	set HDG1=$$^MSG(77)

	// Pre-existing entries within destination group will not be copied
	set HDG2=$$^MSG(86)

	// To copy entries to front-end, run function DDPXFR next
	set HDG3=$$^MSG(93)

	set %TAB("FGRP")=".FGRP"
	set %TAB("TGRP")=".TGRP"

	set %READ="@@%FN,,,@HDG1,@HDG2,@HDG3,,FGRP,TGRP"
	do ^UTLREAD if VFMQ="Q" quit

	if FGRP="",TGRP="" set VFMQ="Q"

	quit


VER	// Verification
	
	type Public String VFMQ

	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit

	do FILE

	do END

	quit


FILE	// File data

	type String CODE
	type Public String FGRP,TGRP,VFMQ
	
	type ResultSet rs=Db.select("CLS","STBLGRP","GRP=:FGRP")
	if 'rs.next() set VFMQ="Q" quit

	if rs.getCol("CLS")="D" do {
		type ResultSet dcodes=Db.select("KEY,LOGF,RFLG","UTBLCNTD","GRP=:FGRP")
		while dcodes.next() do {
			set CODE=dcodes.getCol("KEY")
			type RecordUTBLCNTD cntd=Db.getRecord("UTBLCNTD","GRP=:TGRP,KEY=:CODE",1)
			if cntd.getMode()=0 do { 
				set cntd.logf=dcodes.getCol("LOGF")
				set cntd.rflg=dcodes.getCol("RFLG")
				do cntd.bypassSave()
				}
			else  do {
				// Copy only those that don't already exist
				type ResultSet rs1=Db.select("KEY,LOGF,RFLG","UTBLCNTD","GRP=:TGRP")
				while 'rs1.next() do {
					set cntd.logf=dcodes.getCol("LOGF")
					set cntd.rflg=dcodes.getCol("RFLG")
					do cntd.bypassSave()	
					}
				}
			}
		}
	if rs.getCol("CLS")="L" do {
		type ResultSet lcodes=Db.select("KEY,LOGF,RFLG","UTBLCNTL","GRP=:FGRP")
		while lcodes.next() do {
			set CODE=lcodes.getCol("KEY")
			type RecordUTBLCNTL cntl=Db.getRecord("UTBLCNTL","GRP=:TGRP,KEY=:CODE",1)
			if cntl.getMode()=0 do {
				set cntl.logf=lcodes.getCol("LOGF")
				set cntl.rflg=lcodes.getCol("RFLG")
				do cntl.bypassSave()
				}
			else  do {
				// Copy only those that don't already exist
				type ResultSet rs2=Db.select("KEY,LOGF,RFLG","UTBLCNTL","GRP=:TGRP")
				while 'rs2.next() do {
					set cntl.logf=lcodes.getCol("LOGF")
					set cntl.rflg=lcodes.getCol("RFLG")
					do cntl.bypassSave()	
					}
				}
			}	
		}
	
	quit


END	// End of processing

	type Public String ER,RM,VFMQ

	quit:ER.get()!(%ProcessMode=2)!(%ProcessMode=4)

	set ER="W"

	// Closeout notification table not copied
	if VFMQ="Q" set RM=$$^MSG(75)

	// Closeout notification table copied.  Run function DDPXFR.
	else  set RM=$$^MSG(74)

	quit

vSIG()	quit "60424^38834^Marie Mbui^3544"	// Signature - LTD^TIME^USER^SIZE
