LNFEELM		 /* 
	ORIG: GRAY - 01/17/2000
	DESC: Loan Account Fee Maintenance
   
	---- Comments --------------------------------------------------------
    
	---- Revision History ------------------------------------------------

	03/02/06 - TITOVE - CR 19733
		   Modified PPFEE section to instantiate an LNFEEP object with
		   a "create-if-needed" qualifier.
	
	*/
	
	quit
	
Public PPCID	// Account post processor
	
	set (CLS,ZCLS)="L",%EXT=1 do ^UACN quit:$G(ER)  quit:$G(CID)=""  
	// Account closed
	new stat
	set stat=Db.getOneRow("STAT","LN","X")
	if stat=4 set ER=1,RM=$$^MSG(55) quit		// Closed
	if '%OSAVE quit
	kill FEEARRAY
	
	new FEETYP
	set FEETYP=""
	for  set FEETYP=Db.nextKey("LNFEE","X,FEETYP") quit:FEETYP=""  do {
		new DESC
		set DESC=Db.getOneRow("DESC","LNFEEP","FEETYP")
		set FEEARRAY(FEETYP)=DESC
		}
	quit
	
Public PPFEE	// Fee Type post processor
	
	
	if X="" quit
	if %OSAVE quit
	
	// Fee already exists
	if Db.isDefined("LNFEE","CID,X") set ER=1,RM=$$^MSG("2327") quit    
	
	new FEEREC,PRIMFEE
	
	type RecordLNFEEP FEEREC = Db.getRecord("LNFEEP", "FEETYP = :X", 1)
	
	/*
	If this is a linked fee, make sure its primary fee has already been
	assigned to this account.
	*/
	
	if FEEREC.lfee do {
		set PRIMFEE=FEEREC.basis
		if PRIMFEE="" quit
		if (PRIMFEE="I")!(PRIMFEE="P") quit
	 	// Linked fee.  Specify the primary loan fee plan ID to assess this fee.
	 	if 'Db.isDefined("LNFEE","CID,PRIMFEE") set ER=1,RM=$$^MSG("1612")
		}

	/*
	If this fee is to be assigned at booking, make sure it can only
	be applied on the day the loan is opened.
	Fees assessed at booking can only be added on the date the account 
	is opened.
	*/

	new ODT
	set ODT=Db.getOneRow("ODT","LN","CID")
	if (FEEREC.amth=0),(ODT'=%SystemDate) set ER=1,RM=$$^MSG("1068")
	quit

vSIG()	quit "60326^48947^Eugene Titov^1697"	// Signature - LTD^TIME^USER^SIZE
