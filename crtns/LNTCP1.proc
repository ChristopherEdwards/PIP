LNTCP1		/*
	     Loan tran code pre-processing

	       DESC:  Loan transaction code post-processor
	
	  ---- Revision History -------------------------------------------------

	   01/13/05 - KELLYP - CR 13667
	   	      Modified to conform to current PSL standards and removed
	   	      pre-2002 revision history.  Also cleaned up procedure.
	
	  -----------------------------------------------------------------------
	*/
	quit

public LF1(RecordTRN trn,RecordTTX ttx,String CTL)

	/*
	Arguments:
	.  trn   TRN object		/TYP=RecordTRN/REQ/MECH=REF:R

	.  ttx   TTX object		/TYP=RecordTTX/REQ/MECH=REF:R

	.  CTL: Position 1 - 0 include payoff charges
	                         1 ignore payoff charges
	*/
	
	type Public Number CID,DFT()
	type Public String ER
	
	type Boolean IGNOREPO,INCRS
	type Number AMT,PONPT
	
	set CTL=CTL.get()
	set IGNOREPO=CTL.extract()

	type RecordLN ln=Db.getRecord("LN","CID=:CID")	
	
	// Invalid transaction for non-revolving account
	if trn.pcfl5+ln.revf=0 do Runtime.setErrSTBLER("LN","NONREVF") quit:ER
	
	set AMT=$$EXEC(.ln,.ttx)
	if ln.paylkfrm'>%EffectiveDate,%EffectiveDate'>ln.paylkto set AMT=ln.paylkamt
	else  do {
		set AMT=$$^SCARND(ln.ponpt,0,CID) 
		if CTL.extract() set AMT=AMT-ln.poep-ln.pochg
		}
		
	set INCRS=trn.itc.extract()-ln.trb
	if INCRS set:AMT>0 AMT="" if AMT<0 set AMT=-AMT
	if 'INCRS set:AMT<0 AMT=""
	set AMT=$$^SCARND(AMT,0,CID)
	set DFT("AMT")=AMT
	quit 

	
public EXEC(RecordLN ln,RecordTTX ttx)

	/*
	Arguments:
	.  ln   Loan object		/TYP=RecordLN/REQ/MECH=REF:R
	.  ttx  TTX object		/TYP=RecordTTX/REQ/MECH=REF:R

	*/

	type Public Number AMT,CID
	type Public String CTL,ER

	type Number %ESC,PAYOFF

	if '%EffectiveDate set %EffectiveDate=%SystemDate
	set %ESC=0
	do PAYOFF^LNPO2(.ln,CTL.get(),.ttx) if ER quit ""
	if ln.paylkfrm'>%EffectiveDate,%EffectiveDate'>ln.paylkto set AMT=ln.paylkamt
	else  set AMT=$$^SCARND(PAYOFF,0,CID)
	
	quit AMT

vSIG()	quit "59918^54042^Pat Kelly^1827"	// Signature - LTD^TIME^USER^SIZE
