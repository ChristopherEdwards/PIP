DODMADCY(ACN,RPASEQ)	//; -  - V4.2 - Determine [IRA]DMADYTDD
	/*
	       ORIG:  RIZZO - 11 AUG 1992
	       DESC: Routine will roll thru ^HIST for all accounts under
	       an RPA plan to determine the total resident mandatory
	       distributions made prior to the date of death.
	             For T4RIF year end form production.  Add up MAD amounts
	             having an EFFECTIVE date between January 1st through the
	             Date of Death.

	  ------------------- Revision History-----------------------------------

	   12/12/05 - DHANALAKSHMI R - 16851
		Modified the section PROC by removing "ORDER BY" from the select
		statement of the HIST table.

	   09/09/05 - DHANALAKSHMI R - 16851
		Corrected Warnings - DBI3 clean up.

	   04/10/02 - DATTAR - ARQ 49794
		Converted Routine to procedure

	   06/21/00 - MOHAMEDE - 36760
	        Modified routine entry to set date of birth and calcualte
	        age based on the beginning of tax year rather than calendar

	  -----------------------------------------------------------------------
	*/

	type Date BOTY,DOD
	type Number CID,DMADYTDD,PLANID,TYPE

	set ACN=ACN.get() if ACN.isNull() quit 0
	set RPASEQ=RPASEQ.get() if RPASEQ.isNull() quit 0

	if 'Db.isDefined("IRATYPE","ACN=:ACN,RPASEQ=:RPASEQ") quit 0

	type RecordIRATYPE iratype=Db.getRecord("IRATYPE","ACN=:ACN,RPASEQ=:RPASEQ")
	set PLANID=iratype.planid
	if PLANID.isNull() quit 0

	set CID="" set DMADYTDD=0

	// Date of Death
	type RecordCIF cif=Db.getRecord("CIF","ACN")
	set DOD=cif.DOD

	// Jan. 1 julian date of year in which death occurs
	//EHM This is now changed to look to the beginning of tax year if tax
	// year is different from the financial and the calendar year.
	set BOTY=$$BOTY^SCADAT(DOD,0)

	for TYPE=6,7 do PROC
	quit DMADYTDD


PROC	//
	type public Number ACN,CID,PLANID,RPASEQ
	type Number FOUND,HSEQ
	type String TSO

	type ResultSet rs=Db.select("CID","DEP","IRATYP=:TYPE AND ACN=:ACN AND RPASEQ=:RPASEQ AND CID>:CID") 
	if rs.isEmpty() quit
	while rs.next() do {
		set CID=rs.getCol("CID")

		//This section was perviously handled by UHFETCH
		set FOUND=0
		type ResultSet histt=Db.select("TSEQ","HIST","CID=:CID","TSEQ DESC")	
		if histt.isEmpty() quit
		while histt.next() do { quit:FOUND
			set HSEQ=histt.getCol(1)
			type RecordHIST hist=Db.getRecord("HIST","CID=:CID,TSEQ=:HSEQ")

			//Skip if credit transaction
			if $E(hist.itc)=1 quit

			//Transaction source
			set TSO=hist.tso

			if TSO["MAD" do SUM(.hist)
			set FOUND=1 
			}
		}
	  quit


SUM(RecordHIST hist) //

	type Date TPD,%EffectiveDate
	type Number X
	type public Number DMADYTDD
	type public String TSO
	type public Date BOTY,DOD

	//Trans. Processing Date
	set TPD=hist.tjd

	//Effective Date
	set %EffectiveDate=hist.efd

	// IF the Eff. Date is null, set it to the posting date.
	if %EffectiveDate.isNull() set %EffectiveDate=TPD

	// If Trans. Posting Date prior to Jan. 1 skip it.
	if TPD<BOTY quit

	// If Eff. Date is not between January 1st and Date of Death skip it.
	if (%EffectiveDate<BOTY)!(%EffectiveDate>DOD) quit

	set X=$P(TSO,"MAD#",2) set X=$P(X,"~",1)

	// If error correct or reversal, then subtract amount.
	if hist.tamt<0 set DMADYTDD=DMADYTDD-X quit

	set DMADYTDD=DMADYTDD+X
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60250^3659^Dhanalakshmi R^3059"	// Signature - LTD^TIME^USER^SIZE
