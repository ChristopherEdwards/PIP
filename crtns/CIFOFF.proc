CIFOFF	// Mass change of CIF officer
	/*
	       ORIG:  Bill Greene (1674) - 11/23/88
	       DESC:

	  ---- Revision History ------------------------------------------------
	
	   12/15/03 - HILLANBRAND - 13686
	              Removed "if rs.isEmpty() do CIF quit" in FILE section and added
	              do CIF in VER section which corrected a problem where the CIF
	              section was not being executed.
	
	   09/10/02 - GRAY - 49794
		      Reworked much of this procedure to not use
		      XCLS for the result set and to get rid of the UX
		      array references.
	
	   19/06/02 - SRIVASTAVAN - 49794
		      Converted To PSL

	   05/09/00 - DOUGANM- 39582
	              Removed use of indirection in error trapping.

	  ---------------------------------------------------------------------
	*/

UPD	//
	set %ProcessMode=1

INIT	//
	set %PG=1 
	set %PAGE=1
	kill VFMQ,OLNTB

VPG	// Page control
	do VPG01
	do VPG0 quit

VPG0 	//
	if "DFQ"[VFMQ do VER quit
	set %PG=%PG+1
	do VPG 
	quit

VPG01	// Set-up screen
	set %TAB("OOFF")=".OOFF1/HLP=[CIF]CIFOFF/TBL=[UTBLOFF]"
	set %TAB("NOFF")=".NOFF1/HLP=[CIF]CIFOFF/TBL=[UTBLOFF]/XPP=D POSOFF^CIFOFF"
	set %EffectiveDate=""
	kill OLNTB

	set %READ="@@%FN,,,OOFF/REQ,NOFF/REQ"
	do ^UTLREAD if VFMQ="Q" set ER=1 quit

	quit

FILE	//
	
	type ResultSet rs=Db.select("CLS,CID","ACN",,"CLS,GRP,TYPE")
	while rs.next() do {
                set CLS=rs.getCol(1)
		set CID=rs.getCol(2)
	
		catch vERROR {
                        new ET,RM
                        set ET=vERROR.type
 
                        if ET["%GTM-" do ZE^UTLERR
 
                        set ET=ET_"-"_vERROR.thrownAt
                        set RM=vERROR.description
 
                        do EXC
                        }

		lock +ACN(CID):2 else  set ET="RECLOC" do EXC quit
	
		if CLS="D" do DEP
		if CLS="L" do LN
		
		lock -ACN(CID)
	
		}

	quit
	
	
DEP	// Update DEP

	type RecordDEP dep=Db.getRecord("DEP","CID=:CID")
	do dep.setAuditFlag(1)
	
	if dep.off=OOFF set dep.off=NOFF

	if dep.off2=OOFF set dep.off2=NOFF
	
	do dep.save()
	
        quit

	
LN	// Update LN
	
	type RecordLN ln=Db.getRecord("LN","CID=:CID")
	do ln.setAuditFlag(1)
	
	if ln.off=OOFF set ln.off=NOFF

	if ln.off2=OOFF set ln.off2=NOFF

	if ln.off3=OOFF set ln.off3=NOFF

	if ln.off4=OOFF set ln.off4=NOFF

	if ln.off5=OOFF set ln.off5=NOFF

	if ln.coloff=OOFF set ln.coloff=NOFF

	do ln.save()
	
	quit

CIF	// Update CIF

	type ResultSet rs=Db.select("ACN","CIF")
	if rs.isEmpty() quit
	while rs.next() do {
                set ACN=rs.getCol(1)
	
		catch vERROR {
                        new ET,RM
                        set ET=vERROR.type
 
                        if ET["%GTM-" do ZE^UTLERR
 
                        set ET=ET_"-"_vERROR.thrownAt
                        set RM=vERROR.description
 
                        do EXC
                        }
	
		lock +CIF(ACN):2 else  set ET="RECLOC" do EXC quit

		type RecordCIF cif=Db.getRecord("CIF","ACN")
		if cif.cifoff'=OOFF lock -CIF(ACN) quit
	
		do cif.setAuditFlag(1)
	
		// Officer Code
		set cif.cifoff=NOFF
		do cif.save()
		lock -CIF(ACN)
		}
	quit

POSOFF	// Post-processor for data item NOFF

	quit:X=""  
	if OOFF=X do {
		set ER=1
		// Enter the officer to replace officer ~p1
		set RM=$$^MSG(956,X)
		}
	
	quit

	
VER 	//
	
	if ER!(VFMQ="Q") do END quit
	do FILE
	do CIF

END	//
	
	kill %TAB 
	quit:ER
	set ER="W"
	
	// Officer ~p1 replaced by officer ~p2
	set RM=$$^MSG(2088,OOFF,NOFF)
	
	quit


EXC	// Log error in exception file
	
        new BAL
 
        if $G(ET)="" set ET=$G(RM)
 
        if $G(CID)'="" do {
		type RecordACN acn=Db.getRecord("ACN","CID=:CID")
                set BAL=acn.bal
                }
 
        do LOG^UTLEXC($T(+0),"*",$$^MSG(1374),$G(CID),$G(%ZTSEQ),ET,$G(BAL))
 
        kill ET,%ZTSEQ
        set ER=""
        set RM=""
 
        quit

vSIG()	quit "59884^53182^Laura Hillanbrand^3615"	// Signature - LTD^TIME^USER^SIZE
