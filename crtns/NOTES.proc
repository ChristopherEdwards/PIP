NOTES	
 /*
	ORIG: MOTENJ - 10/24/2000
	DESC: Remove Expired Notes

	---- Comments --------------------------------------------------------

	---- Revision History ------------------------------------------------
	
	05/18/05 - KELLYP - CR 15268
		   Removed VPG* logic and rewrote entire procedure to conform 
		   to current PSL standards.  Also removed pre-2003 revision
		   history.
 */

	quit
	
	
public PURGE   

	type Date PURGE
	type String VFMQ

	set %ProcessMode=4
	
	do QUERY
	
	if VFMQ="Q" do END quit
	
	do PURGEALL
	
	do END

	quit


QUERY	// Query for "Purge" Date

	type String %READ,%TAB()

	set %TAB("PURGE")=".PURGE1/XPP=D PURGEPP^NOTES"
	set %READ="@@%FN,,,PURGE/REQ"

	do ^UTLREAD 

	quit

	
PURGEPP	// Post processor for purge date

	type public Boolean ER
	type public String X

	// Date must be the same as or before the system date
	if X>%SystemDate do Runtime.setErrMSG("NOTES",753) quit:ER

	quit

	
PURGEALL // Purge all expired notes

	type public Date PURGE

	type Number ACN,CID,NUM
	type String FILE

	for FILE="ACN","CIF" do {

		type ResultSet rs=Db.select("CID,NUM,ACN","NOTES","EXP<:PURGE AND FILE=:FILE")

		while rs.next() do {
			set CID=rs.getCol(1)
			set NUM=rs.getCol(2)
			set ACN=rs.getCol(3)

			do Db.delete("NOTES","FILE=:FILE AND CID=:CID AND NUM=:NUM")

			if FILE="CIF" set ACN=CID

			do HIST	// File entry for note delete
			}
		}
	quit


HIST	// File history entries for note delete

	type public Number ACN,CID,NUM
	type public String FILE

	type String TCMT

	if FILE="ACN" do {
		// Expired account note # ~p1 removed
		set TCMT=$$^MSG(5644,NUM) 	
		do HISTBLD^ACNFUNCS(TCMT)	// History Filer
		do FMLD^ACNFUNCS(CID)		// Update [DEP]FMLD=TJD
		}
	if FILE="CIF" do {
		// Expired customer note # ~p1 removed
		set TCMT=$$^MSG(5648,NUM)
		do CIFHBLD^CIFFUNCS(TCMT)	// History Filer
		do FMLD^CIFFUNCS(ACN)		// Update [CIF]FMLD=TJD
		}
	quit


END	// End of processing

	type public String ER,RM,VFMQ

	// Notes not purged
	if VFMQ="Q" set RM=$$^MSG(2072)
	
	// Notes removed
	else  set RM=$$^MSG(2073)
		
	set ER="W"		

	quit

vSIG()	quit "60044^48333^Pat Kelly^1954"	// Signature - LTD^TIME^USER^SIZE
