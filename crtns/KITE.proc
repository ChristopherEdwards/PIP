KITE	// KITE SUSPECT IDENTIFICATION/ANALYSIS
	/*
	       ORIG:  Bill Greene (1674) - 04/22/88

	---- Revision History ------------------------------------------------

        07/26/05 - SkariahV- CR16679
	           Removed #WARN and #OPTIMIZE directives.
	              
	05/21/03 - GRAY - 51351
		   Converted to PSL.

	*/
	

	type Number %PAGE,%PG
	type String IO,OLNTB,VFMQ

	set %ProcessMode=4 set IO=$I
	do OPEN^SCAIO
	set %PG=0 set %PAGE=1
	kill OLNTB,VFMQ

VPG	// Page Control

	type Public Number %PAGE,%PG
	if %PG=%PAGE do VER quit

	do VPG00 
	do VPG0

	quit

VPG0	//

	type Public String OLNTB,VFMQ

	if "DFQ"[VFMQ do VER quit

	do VPG 
	
	quit


VPG00	// Add prompt to print

	type Date LJD
	type Number BALOPT,DAYS,DLRSPR,PCTSPR,QCCLD
	type String %READ,%TAB,DESC1,VAR1,VAR2,VAR3,VAR4,VAR5,YN
	type Public Number %PG
	type Public String VFMQ

	set LJD=Db.currVal("KITE")
	if LJD="" set LJD=%SystemDate
	type RecordKITE0 kite0=Db.getRecord("KITE0","TJD=:LJD",1)

	set DAYS=kite0.days if 'DAYS set DAYS=10
	set QCCLD=kite0.ccld if 'QCCLD set QCCLD=5
	set PCTSPR=kite0.pctspr if 'PCTSPR set PCTSPR=150
	set DLRSPR=kite0.dlrspr if 'DLRSPR set DLRSPR=1000
	set BALOPT=kite0.balopt

	set %PG=1

	// Kite Analysis - Query Conditions
	set DESC1=$$^MSG(3801)
	set DESC1="".justify((80-DESC1.length())/2)_DESC1

	// Days To Include In Analysis:
	set VAR1=$$^MSG(3799)
	set VAR1="".justify(40-VAR1.length())_VAR1_DAYS

	// Days Since LAST Customer Contact:
	set VAR2=$$^MSG(3798)
	set VAR2="".justify(40-VAR2.length())_VAR2_QCCLD

	// Balance Option:
	set VAR3=$$^MSG(3797)
	set VAR3="".justify(40-VAR3.length())_VAR3_BALOPT

	// Percentage Spread (Bal Option/BAL*100):
	set VAR4=$$^MSG(3803)
	set VAR4="".justify(40-VAR4.length())_VAR4_PCTSPR

	// Dollar Spread (BAL-Bal Option):
	set VAR5=$$^MSG(3800)
	set VAR5="".justify(40-VAR5.length())_VAR5_DLRSPR

	set %TAB("YN")=".YN2"

	set %READ="@DESC1,,,@VAR1,@VAR2,,@VAR3,@VAR4,@VAR5,,YN/REQ"

	do ^UTLREAD if VFMQ="Q" quit

	if 'YN set VFMQ="Q"

	quit
 

VER	// Define conditons, run kite analysis

	type Date %FD,%TD,CCLD
	type Number %AVB,CID,CMPBAL,ER,TOT,TOTA,TOTI
	type String %ZTSEQ,ET
	type Public Number BALOPT,DAYS,DLRSPR,PCTSPR,QCCLD
	type Public String UAVB(),VFMQ

	if VFMQ="Q" do END1 quit

	// Initialize counters, set %FD, %TD for UAVB call
	set (TOT,TOTA,TOTI)=0
	set %FD=%SystemDate-DAYS set %TD=%SystemDate
	
	type DbSet ds=Db.selectDbSet("DEP","GRP='DDA'")
	while ds.next() do {
		type RecordDEP dep=ds.getRecord("DEP")
		set CID=dep.cid
		set TOT=TOT+1
		// Check that account not inactive
		if dep.stat=4 quit
		// Check for Reg "D" categories
		if dep.regd'=1,dep.regd'=2,dep.regd'=3 quit
		// Check for CCLD less than defined in query
		set CCLD=dep.ccld if CCLD<(%SystemDate-QCCLD) quit

		// If pass increment counter, run average balance utility.
		set TOTA=TOTA+1
		set ER=""
		do INIT^UAVB(.dep,CID,%FD,%TD,"001")
		if ER do { quit
			// KITE SUSPECT IDENTIFICATION/ANALYSIS
			do LOG^UTLEXC(%RoutineName,"*",$$^MSG(3802),CID,%ZTSEQ.get(),ET.get(),dep.bal)
			}

		set %AVB=+UAVB(1)

		// Set balance used for comparison
		set CMPBAL=UAVB(2).piece("|",3) 
		if BALOPT=1 set CMPBAL=UAVB(2).piece("|",16)

		if %AVB-CMPBAL<DLRSPR quit
		if CMPBAL'>0,(%AVB/CMPBAL)*100<PCTSPR quit

		// If pass, set in ^KITE, increment counter
		set TOTI=TOTI+1
	
		type RecordKITE kite=Class.new("RecordKITE")
		set kite.adat=%SystemDate
		set kite.cid=CID
		set kite.avb=%AVB
		set kite.avc=CMPBAL
		set kite.hb=UAVB(1).piece("|",2)
		set kite.lb=UAVB(1).piece("|",5)
		set kite.lb1=UAVB(1).piece("|",6)
		set kite.tcr=UAVB(1).piece("|",8)
		set kite.tdr=UAVB(1).piece("|",9)
		set kite.ncr=UAVB(1).piece("|",10)
		set kite.ndr=UAVB(1).piece("|",11)
		
		do kite.bypassSave()
		}

	do END

	quit


END	// AFTER ALL PROCESSED, SET COUNTERS, QUERY CONDITIONS AT TOP WITH DATE
	
	type Public Number BALOPT,DAYS,DLRSPR,PCTSPR,QCCLD,TOT,TOTA,TOTI
 	type Public String RM()

	type RecordKITE0 kite0=Class.new("RecordKITE0")
	set kite0.tjd=%SystemDate
	set kite0.days=DAYS
	set kite0.ccld=QCCLD
	set kite0.pctspr=PCTSPR
	set kite0.dlrspr=DLRSPR
	set kite0.balopt=BALOPT
	set kite0.tot=TOT
	set kite0.tota=TOTA
	set kite0.toti=TOTI

	do kite0.bypassSave()

	// Kite analysis completed
	set RM(1)=$$^MSG(1589)

	// Total DDA accounts reviewed - ~p1
	set RM(2)=$$^MSG(2671,TOT)

	// Total subject to analysis - ~p1
	set RM(3)=$$^MSG(2675,TOTA)

	// Total qualifying and filed as suspect - ~p1
	set RM(4)=$$^MSG(2674,TOTI)

	do END1
	
	quit


END1	//
	
	type Public String ER,RM,VFMQ
	
	quit:ER

	// Kite analysis not run
	if VFMQ="Q" set RM=$$^MSG(1590)

	// Kite analysis run
	else  set RM=$$^MSG(1591)

	set ER="W"

	quit

vSIG()	quit "60107^17764^Viji Skariah^4417"	// Signature - LTD^TIME^USER^SIZE
