public MRPC038(String return,Number versn,Number ACN,String select,Boolean primary)	//Public; Commercial Commitment Data
	/*
	   ORIG:  Dan Russell - 27 Dec 96

	   Return commercial commitment heirarchy and data for each commitment
	   related to a customer

	   KEYWORDS:	 MRPC,CIF,Loans

	   ARGUMENTS:
	  	 . return	 Data (see below) 		 /TYP=T/REQ
	        						 /MECH=REFNAM:W

	  	 . versn 	 ^MRPC038 version number 	 /TYP=N/REQ
	    			 Current version = 1 		 /MECH=VAL

	  	 . ACN 		 Customer Number  		 /TYP=N/REQ

	  	 . select	 Select column list 		 /TYP=T/REQ

	  	 . primary	 Primary relationship only	 /TYP=L/DFT=0

	   RETURNS:
	  	 . $$	 Error message
	   		 Null = no error

	  	 return	 Externally Formatted Output record
	   		 CRLF separated records with each record containing
	   		 Master commitment, sub-commitment 1-4,RELCIF.LDI,
	   		 and the requested fields, separated by tabs, in the
	   		 order indicated in the select statement.


	---- Revision History ------------------------------------------------
	
	11/30/05 - KELLYP - CR 18347
		   Modified entire procedure to conform to current PSL 
		   standards and also to return the commitment hierarchy in
		   a consistent format (i.e., ordered by CID).
	
	09/01/05 - KELLYP - CR 16684
		   Modified LNDATA section to add a space b/w the select
		   statement and the from statement to prevent an error
		   from occurring within SELECT^SQL.  Also removed pre-2003
		   revision history.
	  
	08/02/05 - KUMARB - CR16684
		   Modified section GETSUB, changed the table name LNLCOM 
		   to LN in Db.select

	02/26/03 - GRAY - 51349
		   Corrected UNDEFINED error on the variable XCCL when
		   retrieving sub-commitment information.

	01/03/03 - GRAY - 51349
		   Corrected SQLFAIL error caused by variable XACN not being
		   defined.

	*/

	type public Boolean ER

	type Number n
	type String lnrows()

	// Version number of client message is not compatible with server
	if versn.get()'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))
	
	set ER=0 
	set return=""
	set primary=primary.get()

	do GETINFO

	// Account not linked to any commitments
	if 'lnrows.data() quit $$ERRMSG^PBSUTL($$^MSG(82))

	set n=""
	for  set n=lnrows(n).order() quit:n.isNull()  do {
		if 'return.isNull() set return=return_$C(13,10)
		set return=return_lnrows(n)
		}

	set return=$$V2LV^MSG(return)
	
	quit ""


GETINFO	// Get commitment info

	type public Boolean primary
	type public Number ACN

	type Boolean ldi
	type Number i,MCOM,nxtrow,SUB()

	set nxtrow=1

	type ResultSet rs=Db.select("CID","LNMCOM1","ACN=:ACN","CID")
	while rs.next()  do {
       		set MCOM=rs.getCol(1)
		set ldi=$$GETLDI(MCOM)

		// Want only primary
		if primary,'ldi quit

		for i=1:1:4 set SUB(i)=0

		do LNDATA(MCOM,0,ldi)
		do GETSUB(MCOM,1)
		}
	quit


GETSUB(Number cid,Number level)	// Get sub-commitments

	type public Boolean ldi,primary
	type public Number SUB()
	
	type Number i,SUBCID

	// Only go four deep
	quit:level>4
	
	type ResultSet rs=Db.select("CID","LN","CCL=:cid","CID")
	while rs.next()  do {
       		set SUBCID=rs.getCol(1)
		set SUB(level)=SUBCID
		set ldi=$$GETLDI(SUBCID)
		for i=level+1:1:4 set SUB(i)=0
		
		// Want only primary
		if primary,'ldi quit
	
		do LNDATA(SUBCID,level,ldi)
	
		do GETSUB(SUBCID,level+1)
		}
	quit


LNDATA(Number cid,Number level,Boolean ldi)	// Setup up return rows with loan data
	
	type public Boolean ER
	type public Number MCOM,nxtrow,SUB()
	type public String lnrows(),select
	
	type Number i,X,XCID
	type String data
	
	set XCID=cid
	do SELECT^SQL(select_" FROM LN WHERE CID=:XCID",,,.data) quit:ER
	set X=MCOM
	for i=1:1:4 set X=X_$C(9)_SUB(i)
	set X=X_$C(9)_ldi_$C(9)_data
	set lnrows(nxtrow)=X 
	set nxtrow=nxtrow+1

	quit


GETLDI(Number CID)	//Private Get RELCIF.LDI
	
	type public Number ACN
	
	type RecordRELCIF relcif=Db.getRecord("RELCIF","ACN=:ACN,CID=:CID")
	if +relcif.ldi quit 1

	quit 0

vSIG()	quit "60234^61793^Pat Kelly^3611"	// Signature - LTD^TIME^USER^SIZE
