MRPC028(RETURN,VERSN,ORG,CID,STDT)	//Public;MRPC call for Orig Pmt Amts Due Stat/Unsat Fds Summ
	/*

	   DESC: Orig Pmt Amts Due Stat/Unsat Fds Summ.
	   KEYWORDS: RPC

	   ARGUMENTS:
	         . RETURN        Payments due from borrowers.    	/TYP=T/REQ/MECH=REFNAM:W

	         . VERSN         ^MRPC028 version number          	/TYP=N/REQ/MECH=VAL
	                         current version=1

		 . ORG 		 Original Payment flag            	/TYP=N/REQ/MECH=VAL
			     	 0 for Internal Loan Bill Breakdown
		    		 1 for Original Amounts Billed

	         . CID           Account Number         	  	/TYP=N/REQ/MECH=VAL

	         . STDT          Payments Due From Date            	/TYP=D/REQ/MECH=VAL

	   RETURNS:
	         . $$    Error Message                   		/TYP=T
	                 Null= No Error

	   RELATED:
	         . $$^PBSMRPC - MRPC Service Class Driver

	   EXAMPLE:
	         S RM=$$^MRPC028(.VAL,1,ORG,CID,STDT)

	  ---- Revision History ------------------------------------------------
	   12/05/2005 - TELIV - CR 17961
	   		Completed the Do statement with a missing "}"
	   		Also LNUFS dosent necessarily load LNB1(12), so better to
	   		Check with a $G for LNB1(12)

           04/17/2002 - DATTAR - ARQ 49794
			Converted to PSL
          
          ----------------------------------------------------------------------
	*/

	new A,CLS,CRLF,D,E,I,NEW,STR,TAB

	// Version number of client message is not compatible with server
	if $G(VERSN)'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))

	// Invalid account ~p1
	if 'Db.isDefined("ACN","CID=:CID") quit $$ERRMSG^PBSUTL($$^MSG(1259,CID))
	
	type RecordACN acn=Db.getRecord("ACN","CID=:CID")
	set CLS=acn.CLS

	// Invalid Account Class
	if (CLS'="L") set ET="INVLDCLS" do ^UTLERR quit $$ERRMSG^PBSUTL($G(RM))
	
	type RecordLN ln=Db.getRecord("LN","CID=:CID")

	// No internal bills on file for this account
	type ResultSet lnbil1=Db.select("SCHSEQ","LNBIL1","CID=:CID")
	if lnbil1.isEmpty() quit $$ERRMSG^PBSUTL($$^MSG(1523))

	// Default start date in STDT field
	if $G(STDT)="" do {

		// Oldest seq w/ $ due
	        set A=ln.oseq

		// Due date of OSEQ bill
		if A'="" do {
			type RecordLNBIL1 lnbil1=Db.getRecord("LNBIL1","CID=:CID,SCHSEQ=:A")
			set A=lnbil1.cdpd 
			}
	}

	// Date entered exceeds due date of last internal bill on file
	if $G(STDT)>ln.lbdd quit $$ERRMSG^PBSUTL($$^MSG(1518))

	// Load payment data
	set NEW=0
	do LOAD^LNUFS

	if $G(ER) quit $$ERRMSG^PBSUTL($G(RM))

	// Create formatted output string
	set TAB=$C(9)
	set CRLF=$C(13,10)
	set STR=""

	// Oldest Unpaid and Most Recent Bills
	set STR=OSEQDAT_TAB_BSEQDAT_TAB

	// Due Dates (up to 4 columns)
	set STR=STR_$P(LNB1(3),"|",1)_TAB_$P(LNB1(3),"|",2)_TAB_$P(LNB1(3),"|",3)_TAB_$P(LNB1(3),"|",4)_TAB

	// Bill Dates (up to 4 columns)
	set STR=STR_$P(LNB1(2),"|",1)_TAB_$P(LNB1(2),"|",2)_TAB_$P(LNB1(2),"|",3)_TAB_$P(LNB1(2),"|",4)_TAB

	// Total Payments (up to 4 columns)
	set STR=STR_$P(LNB1(4),"|",1)_TAB_$P(LNB1(4),"|",2)_TAB_$P(LNB1(4),"|",3)_TAB_$P(LNB1(4),"|",4)_TAB

	// Amount Due (up to 4 columns)
	set STR=STR_$P(LNB1(1),"|",1)_TAB_$P(LNB1(1),"|",2)_TAB_$P(LNB1(1),"|",3)_TAB_$P(LNB1(1),"|",4)_TAB

	// Original Subsidy
	set STR=STR_$P(LNB1(11),"|",1)_TAB_$P(LNB1(11),"|",2)_TAB_$P(LNB1(11),"|",3)_TAB_$P(LNB1(11),"|",4)_TAB

	// Subsidy Due (up to 4 columns)
	set STR=STR_$P($G(LNB1(12)),"|",1)_TAB_$P($G(LNB1(12)),"|",2)_TAB_$P($G(LNB1(12)),"|",3)_TAB_$P($G(LNB1(12)),"|",4)_CRLF

	// Data for repeating region
	set I=""
	for  set I=$O(LNB2(I)) quit:I=""  do {

		// Lmp Elemnt Loc (description)
		set D=$P(LNB2(I),"|",6)

		// Internal bill amounts due (up to 4 columns) and Lmp Amount
		set E=$P(LNB2(I),"|",1,5)
		set STR=STR_$TR(D_TAB_E,"|",TAB)_CRLF
		}
	set RETURN=$$V2LV^MSG(STR)

	quit ""
 #OPTION ResultClass ON
Public String vSIG()	quit "60283^55233^Vinayak Teli^3541"	// Signature - LTD^TIME^USER^SIZE
