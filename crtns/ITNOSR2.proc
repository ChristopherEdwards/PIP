ITNOSR2	  /*
ORIG: kellyp - 09/14/2005
DESC: Nostro Account Reporting Functions

---- Comments --------------------------------------------------------

	This routine contains common functions necessary for running 
	the Nostro projection reports (SCA439 and SCA441).

---- Revision History ------------------------------------------------

	09/14/05 - KELLY - CR 17050
		Converted to PSL.
 */
 	quit
 	
public CRFILE(Date START, Date END)
	/*
	 This function creates a temporary file that is to be
	 used for running the projected Nostro Report.  The file that
	 is created is ^ITNOSFIL.  This program will go through each 
	 business date in the period range they defined and determine 
	 the balance for each of those days.
	*/
	
	type Date CDATE
	type Number BAL,CID,COUNTER
	type String ITNOSDEF()
	
	// Delete TEMP File
	do Db.delete("ITNOSFIL","T=:%ProcessID")
	
	// Build ITNOSDEF array
	do BLDDEF(.ITNOSDEF())
	
	set COUNTER=0
	set CID=""
	for  set CID=ITNOSDEF(CID).order() quit:CID.isNull()  do {
		set COUNTER=COUNTER+1
		set CDATE=START-1
		if CDATE.isNull() quit
		
		do DATE
		}

	quit

DATE
	type public Date CDATE,END
	type public Number CID
	type public String ITNOSDEF()
	
	type String CRCD
	
	if (CDATE>END)!(CDATE=0) quit

	if '$$BD^UNBD(CDATE) set CDATE=CDATE+1 do DATE quit
	
	set CRCD=ITNOSDEF(CID).piece("|",1)
	
	type RecordITNOSFIL itnosfil=Class.new("RecordITNOSFIL","T=:%ProcessID,CRCD=:CRCD,CID=:CID,PRJ=:CDATE")
	
	set itnosfil.openbal=$$OPENBAL(CID,CDATE)
	
	do itnosfil.save()
	
	set CDATE=CDATE+1
	
	do DATE
	
	quit
	
public OPENBAL(Number CID,Date DATE)

	type Date END,START
	type Number BBAL

	type RecordDEP dep=Db.getRecord("DEP","CID=:CID")
	
	set START=%SystemDate
	set BBAL=dep.bal
	
	if DATE'>START quit BBAL
	
	set END=DATE

	type DbSet ds=Db.selectDbSet("ITNOSTRO","CID=:CID AND DTJD>:START AND DTJD<:END")
	while ds.next() do {
		type RecordITNOSTRO itnostro=ds.getRecord("ITNOSTRO")
		
		if 'itnostro.itc.extract(1) set BBAL=BBAL+itnostro.tamt
		else  set BBAL=BBAL-itnostro.tamt
		}

	quit BBAL

public BLDDEF(String ITNOSDEF())
	// Build array of Nostro Accounts

	type String CRCD

	type DbSet ds=Db.selectDbSet("PRODCTL","GRP='DBD' AND NOSTRO=1")
	while ds.next() do {
		type RecordPRODCTL prodctl=ds.getRecord("PRODCTL")
		
		type DbSet dsdep=Db.selectDbSet("DEP","TYPE=:prodctl.type")
		while dsdep.next() do {
			type RecordDEP dep=dsdep.getRecord("DEP")
		
			// Only include open acct's w/ balances
			if (dep.stat=4)!(dep.bal=0) quit
		
			set CRCD=dep.crcd
			if CRCD.isNull() set CRCD=%SystemCurrency
			set ITNOSDEF(dep.cid)=CRCD_"|"_%CurrentDate
			}
		}

	quit

vSIG()	quit "60157^57802^Pat Kelly^2492"	// Signature - LTD^TIME^USER^SIZE
