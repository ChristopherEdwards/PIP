public	TTXP1	// Standard batch entry point, ^TTX(TPD,BRCD,%UID,TRC) is built

	/*
	   ORIG:  Frank R. Sanchez (2497)
	   Procedure ID: TTXP1
	   Description: General purpose batch transaction posting

	---- Revision History -------------------------------------------------

	03/14/07 - KumarSS - CR 25177
		   Removed Invalid Unicode Characters.

	11/29/05 - HAILEYM - CR18146
		   Changed typing of variables %ODP and %RET from private to 
		   public. Previously, Return Item processing was not being
		   invoked because %RET had been re-typed here.
	
	05/31/05 - SmithCD - CR 15902
		   Removed local scoping of TLO to prevent possible undefined 
		   error elsewhere.
	
	02/01/05 - SmithCD - 13661
		   Removed () after ^TTXLOK in conjunction with same change in 
		   ^TTXLOK. Cleaned up remaining compiler warnings. Added 
		   revision history below that was missing with CR 13611. 
		   Fixed additional problem with CR 13611 in RTTY section in 
		   which EXIT was never set within the loop.
		   
	12/28/04 - JeruchimC - 13611
		   . Label TTX - Replace Class.new for TTXUID with getRecord 
		   w/3rd argument. Data generated by TRNDRV indicating the 
		   last sequence processes was being overwritten by the new 
		   record which causes previously posted transactions to be 
		   posted additional times for each execution of TTXPOS.
		   . Label RTY - Replace NJD with NPD in Db.Select statement.
		   . Removed Label ZT (no longer called).
		   . Cleaned up code per PSL standards.
		   
	03/22/02 - ARCILLAZ - 43583
		   07/30/02 - YENDAPALLIS - 43583		
		   Converted to PSL
	-----------------------------------------------------------------------
	*/

	type public Boolean %ODP, %RET
	type public Number BRCD, MARTY, STATUS
	type public String ER, TLO

	// Batch error (posting aborted)
	set STATUS = 2 do ^TTXLOK if ER quit
		
	type Date TPD
	type String CTL, RM, Z

	set Z = $$NEW()
	new @Z do INIT

	do TRNBATCH^TRNDRV(TPD, BRCD, %UserID)	
	
	type RecordTTXUID ttxuid = Db.getRecord("TTXUID", "TPD,BRCD,%UserID", 1)
	set ttxuid.badttx = ""

	do ttxuid.bypassSave()
	
	if MARTY do RTY
	
	quit


RTY 	// Retry processing

	type public Date TPD
	type public Boolean RTSN
	type public Number BRCD

	type String UID
	type Number BSEQ
	type Date NPD

	set UID = %UserID

	type ResultSet rs = Db.select("TSEQ", "EXC", "TJD=:TPD AND BRCD=:BRCD AND UID=:UID")
	if rs.isEmpty() quit

	if RTSN.get() set NPD = TPD
	else  for NPD = TPD + 1:1 quit:$$BD^UNBD(NPD)

	/* If an entry does not exist in the dayend retry table that has an 
	   Original Posting Date (dayendrty.ljd) equal to the teller posting 
	   date, create one. Because the dayendrty.ljd is not a key, a select 
	   statement must be used to determine if the record exists.
	 */

	type ResultSet rs2 = Db.select("BSEQ", "DAYENDRTY", "TJD=:NPD AND BRCD=:BRCD AND BUID=:%UserID AND LJD=:TPD", "BSEQ DESC")
	if 'rs2.isEmpty() quit

	type RecordDAYENDRTY dayendrty = Class.new("RecordDAYENDRTY")

	set BSEQ = ""
	set dayendrty.tjd = NPD
	set dayendrty.brcd = BRCD
	set dayendrty.buid = %UserID
	set dayendrty.ljd = TPD
	set dayendrty.bseq = rs2.next()
	
	do dayendrty.save()

	quit


INIT	//  Init section

	type public Date %SystemDate, TPD
	type public Number MARTY, REJMET
	type public Boolean %ODP, RTSN
	type public String %UserClass

	set TPD = TPD.get()
	if TPD.isNull() set TPD = %SystemDate

	type RecordSCAU scau = Db.getRecord("SCAU", "%UserID")

	set %UserClass = scau.%ucls
	set REJMET = scau.batrej
	set MARTY = scau.marty
	
	if 'MARTY quit

	/*
	If Same Day Retry is turned on, set RTSN=1.  There is no need
	to build the retry reason array, since ALL rejects will be
	reprocessed. Also, turn off overdraft protection since rejected
	transactions will be reprocessed later in the day.
	*/

	set RTSN = scau.sdrty

	// Same Day Retries
	if RTSN set %ODP = 0 quit

	type ResultSet rs = Db.select("GRP,REST", "UTBLRETRY")
	if 'rs.isEmpty() while rs.next() set RTSN(rs.getCol(1), rs.getCol(2)) = ""

	quit


NEW()	// Exclusive N[ew] list

	quit "("_$$VSAV^SCADRV0_",BRCD,CTL,ER,%ODP,%RET,RM,TLO,TJD,TPD,%UID)"
 #OPTION ResultClass ON
Public String vSIG()	quit "60703^23799^Sudanthiran S. Kumar^3868"	// Signature - LTD^TIME^USER^SIZE
