CHKCLR	//;CHECK REGISTER CHECK CLEAR FUNCTION
	/*
	       ORIG:  Jill Bulkin (6838) - 05/01/86
	       DESC:  Sets up file with check load information (high/low check #)
	              and check type

	  ---- Revision History ------------------------------------------------
	  
	   14/10/05 - PUTTASWH - CR17011	   	      
	  	      Modified VINIT section to contain the code of VPG and 
	  	      VPG0 sections. Removed VPG and VPG0 sections.
	   	      Modified sections VER and POS to declare the variable GL.
	   	      Replaced references to XCHKREG table with CHKREG1 table
	   	      in VPGO1 and FILE sections.	   	     
	   	      
	   12/02/03 - CARROLLJ - CR7239
		      Added #ACCEPT to check before xecute command to correct
		      compilier errors.

	   06/06/02 - DATTAR - ARQ 49794
		      Converted to PSL


	   04/03/00 - TANY - 37915
	              Optimized performance by modifying ^SCADAT1 calls
	              to ^SCAJD. Also remove revision history older than
		      one year.

	 -----------------------------------------------------------------------
	*/
	
CREATE  //
	set %ProcessMode=0 
	do VINIT 
	quit

VINIT	// init variables
	type Boolean FINISH
	
	set (%PG,%PAGE)=1 
	set ER=0
	kill OLNTB,VFMQ
	
	set FINISH=0
	
	for  do { quit:FINISH		
		
		do VPG01 if ER set FINISH=1 quit
	
		if "DFQ"[$G(VFMQ)!$G(ER) do VER set FINISH=1 
	
		}
	
	quit

VPG01	//
	set %TAB("CO")=".CO2/TBL=[UTBLCHKBANK]"
	set %TAB("CKTYP")=".CHKTYP1/TBL=[UTBLCHKS]/XPP=D POS^CHKCLR"

	// Invalid date ~p1
	set %TAB("DATE")=".DATE4/XPP=Q:X=""""  S X=$$^SCAJD(X) I X<0 S ER=1,RM=$$^MSG(1308)"

	set %TAB("CKNO")=".CKNO2/TBL=[CHKREG1]/HLP=[CHKREG1]CKNO/XPP=D EXT^DBSQRY"

	set %READ="@@%FN,,,CO/REQ,CKTYP/REQ,DATE/REQ,CKNO/REQ"
	do ^UTLREAD 
	if VFMQ="Q" quit

	set SEQBY="[SYSDEV,CHKREG1]CKNO"
	do ^DBSQRYA

	quit


POS	// Check Type Post-Processor

	type Number GL
	
	if X="" quit
	
	type RecordUTBLCHKS utblchks=Db.getRecord("UTBLCHKS","CHKS=:X")
	set GL=utblchks.cid

	// Type ~p1 has no G/L account in CHKS user table
	if GL="" set ER=1 set RM=$$^MSG(2770,X) quit
	
	type ResultSet chkreg1=Db.select("GL","CHKREG1","CO=:CO AND GL=:GL")

	// No checks in check register
	if chkreg1.isEmpty() set ER=1 set RM=$$^MSG(1912)
		
	quit


VER	// verify and process

	type public Number GL

	new CKN,MAXCK,MINCK,STATUS

	if VFMQ="Q"!(ER) do VEXIT quit

	if '$D(MIN(1)) set MIN(1)=""
	if '$D(MAX(1)) set MAX(1)=999999999999
	set MINCK=+MIN(1) 
	set MAXCK=MAX(1)

	type ResultSet rs=Db.select("CKNO","CHKREG1","CO=:CO AND GL=:GL AND CKNO>:MINCK AND CKNO<=:MAXCK")
	if 'rs.isEmpty() while rs.next() do {
		set CKN=rs.getCol(1)
		type RecordCHKREG1 chkreg1=Db.getRecord("CHKREG1","CO=:CO,GL=:GL,CKNO=:CKN")
		if CKN'>MIN(1) quit
		if CKN=""!(CKN>MAX(1)) quit
		#ACCEPT DATE=12/02/03;PGM=John Carroll
		xecute Q(1,1) else  quit
		
		// already cleared/voided
		set STATUS=chkreg1.status
		if "12"[STATUS quit
		do FILE(.chkreg1)
		}


VRPT	// report

	set RID="CHKCLR" 
	do ^URID 
	if PGM="" set ET="INVLDRPT" do DSPBP^UTLERR do VEXIT quit
	do ^@PGM


VEXIT	// Exit function

	if "24"[%ProcessMode!(ER) quit

	set ER="W"

	// Check clear not completed
	if VFMQ="Q" set RM=$$^MSG(522) quit

	// Check clear completed
	set RM=$$^MSG(521)
	quit


FILE(RecordCHKREG1 chkreg1)	// File data

	set chkreg1.status=1
	set chkreg1.clear=DATE
	do chkreg1.bypassSave()
	

	quit

vSIG()	quit "60218^24260^Hema Puttaswamy^3160"	// Signature - LTD^TIME^USER^SIZE
