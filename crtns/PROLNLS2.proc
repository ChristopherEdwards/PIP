PROLNLS2		/*
	---- Revision History -------------------------------------------------
	06/06/06 - GIRIDHAL - CR21207
		   The PROLNLS section has been modified to set the Service Fee Method
		   Change Date equal to the current system date if the Service Fee
		   Calculation Method is defined and if the Service Fee Method 
		   Change Date has not been modified under the same transaction.
		   
	11/23/05 - chhabris - CR18008
		   Replaced deprecated methods. Also, removed the revision 
		   history prior to 2004.
	
	----------------------------------------------------------------------
	*/
	
	quit

PROLNLS(RecordLNLS2 lnls2)

 	new NJD,X
	new LEAD
	new INCD,PL,GRP,XCID,CCY1,CCY2,WASH

	set INCD=lnls2.incd
	if lnls2.picm=6 do {
		type RecordLNLS1 lnls1=Db.getRecord("LNLS1","INCD=:INCD")
		set LEAD=lnls1.leadinst
		}

	set PL=lnls2.pl 
	set GRP="",XCID="",CCY1="",CCY2=""
	
	type ResultSet rs=Db.select("GRP","LNLS4","INCD=:INCD AND PL=:PL AND GRP>:GRP","GRP")
	if rs.next() set GRP=rs.getCol("GRP")

	if GRP.get()'=""  do {
		type ResultSet rs1=Db.select("CID","LNLS4","INCD=:INCD AND PL=:PL AND GRP=:GRP AND CID>:XCID","CID")
		if rs1.next() set XCID=rs1.getCol("CID")
		}

	if lnls2.picm'=6 do {
		if lnls2.pp="" set ER=1,RM=$$^MSG(1767,"lnls2.pp")
		if lnls2.remfre=""  set ER=1,RM=$$^MSG(1767,"lnls2.remfre")
		if lnls2.remnd="" set ER=1,RM=$$^MSG(1767,"lnls2.remnd")
		if lnls2.sfcm="" set ER=1,RM=$$^MSG(1767,"lnls2.sfcm")
		}

	quit:ER

	if $G(XCID)'="" do {
		type RecordLN ln=Db.getRecord("LN","CID=:XCID")
		set CCY1=ln.crcd
		set WASH=lnls2.wash
		if WASH'="" do {
			type RecordDEP dep=Db.getRecord("DEP","CID=:WASH")
			set CCY2=dep.crcd
			}
		}

	if $G(WASH)'="",CCY1'=CCY2 set ER=1,RM=$$^MSG(4259) Q

	if (lnls2.efd'=""),(%ProcessMode=0) do {

		// Set remittance last date as agreement effective date
		if lnls2.remld="" set lnls2.remld=lnls2.efd
		}

	if lnls2.ignadj'="" do { quit:ER 

		// Only valid for remittance method of 5
		if (lnls2.picm'=5),(lnls2.ignadj) set ER=1 do Runtime.setErrMSG("LNLS2",7645) quit:ER 
		}

	if (lnls2.remfre'=""),(%ProcessMode=0) do {

		// get remittance next date
		set FRE=lnls2.remfre 
		set JD=lnls2.remld quit:JD="" 
		set NJD=$$NJD^UFRE(JD,FRE) quit:ER
		if lnls2.remnd="" set lnls2.remnd=NJD
		}

	if lnls2.sfcm=0 do {
		set lnls2.sfpf="" 
		set lnls2.sfgl=""
		}

	// Data required in data item service fee G/L account offset
	if (lnls2.sfpf),(lnls2.sfgl="") do Runtime.setErrMSG("LNLS2",2404) quit:ER     //Data required

	// LNLS2.GSFEE  Data required
	if (lnls2.gsfee=""),(+lnls2.sfcm'=0) do Runtime.setErrMSG("LNLS2",7531,"[LNLS2]GSFEE") quit:ER 
	
	// If Auto/GL for Service Fee is on, Must use Auto GL here
	if ('lnls2.pipf),(lnls2.sfpf) do Runtime.setErrMSG("LNLS2",7835) quit:ER 
	
	if lnls2.wash="" do { quit:ER 

		// Required with contra account processing option
		if lnls2.picm=6,(LEAD'=1) set ER=1 do Runtime.setErrMSG("LNLS2",2397) quit:ER 

		// Required when P&I processing flag is on
		if lnls2.pipf set ER=1 do Runtime.setErrMSG("LNLS2",2396) quit:ER 

		// Required when fee processing flag is on
		if lnls2.sfpf set ER=1 do Runtime.setErrMSG("LNLS2",2395) quit:ER 
		}

	else  set XWASH=lnls2.wash do { quit:ER 

		type RecordDEP dep=Db.getRecord("DEP","CID=:XWASH")
		set VALUE=dep.cid
		if VALUE="" set ER=1 do Runtime.setErrMSG("LNLS2",1259,XWASH) quit:ER 
		}

	if lnls2.escwash="" do { quit:ER 
		// Required when escrow participation percent is greater than zero
		if lnls2.escpp set ER=1 do Runtime.setErrMSG("LNLS2",2394) quit:ER 
		}

	else  set XESCWASH=lnls2.escwash do { quit:ER 
		type RecordDEP dep=Db.getRecord("DEP","CID=:XESCWASH")
		set VALUE=dep.cid
		if VALUE="" set ER=1 do Runtime.setErrMSG("LNLS2",1259,XESCWASH) quit:ER 
		}
	
	// Data required in data item escrow G/L account offset
	if (lnls2.escpp),(lnls2.escgl="") set ER=1 do Runtime.setErrMSG("LNLS2",2403) quit:ER 
	
	//BASEL II
	if 'lnls2.sfcm.isNull(),'lnls2.isChanged("SFCHGD") set lnls2.sfchgd = %SystemDate
 
	quit 

vSIG()	quit "60436^57760^Lakshmi Giridharan^3850"	// Signature - LTD^TIME^USER^SIZE
