USRESTAT	
	/* ORIG: HAYMANP August 12, 1999

	   DESC: Linetags in this procedure will be called from the CIF and
	   RELCIF filers to determine the appropriate USRESTAT value.


	   --- Revision History ------------------------------------------------

	   03/10/00 - HAYMANP - 37354
                   Modified section RELAT to not update DEP.USRESTAT to 0
                   if it is already null.

	   08/25/99 - HAYMANP - 33049
		      Convert to PSL.

	   ---------------------------------------------------------------------
	*/

	// can't call from the top
	quit


ACCT(XCID,XACN,NEWRS) // Extrinsic - get the U. S. residency status of an account

	new LACN,RSTAT,RSTAT1,RSTAT2

	set XACN=$G(XACN)
	set NEWRS=$G(NEWRS)
	set (RSTAT1,RSTAT2)=0

	// retrieve all linked CIFs
	new rs
	type ResultSet rs=Db.select("ACN","RELCIF","CID=:XCID")
	while rs.next() do {  quit:RSTAT1

		set LACN=rs.getCol(1)
		if LACN=XACN set RSTAT=NEWRS
		else  set RSTAT=$$GETRES(LACN)
		if RSTAT=1 set RSTAT1=1 quit
		if RSTAT=2 set RSTAT2=1
		}

	if RSTAT1 quit 1	// Nonresident Alien
	if RSTAT2 quit 2	// Resident Alien
	quit 0			// Resident


CUST(XACN,NEWRS) // Called from CIF filer

	new XCID

	// update all customer's accounts
	new rs
	type ResultSet rs=Db.select("CID","RELCIF","ACN=:XACN")
	while rs.next() do {
	
		set XCID=rs.getCol(1)
		do RELAT(XCID,XACN,NEWRS) quit:ER
		}

	quit


RELAT(XCID,XACN,NEWRS) // Called from RELCIF filer

	new XCLS,RSTAT,OVAL

	set XCLS=Db.getOneRow("CLS","ACN","XCID")
	quit:XCLS'="D"
	
	set RSTAT=$$ACCT(XCID,$G(XACN),$G(NEWRS)) quit:ER

	// don't update null to zero            nph - 3/10/00
	set OVAL=Db.getOneRow("USRESTAT","DEP","XCID")
	if (OVAL="")&(RSTAT=0) quit

	// update USRESTAT on account
	do Db.update("DEP","USRESTAT=:RSTAT","CID=:XCID")

	quit


GETRES(XACN) // Get U. S. Residency Status

	new RSTAT
	set RSTAT=Db.getOneRow("USRESTAT","CIF","XACN")

	if RSTAT="" quit 0
	quit RSTAT

vSIG()	quit "59886^43628^Sanchez SCM Administrator^1830"	// Signature - LTD^TIME^USER^SIZE
