SWFMTCNV	// SWIFTFMT system table conversion routine ; Procedure SWFMTCNV - SWIFTFMT system table conversion routine
 ;;Copyright(c)2005 Sanchez Computer Associates, Inc.  All Rights Reserved - 08/09/05 10:36 - spatola
 ;
 ; **** This is a DATA-QWIK generated routine (level 25) ****
 ;
	/*

	   ORIG: TSAIT - 30 AUGUST 1994
	   DESC: SWIFTFMT system table conversion routine
	   This routine will generate SWIFTFLD and SWIFTMSG system tables
	   based on the SWIFTFMT table.

	   ARGUMENTS: None

	   INPUTS:
	   . Data ^STBL("SWIFTFMT"  (File STBLSWIFT)

	   RETURNS: None

	   RELATED:
	   . ^SWIFTGEN SWIFTGEN is using the system table generated by
	     this routine to set up the SWIFT global.

	   EXAMPLE:
	   do  ^SWFMTCNV

	---- Revision History -------------------------------------------------

	06/25/03 - GRAY - 51351
		   Converted to PSL.  Removed old revision history.


	*/
	
	#WARN
	#OPTIMIZE

	type Public String tagarr

	if tagarr.data() kill tagarr
	
	do Db.fastDelete("STBLSWMSG")

	do Db.fastDelete("STBLSWFLD")

	type DbSet dsfmt=Db.selectDbSet("STBLSWIFT")
	if dsfmt.isEmpty() quit

	while dsfmt.next() do {
		type RecordSTBLSWIFT swfmt=dsfmt.getRecord("STBLSWIFT")
		do BLDMSG(.swfmt)
		do BLDFLD(.swfmt)
		}

	kill tagarr

	// Fix MT103 entries in SWIFTMSG and SWIFTFLD
	do MT103

	quit


BLDMSG(RecordSTBLSWIFT swfmt)	// Build the SWIFTMSG system table entry.

	type Number option,SEQ
	type String MSG,msgrec,temp
	type Public String tag

	set temp=swfmt.tag.curVal.piece(":",1)

	set option=$select("0123456789"[temp.extract(temp.length()):0,1:temp.extract(temp.length()))
	if option'=0 set temp=temp.piece(option,1)

	// no new entry in MSG table
	if temp=tag quit

	set tag=temp
	set MSG=swfmt.type
	set SEQ=Db.nextVal("STBLSWMSG","MSGTYPE=:MSG")

	set msgrec=tag_$select((swfmt.option'="")&(option'=0):"",option=0:"",1:option)_":"

	do TAGPP
	
	type RecordSTBLSWMSG swmsg=Db.getRecord("STBLSWMSG","MSGTYPE=:MSG,SEQ=:SEQ",1)
	set swmsg.field=msgrec
	do swmsg.bypassSave()
	
	quit


BLDFLD(RecordSTBLSWIFT swfmt)	// Build the SWIFTFLD system table entry.

	type Boolean QUIT=0
	type Number I,option
	type String di,pce,VAR
	type Public String msgrec

	if msgrec.extract(msgrec.length())=":" set msgrec=msgrec.piece(":",1)
	if msgrec="" quit

	//third key of the table
	if swfmt.option="" set option=0

	// record exists
	type RecordSTBLSWFLD swfld=Db.getRecord("STBLSWFLD","MSG=:msgrec,SEQ=:option",1)
	if swfld.getMode() quit

	for I=1:1:10 set VAR="DI"_I do { quit:QUIT
		set di=swfmt.@VAR 
		if I<6&(di="") set QUIT=1 quit
		if di="" quit
		type RecordDBTBL1D dbtbl1d=Db.getRecord("DBTBL1D","%LIBS='SYSDEV',FID='SWIFT',DI=:di")
		set pce=dbtbl1d.pos
		if pce["*" set pce=1
		if pce'="" set swfld.@VAR=pce
		}

	do swfld.bypassSave()

	quit


TAGPP	// Field tag post processer.

	type Public Number MSG,option
	type Public String msgrec,tagarr(,,)

	// Special case for field 56 (buying part) of types 100 & 202.
	if ((MSG=100)!(MSG=103)!(MSG=202))&((msgrec["56")!(msgrec["57")) set msgrec="1"_msgrec

	if 'tagarr(MSG,msgrec,option).data() set tagarr(MSG,msgrec,option)="" quit

	set msgrec=(100+msgrec.piece(":",1))_":"

	quit


MT103	// Special processing for MT103 messages

	// Fix STBLSWIFTMSG
	do BLD103

	// Fix STBLSWIFTFLD (Receiver's Charges piece not built)
	type RecordSTBLSWFLD swfld=Db.getRecord("STBLSWFLD","MSG='71G',SEQ=0",1)
	set swfld.di1="3"
	do swfld.bypassSave()

	/*
	Note: 23E and 77T are also both missing from SWIFTFLD but do not
	need a node/piece designation as they are computed fields.
	*/

	quit


BLD103	// Set up STBLSWIFTMSG with MT103 fields

	type Number SEQ
	type String VALUES

	// Clear old values before setting up new ones	
	do Db.fastDelete("STBLSWMSG","MSGTYPE=103")

	set VALUES="20:|23B:|23E:|26T:|32A:|33B:|36:|50:|51:|52:|53:|54:|55:|"
	set VALUES=VALUES_"156:|157:|59:|70:|71A:|71F:|71G:|72:|77B:|77T:"

	for SEQ=1:1:23 do {
		type RecordSTBLSWMSG swmsg=Class.new("RecordSTBLSWMSG")
		set swmsg.msgtype=103
		set swmsg.seq=SEQ
		set swmsg.field=VALUES.piece("|",SEQ)
		do swmsg.bypassSave()
		}

	quit

vSIG()	quit "59886^43613^Sanchez SCM Administrator^3625"	// Signature - LTD^TIME^USER^SIZE
