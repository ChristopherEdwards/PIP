MRPC018(String RETURN,Number VERSN,Number CID,String ESCNUM,String FD)	//Public;MRPC call for Escrow Analysis Detail.
	/*
	   DESC: MRPC call for Escrow Analysis Detail.

	   KEYWORDS: RPC

	   ARGUMENTS:
	         . RETURN    	Escrow Analysis Detail 	/TYP=N/REQ/MECH=REFNAM:W

	         . VERSN     	^MRPC018 version number /TYP=N/REQ/MECH=VAL
	                      	current version=1

	         . CID        	Account Number          /TYP=T/REQ/MECH=VAL

	   	 . ESCNUM 	Escrow ID  		/TYP=T/REQ/MECH=VAL

	         . FD           From Date               /TYP=T/REQ/MECH=VAL

	   RETURNS:
	         . $$    	Error Message           /TYP=T
	                 	Null= No Error

	   RELATED:
	         . $$^PBSMRPC - MRPC Service Class Driver

	   EXAMPLE:
	         S RM=$$^MRPC018(.VAL,1,CID,ESCNUM,FD)

	-------Revision History------------------------------------------

        31/01/06 - Srinivar - CR19137
        	   Modified the MRPC section to pass additional NULL parameter
        	   for lnapchge object while calling EXEC^LNEA.	
        
        07/26/05 - SkariahV- CR16679
	           Removed #WARN directive.
	              
	01/10/03 - GRAY - 51351
		   Converted to PSL.


	*/

	type Number ESNUM,ESNUM1,POS
	type String data,ESID,ESTYP,ESTYP1,STR

	type Public Number ER
	type Public String ET,RM

	// Version number of client message is not compatible with server
	if VERSN.get()'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))
	
	type RecordLN ln=Db.getRecord("LN","CID=:CID",1)

	// Invalid account ~p1
	if ln.getMode()=0 quit $$ERRMSG^PBSUTL($$^MSG(1259,CID))

	// Verify ESCNUM parameter is valid for CID.
	
	set ESTYP=ESCNUM.extract(1,3) 
	set ESNUM=ESCNUM.extract(4,99)
	set ESID=ln.pmtdist
	set POS=ESID.find("ESC")

	// No escrow accounts tied to this loan
	if 'POS quit $$ERRMSG^PBSUTL($$^MSG(1934))

	set ESID=ESID.extract(POS-3,255).piece("-",1)
	set ESTYP1=ESID.extract(1,3) 
	set ESNUM1=ESID.extract(4,99)

	// Invalid escrow account
	if ESNUM>ESNUM1 quit $$ERRMSG^PBSUTL($$^MSG(749))

	do EXEC^LNEA(.ln,,CID,,ESCNUM,FD,,"00",.data)

	if ER.get() quit $$ERRMSG^PBSUTL(RM.get())

	/*
	Format output containing Escrow Details.
	piece 1: New Escrow Pmt.
	piece 2: Amount
	piece 3: Calculated Beginning Bal.
	piece 4: Actual Beginning Bal.
	piece 5: Low Bal. for Period
	piece 6: Current Escrow Bal.
	piece 7: Total Remittance Amount for Period
	piece 8: Necessary Cushion.
	*/

	set STR=data.translate("|",$C(9))_$C(13,10)

	set RETURN=$$V2LV^MSG(STR)

	quit ""

vSIG()	quit "60299^30517^Srinivasan, Rajesh^2266"	// Signature - LTD^TIME^USER^SIZE
