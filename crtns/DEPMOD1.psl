public DEPMOD1(return,versn,odt,iacm,icompfre,tranfre,tranamt,presval,futval,irn,trm,busopt,nbdc,type)
 
 /*  ORIG: ESTERLYP - 01/12/01
     DESC: Deposit Modeler
 
     ---- Comments --------------------------------------------------------
    Code was taken from MRPC045.M.  See that routine for explaination of
    parameters.
  
    ---- Revision History ------------------------------------------------

	10/13/05 - S.Krishnan - CR16885
		   Top of the procedure is made as Public.
		   
	11/04/02  - JERUCHIMC - 51283
		    Remove $G on type parameter.
	
	01/12/01  - ESTERLYP - ARQ 43481
		    Procedure copied from procedure DEPMOD in v6.2.

	01/02/01  - ESTERLYP - ARQ 42289
		    Add parameter type for routine DEPMOD when
		    using Cumulative Deposit types.
 */ 
  
	if $G(versn)'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))
	//
	new ER,FIN,RM,count,fld,retfld,retval,value
	//
	set tranamt=$G(tranamt),presval=$G(presval),futval=$G(futval)
	set irn=$G(irn),trm=$G(trm),busopt=$G(busopt),nbdc=$G(nbdc)
	//
	// Determine the field to return.
	set (count,retfld,retval,tmpcnt)=""
	for I="tranamt","presval","futval","irn","trm" do {
		set tmpcnt=tmpcnt+1
		set value=@I
		if value'="" set fld(tmpcnt)=value quit
		set retval=I,retfld=tmpcnt
		set count=count+1
		}
      
	// Verify input received from client
	set ER=0 do VERINP quit:ER $$ERRMSG^PBSUTL($G(RM),$G(ET))
	//
	// Construct array used by calculation routine.
	set FIN(1)=tranamt
	set FIN(2)=presval
	set FIN(3)=trm
	set FIN(4)=futval
	set FIN(5)=irn
	set FIN(6)=""
	set FIN(8)=tranfre
	set FIN(9)=""
	set FIN(10)=odt
	set FIN(11)=icompfre
	set FIN(20)=iacm
	set FIN(21)=busopt
	set FIN(22)=nbdc
	set FIN(23)=type
	//
	set calc1=$$EXT^DEPMOD(.FIN)
	if 'ER set calc2=$J($$EAY^DEPMOD,0,5)
	//
	if ER Q $$ERRMSG^PBSUTL($G(RM),$G(ET))
        
	set fld(retfld)=calc1
	set fld(6)=calc2
	set return=$$V2LV^MSG(.fld)
	quit ""
	//
	//----------------------------------------------------------------------
VERINP  // Verify input data
	//----------------------------------------------------------------------
	//
	new tmp
	//
	// Ensure that client requests exactly one return field
	if count'=1 set ER=1,RM=$$^MSG(403) quit
	//
	// Ensure that appropriate syntax is used for term
	if trm'="" set tmp=$$EXT^UMDT(trm,+$h) quit:ER
	//
	// Ensure that appropriate syntax is used for compounding frequency.
	do VERFRE(icompfre) quit:ER
	//
	// Similar check for transaction frequency
	do VERFRE(tranfre) quit:ER
	//
	// Transaction frequency must be defined if either 1. transaction amount
	// is other than zero, or 2. transaction amount does not have a value
	// but it is the value requested by the client.
	if (tranamt!(retval="tranamt"))&(tranfre="") set ER=1,RM=$$^MSG(809) quit
	//
	quit
	//
	//----------------------------------------------------------------------
VERFRE(X)       // Verify frequencies
	//----------------------------------------------------------------------
	//
	// Only standard IBS frequencies can be used.  User-defined frequencies
	// are only valid if they point to a standard IBS frequency.
	//
	do FRECK^DEPMOD
	//
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60186^22132^S. Krishnan^2900"	// Signature - LTD^TIME^USER^SIZE
