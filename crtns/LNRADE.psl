LNRADE	
	/*
	       ORIG:  Kathie Jalbuena (7070) - 07/08/87
	       DESC:

	---- Revision History ------------------------------------------------

        31/01/06 - Srinivar -CR 19137
        	   Cleaned up the warnings.
        	   Modified the code to confirm PSL standards.
        
        07/26/05 - SkariahV- CR16679
	           Removed #WARN directive.
	              
	01/14/04 - RussellDS - CR 7514
		   Eliminated use of table LNRAMT1 (obsolet) and replace with
		   local array.
	  
	12/15/03 - RussellDS - 7514
	  	   Eliminated use of table XREFLNTRS, which was simply a table
	  	   representation of index LNTRS on TRTYPE and has been
	  	   obsoleted.  Replaced with direct use of table TRTYPE.

	10/04/02 - CHHABRIAS - 49451
	           Converted to PSL.

	---------------------------------------------------------------------
	*/
	
	do INIT 
	
	quit

INIT 	//

	type Public String ER

	type Number %PAGE,%PG,RECCNT,TCNT
	type String LNRAMT()

	set RECCNT=0
	set %PG=0 
	set %PAGE=1 
	set ER=0
	set %ProcessMode=1

	do VPG(.LNRAMT())
	quit


VPG(String LNRAMT()) // Page control

	type Public Number %PG
	type Public String ER

	type Boolean FINISH
	type String FRE,VFMQ

	set FINISH=0
	for  do { quit:FINISH
		if ER do ERR set FINISH=1 quit
		if %PG=0 do VPG00(.LNRAMT()) if VFMQ="Q" set FINISH=1 quit
 		if %PG>0 do VPG01(.LNRAMT())
		if "DFQ"[VFMQ do VER(.LNRAMT()) set FINISH=1 quit
		set %PG=%PG+1
 		}
	quit


VPG00(String LNRAMT())	 // Set up

	type public Boolean ER
	type public Date RDT1
	type public Number RECCNT
	type public String PAYEE,%READ,%TAB,TRTYPE,VFMQ
	
	type Date RDT

	set %TAB("TRTYPE")=".TRTYPE1/HLP=[DEP]TRTYPE/TBL=[LNTRS]"
	set %TAB("PAYEE")=".PAYEE1/HLP=[DEP]PAYID/TBL=[LNTRS1]PAYID:QU ""[LNTRS1]TRTYPE=<<TRTYPE>>"""
	set %TAB("RDT1")=".RDT11/HLP=[EABE]RDT/XPR=D PRP^LNRADE"
	set %READ="@@%FN,,,TRTYPE/REQ,PAYEE/REQ,RDT1/REQ"

	do ^UTLREAD 

	if VFMQ="Q" quit

	do POP
	do ARRAY(.LNRAMT())

	set RDT=RDT1.toString()

	if 'RECCNT set VFMQ="Q",ER=1
	
	quit


VPG01(String LNRAMT()) // Screen

	type Public Number PCNT,%PG,%REPEAT,TCNT
	type Public String RM,TRTYPE,VFMQ
	
	type Number I,%MODS
	
	kill RM
	
	set PCNT=%PG
	set %REPEAT=7 
	set %MODS=(PCNT*7)-6
	
	if %REPEAT+%MODS>TCNT set %REPEAT=TCNT-%MODS+1
	
	// for I=1:1:%REPEAT if '$D(LNRAMT(I)) set LNRAMT(I)=""
	
	type RecordLNTRS fLNTRS=Db.getRecord("LNTRS","TRTYPE=:TRTYPE")

	do DRV^USID(%ProcessMode,"LNRAMTM",.fLNTRS)

	if VFMQ="Q" do VER(.LNRAMT())

	quit


ERR	//

	type Public String ER

	do ^UTLERR 

	set ER=0
	
	quit


VER(String LNRAMT()) //

	type Public String VFMQ

	if VFMQ="Q" do END quit
	
	do FILE(.LNRAMT())
	do END

	quit


FILE(String LNRAMT()) 	// File data

	
	type public Date RDT1
	
	type Number AMT,ECID,ESC,LCID,N
	type String EA,ESCT,PAYEE
	
	set N=""
	for  set N=LNRAMT(N).order() quit:N.isNull()  do {
	
		set LCID=$P(LNRAMT(N),"|",2)
		set PAYEE=$P(LNRAMT(N),"|",3)
		set AMT=$P(LNRAMT(N),"|",4)
		set EA=$P(LNRAMT(N),"|",5)
		set ESC=$P(LNRAMT(N),"|",6)
		set ESCT=$P(LNRAMT(N),"|",7)
		
		// Only update if changes
		if (AMT'=$P(LNRAMT(N),"|",8))!(EA'=$P(LNRAMT(N),"|",9)) do {
		
			type RecordLNBIL0 lnbil0=Db.getRecord("LNBIL0","CID=:LCID")

			set ECID=$$ECID^LNU(LCID,ESC,.lnbil0)

			// RAMT was not changed for this date for this escrow.
			type RecordRAMT ramt=Db.getRecord("RAMT","CID=:ECID,TYP=:ESCT,REMDT=:RDT1",1)
			if AMT=ramt.remamt,EA=ramt.estflg quit

			set ramt.remamt=AMT 
			set ramt.estflg=EA
		
			do ramt.save()
			}
		}
	quit


END	//

	type Public String ER,PAYEE,RM,VFMQ

	quit:ER

	// Payee ~p1 not modified
	if VFMQ="Q" set RM=$$^MSG("2153",$G(PAYEE))
	// Payee ~p1 modified
	else  set RM=$$^MSG("2152",$G(PAYEE))

	set ER="W"

	quit


PRP	// pre-processor for date

	type Public Date RDT1
	type Public String %MSKD,PAYID,RM,TRTYPE,UTBL(),PAYEE

	type RecordLNTRS1 lntrs1=Db.getRecord("LNTRS1","TRTYPE=:TRTYPE,PAYID=:PAYEE",1)
	
	set UTBL("LNTRS")=lntrs1.remnd_"|"_lntrs1.cremfre

	// ~p1|~p2
	set RDT1=$P(UTBL("LNTRS"),"|",1)

	set RM=$$DAT^%ZM(RDT1,$G(%MSKD))_"|"_3

	quit


POP	// post processor for date

	type Public Date RDT1,JD
	type Public String ER,ET,UTBL()
	
	type Date NJD
	type Number FG,I,LS
	type String FRE

	if +RDT1=(+$P(UTBL("LNTRS"),"|",1)) quit

	set FRE=UTBL("LNTRS").piece("|",2)
	set LS=0
	if RDT1<%SystemDate do {
		set FRE="-"_FRE 
		set LS=1
		}
	set FG=0 
	set NJD=%SystemDate
	for I=1:1:1 do { 
		if FG=1 quit
		set JD=NJD 
		set NJD=$$NJD^UFRE(JD,FRE) 

		if NJD=RDT1 quit
		if (LS&(NJD<RDT1)) set FG=1
		if ('LS&(NJD>RDT1)) set FG=1
		}

	if FG do {
		set ER=1 
		set ET="LNRADE"
		}

	quit


ARRAY(String LNRAMT())	// CREATE LNRAMT ARRAY

	type public Date RDT1
	type Public Number %PAGE,PCNT,%PG,RECCNT,TCNT
	type Public String PAYEE,TRTYPE

	type Date REMND
	type Number CID,CNT,ESC,LCID,RAMT
	type String ACT,ELE

	set CNT=1

	type DbSet rs=Db.selectDbSet("TRTYPE","TRTYPE=:TRTYPE AND PAYID=:PAYEE","TYP,CID")
	while rs.next() do {

		type RecordTRTYPE trtype=rs.getRecord("TRTYPE")
		set CID=trtype.cid
		set ELE=trtype.typ
		type RecordLN ln=Db.getRecord("LN","CID=:CID",1)
		quit:ln.stat=4

		set REMND=trtype.remnd
		if REMND'=RDT1 quit

		type RecordDEP dep=Db.getRecord("DEP","CID=:CID",1)
		set LCID=dep.aref
		set ESC=dep.esc

		type RecordRAMT ramt=Db.getRecord("RAMT","CID=:CID,TYP=:ELE,REMDT=:RDT1",1)
		set RAMT=ramt.remamt
		set ACT=ramt.estflg
		
		// Save amount and action in pieces 8&9 to check for changes
		set LNRAMT(CNT)=CID_"|"_LCID_"|"_PAYEE_"|"_RAMT_"|"_ACT_"|"_ESC_"|"_ELE_"|"_RAMT_"|"_ACT

		set CNT=CNT+1
		set RECCNT=RECCNT+1
		}

	set TCNT=CNT-1
	set PCNT=0
	set %PAGE=((TCNT-1)\7)+1
	set %PG=%PG+1
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60303^24908^Srinivasan, Rajesh^5330"	// Signature - LTD^TIME^USER^SIZE
