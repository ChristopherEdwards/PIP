IRSBNOT		/*
	ORIG: swarnalp - 10/23/2005
	DESC: IRS Bad TIN ('B') Notice

	---- Comments --------------------------------------------------------

	---- Revision History ------------------------------------------------

	10/24/05 - SWARNALP - CR 17293
		   Converted into PSL. Removed tags HDR, INIT, LOAD, READ,
		   RMS, SCR1, SCR2 and old revision history.

	*/
	
 	type public Number ER
	type public String RM

	type Boolean FINISH=0
	type Number %PAGE,%PG,%ProcessMode,OLNTB
	type String VFMQ
	
	set %PG=1
	set %PAGE=1
	set %ProcessMode=0

	// Page control
	for  do { quit:FINISH
		if %PG=1 do VPG01 if ER!(VFMQ="Q") set FINISH=1 quit
		if "DFQ"[VFMQ do VER set FINISH=1 quit
		set %PG=%PG+1
	}
	quit

VPG01	// Set up
	type String %READ, %TAB
	
	set %TAB("IO")=".FILE3"
	set %READ="@@%FN,,,IO/REQ"
	do ^UTLREAD
	quit

ERR	//
	type public Number ER
	type public String VFMQ
	
	set ER=1
	do ^UTLERR
	set VFMQ="Q"
	quit

VER	//
	type public String VFMQ
	
	if VFMQ'="Q" do FILE
	do END
	quit

FILE	// File data
	type public Number ER,%ZTHALT,%ZTSEQ
	type public String IO,RM,VFMQ
	
	type Boolean DONE = 0
	type Number TOT()
	type String REC,X
	
	if $$NEW^%ZT
	
	type Number $ZT
	// Error trap
	catch vERROR {
		set %ZTHALT=0 do ZE^UTLERR
		set ER=1,RM="Processing error, #"_%ZTSEQ.get(),VFMQ="Q"
		do END
		quit
		}

	set (TOT(1),TOT(2),TOT(3),TOT(4),ER)=0
	
	kill RM

	do Db.fastDelete("IRSBNOT")

	// Open the RMS file
	set X=$$FILE^%ZOPEN(IO,"READ",1)
	if 'X set ER=1,RM=X.piece("|",2) quit

	// Read the records from file
	for  do { quit:ER!DONE
		
		type Number CID,ST
		type String READER
		
		set REC=$$^%ZREAD(IO,.READER)
		
		if +READER>1 set ER=1,RM=READER.piece("|",2) set DONE = 1 quit
		// end of file
		if +READER=1 do REND set DONE=1 quit

		// Formatting
		if REC.extract()="A" do RECH quit                 // header
		if REC.extract()="C" do REND set DONE = 1 quit    // trailer

		set CID=+(REC.extract(19,38)) if 'CID quit
		
		set ST=REC.extract(11)

		// File record
		set TOT(ST)=TOT(ST).get()+1              // total number of accts by stat
		
		type RecordIRSBNOT irsbnot=Db.getRecord("IRSBNOT","CID=:CID",1)
		
		set irsbnot.tin=REC.extract(2,10)
		set irsbnot.stat=ST
		set irsbnot.tcc=REC.extract(12,16)
		set irsbnot.doc=REC.extract(17,18)
		set irsbnot.nam=REC.extract(39,78)
		set irsbnot.nam2=REC.extract(79,118)
		set irsbnot.ad1=REC.extract(119,158)
		set irsbnot.city=REC.extract(159,188)
		set irsbnot.st=REC.extract(189,190)
		set irsbnot.lzip=REC.extract(191,195)
		set irsbnot.seq=REC.extract(201,208)
		
		do irsbnot.bypassSave()
		}
	quit

RECH	// header
	type public String AKEY,NDOC,PEIN,PNAM,REC,SRV(),SRVC,TYR

	set PNAM=REC.extract(28,67)
	set PEIN=REC.extract(19,27)
	set NDOC=REC.extract(68,75)
	set SRVC=REC.extract(17,18)
	set AKEY=REC.extract(2,16)
	set TYR=REC.extract(125,128)

	// initalize service center descriptions
	set SRV(8)="Andover"
	set SRV(7)="Altanta"
	set SRV(18)="Austin"
	set SRV(19)="Brookhaven"
	set SRV(17)="Cincinnati"
	set SRV(89)="Fresno"
	set SRV(9)="Kansas City"
	set SRV(49)="Memphis"
	set SRV(29)="Ogden"
	set SRV(28)="Philadelphia"
	quit

REND	// Finish up
	type public Number TOT()
	type public String AKEY,NDOC,PEIN,PNAM,SRV(),SRVC,TYR
	
	use 0
	write #,*27,*91,*63,*51,*108
	write !
	write !,"                 Payer's Name: ",PNAM.get()
	write !,"          Payer's Federal EIN: ",PEIN.get()
	write !,"          Number of Documents: ",NDOC.get()
	write !,"               Service Center: ",SRVC.get()," - ",SRV(+SRVC.get()).get()
	write !,"                   Access Key: ",AKEY.get()
	write !,"                     Tax Year: ",TYR.get()
	write !!!,"             Total Number of Accounts"
	write !,"             ------------------------",!
	write !,"          Missing TIN w/o BWH: ",{String}TOT(1).justify(5)
	write !,"         Missing TIN with BWH: ",{String}TOT(2).justify(5)
	write !,"        Incorrect TIN w/o BWH: ",{String}TOT(3).justify(5)
	write !,"       Incorrect TIN with BWH: ",{String}TOT(4).justify(5)
	
	quit
	
END	//
	type public String ER,IO,RM,VFMQ
	
	if IO.exists() close IO
	quit:ER

	if VFMQ="Q" set RM="IRS invalid tax ID file not loaded"
	else  set RM="IRS invalid tax ID file loaded"
	set ER="W"
	quit

vSIG()	quit "60269^19952^P.R. Swarnalatha^4012"	// Signature - LTD^TIME^USER^SIZE
