public TTXGLA(Date XJD)	// Create SYSBAL2 from TPCTRL,TPETC & TPPMTOT
	/*
	ORIG:  Frank R. Sanchez (2497) - 01/28/86
	
	DESC: Utility to accumulate level 2 of SYSBAL2 for comparison with
	      daily transaction totals in level 1 of SYSBAL (in RECONDF).
	      
	      Called by RECONDF and TGLMON.
	
	-----Revision History-------------------------------------------------
	
	01/13/05 - RussellDS - CR13817
		   Clean up code, correct PSL errors.
		   
		   Remove old revision history.   
	----------------------------------------------------------------------
	*/
	
	/*
	Build level 2 of SYSBAL2
	
	Pass through the teller proof file and, for each branch and teller
	having a teller proof on today's date, summarize teller activity
	into SYSBAL.
	
	SYSBAL(XJD,2,CRCD,ETC)=cnt1|amt1|cnt2|amt2|cnt3|amt3
	
	cnt1,amt1 = Prior day PM count/amount onto today
	cnt2,amt2 = Today's total count/amount
	cnt3,amt3 = Today's PM count/amount
	*/

	type public Boolean ER = 0

	type Date JD
	type String BRCD, CRCD, ETC, SYSBAL(,), UID
	
	set SYSBAL(XJD, 2) = ""

	lock +SYSBAL(XJD,2):2 
	else  do { quit
		
		type String ET = "RECLOC"
		
		set ER = 1
		do ^UTLERR
	}

	do Db.fastDelete("SYSBAL2", "XJD")	
	
	if 'XJD.isNull() do CATCHUP^TTXSUM(XJD)
	
	type DbSet ds = Db.selectDbSet("TPCTRL", "TPD=:XJD")

	while ds.next() do {	// Prior days PMed onto today and today's non-PMed activity
		
		type Date PM

		type RecordTPCTRL tpctrl = ds.getRecord("TPCTRL")
		
		set BRCD = tpctrl.brcd
		set UID =  tpctrl.uid

		type ResultSet rs2 = Db.select("CRCD,ETC", "TPETC", "BRCD=:BRCD AND UID=:UID AND TPD=:XJD")

		while rs2.next() do {
			
			set CRCD = rs2.getCol("CRCD")
			set ETC = rs2.getCol("ETC")
			
			type RecordTPETC tpetc = Db.getRecord("TPETC", "BRCD=:BRCD,UID=:UID,TPD=:XJD,CRCD=:CRCD,ETC=:ETC", 1)
			
			type RecordSYSBAL2 sysbal2 = Db.getRecord("SYSBAL2","TDATE=:XJD,CRCD=:CRCD,ETC=:ETC", 1)

			set sysbal2.ppmcount = sysbal2.ppmcount + tpetc.ppmcount
			set sysbal2.ppmamt = sysbal2.ppmamt + tpetc.ppmamt
			set sysbal2.tcount = sysbal2.tcount + tpetc.count
			set sysbal2.tamt = sysbal2.tamt + tpetc.total
 
			do sysbal2.save()
		}
	
		// Accumulate activity PMed from today

		set PM = tpctrl.pmdate 
		quit:(+PM = 0) 
		
		type ResultSet rs3 = Db.select("CRCD,ETC", "TPETC", "BRCD=:BRCD AND UID=:UID AND TPD=:PM")

		while rs3.next() do {
			set CRCD=rs3.getCol("CRCD")
			set ETC=rs3.getCol("ETC")
	
			type RecordTPPMTOT tppmtot = Db.getRecord("TPPMTOT", "BRCD=:BRCD,UID=:UID,TPD=:PM,CRCD=:CRCD,ETC=:ETC,CDATE=:XJD", 1)
						
			type RecordSYSBAL2 sysbal2 = Db.getRecord("SYSBAL2", "TDATE=:XJD,CRCD=:CRCD,ETC=:ETC", 1)
					
			set sysbal2.tpmcount = sysbal2.tpmcount + tppmtot.tcount
			set sysbal2.tpmamt = sysbal2.tpmamt + tppmtot.tamt			
			
			do sysbal2.save()
		}
	}
		
	lock -SYSBAL(XJD, 2)
	
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59934^74415^Dan Russell^2642"	// Signature - LTD^TIME^USER^SIZE
