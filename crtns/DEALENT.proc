DEALENT		/*
	DESC: Deal Accounting Entry Record Maintenance

	This procedure is used by the screen DEALENT. It is necessary for deal 
	accounting entry maintenance. It is called by a DQ function (DEALxx) 
	to create/modify/display/delete an accounting entry.  
	It prompts for the type of deal, the side (buy vs. sell), the offset 
	type, DEALENT screen files the record into DEALENT table.
	
	Returns:
	       . ER    Error indicator         
	               Returns ER=1 if an error, otherwise ER="W"
	
	       . RM    Return message         
	               Returns error message if ER=1, otherwise completion 
	               message
	
	---- Revision History ------------------------------------------------
	
	01/11/06 - KinI - 16664
		Converted to PSL.
		
 	----------------------------------------------------------------------
	*/

	quit
	 

public	NEW

	do INIT(0)

	quit

		
public	UPD	
	
	do INIT(1)
	
	quit	


public	DIS
	
	do INIT(2)
	
	quit
	
	
public	DEL

	do INIT(3)
	
	quit
	
	
INIT(Number %ProcessMode)

	type RecordDEALENT fDEALENT

	do VPG(.fDEALENT,%ProcessMode)
	
	quit
	
VPG(RecordDEALENT fDEALENT,	// DEALENT record
    Number %ProcessMode)	// Process Mode

	// Page control

	type public String ER

	type Boolean FINISH
	type Number %PAGE,%PG
	type String IO,OLNTB,VFMQ
	
	set %PG=0 
	set %PAGE=1
	set FINISH=0

	for  do { quit:FINISH 
		if %PG=0 do VPG00(.fDEALENT,%ProcessMode,.VFMQ) if ER!(VFMQ="Q") set FINISH=1 quit
                
		if %PG>0 do VPG01(.fDEALENT,%ProcessMode,.VFMQ) if ER!(VFMQ="Q") set FINISH=1 quit
	
		if "DFQ"[VFMQ do VER(.fDEALENT,%ProcessMode,VFMQ) set FINISH=1 quit

  		set %PG=%PG+1
		}
	quit	


VPG00(RecordDEALENT fDEALENT,	// DEALENT record
      Number %ProcessMode,	// Process Mode
      String VFMQ)	
      
	type public String ET
	type public Boolean ER
	type String BUYSELL,DEALTYPE,DEALENT(,,,),%NOPRMT,OFFTYPE,%READ,SEQ,%TAB()

	if %ProcessMode=0 do {
		set %TAB("DEALTYPE")="[DEALENT]DEALTYPE/TBL=[STBLDEALTYPE]"
		set %TAB("BUYSELL")="[DEALENT]BUYSELL/TBL=[STBLDEALBS]"
		set %TAB("OFFTYPE")="[DEALENT]OFFTYPE/TBL=[STBLDEALOFF]"
		set %TAB("SEQ")="[DEALENT]SEQ/XPP=D SEQPP^DEALENT(DEALTYPE,BUYSELL,OFFTYPE,X)"
		}
	else  do {
		set %TAB("DEALTYPE")="[DEALENT]DEALTYPE/TBL=[DEALENT]DEALTYPE:DISTINCT"
		set %TAB("BUYSELL")="[DEALENT]BUYSELL/TBL=[DEALENT]BUYSELL:DISTINCT:QU ""[DEALENT]DEALTYPE=<<DEALTYPE>>"""
		set %TAB("OFFTYPE")="[DEALENT]OFFTYPE/TBL=[DEALENT]OFFTYPE:DISTINCT:QU ""[DEALENT]DEALTYPE=<<DEALTYPE>> AND [DEALENT]BUYSELL=<<BUYSELL>>"""
		set %TAB("SEQ")="[DEALENT]SEQ/TBL=[DEALENT]"
		}

	set %TAB("IO")=$$IO^SCATAB($I)
	
	set %READ="@@%FN,,,DEALTYPE/REQ,BUYSELL/REQ,OFFTYPE/REQ,SEQ/REQ"
	if %ProcessMode=2 set %READ=%READ_",IO#1"

	set %NOPRMT="N"

	do ^UTLREAD

	if VFMQ="Q" quit
	
	set fDEALENT=Db.getRecord("DEALENT","DEALTYPE=:DEALTYPE,BUYSELL=:BUYSELL,OFFTYPE=:OFFTYPE,SEQ=:SEQ",1)	
	    	
	if %ProcessMode,'fDEALENT.getMode() set ET="RECNOF" do ERR quit
	if '%ProcessMode,fDEALENT.getMode() set ET="RECOF" do ERR quit

	// Record locked by another user
	if %ProcessMode,(%ProcessMode'=2) lock +DEALENT(DEALTYPE,BUYSELL,OFFTYPE,SEQ):2 else  set VFMQ="Q" do Runtime.setErrMSG("DEALENT",2333) quit:ER

	quit
	

VPG01(RecordDEALENT fDEALENT,	// DEALENT record
      Number %ProcessMode,	// Process Mode
      String VFMQ)
      
	type public Boolean ER
	type public String IO
	      
	if %ProcessMode=2,IO'=$I do OPEN^SCAIO if ER set VFMQ="Q" quit

	do fDEALENT.setAuditFlag(1)
 	      		
	do DRV^USID(%ProcessMode,"DEALENT",.fDEALENT)
	
	quit

	
ERR	

	type public String VFMQ
	type public Boolean ER = 1

	do DSPBP^UTLERR

	set VFMQ="Q"

	quit
	
	
VER(RecordDEALENT fDEALENT,	// DEALENT record
    Number %ProcessMode,	// Process Mode
    String VFMQ)
    
	type String BUYSELL,DEALTYPE,OFFTYPE
	type Number SEQ
	
	set DEALTYPE=fDEALENT.dealtype
	set BUYSELL=fDEALENT.buysell
	set OFFTYPE=fDEALENT.offtype
	set SEQ=fDEALENT.seq		

	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END(%ProcessMode,DEALTYPE,BUYSELL,OFFTYPE,SEQ,VFMQ) quit

	do FILE(.fDEALENT,%ProcessMode,DEALTYPE,BUYSELL,OFFTYPE,SEQ)
	
	do END(%ProcessMode,DEALTYPE,BUYSELL,OFFTYPE,SEQ,VFMQ) 
 	
	quit	
	
		
FILE(RecordDEALENT fDEALENT,	// DEALENT record	
     Number %ProcessMode,	// Process Mode
     String DEALTYPE,		// Type of Deal
     String BUYSELL,		// Bank Buying/Selling Indicator
     String OFFTYPE,		// Offset Type
     Number SEQ)		// Deal Sequence
     	     
	if %ProcessMode=3 do Db.delete("DEALENT","DEALTYPE=:DEALTYPE AND BUYSELL=:BUYSELL AND OFFTYPE=:OFFTYPE AND SEQ=:SEQ")

	do fDEALENT.save()
	
	quit
	

END(Number %ProcessMode,	// Process Mode	
    String DEALTYPE,		// Type of Deal
    String BUYSELL,		// Bank Buying/Selling Indicator
    String OFFTYPE,		// Offset Type
    Number SEQ,			// Deal Sequence
    String VFMQ)
    
	type public Boolean ER
	type public String DEALENT(,,,)

	if DEALTYPE.exists()&BUYSELL.exists()&OFFTYPE.exists()&SEQ.exists() lock -DEALENT(DEALTYPE,BUYSELL,OFFTYPE,SEQ)

	quit:ER.get()!(%ProcessMode=2)!(%ProcessMode=4)
	
        if VFMQ="Q" do {
		// Deal accounting entry not created
		if %ProcessMode=0 do Runtime.setErrMSG("DEALENT",7587) quit:ER
        
		// Deal accounting entry not modified
		if %ProcessMode=1 do Runtime.setErrMSG("DEALENT",7589) quit:ER
		
		// Deal accounting entry not deleted
		do Runtime.setErrMSG("DEALENT",7588) quit
        	}
        	
        else  do {
		// Deal accounting entry created
		if %ProcessMode=0 do Runtime.setErrMSG("DEALENT",7584) quit:ER

		// Deal accounting entry modified
		if %ProcessMode=1 do Runtime.setErrMSG("DEALENT",7586) quit:ER

		// Deal accounting entry deleted
		do Runtime.setErrMSG("DEALENT",7585)
        	}
        	
        set ER="W"
        
        quit      	

	
SEQPP(String DEALTYPE,		// Type of Deal
      String BUYSELL,		// Bank Buying/Selling Indicator
      String OFFTYPE,		// Offset Type
      Number X)			// Deal Sequence

	// Sequence Post-Processor
	// Verifies that this accounting entry has not already been created.

	type public Boolean ER

	type RecordDEALENT dealent=Db.getRecord("DEALENT","DEALTYPE=:DEALTYPE,BUYSELL=:BUYSELL,OFFTYPE=:OFFTYPE,SEQ=:X",1)	

	// Record already exists
	if dealent.getMode() do Runtime.setErrMSG("DEALENT",2327) quit:ER
	
	quit
	
	        

vSIG()	quit "60299^47196^Irina Kin^6008"	// Signature - LTD^TIME^USER^SIZE
