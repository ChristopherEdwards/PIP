STMSG	//;PBS - CIF - V3.5 - Statement Marketing Message

 /*
 ORIG:  Marty Ronky (3623) - 07/28/88

 ---- Revision History ------------------------------------------------
 
 01/28/05 - HILLANBRAND - 14046
	    Per code review change FINISH to be type Boolean and 
	    type String MSG to be declared type String MSG()
	    
 01/20/05 - HILLANBRAND - 14046
 	    Modifications throughout to correct problems found in
 	    DBI2 pre-system test verification for functions STM016,
 	    STM017, and STM018.
 
 06/02/03 - Allan Mattson - 51351
 	    Converted to PSL.

 */

	quit
	
 
UPD	// Update

	do INIT(1)
	quit 


INQ	// Inquiry

	do INIT(2)
	quit 


DEL	// Delete

	do INIT(3)
	quit 
	
	
INIT(%ProcessMode)

	type Public String VFMQ
	
	type Boolean FINISH
	type Number %PAGE,%PG
	type String MSG()

	type RecordSTMMSG msg
	type ResultSet msgdtl
	
	set %PG=0 
	set %PAGE=2

	set FINISH=0
	for  do { quit:FINISH

		do {
			if %PG=0 do VPG00 quit
			if %PG=1 do VPG01(.msg,.msgdtl) quit
			if %PG>1 do VPG02(.msg,.msgdtl,.MSG) quit
			}
			
		if "DFQ"[VFMQ do VER(.msg,.msgdtl,.MSG) set FINISH=1 quit
		set %PG=%PG+1
		}
	quit 

	
VPG00

	type Public String VFMQ

	type String %READ,%TAB()
	
	set %TAB("MSGID")=".MSG1/TBL=[STMMSG]:NOVAL"
	set %READ="@@%FN,,,MSGID/REQ"

	do ^UTLREAD if VFMQ="Q" quit
	quit


VPG01(RecordSTMMSG msg,ResultSet msgdtl)

	type Public Number MSGID
	type Public String VFMQ

	type Number MAXCNT,MAXLEN,OLNTB
	type String DES,%READ,%TAB()

	set msg=Db.getRecord("STMMSG","MSGID=:MSGID",1)
	do msg.setAuditFlag(1)

	set msgdtl=Db.select("SEQ,MSG","STMMSGD","MSGID=:MSGID")

	set DES=msg.des
	set MAXCNT=msg.maxcnt
	set MAXLEN=msg.maxlen
		
	set %TAB("MAXCNT")=".LIN1/MIN=1/MAX=20"
	set %TAB("MAXLEN")=".LEN1/MIN=1/MAX=80"
	set %TAB("DES")=".DESC2"
	set OLNTB=4000

	set %READ="MAXCNT/REQ,MAXLEN/REQ,DES/REQ"
	do ^UTLREAD if VFMQ="Q" quit
	
	if MAXCNT'=msg.maxcnt set msg.maxcnt=MAXCNT
	if MAXLEN'=msg.maxlen set msg.maxlen=MAXLEN
	if DES'=msg.des set msg.des=DES

	quit


VPG02(RecordSTMMSG msg,ResultSet msgdtl,String MSG())

	type Public String VFMQ

	type String %READ,%REPEAT
	type Number MAXCNT,MAXLEN,SEQ
	
	set MAXCNT=msg.maxcnt
	set MAXLEN=msg.maxlen
	
	while msgdtl.next() do {
		set SEQ=msgdtl.getCol(1)	// getCol("SEQ")
		set MSG=msgdtl.getCol(2)	// getCol("MSG")
		set MSG(SEQ)=MSG
		}
	
	if %ProcessMode>1 do {
		set %READ="MSG/TYP=T/LEN="_MAXLEN_"/REP="_MAXCNT
		set %REPEAT=MAXCNT
		do ^UTLREAD
		}
		
	else  for  quit:$$EDIT(.MSG,MAXCNT,MAXLEN)
	quit


EDIT(String MSG(),Number MAXCNT,Number MAXLEN)

	type Number CNT,ER,I
	type String RM

	set ER=0
	set RM=""
	
	do ^DBSWRITE("MSG",3,22,MAXCNT,"X",$$^MSG(5178,MAXLEN))

	set CNT=MSG("").order(-1)

	// Maximum number of lines is ~p1
	if CNT>MAXCNT set ER=1,RM=$$^MSG(1693,MAXCNT)

	// One (or more) lines exceeds maximum length
	else  for I=1:1:CNT set MSG=MSG(I).trim(1," ") if MSG.length()>MAXLEN set ER=1,RM=$$^MSG(1691,MAXLEN) quit

	if ER write $$MSG^%TRMVT(RM,0,1) quit 0
	quit 1


VER(RecordSTMMSG msg,ResultSet msgdtl,String MSG())

	type Public Number MSGID
	type Public String VFMQ
	
	if %ProcessMode=2!(VFMQ="Q") do END quit

	do Db.delete("STMMSGD","MSGID=:MSGID")
	if %ProcessMode=3 do { quit
		do Db.delete("STMMSG","MSGID=:MSGID")
		do END quit
		}

	type Number UPD=0
	if msg.isChanged("DES") set UPD=1
	else  if msg.isChanged("MAXCNT") set UPD=1
	else  if msg.isChanged("MAXLEN") set UPD=1
		
	if UPD do msg.save()
		
	type Number SEQ
	for SEQ=1:1 quit:'MSG(SEQ).data()  do {
		type RecordSTMMSGD stmmsgd=Class.new("RecordSTMMSGD")
		set stmmsgd.msgid=MSGID
		set stmmsgd.seq=SEQ

		set stmmsgd.msg=MSG(SEQ)
			
		do stmmsgd.save()
		}
	
	do END
	quit


END

	type Public Number MSGID
	type Public String ER,RM,VFMQ

	if ER!(%ProcessMode=2) quit

	if VFMQ="Q" do {
		if 'MSGID.data() set RM="" quit

		// Marketing message ~p1 not modified
		if %ProcessMode<2 set RM=$$^MSG(1675,MSGID) quit

		// Marketing message ~p1 not deleted
		set RM=$$^MSG(1674,MSGID)
		}

	else  do {
		// Marketing message ~p1 modified
		if %ProcessMode<2 set RM=$$^MSG(1673,MSGID) quit

		// Marketing message ~p1 deleted
		set RM=$$^MSG(1672,MSGID)
		}

	set ER="W"
	quit

vSIG()	quit "59928^60337^Laura Hillanbrand^3879"	// Signature - LTD^TIME^USER^SIZE
