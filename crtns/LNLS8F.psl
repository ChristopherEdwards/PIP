LNLS8F	// Calculate loan sale service fee amounts
	/*
	   ORIG:  Tom Locke (2424) - 05/31/87

	---------Revision History----------------------------------------------
	
	06/22/07 - Nagarjuna - CR25511
        	   Modified TEMPRT Section to assign 
        	   values to the variables which are used 
        	   in RATE^BCHLNLS8.
	
        07/26/05 - SkariahV- CR16679
	           Removed #WARN and #OPTIMIZE directives.
	              
	05/29/03 - GRAY - 51351
		   Converted to PSL.

	*/


	quit

Public CALC(Number PLRFR,
	Number SFCM,
	Number PLRIC,
	Number PP,
	Number PLRPB,
	Number REMAF,
	Number FEEP,
	Number IRN,
	Number YLD,
	Number OPTR,
	Number TSEQ)

	/*

	   B  = Previous Principal Balance
	   R  = Rate from Rate Hierarchy (Investor's Yield)
	   P  = Participation Percentage
	   I  = Interest Collected
	   IR = Account Interest Rate
	   F  = Flat Percentage

	*/

	if +SFCM=0 quit
	
	set PLRFR=0

	type Number BSEQ,CNT,FEE
	type String VAR
	type Public Number CID

	//  Not a reversal condition !!
	if TSEQ.get()="" do EXEC set PLRFR=FEE quit

	//  Do the following when doing reversal/ec (ie. TSEQ has a value)
	type DbSet ds=Db.selectDbSet("HISTSB","CID=:CID AND SLN=:TSEQ")
	while ds.next() do {
		type RecordHISTSB histsb=ds.getRecord("HISTSB")
		for CNT=1:1:5 do {
			set VAR="pe0"_CNT_"ea"
			if histsb.@VAR="I" do {
				set BSEQ="pe0"_CNT_"bseq"
				set BSEQ=histsb.@BSEQ

				set PLRIC="pe0"_CNT_"amt"
				set PLRIC=histsb.@PLRIC

				type RecordLNBIL1 lnbil1=Db.getRecord("LNBIL1","CID=:CID,SCHSEQ=:BSEQ",1)

				//  Quit if method is 3 or 5 and partial
				//  interest is found in bill record
				if SFCM=3!(SFCM=5) quit:$$PARINT(.lnbil1)

				//  determine rate to use (either bill or acct)

				if lnbil1.getMode() set IRN=$$GETIRN(.lnbil1,IRN)

				//  Calc fee
				do EXEC

				set PLRFR=PLRFR+FEE
				}
			}
		}

	quit



EXEC	//  Calc Fee

	type Public Number FEE
	type Public Number CID,SFCM

	set FEE=0
	do { set FEE=$$^SCARND(FEE,0,CID)
		if SFCM=1 do FEE1 quit
		if SFCM=2 do FEE2 quit
		if SFCM=3 do FEE3 quit
		if SFCM=4 do FEE4 quit
		if SFCM=5 do FEE5 quit
		if SFCM=6 do FEE6 quit
		}

	quit


FEE1	// I x F x P
	
	type Public Number FEE,FEEP,PLRIC,PP

	set FEE=PLRIC*FEEP*PP

	quit


FEE2	// (I x P) - (B x R x P) / AF

	type Public Number FEE,PLRIC,PLRPB,PP,REMAF

	set FEE=(PLRIC*PP)-(PLRPB*$$TEMPRT*PP/REMAF)

	quit


TEMPRT()	//

	type Public Number CID,PSEQ
	type Public String INCD,PL
	type Number FEEP,IRN,MINYLD,MAXYLD,OPTR,PASSTHRU,PRNDF,TEMPRATE,YLD
	type String PGM,PRNDM,XECUTE
	type Date TREXD
	type RecordLNLS2 lnls2 = Db.getRecord("LNLS2","INCD=:INCD,PL=:PL")
	set YLD=lnls2.yield/100
	set FEEP=lnls2.feep
	
	type RecordLNLS6 lnls6=Db.getRecord("LNLS6","CID,PSEQ")
	set OPTR=lnls6.optr/100
	set MINYLD=lnls6.minyld
	set MAXYLD=lnls6.maxyld
	set PRNDM=lnls6.prndm
	set PRNDF=lnls6.prndf
	set PASSTHRU=lnls6.passthru/100

	type RecordLN ln=Db.getRecord("LN","CID")
	set TREXD=ln.trexd
	set IRN=ln.irn/100

	set PGM=$$GET^UBCHID("BCHLNLS8") if PGM="" quit
	set XECUTE="set TEMPRATE=$$RATE^"_PGM_"()"
	
	#ACCEPT Date=05/29/03; PGM=Shaun Gray
	xecute XECUTE

	quit TEMPRATE


FEE3	// (B x F x P)/AF

	type Public Number FEE,FEEP,PLRPB,PP,REMAF

	set FEE=(PLRPB*FEEP*PP)/REMAF

	quit


FEE4	// (I x P) - (I x R x P) / IR

	type Public Number FEE,IRN,PLRIC,PP

	set FEE=(PLRIC*PP)-(PLRIC*$$TEMPRT*PP/IRN)

	quit


FEE5	// (B x IR x P)   (B x R x P)
	// ------------	  -----------
	//      AF            AF

	type Public Number FEE,IRN,PLRPB,PP,REMAF

	set FEE=(PLRPB*IRN*PP/REMAF)-(PLRPB*$$TEMPRT*PP/REMAF)

	quit


FEE6	// (I * P * F)/IR

	type Public Number FEE,FEEP,IRN,PLRIC,PP

	set FEE=PLRIC*PP*FEEP/IRN

	quit


PARINT(RecordLNBIL1 lnbil1)	//  Find partial interest amount in a bil record

	type Boolean parint=0
	type Number piece
	type String element

	for piece=1:1:20 do { quit:parint
		set element=$$GETFIELD^BILFUNCS(piece,.lnbil1)
		if element.piece("#",1)="I",element.piece("#",4) set parint=1
		}

	quit parint


GETIRN(RecordLNBIL1 lnbil1,Number currirn)	//  Determine if we should change rate for calc

	type Number newirn

	//  Get rate from bill
	set newirn=lnbil1.cirn
	set newirn=newirn/100

	//  If bill rate, return bill reate
	if newirn quit newirn

	//  Otherwise return current rate
	quit currirn
 #OPTION ResultClass ON
Public String vSIG()	quit "60803^26156^Nagarjuna Pesalaparthi Rajeswarareddy^4005"	// Signature - LTD^TIME^USER^SIZE
