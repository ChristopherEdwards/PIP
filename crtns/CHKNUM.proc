CHKNUM	//;PBS - CHK - V2.0 - PUT CHECK AMOUNT INTO WORDS
	/*
	       ORIG:  DSR
	  CALLED BY:  CHECK PRINT ROUTINES (^CUVAR("CHKPNT"))

	      CALLS:
	       DESC:  PUT CHECK AMOUNT INTO WORDS

	      INPUT:  CHKSIZ,N
	     OUTPUT:  EN,ENC

	  --------- Revision History --------------------------------------------
	   04/10/2002 - DATTAR - ARQ# 49794
			Converted routine to procedure
	  ----------------------------------------------------------------------

	*/

	if N<0 set ENC="" set EN=N_" ...NEGATIVE AMOUNT...INVALID" quit

	if '$D(CHKSIZ) set CHKSIZ=64
	set %CN=$$^SCARND((1000000000+N),0,"","",2,,1)
	set %CN=$E(%CN,2,13) 
	set (EN,ENC)=""
	set N=$$^SCARND(N,0,"","",2,,1)

	set %CZ=+$E(%CN,1,3) if %CZ>0 do WORDS set EN=EN_"MILLION "

	set %CZ=+$E(%CN,4,6) if %CZ>0 do WORDS set EN=EN_"THOUSAND "

	set %CZ=+$E(%CN,7,9) if %CZ>0 do WORDS
	
	if EN="" set EN="ZERO "
	set EN=EN_"AND "_$P(N,".",2)_"/100 DOLLARS"

	if $L(EN)'>CHKSIZ do EXIT quit
	for %CX=0:1 set %CZ=$F(EN," ",CHKSIZ-%CX) if +%CZ,%CZ-2'>CHKSIZ quit
	set ENC=$E(EN,%CZ,999) set EN=$E(EN,1,%CZ-2)


EXIT	//
	kill CHKSIZ,%CX,%CZ,%CN 
	quit 


WORDS	//
	if %CZ>99 set %CX=$E(%CZ,1) do ONES set EN=EN_"HUNDRED " set %CZ=+$E(%CZ,2,3) if %CZ=0 quit

	if %CZ<10 set %CX=%CZ do ONES quit

	if %CZ<20 set %CX=%CZ do TEENS quit

	set %CX=+$E(%CZ,1) do TENS set %CX=$E(%CZ,2) if %CX>0 do ONES

	quit


ONES	//
	set EN=EN_$P("ONE/TWO/THREE/FOUR/FIVE/SIX/SEVEN/EIGHT/NINE","/",%CX)_" " 
	quit 

TEENS	//
	set EN=EN_$P("TEN/ELEVEN/TWELVE/THIRTEEN/FOURTEEN/FIFTEEN/SIXTEEN/SEVENTEEN/EIGHTEEN/NINETEEN","/",%CX-9)_" " 
	quit 

TENS	//
	set EN=EN_$P("TWENTY/THIRTY/FORTY/FIFTY/SIXTY/SEVENTY/EIGHTY/NINETY","/",%CX-1)_" " 
	quit

	

vSIG()	quit "60025^38898^Balaji Gopalkrishnan^1563"	// Signature - LTD^TIME^USER^SIZE
