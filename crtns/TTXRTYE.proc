TTXRTYE	//	Exception File Edit
	/*
	       ORIG: Marty Ronky - 3623

	  ---- Revision History ------------------------------------------------
	  
	  12/11/06 - RengaSP - 23345
	  	     Modified section VPG01 to avoid record not on file error 
	  	     by changing misspelled variable name in the getRecord 
	  	     method.
	
	  11/18/04 - JERUCHIMC - 13107
	             Converted to new PSL standards.
	
	  09/30/02 - SRIVASTAVAN - 49451
	  	     Converted To PSL		            
	  ---------------------------------------------------------------------
	*/

INIT 	//


	type String %PAGE
	type String %PG
	type String %ProcessMode
	
	type String BRCD
	type String UID
	type String VFMQ


	set %PG=0 
	set %PAGE=1 
	set %ProcessMode=1

	do ^SETRETRY 
	
	do PRE00

	type RecordEXC fEXC

        do VPG(.fEXC)

        quit


VPG(RecordEXC fEXC)	// Page control


	type public String %PG
	
	type String FINISH
	type String VFMQ
	
        set FINISH=0
        for  do { quit:FINISH
                if %PG=0 do VPG00 if VFMQ="Q" set FINISH=1 quit

                if %PG>0 do VPG01(.fEXC)

                if "DFQ"[VFMQ do VER(.fEXC) set FINISH=1 quit

		set %PG=%PG+1
                }
        quit


VPG00	// Set up


	type public String %SystemDate
	type public String OLNTB
	type public String SEQ
	type public String TPD	
	
	type String %NOPRMT
	type String %READ
	type String %TAB
	
	
	kill OLNTB,SEQ

 	set %TAB("TPD")=".TJD4/TBL=DAT(" 
 	set TPD=%SystemDate-1
	set %TAB("BRCD")=".BRCD1/TBL=DAT(TPD,"
	set %TAB("UID")=".UID1/TBL=DAT(TPD,BRCD,"
	set %TAB("TSEQ")=".SEQ2/TBL=[EXC]TSEQ:QU ""[EXC]TJD=<<TPD>> AND [EXC]BRCD=<<BRCD>> AND [EXC]UID=<<UID>>""" 

	set %READ="@@%FN,,,TPD/REQ,BRCD/REQ,UID/REQ,TSEQ/REQ" 
	set %NOPRMT="N"
	
	do ^UTLREAD 
	
	quit


VPG01(RecordEXC fEXC)	// Exception sequence screen

       /*  
	   Earlier Screen EXCRTY call this section to load the repeat 
	   field region with the different values for each override 
	   encountered for the transaction entered by the user.
 
           INPUTS:
               . TPD,BRCD,UID,TSEQ
 
           RETURNS:
               . OVR array with 4 pieces of data:
                 CID | "OVR" or "RFLG" | override | override description
        */
 	
	type public String %ProcessMode
	type public String BRCD
	type public String OVR
	type public String TJD
	type public String TPD
	type public String TSEQ
	type public String UID

	type String %REPEAT	
 	type String DESC
 	type String GRP
 	type String D
 	type String X
 	type String Y
 	type String Z

	set (D,X,Y,Z,%REPEAT)=""
	
	type DbSet rs=Db.selectDbSet("EXC1","TJD=:TPD AND BRCD=:BRCD AND UID=:UID AND TSEQ=:TSEQ")
	while rs.next() do {
        	type RecordEXC1 exc1=rs.getRecord("EXC1")
                set X = exc1.cid
                set Y = exc1.ovrflg
                set Z = exc1.ovr
                set D = exc1.amt
		type RecordACN acn=Db.getRecord("ACN","CID=:X")
                set GRP=acn.grp
                if Y="OVR" do {
                        type RecordUTBLOVR utblovr=Db.getRecord("UTBLOVR","GRP=:GRP,OVR=:Z")
                        set DESC=utblovr.desc
                        }
                if Y="RFLG" do {
                        type RecordUTBLRFLG utblrflg=Db.getRecord("UTBLRFLG","GRP=:GRP,RFLG=:Z")
                        set DESC=utblrflg.desc
                        }
                if DESC["<" set:DESC["$" D=$$^SCARND(D,0,X) set DESC=$P(DESC,"<",1)_D
                
                set %REPEAT=%REPEAT+1
                
                set OVR(%REPEAT)=X_"|"_Y_"|"_Z_"|"_DESC
                }


	set fEXC=Db.getRecord("EXC","TJD=:TPD,BRCD=:BRCD,UID=:UID,TSEQ=:TSEQ")

	do DRV^USID(%ProcessMode,"EXCRTY",.fEXC) 
	
	quit


PRE00	// Pre-processor

	
	type public String %SystemDate
	type public String DAT
	
	type String BC
	type String U
	
	kill DAT 
	
	set (BC,U)=""
	
	set DAT=$$PRBD^UNBD(%SystemDate,1)

	type DbSet rs=Db.selectDbSet("DAYENDRTY","TJD>:DAT") 

	while rs.next() do {
		type RecordDAYENDRTY dayend=rs.getRecord("DAYENDRTY")
		set BC=dayend.brcd
		set U=dayend.buid
		if dayend.stat'["*" set DAT(dayend.ljd,BC,U)=""
		}

	quit


VER(RecordEXC fEXC) 	//


	type public String VFMQ
	
	if VFMQ="Q" quit
	
	do FILE(.fEXC)
	
	quit


FILE(RecordEXC fEXC) // File data

	type public String CID
	
	set CID=fEXC.cid 
	
	type RecordACN acn=Db.getRecord("ACN","CID=:CID",1)
	
	if acn.getMode() = 0 do { quit
		// Invalid account.  Cannot retry.
		do Runtime.setErrMSG("ACN",1263)
		}
	do fEXC.save()

	quit

vSIG()	quit "60612^9099^Renga SP^4236"	// Signature - LTD^TIME^USER^SIZE
