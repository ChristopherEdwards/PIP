UCDC	//	CHECK DIGIT VERIFICATION
	/*
	       ORIG:  Frank R. Sanchez
	  CALLED BY:
	      CALLS:
	   PROJ #'S:  PHA 150, ACT 162, ACT 005
	       DESC:

   	;---- Revision History ------------------------------------------------
	   04/10/02 - CHHABRIAS - 49794
                      Converted to PSL


	   Weighting (WT) is the factor to multiply each position in
	   the record by to determine the sum

	   The starting position (from left to right) of the record to use
	   when calculating the check digit is determined by the value SP.

	    Ex:  Record (X) 123456 with a weight (WT) of 121212 yields a
	         sum of 1*1+(2*2)+(1*3)+(2*4)+(1*5)+(2*6) = 33, assuming
	         a starting position of 1.

	    Modulo divide (MOD) produces a single character check digit (CD)

	    Ex:  Record (X) 123456 * Weight (WT) 121212 = 33 (SUM)
	         MOD-(SUM#MOD)=CD
	         10-(33#10)=7

	    POS = Position in (X) that contains check number (from right to left)
	    Weighting for Route & Transit = 37137137 , POS = 1

	   GLOBALS -
	       READ:
	        SET:

	      INPUT:  CDM
	     OUTPUT:  CD,X
	     

	*/

NEW	// New account
	do DFT set X=+$E(X,1,$L(X)-POS+1)_CD_$E(X,$L(X)-POS+2,99)

	quit

DFT	// Default parameters

	new MOD,MSK,SO,SP,V,WT,Y,Z

	// Check digit calc method
	type RecordUTBLCDM utblcdm=Db.getRecord("UTBLCDM","KEY=:CDM")
	set WT=+utblcdm.wt
        set MOD=+utblcdm.mod
        set POS=+utblcdm.pos
        set SO=+utblcdm.so
        set SP=+utblcdm.sp
        set MSK=+utblcdm.msk

	if 'WT set WT=371371371371
	if 'MOD set MOD=10
	if 'POS set POS=1
	if 'SP set SP=1

CALC	// Calculate check digit

	// Add account mask prior to calculation
	set V=MSK+$E(X,SP,99)

	// Truncate weighting factor
	set WT=$E(WT,$L(WT)-$L(V)+1,$L(WT))
	set Z=0 for I=$L(V):-1:1 set Y=$E(V,I)*$E(WT,I) do:SO SO set Z=Z+Y
	set CD=MOD-(Z#MOD) if CD=MOD set CD=0
	quit

SO	// Summation option
	if SO=1,Y>9 set Y=$E(Y)+$E(Y,2) do SO
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60025^43673^Pat Kelly^1835"	// Signature - LTD^TIME^USER^SIZE
