IRSC99D		/*
	ORIG: Vince Arpa 1/23/98
	DESC: IRS Processing - Form 1099-DIV Correction
	I18N=QUIT: Excluded from I18N standards.

	---- Comments --------------------------------------------------------
	======================================================================
		*** THIS ROUTINE IS TO BE COMPILED WITH IRSTPMTR ***
		     *** DO NOT RUN THIS ROUTINE STANDALONE ***
	======================================================================
	
	---- Revision History ------------------------------------------------
	
	05/15/06 - DESHPANDE S K - CR 21167
		   Modified CIFAD section to set address variables to mailing 
		   address instead of permanent address.
	
	05/08/06 - TITOVE - CR 21140
		   Modified BRECBLD to set "C" corrected return indicator
		   for a second step of a two-step correction.

	09/28/05 - HAILEYM - CR17143
		Converted to PSL.
		
	*/
	
	// Accept "Dead code" warning during runtime of @IRSTAPE.
	#ACCEPT Date=10/14/2005;PGM=Marie Hailey;CR=17143
	quit
	
	
MTRCNTRL	// Master control
	
	type public Boolean ER
	type public String EIN,Q()
	
	type Boolean BONDS,CIFCTOF,CORR
	type Number PASSFLG,REG
	type String INSTEIN,PAYERNAM
	
	set CIFCTOF=CUVAR.CTOF1099
	set REG=Q(1).data()
	set BONDS=0
	set CORR=1
	
	do Db.fastDelete("B1099D")
	do Db.fastDelete("REP099D","JOB=:%ProcessID")
	do ^FRM99DIV
	
	set INSTEIN=EIN.get()
	if INSTEIN="" set INSTEIN=CUVAR.EIN  
	set PAYERNAM=CUVAR.CNAME
	
	set PASSFLG=1
	do ARECBLD quit:ER	// Build and write the "A" record - First pass
	do BRECBLD quit:ER	// Build and write the "B" records - First pass
	do CRECBLD quit:ER	// Build and write the "C" record - First pass
	
	set PASSFLG=2
	do ARECBLD quit:ER	// Build and write the "A" record - Second pass
	do BRECBLD quit:ER	// Build and write the "B" records - Second pass
	do CRECBLD quit:ER	// Build and write the "C" record - Second pass
	
	do STORETOT		// Store the TTR report totals
	
	do Db.fastDelete("REP099D","JOB=:%ProcessID")
	quit

ARECBLD	//"A" record builder
	
	type public String AMTIND,FORMCODE,TAMT()
	
	set FORMCODE=1	// IRS form code
	set AMTIND="1A"	// IRS amount type indicator
	set TAMT(1).piece("|",2)="Total ordinary dividends"
	set TAMT("A").piece("|",2)="Federal Income Tax Withheld"
	do ARECWRT	// Write the record to tape
	quit
	
	
BRECBLD	// "B" record builder
	
	type public Number AMT(),FAMT(),FCNT,PASSFLG
	
	type Number ACN,CID,I,SEQ
	type String ADDR,CITY,CNTRY,CORRTN,DOCCODE,FORCNTY,FULLADDR,LNAME,NAME
	type String SPECIAL,STATE,TIN,TINTYPE,X,XTIN,XZIP,ZIP
	
	type DbSet ds
	type RecordB1099D b1099d
	type RecordREP099D rep099d
	
	for I=1:1:9,"A","B","C","D","E" set AMT(I)=0
	
	set DOCCODE="  "				 // Document specific code
	set CORRTN=$select(PASSFLG=1:"G",PASSFLG=2:"C")  // Corrected return indicator
	
	set b1099d=Class.new("RecordB1099D")
		
	set ds=Db.selectDbSet("REP099D","JOB=:%ProcessID")
	while ds.next() do {
		set rep099d=ds.getRecord("REP099D")
		
		if rep099d.corrflg=1,(PASSFLG=1) quit	// Skip duplicates when writting to tape
		if rep099d.corrflg=2,(PASSFLG=1) quit	// Skip new corrected taxid on first pass
		if rep099d.corrflg=4,(PASSFLG=1) quit	// Skip "New Entry" or correct original taxid for first pass
		if rep099d.corrflg=3,(PASSFLG=2) quit	// Skip "change amount" for secon^
		
		set TIN=rep099d.taxid
		set ACN=rep099d.acn
		set CID=rep099d.cid
		
		set TINTYPE=$select(TIN?2N1"-"7N:1,TIN?3N1"-"2N1"-"4N:2,1:" ")
		set XTIN=TIN.piece("|",1).translate(" ","-")
		
		if CID=0 do M1099AD(TIN)		// M1099 ADDRESS
		if CID do CIFAD(TIN,ACN)		// CIF ADDRESS
		
		set AMT(1)=rep099d.orddiv*100		// Total Ordinary Dividends
		set AMT("A")=rep099d.fedinc*100		// Federal Income Tax Withheld
		
		if rep099d.corrflg<>1 do BRECWRT	// Skip duplicates
		
		// Inserting "B" Records into B1099D File.
		if CID=0 set b1099d.key=XTIN,b1099d.bseq=SEQ
		if CID set b1099d.key=ACN,b1099d.bseq=CID
		
		if TINTYPE=2 set X=LNAME
		else  set X=NAME
		do NAMCNTRL^IRSTPMTR set b1099d.bnamcon=X
		
		if b1099d.getMode() do b1099d.setMode(0)		
		set b1099d.pdate=rep099d.cjd
		set b1099d.btintype=TINTYPE
		set b1099d.btin=XTIN
		set b1099d.bcid=CID
		set b1099d.bamt1=AMT(1)/100
		set b1099d.bamta=AMT("A")/100
		set b1099d.bname=NAME
		set b1099d.baddr1=ADDR.piece(" ",1)
		set b1099d.baddr2=ADDR.piece(" ",2)
		set b1099d.baddr3=ADDR.piece(" ",3)
		set b1099d.bcity=CITY
		set b1099d.bstate=STATE
		set b1099d.bzip=XZIP
		do b1099d.bypassSave()
		}	
	quit
		
		
CIFAD(String TIN,Number ACN)	// CIF ADDRESS

	type public String ADDR,CITY,CNTRY,FORCNTY,FULLADDR,LNAME,NAME,SPECIAL
	type public String STATE,XZIP,ZIP
	
	type RecordCIF cif
	
	set (ADDR,CITY,CNTRY,FULLADDR,LNAME,NAME,SPECIAL,STATE,ZIP)=""
	set FORCNTY=" "
	
	set cif=Db.getRecord("CIF","ACN=:ACN",1)
	if 'cif.getMode() quit
	
	set NAME=cif.nam
	set LNAME=cif.lnm
	set FULLADDR=cif.mcity_"|"_cif.mstate_"|"_cif.mzip_"|"_cif.mcntry_"|"_cif.mad1_"|"_cif.mad2_"|"_cif.mad3_"|"_cif.mad4_"|"_cif.mloc
	set ADDR=cif.mad1_" "_cif.mad2_" "_cif.mad3
	set CITY=cif.mcity
	set STATE=cif.mstate
	set ZIP=cif.mzip.piece("-",1)_cif.mzip.piece("-",2)
	set CNTRY=cif.mcntry
	set FORCNTY=$select(CNTRY="US":" ",1:1)	// foreign country indicator
	set XZIP=cif.mzip
	
	// Special data position 544-750
	//; Second TIN Notice(optional)|544
	set SPECIAL="".justify(1)
	//; Blank|545|546
	set SPECIAL=SPECIAL_"".justify(2)
	//; Foreign Country or U.S. Possession|547|586
	set SPECIAL=SPECIAL_"".justify(40)
	//; Blank|587|662
	set SPECIAL=SPECIAL_"".justify(76)
	//; Special data entries|663|722
	set SPECIAL=SPECIAL_"".justify(60)
	//; State Income Tax Withheld|723|734
	set SPECIAL=SPECIAL_"".justify(12,,"0")
	//; Local Income Tax Withheld|735|746
	set SPECIAL=SPECIAL_"".justify(12,,"0")
	//; Combined Federal/State Code|747|748
	set SPECIAL=SPECIAL_"".justify(2)
	//; Blank|749|750
	set SPECIAL=SPECIAL_"".justify(2)
	quit
	
	
M1099AD(String TIN)	// ADDRESS FOR 1099DIV BONDS

	type public Number SEQ
	type public String ADDR,CITY,CNTRY,FORCNTY,FULLADDR,LNAME,NAME,SPECIAL
	type public String STATE,XZIP,ZIP
	
	type DbSet ds
	type RecordM1099 m1099
	
	set (ADDR,CITY,CNTRY,FULLADDR,LNAME,NAME,SPECIAL,STATE,ZIP)=""
	set FORCNTY=" " 
	
	set ds=Db.selectDbSet("M1099","TAXID=:TIN","SEQ DESC")
	if ds.next() do {
		set m1099=ds.getRecord("M1099")
		
		set SEQ=m1099.seq
		set NAME=m1099.nam
		set LNAME=m1099.lnam
		set FULLADDR=m1099.ad1_"|"_m1099.ad2_"|"_m1099.ad3_"|"_m1099.city_"|"_m1099.state_"|"_m1099.mzip
		set ADDR=m1099.ad1_" "_m1099.ad2_" "_m1099.ad3
		set CITY=m1099.city
		set STATE=m1099.state
		set ZIP=m1099.mzip.piece("-",1)_m1099.mzip.piece("-",2)
		set CNTRY=m1099.cntry
		set FORCNTY=$select(CNTRY="US":" ",1:1)	// Foreign country indicator
		set XZIP=m1099.mzip
		}
	quit
	
	
CRECBLD	// "C" record builder
	
	do CRECWRT	// Write the record to tape
	quit


%STOPLOD	// Stop %ZRTNLOD from this point on down

	quit
	
	
TRECWRT		// Dummy line reference for GT.M

	quit
	

ARECWRT		// Dummy line reference for GT.M

	quit
	
	
BRECWRT		// Dummy line reference for GT.M

	quit
	
	
CRECWRT		// Dummy line reference for GT.M

	quit
	
	
STORETOT	// Dummy line reference for GT.M

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60403^47700^Shriram Deshpande^6832"	// Signature - LTD^TIME^USER^SIZE
