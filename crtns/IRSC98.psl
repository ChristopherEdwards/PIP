IRSC98		/*
	ORIG: titove - 11/02/2005
	DESC: IRS Processing - Form 1098 Correction

	---- Comments --------------------------------------------------------
			  THIS PROCEDURE IS TO BE COMPILED
			      DO NOT RUN STANDALONE

	I18N=QUIT: Excluded from I18N standards.

	---- Revision History ------------------------------------------------

	05/15/06 - DESHPANDE S K - 21167
		   Modified BRECBLD section to set address variables to mailing 
		   address instead of permanent address.
	
	05/08/06 - TITOVE - CR 21140
		   Modified BRECBLD to set "C" corrected return indicator
		   for a second step of a two-step correction.

	04/07/06 - Hillanbrand - CR 20547
                   Modified the MTRCNTRL section. Declared String INSTADDR, 
                   INSTEIN, PAYERNAM as public to correct undefined since 
                   these variables are defined in IRSTPMTR and only should 
                   be redefined in this section if regions are used.

	04/06/06 - SWARNALP - CR 20388
		   Modified BRECBLD section to remove the parameter %SystemDate 
		   from the extrinsic function PNTSPY^LNCO since the signature of 
		   the same is changed to remove TJD.

	04/03/06 - TITOVE - CR 20556
		   Added DISTINCT to select statement from TMPRPT3 to prevent
		   processing same records multiple times. Added TJD to
		   select in SORT section to account for multiple dates.


	11/02/05 - TITOVE - CR 17287
		   Converted into PSL. Replaced call to BRREG^DEPDI with code
		   and removed old revision history. Deleted tags C2, CID, 
		   CNTRL2, CNTRL3, JD, SJD and SCID.

	*/

        // Accept "Dead code" warning during runtime of @IRSTAPE
        #ACCEPT Date=11/01/2005;PGM=Eugene Titov;CR=17287
        quit


MTRCNTRL	// Master control

	type public Number ER
	type public String INSTADDR, INSTEIN, PAYERNAM, Q(,)
	
	type Number PASSFLG, REG
	type String BRREG, REGION()

	set REG = Q(1).data()
	
	do Db.delete("B1098")
	
	do SORT
	
	type ResultSet rs = Db.select("DISTINCT KEY1","TMPRPT3","PID=:%ProcessID")
	
	while rs.next() do { quit:ER
		
		set BRREG = rs.getCol("KEY1")

		if 'REGION(BRREG).piece("|",2,9).isNull() do {
		
			set INSTEIN = REGION(BRREG).piece("|",2)
			set PAYERNAM = REGION(BRREG).piece("|",1)
			set INSTADDR = REGION(BRREG).piece("|",3,5)_"|"_REGION(BRREG).piece("|",7,9)
			}

		set PASSFLG = 1
		
		do ARECBLD quit:ER	// Build and write the "A" record - First pass
		do BRECBLD quit:ER	// Build and write the "B" records - First pass
		do CRECBLD quit:ER	// Build and write the "C" record - First pass
	
		set PASSFLG = 2
		
		do ARECBLD quit:ER	// Build and write the "A" record - Second pass
		do BRECBLD quit:ER	// Build and write the "B" records - Second pass
		do CRECBLD quit:ER	// Build and write the "C" record - Second pass
		
		do STORETOT		// Store the TTR report totals
		}
	
	do Db.delete("TMPRPT3","PID=:%ProcessID")
	
	quit


ARECBLD	// "A" record builder

	type public String TAMT()

	type Number AMTIND, FORMCODE

	set FORMCODE = 3	// IRS form code
	
	set AMTIND = 123	// IRS amount type indicator
	
	set TAMT(1).piece("|",2) = "Mortgage Interest Received"
	set TAMT(2).piece("|",2) = "Points paid prior year"
	set TAMT(3).piece("|",2) = "Refund of overpaid interest"
	
	do ARECWRT		// Write the record to tape
	
	quit
	
	
BRECBLD	// "B" record builder

	type public Number AMT(), PASSFLG
	type public String BRREG
	
	type Number ACN, BSEQ, CID, IPY
	type String ADDR, CITY, CNTRY, CORRTN, DOCCODE, FORCNTY, FULLADDR, LNAME
	type String  NAMCON, NAME, SIN, SPECIAL, STATE, TIN, TINTYPE, X, ZIP

	type DbSet ds = Db.selectDbSet("TMPRPT3","PID=:%ProcessID AND KEY1=:BRREG")
	
	while ds.next() do {
		
		type RecordTMPRPT3 tmp3 = ds.getRecord("TMPRPT3")
		
		set CID = tmp3.key3

		type RecordIRSUPD irsupd = Db.getRecord("IRSUPD","TJD=:tmp3.key2,FTYPE=5,MISCSEQ=:CID,BSEQ=0")
	
		// Skip duplicates when writting to tape
		if (irsupd.ccode = 1),(PASSFLG = 1) quit
		// Skip "New Entry" for first pass
		if (irsupd.ccode = 4),(PASSFLG = 1) quit
		// Skip "change amount" for second pass
		if (irsupd.ccode = 3),(PASSFLG = 2) quit
		
		type RecordLN ln = Db.getRecord("LN", "CID = :CID", 1)

		if (ln.irsexm = 1) quit		// Account exempt from 1098 reporting

		set IPY = $select('ln.iplcf:ln.ipty,1:ln.ipty+ln.penpty)

		set ACN = $select(PASSFLG=1:irsupd.ocif,PASSFLG=2:ln.acn) // Original/New CIF number

		type RecordCIF cif = Db.getRecord("CIF", "ACN = :ACN", 1)

		set TIN = $select(PASSFLG=1:irsupd.otin,PASSFLG=2:cif.taxid) // Tax ID number

		set TINTYPE = $select(TIN?2N1"-"7N:1,TIN?3N1"-"2N1"-"4N:2,1:" ")  // Type of TIN

		// The rest of AMT() entries are initialized at the top of template routine
		// Mortgage Interest Received
		set AMT(1) = $select((PASSFLG=1)&(irsupd.ccode=2):0,1:IPY*100)
		set AMT(2) = $$PNTSPY^LNCO(.ln)*100	// Points Paid - Prior Tax year
		set AMT(3) = ln.prefint*100		// Refund of overpaid interest

		set DOCCODE = "  "
		set CORRTN = $select(PASSFLG=1:"G",PASSFLG=2:"C")
		
		set NAME = $select(PASSFLG=1:irsupd.oname,PASSFLG=2:cif.nam)  // Payee First Name
		set LNAME = $select(PASSFLG=1:irsupd.olnam,PASSFLG=2:cif.lnm) // Payee Last Name

		set ADDR = cif.mad1_" "_cif.mad2_" "_cif.mad3	// Payee address
		set CITY = cif.mcity 				// Payee city
		set STATE = cif.mstate 				// Payee state
		set ZIP = cif.mzip.piece("-",1)_cif.mzip.piece("-",2) // Payee zip
		set CNTRY = cif.mcntry	 			// Foreign country
		set FORCNTY = $select(cif.mcntry="US":" ",1:1)  	// Foreign country indicator

		//; Blank|544|662
		set SPECIAL = "".justify(119)
		//; Special Data Entries|663|722
		set SPECIAL = SPECIAL_"".justify(60)
		//; Blank|723|750
		set SPECIAL = SPECIAL_"".justify(28)

		// Skip duplicates
		if (irsupd.ccode '= 1) do BRECWRT

		// Insert "B" Records into B1098 File

		if (TINTYPE = 2) set X = LNAME
		else  set X = NAME
	
		do NAMCNTRL^IRSTPMTR
	
		set NAMCON = X

		set SIN = $select(PASSFLG=1:irsupd.otin,PASSFLG=2:cif.taxid)

		set BSEQ = Db.nextVal("B1098","PDATE=:tmp3.key2,KEY=:CID")

		type RecordB1098 b1098 = Db.getRecord("B1098","PDATE=:tmp3.key2,KEY=:CID,BSEQ=:BSEQ", 1)

		set b1098.bnamcon = NAMCON
		set b1098.btintype = TINTYPE
		set b1098.btin = SIN.piece("|",1).translate(" ","-")
		set b1098.bcid = CID
		set b1098.bamt1 = AMT(1)/100
		set b1098.bamt2 = AMT(2)/100
		set b1098.bamt3 = AMT(3)/100
		set b1098.bname = NAME
		set b1098.baddr1 = cif.mad1
		set b1098.baddr2 = cif.mad2
		set b1098.baddr3 = cif.mad3
		set b1098.bcity = CITY
		set b1098.bstate = STATE
		set b1098.bzip = cif.mzip
		
		do b1098.save()
		}
		
	quit
	

SORT	// Sort all reportable accounts into a temporary table

	type public Number SRT
	type public String FORM, FORMS(), Q(,), REGION()
	
	type Date DT, JD
	type Number CID
	type String BRREG, N

	set JD = FORMS(FORM) - 1
	
	type ResultSet rs = Db.select("DISTINCT TJD,MISCSEQ","IRSUPD","TJD>:JD AND FTYPE=5","TJD")

	while rs.next() do {
		
		set DT = rs.getCol("TJD")
		set CID = rs.getCol("MISCSEQ")

        	set BRREG = "*"
 
        	if SRT do { 
        	
        		type RecordACN acn = Db.getRecord("ACN", "CID = :CID", 1)
        
        		if acn.boo.isNull() quit
 
        		type RecordUTBLBRCD ubrcd = Db.getRecord("UTBLBRCD", "BRCD = :acn.boo", 1)
        	
        		set BRREG = ubrcd.region
        		}
		
		#ACCEPT Date=11/02/2005;PGM=Eugene Titov;CR=17287
		if REG set N = "" if 1 for  set N = Q(1,N).order() quit:N.isNull()  xecute Q(1,N) else  quit
			
		if 'REGION(BRREG).exists() do {
		
			type RecordUTBLREGION uregion = Db.getRecord("UTBLREGION","KEY = :BRREG", 1)
			
			if 'uregion.getMode() set REGION(BRREG) = "" quit
			
			set REGION(BRREG).piece("|",1) = uregion.desc
			set REGION(BRREG).piece("|",2) = uregion.taxid
			set REGION(BRREG).piece("|",3) = uregion.city
			set REGION(BRREG).piece("|",4) = uregion.state
			set REGION(BRREG).piece("|",5) = uregion.mzip
			set REGION(BRREG).piece("|",6) = uregion.cntry
			set REGION(BRREG).piece("|",7) = uregion.ad1
			set REGION(BRREG).piece("|",8) = uregion.ad2
			set REGION(BRREG).piece("|",9) = uregion.ad3
			set REGION(BRREG).piece("|",12) = uregion.ctof1099
			}
		
		type RecordTMPRPT3 tmprpt3 = Db.getRecord("TMPRPT3", "PID = :%ProcessID, KEY1 = :BRREG, KEY2 = :DT, KEY3 = :CID", 1)
		
		set tmprpt3.data = ""

		do tmprpt3.save()
		}
		
	quit

	
CRECBLD	// "C" record builder 

	do CRECWRT	// Write the record to tape
	
	quit

%STOPLOD	// Stop %ZRTNLOD from this point on down
	quit
TRECWRT		// Dummy line reference for GT.M
	quit
ARECWRT		// Dummy line reference for GT.M
	quit
BRECWRT		// Dummy line reference for GT.M
	quit
CRECWRT		// Dummy line reference for GT.M
	quit
STORETOT	// Dummy line reference for GT.M
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60403^47676^Shriram Deshpande^8354"	// Signature - LTD^TIME^USER^SIZE
