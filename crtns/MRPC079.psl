MRPC079(return,versn,CID,EFD)
	/*
	Procedure ID: MRPC079
	DESC: Loan Payoff Rebate Support Status
	KEYWORDS: RPC

	ARGUMENTS:
		. return  Loan Payoff Status data	/TYP=T/REQ/MECH=REFNAM:W
	
		. versn	  ^MRPC078 version number   	/TYP=N/REQ/MECH=VAL
			  current version=1
	
		. CID	  Account Number	  	/TYP=N/REQ/MECH=VAL
	
		. EFD	  Payoff Effective Date		/TYP=D/NOREQ/MECH=VAL
	
	RETURNS:
		. $$      Error Message	   		/TYP=T
			  Null= No Error
	
	RELATED:
		. $$^PBSMRPC - MRPC Service Class Driver
	
	EXAMPLE:
		S RM=$$^MRPC079(.VAL,1,CID,EFD)

	---- Revision History ------------------------------------------------

	02/15/99 - SIGDAE - 30971
	           Code has been modified to return an error if the Payoff
	           Date is less than the Original Disbursement Date.
	----------------------------------------------------------------------
	*/
	//----------------------------------------------------------------------
	new %,%A,%ACR,%NOLOCK,A,OPCHG,PAMT,PREREB,RB,RB1,data,rb1,str,x
	set %NOLOCK=1
	set %O=2
	
	// Version number of client message is not compatible with server
	if $G(versn)'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))
	// Invalid account ~p1
	set x=Db.isDefined("LN","CID")
	if x="" quit $$ERRMSG^PBSUTL($$^MSG(1259,CID))
	// Default payoff effective date
	if $G(EFD)="" set EFD=%SystemDate
	set ODD=Db.getOneRow("ODD","LN","CID")
	// Effective date may not be prior to ~p1
	if EFD<ODD quit $$ERRMSG^PBSUTL($$^MSG(878,$$DAT^%ZM(ODD,$G(%MSKD))))	
	set %=$C(9),str=""
	
	// Load Rebate data
	set OPCHG=0
	set PAMT=0
	do EXT^LNPO1 quit:ER $$ERRMSG^PBSUTL($G(RM))

	// Create formatted output string
	// typ,desc,payee,org,reb,rebl
	set i=""
	for  set i=$O(RB1(CID,i)) quit:i=""  do {
		set rb1=RB1(CID,i)
		set str=str_$P(rb1,"|",5)_%_$P(rb1,"|",2)_%_$P(rb1,"|",3)
		set str=str_%_$P(rb1,"|",4)_%_$P(rb1,"|",6)_%_$P(rb1,"|",1)_$C(13,10)
		}

	// Remove last $(13,10) from end of string
	set str=$P(str,$C(13,10),1,$L(str,$C(13,10))-1)
	set return=$$V2LV^MSG(str)
	Q ""
	//-----------------------------------------------------------------
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43588^Sanchez SCM Administrator^1960"	// Signature - LTD^TIME^USER^SIZE
