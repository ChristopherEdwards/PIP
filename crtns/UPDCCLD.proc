UPDCCLD(RecordDEP dep,RecordTTX ttx)
	/*
	 Update Customer Last Contact Date on Reversals and Error Corrects

	 ORIG:  WAT - 16 JUN 1992
	 DESC:  Backdated the CCLD when reversal or error correct
		transaction took place

	--- Revision History -------------------------------------------------
	
	07/20/06 - DESHPANDE S K - CR 20748
		   Replaced Transaction Variable (%TRNPRIM) with Transaction
		   Stored Value.		
	
	11/14/02 - ZWITKOWITSM - 43583
		   PSL clean-up.

	06/13/02 - ZWITKOWITSM - 43583
		   Converted to PSL.

	----------------------------------------------------------------------
	*/

	type Public Cache %CACHE()

	if 'ttx.getStoredValue("isPrimary") quit

	new CID,ETC,HISTTJD,HIT,OSEQ,TCMT,UTSO

	set CID=dep.cid
	set ETC=ttx.etc

	type RecordTRN trn=%CACHE("TRN").getRecord("TRN","ETC")

	/*
	 If this transaction doesn't update CCLD let's quit here so we don't
	 run through History needlessly.
	*/
	if 'trn.pcfd2 quit

	// Find reversed transaction
	do OUT^UTSO(.UTSO,ttx.tso)

	// upper limit
	set HISTTJD=%SystemDate+1
	// Original sequence
	set OSEQ=$G(UTSO("REV"))

	if OSEQ'="" do {

		type RecordHIST hist=Db.getRecord("HIST","CID,OSEQ")

		set HISTTJD=hist.tjd
		}

	/*
	 If the current CCLD is greater than the reversed transaction
	 entry date, then don't reset CCLD.
	*/
	if HISTTJD<dep.ccld quit

	set HIT=""

	/*
	If transaction entry date is greater than the date of the reversed
	transaction's entry date, it can be skipped because it did not
	update CCLD.  "TJD NOT >:HISTTJD"
	*/
	type ResultSet rs1=Db.select("TCMT,ETC,TJD","HIST","CID=:CID AND TSEQ NOT =:OSEQ AND TJD NOT >:HISTTJD","TSEQ DESC")

	while rs1.next() do { quit:HIT

		set TCMT=rs1.getCol(1)
		set ETC=rs1.getCol(2)

		// If CCLD was file-maintained, use that
		if TCMT["[DEP]CCLD" do { quit 
			set dep.ccld=$P(TCMT,":",3)
			set HIT=1
			}

		if ETC=""!(ETC="FM") quit 

		type RecordTRN trn2=%CACHE("TRN").getRecord("TRN","ETC")

		// If the Update CCLD flag on the trancode is set, then use this transaction.
		if trn.pcfd2 do {
			// Update Last Customer Contact Date
			set dep.ccld=rs1.getCol(3)
			set HIT=1
			}
		}

	// If no other transactions found, reset CCLD to ODT
	if 'HIT set dep.ccld=dep.odt

	quit 

vSIG()	quit "60466^42523^Shriram Deshpande^2090"	// Signature - LTD^TIME^USER^SIZE
