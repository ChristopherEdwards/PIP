UCO1	//Data Item Computed Operations Utility 
	/*
	ORIG: skariahv - 08/18/2005
	DESC: Data Item Computed Operations Utility

	---- Revision History ------------------------------------------------
	
	08/23/2005 - SkariahV - CR16680
			Converted to PSL.
			Removed revision history prior to 2004.
	
	----------------------------------------------------------------------	

	*/
 
 	quit
	
		
public ROLE(Number CID,	//Account Number
	Number ACN)	//Customer Number
	
	/* Returns computed values from RELCIF file
	
	Piece   Description
	
		1 Indicator (0/1) of whether or not customer is primary owner
		2 Account role code appended with ownership indicator
		
		Symbol   Description

		p      Primary Owner
		i      Loans - Indirect Liability
		d      Loans - Direct Liability
		s      Deposits - Secondary Owner
		n      Deposits - Not An Owner
		
		3     Indicator of whether or not a deposit account owner
		4  Indicator of whether or not customer has direct liability
		for borrowed funds (loan acccounts only).
	
	*/
	
	type String ROLECODE

	if ACN.isNull()!CID.isNull() quit ""

	type RecordRELCIF relcif=Db.getRecord("RELCIF","ACN=:ACN,CID=:CID",1)
	if relcif.role.isNull() quit ""
	
	type RecordACN acn=Db.getRecord("ACN","CID=:CID",1)
        if acn.acnrelc.isNull() quit ""
        
	if "D,L"'[acn.cls quit ""
	
        type RecordRELCODE relcode=Db.getRecord("RELCODE","REL=:acn.acnrelc,ROLE=:relcif.role",1)
	
	if acn.cls="D" do ROLED(ACN,CID,relcif.role,relcode.dl,.ROLECODE) quit ROLECODE 
	
	do ROLEL(ACN,CID,relcif.role,relcode.dl,.ROLECODE)
	
	quit ROLECODE
	
	
ROLED(Number ACN,	//Customer Number
	Number CID,	//Account Number	
	Number ROLE,	//Role Code for Relationship
	Boolean DL,	//Direct Liability/Owner        
	String ROLECODE)	//Relationship code

	//The section ROLED is for Deposit
   
	type RecordACN acn=Db.getRecord("ACN","CID=:CID",1)
	
 	if acn.acn=ACN do { quit
		set ROLECODE.piece("|",2)=ROLE_"p"
		set ROLECODE.piece("|",1)=1
		set ROLECODE.piece("|",3)=1
		}
	
	if DL do { quit
		set ROLECODE.piece("|",2)=ROLE_"s"
		set ROLECODE.piece("|",3)=1
		}
	
	set ROLECODE.piece("|",2)=ROLE_"n"
	
	quit


ROLEL(Number ACN,	//Customer Number 
	Number CID,	//Account Number
	Number ROLE,     //Role Code for Relationship
	Boolean DL,	//Direct Liability/Owner        
	String ROLECODE)	//Relationship code	 

	//This section ROLEL is for Loans
		
	type RecordACN acn=Db.getRecord("ACN","CID=:CID",1)
		
	if acn.acn=ACN do { quit
		set ROLECODE.piece("|",2)=ROLE_"d"
		set ROLECODE.piece("|",1)=1
		set ROLECODE.piece("|",4)=1
	        }
	
	if DL do { quit
		set ROLECODE.piece("|",2)=ROLE_"d"
		set ROLECODE.piece("|",4)=1
		}
	
	set ROLECODE.piece("|",2)=ROLE_"i"
	
	quit
	
	
public SPOUSE(Number CID,	//Account Number
		Number ACN)	//Customer Number

	type RecordRELCIF relcif=Db.getRecord("RELCIF","ACN=:ACN,CID=:CID",1)
	if relcif.role.isNull() quit 0
	
	type RecordACN acn=Db.getRecord("ACN","CID=:CID",1)
	if acn.acnrelc.isNull() quit 0
	
	type RecordRELCODE relcode=Db.getRecord("RELCODE","REL=:acn.acnrelc,ROLE=:relcif.role",1)
	
	quit $S(relcode.spouse="":0,1:1)
	
 #OPTION ResultClass ON
Public String vSIG()	quit "60135^39186^Viji Skariah^2926"	// Signature - LTD^TIME^USER^SIZE
