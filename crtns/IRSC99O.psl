IRSC99O		/*
	ORIG: Michael Winigrad (6969)
	DESC: IRS Processing - Form 1099-OID Correction
	I18N=QUIT: Excluded from I18N standards.

	---- Comments --------------------------------------------------------
	======================================================================
		*** THIS ROUTINE IS TO BE COMPILED WITH IRSTPMTR ***
		     *** DO NOT RUN THIS ROUTINE STANDALONE ***
	======================================================================
	
	---- Revision History ------------------------------------------------
	

	07/09/07 - NATRAJAH - CR 27921
		   Modified BRECBLD section to set the field position 547-585 
		   of the variable SPECIAL to dep.irn/year of maturity date and
		   added code for inserting Payee "B" record into B1099OID table. 
		   
	05/15/06 - DESHPANDE S K - CR 21167
		   Modified BRECBLD section to set address variables to mailing 
		   address instead of permanent address.
	
	05/08/06 - TITOVE - CR 21140
		   Modified BRECBLD to set "C" corrected return indicator
		   for a second step of a two-step correction.

	09/28/05 - HAILEYM - CR17143
		Converted to PSL.
		
	*/
	
	// Accept "Dead code" warning during runtime of @IRSTAPE.
	#ACCEPT Date=10/14/2005;PGM=Marie Hailey;CR=17143
	quit
	
	
MTRCNTRL	// Master control
	
	type public Boolean ER
	
	type Number PASSFLG
		
	set PASSFLG=1
	do ARECBLD quit:ER	// Build and write the "A" record - First pass
	do BRECBLD quit:ER	// Build and write the "B" records - First pass
	do CRECBLD quit:ER	// Build and write the "C" record - First pass
	
	set PASSFLG=2
	do ARECBLD quit:ER	// Build and write the "A" record - Second pass
	do BRECBLD quit:ER	// Build and write the "B" records - Second pass
	do CRECBLD quit:ER	// Build and write the "C" record - Second pass

	do STORETOT		// Store the TTR report totals
	quit
	
	
ARECBLD	// "A" record builder
	
	type public String AMTIND,FORMCODE,TAMT()
	
	set FORMCODE="D"	// IRS form code
	set AMTIND=1234		// IRS amount type indicator
	set TAMT(1).piece("|",2)="Total Original Issue Discount"
	set TAMT(2).piece("|",2)="Other Periodic Interest"
	set TAMT(3).piece("|",2)="Early Withdrawal Penalty"
	set TAMT(4).piece("|",2)="Federal Income Tax Withholding"
	do ARECWRT		// Write the record to tape
	quit
	
	
BRECBLD	// "B" record builder
	
	type public Date FORMS(),YEAR
	type public Number AMT(),PASSFLG
	type public String FORM
	
	type Date MDT,XJD
	type Number ACN,BSEQ,CID,I,OID
	type String ADDR,CITY,CNTRY,CORRTN,DOCCODE,FORCNTY,FULLADDR,LNAME
	type String NAMCON,NAME,SPECIAL,STATE,TIN,TINTYPE,X,XTIN,ZIP
	
	type DbSet ds
	type RecordCIF cif
	type RecordDEP dep
	type RecordIRSUPD irsupd
	type RecordOID lyoid,oid
	
	set XJD=FORMS(FORM).get()
	
	for I=1:1:9 set AMT(I)=0
	
	set DOCCODE="  "
	set CORRTN=$select(PASSFLG=1:"G",PASSFLG=2:"C")
	
	set ds=Db.selectDbSet("IRSUPD","TJD NOT <:XJD AND FTYPE=4")
	while ds.next() do {
		set irsupd=ds.getRecord("IRSUPD")
		
		if irsupd.ccode=1,(PASSFLG=1) quit	// Skip duplicates when writting to tape
		if irsupd.ccode=4,(PASSFLG=1) quit	// Skip "New Entry" for first pass
		if irsupd.ccode=3,(PASSFLG=2) quit	// Skip "change amount" for second pass
		
		set CID=irsupd.miscseq
		
		set dep=Db.getRecord("DEP","CID=:CID",1)
		
		if dep.irsexm quit	// Account exempt from 1099 reporting
		
		set OID=""
		set oid=Db.getRecord("OID","CID=:CID,TAXYR=:YEAR",1)
		if oid.getMode() set OID=oid.ial
		
		set lyoid=Db.getRecord("OID","CID=:CID,TAXYR=:(YEAR-1)",1)
		if lyoid.getMode() set OID=OID-lyoid.ial
		
		set ACN=$select(PASSFLG=1:irsupd.ocif,PASSFLG=2:dep.acn)	// Original/New CIF number
		
		set cif=Db.getRecord("CIF","ACN=:ACN",1)

		set TIN=$select(PASSFLG=1:irsupd.otin,PASSFLG=2:cif.taxid)
		set TINTYPE=$select(TIN?2N1"-"7N:1,TIN?3N1"-"2N1"-"4N:2,1:" ")
		
		if PASSFLG=1,(irsupd.ccode=2) for I=1:1:4 set AMT(I)=0
		else  set AMT(1)=OID*100,AMT(2)=dep.ipy*100,AMT(3)=dep.ppy*100,AMT(4)=dep.bwp*100
		
		if PASSFLG=1 set NAME=irsupd.oname,LNAME=irsupd.olnam	
		if PASSFLG=2 set NAME=cif.nam,LNAME=cif.lnm
		set FULLADDR=cif.mcity_"|"_cif.mstate_"|"_cif.mzip_"|"_cif.mcntry_"|"_cif.mad1_"|"_cif.mad2_"|"_cif.mad3_"|"_cif.mad4_"|"_cif.mloc
		set ADDR=cif.mad1_" "_cif.mad2_" "_cif.mad3
		set CITY=cif.mcity
		set STATE=cif.mstate
		set ZIP=cif.mzip.piece("-",1)_cif.mzip.piece("-",2)
		set CNTRY=cif.mcntry
		set FORCNTY=$select(CNTRY="US":" ",1:1)	// foreign country indicator
		
		set MDT=dep.mdt

		set SPECIAL="".justify(3)_((dep.irn_"/"_MDT.year())_"".justify(39)).extract(1,39)
		
		set ACN=cif.acn
		type ResultSet rs = Db.select("BSEQ","B1099OID","PDATE=:%SystemDate AND KEY=:ACN","BSEQ DESC")
		if rs.next() set BSEQ = rs.getCol("BSEQ") + 1
		else  set BSEQ = 1
	
		type RecordB1099OID b1099oid = Db.getRecord("B1099OID","PDATE=:%SystemDate,KEY=:ACN,BSEQ=:BSEQ", 1)

		if TINTYPE=2 set X=LNAME
		else  set X=NAME
		do NAMCNTRL^IRSTPMTR set NAMCON=X
		
		set b1099oid.bnamcon=NAMCON
		set b1099oid.btintype = TINTYPE
		set b1099oid.btin = TIN.translate(" ","-")
		set b1099oid.bcid = CID
		set b1099oid.bamt1 = AMT(1)/100
		set b1099oid.bamt2 = AMT(2)/100
		set b1099oid.bamt3 = AMT(3)/100
		set b1099oid.bamt4 = AMT(4)/100
		set b1099oid.bname = NAME
		set b1099oid.baddr1 = cif.mad1
		set b1099oid.baddr2 = cif.mad2
		set b1099oid.baddr3 = cif.mad3
		set b1099oid.bcity = cif.mcntry
		set b1099oid.bstate = cif.mstate
		set b1099oid.bzip = cif.mzip
				
		do BRECWRT	// Write the record to tape
		
		do b1099oid.save()
		}
	quit
	
	
CRECBLD	// "C" record builder
	
	do CRECWRT	// Write the record to tape
	quit


%STOPLOD	// Stop %ZRTNLOD from this point on down

	quit
	
	
TRECWRT		// Dummy line reference for GT.M

	quit
	

ARECWRT		// Dummy line reference for GT.M

	quit
	
	
BRECWRT		// Dummy line reference for GT.M

	quit
	
	
CRECWRT		// Dummy line reference for GT.M

	quit
	
	
STORETOT	// Dummy line reference for GT.M

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60820^23061^Hari Natrajan^5614"	// Signature - LTD^TIME^USER^SIZE
