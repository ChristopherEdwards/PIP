LNNEWF1A(PAS,PAP,PMTDIST,PMTDISTF,NPC,PPDCAT)	
	/*
	ORIG: Vitaliy Antonov - 6/19/2002
        DESC: Utility;Account payment string create & maintenance
        PROCEDURE ID: LNNEWF1A

	Creates and/or modifies LN.PMTDIST and LN.PPDCAT1-10

	ARGUMENTS:
		. PAS  LN.PAS  /TYP=T/REQ/MECH=VAL
			Payment Application String

		. PAP  LN.PAP  /TYP=T/REQ/MECH=VAL
			Payment Application Path

		. PMTDIST LN.PMTDIST /TYP=T/MECH=REFNAM:W
			Payment distribution

		. PMTDISTF LN.PMTDISTF /TYP=T/MECH=REFNAM:W
			Payment distribution frequency numbers

		. NPC  LN.NPD  /TYP=N/MECH=REFNAM:W
			Number of payment due categories

		. PPDCAT LN.PPDCAT1-10 /TYP=larray/MECH=REFARR:W
			Payment Path Due Category 1-10
			Returned in array PPDCAT(num)=value

	INPUTS:
		. %PR  Priority array  /TYP=larray

		. PART  Participation flag /TYP=L

	---- Revision History ------------------------------------------------

	2/20/2002 - Vitaliy Antonov - 43583
		Converted To PSL.

	*/

	new A,ASTRL,ASTRS,CT,CONTINUE,DL,EXITSTR,I,IND,J,K,L,MSTR
	new P,P1,P2,P1A,P2A,PC,PCT,PR,RR,S,ST,ST1,TST,V,VH,X,XN,XR

	type ResultSet rs=Db.select("DISTRIB","LNPAS2","TABLE=:PAS")
	if rs.next() set MSTR=rs.getCol(1)

	if $D(PART) do PART

	// Init PPDCAT1-10
	for I=1:1:10 set PPDCAT(I)=""

	// Sets up ASTR (account priority string) based upon %PR from screen
	set ER=0 
	set DL=">#v0*" 
	set ASTRL=MSTR_"-"
	set ASTRS=MSTR_"-"

	if %PR(1)'="" do S2 quit:ER
	do S10 quit:ER
	do MID 
	do STR1 quit:ER
	do STR2 quit:ER
	do STR3 quit:ER
	do STR4

	quit


S2	// Private
	set P=""
	for  set P=$O(%PR(P)) quit:P=""  do {
		set IND=$P(%PR(P),"|",2)
		if $P(%PR(P),"|",1)="ESC" do {
			set X=P
			for  set X=$O(%PR(X)) quit:X=""  do {
				if $P(%PR(X),"|",1)'="ESC" quit
				set P=X
				set IND=IND+$P(%PR(X),"|",2)
				}
			}
		set P1=$P(ASTRS,$P(%PR(P),"|",1)_"-",1) 
		set P2=$P(ASTRS,$P(%PR(P),"|",1)_"-",2,999)
		set P1A=$P(ASTRL,$P(%PR(P),"|",1)_"-",1) 
		set P2A=$P(ASTRL,$P(%PR(P),"|",1)_"-",2,999)

		if 'IND set ASTRS=P1_P2 set ASTRL=P1A_P2A quit

		if IND=1,'$$ESC^LNU($P(%PR(P),"|",1)) quit

		set P1A=P1A_$P(%PR(P),"|",1) 
		for I=2:1:IND set P1A=P1A_"-"_$P(%PR(P),"|",1)_I
		set ASTRL=P1A_"-"_P2A 
		set ASTRS=P1_$P(%PR(P),"|",1)_IND_"-"_P2
		}
	quit

S10	// Privtae
	if $E(ASTRS,$L(ASTRS))="-" set ASTRS=$E(ASTRS,1,($L(ASTRS)-1))
	if $E(ASTRL,$L(ASTRL))="-" set ASTRL=$E(ASTRL,1,($L(ASTRL)-1))

	// Payment string does not contain interest
	if ASTRS="" set ER=1 set RM=$$^MSG(2169) quit
	if $L(ASTRS)>100 set ER=1 set ET="PRIO2LGE" do ^UTLERR quit
	
	quit

MID	// Private
	set P=0

	type ResultSet rs=Db.select("STAB,PASTR","LNPATH1","TABLE=:PAS AND PATH=:PAP")
	if 'rs.isEmpty() while rs.next() do {
		new PASTR
		set P=P+1
		set PASTR=rs.getCol(2)
		if PASTR'=0 set PR(P)=PASTR
		else  set P=P-1
		}

	quit

STR1	// Private; Validate Priority Items
	set I=1 
	set XR=0

 	for  set P=$P(ASTRS,"-",I) quit:P=""  do { quit:ER
		if $$ESC^LNU(P),P?1A.A1N.N set L=$L(P) for Z=1:1:L set A=$E(P,Z) if A?1N.N set P=$E(P,1,Z-1) quit
		if MSTR'[P set ER=1 set ET="INVLDITM" do ^UTLERR quit
		set I=I+1
		}
	quit

STR2	// Private; Build Custom String From Input (used to be STR2 line tag)
	set RR=""
	for  set RR=$O(PR(RR)) quit:RR=""  do { quit:ER

		set J=1 
		set ST="" 
		set I=2
		// PR IS MASTER
		set PR="-"_PR(RR) 
		set PCT=1
		set XR=XR+1

		// used to be STR3 line tag 	
		set EXITSTR=0	
		for  set P=$P(ASTRS,"-",J) quit:(P="")!EXITSTR  do { quit:ER
			if ($$ESC^LNU(P)&(P?1A.A1N.N)) do NUM
			// flag to quit the loop to the next statement
			set CONTINUE=0
	 		for  set PC=$P(PR,"-",I) quit:EXITSTR!CONTINUE  do { quit:ER
		 		if PC="" set EXITSTR=1 quit
				if P'=PC do STR5 quit		
				set CONTINUE=1
				}
			quit:EXITSTR

			if $D(XP) do { quit
				set S=$P(PR,"-",I-1,I+1) 
				set X=$P(S,"-",2) 
				set X=X_N 
				set ST1=$P(S,"-",1)_"v-"_X_"-"_$P(S,"-",3) 
				set CT=0
				if ST'=""&($P(ST,"-",1)="") set ST=$P(ST,"-",2,999)
	
				for K=1:1 set X=$P(ST,"-",K) quit:X=""  set CT=CT+1
				set ST=$P(ST,"-",1,CT-1)_"-" 
				set J=J+1 
				set ST=ST_ST1 
				set PCT=PCT+2 
				kill XP 
				}

			set ST1=$P(PR,"-",I-1,I+1) 
			set ST=ST_ST1 
			set J=J+1 
			set PCT=PCT+2
			}

		if ST'="",$P(ST,"-",1)="" set ST=$P(ST,"-",2,999)
		if ST'="",DL[$E($P(ST,"-",1)) set ST=$P(ST,"-",2,999)
		set TST(RR)=ST
		}
	quit

STR3	// Private
	new EXIT
	set I=""

	for  set I=$O(TST(I)) quit:I=""  do { quit:ER
		set PR=TST(I) 
		set J=1
		set EXIT=0	

		for  set P=$P(PR,"-",J) quit:EXIT  do { quit:ER
			set S=$E(P) 
			set VH=0

			if S="",PR'="",J>1 set TS(I)=PR set EXIT=1 quit

			if DL'[S set J=J+1 quit

			if J=1 set PR=$P(PR,"-",2,999) quit

			// Determine which pathfinder has the highest value
			set L=$L(P) 
			for K=1:1:L set A=$E(P,K) do VAL quit:ER  set A(K)=V_"|"_A
			for K=1:1:L set A=A(K) quit:A<0  set V=+A if V>+VH set VH=A
			set $P(PR,"-",J)=$P(VH,"|",2) 
			set J=J+1
			}
		}
	quit

STR4	// Set PMTDIST & PMTDISTF
	do PMTDIST(.%PR,.PMTDIST,.PMTDISTF)
	set NPC=XR
	set J=""
	for I=1:1:XR set PPDCAT(I)=TS(I)

	set I=XR+2
	type ResultSet rs=Db.select("STAB","LNPATH1","TABLE=:PAS AND PATH=:PAP AND STAB<>0")
	if 'rs.isEmpty() while rs.next() do {
		set PPDCAT(I-1)=rs.getCol(1)
		set I=I+1
		}
	quit

STR5	//Private
	if DL[PC&(ASTRS'[$P(PR,"-",I-1)) set ST=ST_PC set I=I+1 quit
	if DL'[PC set PCT=PCT+2
	set I=I+1
	quit


VAL	set V=$S(A="*":5,A=">":4,A="#":3,A="v":2,A=0:1,1:0) 
	set:'V ER=1
	if ER set ET="INVLDPFNDR" do ^UTLERR
	quit

NUM	set L=$L(P) 
	set XP=P 
	for Z=1:1:L set A=$E(P,Z) if A?1N.N set P=$E(P,1,Z-1) set N=$E(XP,Z,L) set PCT=PCT-2 set:$D(XN) XN=XN+N-1 quit 
	quit

PART	// Provide string creation for loans sold
	new PAS

	set PAS=pas
	type ResultSet rs=Db.select("DISTRIB","LNPAS2","TABLE=:PAS")
	if '$D(MST(PAS)),rs.next() set MST(PAS)=rs.getCol(1)
	set MSTR=MST(PAS)
	set A="|0|1|X" 
	set B=0
	for I=1:1 set E=$P(MSTR,"-",I) quit:E=""  do {
		set PR(E)=I 
		set %PR(I)=E_A 
		set:($$ESC^LNU(E)) B=1
		}
	for I="L","I","P" if $D(PR(I)) set $P(%PR(+PR(I)),"|",2)=1
	quit


Public PMTDIST(%PR,PMTDIST,PMTDISTF)	// Create PMTDIST and PMTDISTF from %PR
	// Also called from LNNEW6
	new DISTNO,N
	set N=""
	set PMTDIST=""
	set PMTDISTF=""
	for  set N=$O(%PR(N)) quit:N=""  do {
		new CNT,I,NAME,X
		set X=%PR(N) 
		set CNT=$P(X,"|",2) 
		set DISTNO=$P(X,"|",3)
		quit:'CNT
		set NAME=$P(X,"|",1)
		if 'DISTNO set DISTNO=1
		if '$$ESC^LNU(NAME) do { quit
			set PMTDIST=PMTDIST_NAME_"-"
			set PMTDISTF=PMTDISTF_DISTNO_"-"
			}
		for I=1:1:CNT do {
			set PMTDIST=PMTDIST_NAME_I_"-"
			set PMTDISTF=PMTDISTF_DISTNO_"-"
			}
		}
	set PMTDIST=$E(PMTDIST,1,$L(PMTDIST)-1)
	set PMTDISTF=$E(PMTDISTF,1,$L(PMTDISTF)-1)
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59991^50698^Irina Kin^6297"	// Signature - LTD^TIME^USER^SIZE
