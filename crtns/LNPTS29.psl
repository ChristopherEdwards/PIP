LNPTS29(RecordDEP dep, RecordTTX ttx, String CTL)

	/*
	       ORIG:  David Caliendo (5527) - 03/31/87
	       
	       DESC:  Execute an Escrow accrual and/or interest payment 
	              transaction to escrow account.
	              
        ARGUMENTS:
		.  CTL = Control indicators	/REQ/MECH=VAL
		
		   0 - Accrual (POSACR) processing
		   1 - Authorized Negative accrual (NEGACR) processing
		   2 - Unauth Negative accrual (NEGACRUN) processing
		   3 - Available Interest accrual (AIACR) processing
		   4 - Uncollected Accrual (UNCACR) processing
		   5 - Interest posting/adjustment

	------------Revision History-------------------------------------------
	
	01/24/06 - Srinivar / SmithCD - CR 16890
		   . Modifications were made to accomodate escrow account 
	 	     interest accrual processing
	 	   . Removed DIV section
	 	   . Removed code that updated ln.teb (s/b done when 
	 	     processing the loan side of the transaction, where the 
	 	     loan object is present)
	 	   . Removed old revision history
	 	   
	*/
	
	type RecordTRN trn = Db.getRecord("TRN", "ETC=:ttx.etc")

	if CTL < 5 do ACR(.dep, .ttx, .trn, CTL) quit

	do INT(.dep, .ttx, .trn)
	
	quit
	

ACR(RecordDEP dep,		// Deposit (escrow) account		/REF:RW
    RecordTTX ttx,		// Deposit (escrow) transaction		/REF:R
    RecordTRN trn,		// Transaction code			/REF:R
    Boolean CTL)		// Control indicators

    	// Accrual adjustment
    	
    	type public Boolean DIRECT
 
	if 'ttx.itc6, ('ttx.itc12) do ^DEPACR1(.dep, .ttx, .trn, CTL)
	if ttx.itc6 ! (ttx.itc12) do EC^DEPACR1(.dep, .ttx, .trn, CTL)

	do FILE(.dep, .ttx, DIRECT.get())

	quit


INT(RecordDEP dep,	// Deposit (escrow) account		/REF:RW
    RecordTTX ttx,	// Deposit (escrow) transaction		/REF:R
    RecordTRN trn)	// Transaction code			/REF:R

	// Interest Posting

    	type public Boolean DIRECT
	
	// Check for error correct or reversal and adjust interest paid amounts
	if 'ttx.itc6, ('ttx.itc12) do ^DEPINT(.dep, .ttx, .trn,1)
	if ttx.itc6 ! (ttx.itc12) do EC^DEPINT(.dep, .ttx, .trn,1)

	do FILE(.dep, .ttx, DIRECT.get())
	
	quit


FILE(RecordDEP dep, 		// Deposit (escrow) account		/REF:R
     RecordTTX ttx,		// Deposit (escrow) transaction		/REF:RW
     Boolean DIRECT)		// Post direct to escrow indicator

	/*
	File miscellaneous account-related data
	
	Additional notes on ARGUMENTS:
	
		. DIRECT
			0 - Interest adjustment was generated through loan 
			    escrow interest transaction (LNPTS25)
			1 - Interest adjustment was generated directly to the 
			    escrow account (ultimately as part of a loan 
			    payoff (LNPTS23) or escrow closeout transaction 
			    (LNPTS28))
	
	*/

	type public String TB()
	type public Number ZAMT
	
	/*
	At this point, ttx.tamt array may or may not have the escrow 
	suspense position populated. Either way, to prevent an 
	out-of-balance condition from occuring, instead of calling into 
	GL^LNPTSU(ZAMT, 7), which will add ZAMT onto ttx.tfr, directly set 
	ttx.tfr equal to the full transaction amount.
        */
	if 'DIRECT do {
		type String TAMT = ttx.tamt
		set TAMT.piece("#", 7) = +TAMT
		set ttx.tamt = TAMT
		}
	
	set TB(dep.esc) = ZAMT

	set ZAMT = 0

	// Code in this section in the p01 version may need to be added here -
	// testing will determine if its needed
	
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60297^71096^Chad Smith^3124"	// Signature - LTD^TIME^USER^SIZE
