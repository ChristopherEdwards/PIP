UTLHIST(CID,H,fHIST)	//;IBS - UTL - V4.2 - History Sequence Locator
	/*
	       ORIG:  CHENARDP - 21 MAY 1992

	This utility returns the original history sequence number.

	ARGUMENTS:

	 . CID	Account Number          			/REQ

	 . H	An array that contains one or more of the 	/REQ
		following data items:
	      	TJD,ITC,ETC,TAMT,EFD,TLO,TSO,TCMT,CDT,TIME,BRCD,TRC,UID,SPR

	      	Example:
			h(3)="DW"
        		h(4)=100
        		h(5)=59308
        		h(8)="TEST"

 	 . fHIST   History record				/NOREQ			 		

        RETURNS:
	 . $$ History Sequence Number
 
        EXAMPLE:
         $$^UTLHIST(8000123,.H,.HIST)


	  ----------- Revision History ---------------------------------------

	   05/20/02 - KESTELMANN - 43583
		      PSL conversion. Remove old revision history.

	*/

	new FL,HSEQ,MISS,OUT,POS,STR,TSO,VAL,X

	set STR="TSEQ,TJD,ITC,ETC,TAMT,EFD,TLO,TSO,TCMT,CDT,TIME,BRCD,TRC,UID,SPR" 
				
	type ResultSet rs
	set rs=Db.select(STR,"HIST","CID=:CID")
		
	if rs.isEmpty() quit
			   
	while rs.next() do { quit:(MISS=1)
		set (FL,MISS,POS)=""

		// create OUT arrray to compare with the H array.
		// rs.getCol method gives an error when used with POS.
		set VAL=rs.getRow()
		set VAL=$TR(VAL,$C(9),"|")

		for  set POS=$O(H(POS)) quit:POS=""  do {
			set OUT(POS)=$P($G(VAL),"|",POS+1)
			// get amount from TAMT
			if POS=4 set OUT(POS)=+OUT(POS)
			}
		
		// if arrays are not equal, quit.
		set POS=""
		for  set POS=$O(H(POS)) quit:POS=""  do { quit:FL
			if OUT(POS)'=H(POS) set FL=1
			}

		if FL quit	// go get another record

		// if arrays are equal, check TSO for values.
		set HSEQ=rs.getCol(1)
		set TSO=rs.getCol(8)	
		if TSO="" quit
		do OUT^UTSO(.X,TSO)

		/*
		Variables RECINST, RECACCT and SPEC are set in
		the routine EFTNDPO2 and batch BCHEFT.
		*/
		if $G(RECINST)'=$G(X("SENDINST")) quit
		if +$G(RECACCT)'=+$G(X("SENDACCT")) quit
		if ($G(EFTTYPE)["NDPO"),($G(spec)'=$G(x("SPEC"))) quit
		set MISS=1
		}

	quit HSEQ

vSIG()	quit "59886^43629^Sanchez SCM Administrator^1805"	// Signature - LTD^TIME^USER^SIZE
