MRPC057(return,versn,cid,changes,delete,inttrn)	// Public; LNBIL1 update
	/*
	   ORIG: GORMAN - 05/01/97

	   Process loan bill LNBIL1 record updates

	   KEYWORDS: Client/Server, Loan Utilities

	   ARGUMENTS:

	  	 . return	 Payment record detail

	  	 . versn 	 ^MRPC057 version number 	 /TYP=N/REQ
	    			 Current version = 1 		 /MECH=VAL

	  	 . cid 		 Account number  		 /TYP=N/REQ
	        						 /MECH=VAL

	  	 . changes       List of payment record 	 /TYP=T/REQ
	    			 changes in the following	 /MECH=VAL
	    			 format:
	    

				 pmtdat^ele,col,new^ele,col,new|
	    			 pmtdat^ele,col,new^ele,col,new| etc.

	     			 where	 pmtdat  - Payment date, or "*" if
	               				   change is not associated
	        				   with a specific date (i.e.
	        				   if it is an LNBIL0 column)



	     				 element - Payment element, or blank
	        				   if direct set to specific
	        				   column

	     				 column - Either:

	        				  A.  If 'element' parameter is
	            				      blank, then 'column'
	            				      is equal to column name
	            				      in table.column syntax
	            				      where table is equal to
	            				      either "LNBIL1" or
		 			              "LNBIL5".

	        				  B.  If the 'element'
	            				      parameter is not blank,
	            				      and 'pmtdat' is a date,
	            				      then 'column' is equal
	            				      to the literal:

	            				      DUEAMT:  Amount due

	            				      If 'pmtdat' is equal to
	            				      "*", then 'column' is
	            				      equal to one of the
	            				      following literals
	            				      associated with LNBIL0:

	            				      LA Lump Amount
	            				      NA New Pmt Amt
	            				      PA Current Pmt

	     				 new 	- New field value after change


	  	 . delete	 List, with comma delimeter	 /TYP=T/NOREQ
	    			 of payment dates which 	 /MECH=VAL
	    			 should be deleted.

	  	 . inttrn	 Indicator (0/1) of whether	 /TYP=L/NOREQ
	    			 or not interest accrual 	 /MECH=VAL
	    			 adjustment transactions 	 /DEF=1
	    			 should be generated to
	    			 support changes in interest
	    			 amounts.


	   RETURNS:
	  	 . $$ 		 Error message  		 /TYP=T
	    			 Null = No error

	   RELATED:
	  	 . $$^PBSMRPC - MRPC Service Class Driver

	   EXAMPLE:
	  	 Text of example (line one)

	  ---- Revision History ------------------------------------------------

	   04/10/2002 - SRIVASTAVAN - 49794
			Converted to PSL

        */
	
	catch vERROR {
		new ET,RM
		set ET=vERROR.type

		if ET["%GTM-" do { quit
			do ZE^UTLERR
			}

		set ET=ET_"-"_vERROR.thrownAt
		set RM=vERROR.description
		do ^UTLERR
		}
	
	new change,chgdtl,dateinfo,eledtl,I,J,K	
	
	if $G(inttrn)="" set inttrn=1

	// Version number of client message is not compatible with server
	if $G(versn)'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))

	// Build change array from client input
	for I=1:1 set dateinfo=$P(changes,"|",I) quit:dateinfo=""  do {

		// Payment due date
		set date=$P(dateinfo,"^",1)

		for J=2:1 set chgdtl=$P(dateinfo,"^",J) quit:chgdtl=""  do {

			new column,element,newval

			set element=$P(chgdtl,",",1)
			if element="" set element="*"

			set column=$P(chgdtl,",",2)
			set newval=$P(chgdtl,",",3)

			set change(date,element,column)=newval

			}
		}

	set ER=$$CHANGE^PROCLNBL(cid,.change,$G(delete),inttrn)

	if ER quit $$ERRMSG^PBSUTL($G(RM))

	set return=$$V2LV^MSG(1)
	quit ""

vSIG()	quit "59886^43585^Sanchez SCM Administrator^3320"	// Signature - LTD^TIME^USER^SIZE
