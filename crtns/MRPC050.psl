MRPC050(String return,Number versn,Number CID,Date EFD,String LN)

	/*

	MRPC call for Backdated Loan Renewals

	   ORIG: THOROGOODA - 03/25/97

	   DESC: Renewal Processing Maintenance (LNM006)
	   KEYWORDS: Client/Server, Loan Utilities, RPC

	   ARGUMENTS:
	   	. return 	Data (see below)  		/TYP=T/REQ
	         						/MECH=REFNAM:W

	   	. versn  	^MRPC050 version number  	/TYP=N/REQ
	     			Current version = 1  		/MECH=VAL

	   	. CID  		Account number   		/TYP=N/REQ
	         						/MECH=VAL

	   	. EFD  		Effective date   		/TYP=D/NOREQ
	         						/MECH=VAL

	   	. LN  		Partial SQL syntax for LN info 	/TYP=T/REQ
	     			(col,...) VALUES (val,...) 	/MECH=VAL

	   RETURNS:
	   	. $$  		Error message   		/TYP=T
	     			Null = No error

	   RELATED:
	         . $$^PBSMRPC - MRPC Service Class Driver

	   EXAMPLE:
	   	S RM=$$^MRPC050(.val,1,CID,EFD,.LN)


	----- Revision History -----------------------------------------------
	
	01/31/06 - SmithCD - CR 19343 (16890)
		   Changed call to VPG00A section in LNUEFD to BACKWARD in 
		   conjunction with change in that procedure.
      
        07/26/05 - SkariahV- CR16679
	           Removed #WARN directive.
	
	05/19/05 - MBUIM - CR15982
		   Modified to clean out PSL code.

	11/10/03 - CARROLLJ - 51630
		   Instantiated ttx object before call to FILE^LNRENEW.

	03/14/03 - GRAY - 51351
		   Converted to PSL.

	*/

	type Number X
	type Public Number ER
	type Public String RM

	type Date CUVAR2,SAVTJD
	type Number %ProcessMode,TYPE
	type String CLS,n
	type Public String data,insrecs,ndfrec

	set CLS="L" 
	set (CUVAR2,SAVTJD)=%SystemDate
	set (ndfrec,return)="" 
	set %ProcessMode=1

	// Version number of client message is not compatible with server
	if versn.get()'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))

	// Load info in ln into data array by name
	do getinfo(LN,.data)

	// Determine account, effective date and product type
	type RecordLN ln=Db.getRecord("LN","CID=:CID",1)

	// Record not found
	if ln.getMode()=0 quit $$ERRMSG^PBSUTL($$^MSG(2335))

	set X=CID do PPCID^LNRENEW(.ln,1) if ER quit $$ERRMSG^PBSUTL(RM.get())
	

	type Date X

	if '%EffectiveDate.get() set %EffectiveDate=%SystemDate
	else  set X=%EffectiveDate

	do EFD^LNRENEW(.ln) if ER quit $$ERRMSG^PBSUTL(RM.get())

	set TYPE=data("TYPE").get()

	// Product Type
	if TYPE="" set TYPE=ln.type
	
	type RecordTTX ttx(,)

	do BACKWARD^LNRENEW(.ln,.ttx(,)) if ER quit $$ERRMSG^PBSUTL(RM.get())

	do ln.setAuditFlag(1)

	set n=""
	for  set n=data(n).order() quit:n=""  set ln.@n=data(n)

	/* 
	Since this loan is "unwound", changes to the following date
	fields should not be passed to the filer.
	*/
	if %EffectiveDate<CUVAR2 do {
		set ln.ichnd.journal=0
		set ln.ichld.journal=0
		do CHECKDI^LNRENEW(.ln) if ER quit $$ERRMSG^PBSUTL(RM.get())
		}

	/*
	Even though LN2A does not exist anymore in LNPTSU, variables like
	ODT and TCMT were being used in that section to include the comment
	for New Account based on when the account was opened.
	Not sure how the removal of the call into LN2A^LNPTSU will affect 
	filing of transactions.  SPG
	*/

	// do LN2A^LNPTSU

	type RecordTTX ttx1
	do FILE^LNRENEW(.ln,.ttx1)

	// If error is a warning, transaction will still be processed
	if ER="W" set RM=""

	set return=$$V2LV^MSG(RM.get())

	quit ""


getinfo(String in,String out)	// Parse input strings into out array
	
	type String c,columns,values
	type Number i

	set in=in.upperCase()
	set columns=in.piece("VALUES",1)
	set values=in.piece("VALUES",2)

	set columns=columns.piece("(",2).piece(")",1)
	set values=values.piece("(",2).piece(")",1)

	for i=1:1 set c=columns.piece(",",i) quit:c=""  set out(c)=$TR(values.piece(",",i),"'")

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60297^70605^Chad Smith^3472"	// Signature - LTD^TIME^USER^SIZE
