LIMCDI	//Library;Consolidated Limits - computed data items
	/*
	   ORIG: LOCKE - 09/19/97
	   DESC: Consolidated Limits - computed data items

	   KEYWORDS: Limits


	   LIBRARY:
	   . AVLCOMC - committed country limits

	   . AVLOUTC - outstanding country limits

	   . AVLCOMI - committed industry limits

	   . AVLOUTI - outstanding industry limits

	   . AVLCOMH - committed household limits

	   . AVLOUTH - outstanding household limits

	   . AVLCOMS - committed sovereign limits

	   . AVLOUTS - outstanding sovereign limits

	  ---- Revision History ------------------------------------------------

           08/17/05 - KumarSS - CR16667
           	      HHACN is typed as Public in LEGPOS section.
           	      
           07/26/05 - SkariahV- CR16679
	              Removed #WARN directive.

	   12/08/03 - CARROLLJ - CR7239
		      Correct precedence errors.

	   02/19/03 - THONIYILM - 51351
		      Converted to PSL

	*/

	quit
	
		
Public AVLCOMC(CNTRY)	// Available versus commited - country limits

	type RecordUTBLCNTRYLIM utblcntrylim=Db.getRecord("UTBLCNTRYLIM","CNTRY=:CNTRY",1)
	type RecordEXPCNTRY expcntry=Db.getRecord("EXPCNTRY","CNTRY=:CNTRY",1)

	quit utblcntrylim.amt-expcntry.totcomexp


Public AVLOUTC(CNTRY)	// Available versus outstanding - country limits

	type RecordUTBLCNTRYLIM utblcntrylim=Db.getRecord("UTBLCNTRYLIM","CNTRY=:CNTRY",1)
	type RecordEXPCNTRY expcntry=Db.getRecord("EXPCNTRY","CNTRY=:CNTRY",1)

	quit utblcntrylim.amt-expcntry.totoutexp


Public AVLCOMI(IND)	// Available versus commited - industry limits

	type Number AMT
	type RecordUTBLSIC utblsic=Db.getRecord("UTBLSIC","SIC=:IND",1)
	set AMT=utblsic.amt
	if AMT="" do {
		type RecordCUVAR cuvar=Db.getRecord("CUVAR")
		set AMT=cuvar.totass/100*utblsic.percent
	}
	type RecordEXPIND expind=Db.getRecord("EXPIND","IND=:IND",1)

	quit AMT-expind.totcomexp


Public AVLOUTI(IND)	// Available versus outstanding - industry limits

	type Number AMT
	type RecordUTBLSIC utblsic=Db.getRecord("UTBLSIC","SIC=:IND",1)
	set AMT=utblsic.amt
	if AMT="" do {
		type RecordCUVAR cuvar=Db.getRecord("CUVAR")
		set AMT=cuvar.totass/100*utblsic.percent
	}
	type RecordEXPIND expind=Db.getRecord("EXPIND","IND=:IND",1)

	quit AMT-expind.totoutexp


Public AVLCOMH(ACN)	// Available versus commited - total households

	type Number HHDATA
	set HHDATA=$$HHDATA(ACN)
	type RecordCUVAR cuvar=Db.getRecord("CUVAR")

	quit (cuvar.totcap*cuvar.pctcap)/(100-HHDATA)


Public AVLOUTH(ACN)	// Available versus outstanding - total households

	type Number HHDATA
	set HHDATA=$$HHDATA(ACN)
	type RecordCUVAR cuvar=Db.getRecord("CUVAR")

	quit (cuvar.totcap*cuvar.pctcap)/(100-$P(HHDATA,"|",2))


HHDATA(ACN)	//

	type String HHDATA,HHTYP
	type Number MACN
	type RecordCIF cif=Db.getRecord("CIF","ACN=:ACN",1)
	set HHDATA=cif.totcom_"|"_cif.totout
	type ResultSet rs=Db.select("HHTYP,MACN","CIFHH0","ACN=:ACN")
	while rs.next() do {
		set HHTYP=rs.getCol("HHTYP")
		set MACN=rs.getCol("MACN")
		type RecordCIF cif1=Db.getRecord("CIF","ACN=:MACN",1)
		set $P(HHDATA,"|",1)=HHDATA+cif1.totcom
		set $P(HHDATA,"|",2)=$P(HHDATA,"|",2)+cif1.totout
	}
	quit HHDATA


Public AVLCOMS(CNTRY)	// Available versus commited - Sovereign Risk limits

	type RecordUTBLCNTRYLIM utblcntrylim=Db.getRecord("UTBLCNTRYLIM","CNTRY=:CNTRY",1)
	type RecordEXPCNSOV expcnsov=Db.getRecord("EXPCNSOV","CNTRY=:CNTRY",1)

	quit utblcntrylim.govtamt-expcnsov.totcomexp


Public AVLOUTS(CNTRY)	// Available versus outstanding - Sovereign Risk limits

	type RecordUTBLCNTRYLIM utblcntrylim=Db.getRecord("UTBLCNTRYLIM","CNTRY=:CNTRY",1)
	type RecordEXPCNSOV expcnsov=Db.getRecord("EXPCNSOV","CNTRY=:CNTRY",1)

	quit utblcntrylim.govtamt-expcnsov.totoutexp


Public LEGPOS	// CIF post processor for SCA585, SCA587

	type Public Number X
	type Boolean %EXT
	type Public Number HHACN
	set %EXT=1
	do ^UCIF quit:ER
	type ResultSet rs=Db.select("HEADACN","CIFHH","ACN=:X AND HEADACN>0")
	if rs.next() set X=rs.getCol("HEADACN")

	set HHACN=X
	quit

	

vSIG()	quit "60129^22956^Sudanthiran S. Kumar^3785"	// Signature - LTD^TIME^USER^SIZE
