Public MRPC076(String return,Number versn,String input)	// Transaction Set MRPC
	/*
	ORIG: JERUCHIMC - 09/04/98
	PROCEDURE ID: MRPC076
	DESC: Return Transaction Set TSSP message
	      
	---- Comments ----------------------------------------------------------
	
	Converts a TSSP formatted message containing a transaction that
	represents a transaction set (UTBLTRNSET) to a ttx() sequence, builds
	additional ttx() sequences based on the  transaction set definition, then
	converts the entire ttx() array back to a TSSP formatted messaage to return
	to calling program.
	      
	ARGUMENTS:
		. return	Reply transaction	/REQ/MECH=REFNAM:W
				set in TSSP format
 
                . versn		Version number		/REQ/MECH=VAL
				Current version = 1
				
		. input		Input TSSP message	/REQ/MECH=VAL
		
	RETURNS:
		. $$		Null for success,	/TYP=T
				else standard MRPC
				error return	

	EXAMPLE:
		set ermsg=$$^MRPC076(.ret,1,TSSP)

		
	------ Revision History ------------------------------------------------
	01/11/07 - chhabris - CR
		   Retrofit from ICGWeb01_Dev_Profile view for Profile Direct
		   Enhancements.
	
		   12/01/06 - SigdaE - CR23919 (ICGWeb01_Dev_Profile)
		   	Added BRCD as an argument to the call to MRPC076B. BRCD 
		   	is defined by section FMTTR^PBSTSSP.
	
	10/29/04 - RussellDS - CR12968
		   Variable vzfrn, which is required by FMTTR^PBSTSSP, was not
		   being defined.  For TSSP transaction, it is defined on entry
		   to PBSTSSP.  In this case, the MRPC needs to define.
	
	08/17/04 - RussellDS - CR11678
		   Rewrote to PSL.  Modified significantly for more efficient
		   approach.  PSL compilation of MRPC076 will now trigger the
		   generation of MRPC076A, which contains the run-time code
		   implementation of the transaction sets defined in table
		   UTBLTRNSET.  This eliminates much of the need for extensive
		   database references required under the old approach.
		   
		   Removed old revision history.
		   
	------------------------------------------------------------------------
	*/

	/*
	MRPC076 relies on calls to MRPC076B, which is generated by procedure
	MRPC076A (MRPC076B Builder).  MRPC076A uses the information contained
	in table UTBLTRNSET to build MRPC076B.
	   
	The following code ensures that procedure MRPC076A has been compiled,
	and then calls it to build MRPC076B.  This code is only executed when
	MRPC076 is compiled.
	*/
 
        // Compile procedure MRPC076A (MRPCO76B Builder)
        #XECUTE do COMPILE^DBSPROC("MRPC076A")
 
        // Generated MRPC076B (from UTBLTRNSET definitions)
        #XECUTE do ^MRPC076A
        
        
        // Start of main code section -----------------------------------------
        
        type Public Number ER
        type Public String ET,RM
        
        type Boolean vzfrn = 0
        type Number BRCD
        type String ERMSG,LVL1(),LVL2(),PROT,REPLY,TR(),TSSPMSG,X

	// Unpack the incoming message twice to get to the transaction
	set X = $$LV2V^MSG(input,.LVL1)
	set X = $$LV2V^MSG(LVL1(1),.LVL2)
	set TSSPMSG = LVL2(1)
	
	// Convert the TSSP formatted message to a TR sequence.
	type RecordTTX ttx()
	set ttx(1) = Class.new("RecordTTX")

	// Format the first transaction in the message
	set ER = $$FMTTR^PBSTSSP(1,TSSPMSG,.ttx(1))
	if ER quit $$ERRMSG^PBSUTL(RM.get(),ET.get())

	// Section FMTTR defines BRCD
	set BRCD=BRCD.get()
	
	set ERMSG = $$BLDSET(.ttx(),BRCD,.return)	// Build transaction set return

	if 'ERMSG.isNull() quit $$ERRMSG^PBSUTL(ERMSG)
	
	quit ""					// Success
	

Private BLDSET(RecordTTX ttx(),Number BRCD,String return)	// Build transaction set
	/*
	ARGUMENTS:
		. ttx()		Transaction set		/REQ/MECH=REFNAM:RW
				contains one transaction
				on input, the full set
				on return

		. BRCD		Branch Code		/REQ/MECH=VAL
				
		. return	Return string from	/REQ/MECH=REFNAM:W
				MRPC - LV TSSP
		
	RETURNS:
		. $$		Error message,		/TYP=L
				null indicates
				no error
	*/

	type String ERMSG,TRNSET

	set ERMSG = ""

	// Get the transaction set for this transaction
	type RecordTRN trn = Db.getRecord("TRN","ETC = :ttx(1).etc",1)
	
	set TRNSET = trn.trnset

	// No transaction set defined for transaction code ~p1
	if TRNSET.isNull() quit $$^MSG(3626,ttx(1).etc)

	// Transaction Set Pointers
	type RecordTRNSETPTR tsetptr = Db.getRecord("TRNSETPTR","PTRNAME = :TRNSET",1)
	if tsetptr.getMode() do { if 'ERMSG.isNull() quit ERMSG

		type String LNCOL = tsetptr.lnptr
		type String PRCOL = tsetptr.prodptr
		
		set TRNSET = ""

		type RecordLN ln = Db.getRecord("LN","CID = :ttx(1).cid")

		// If LN column pointer, get the transet from that column
		if 'tsetptr.lnptr.isNull() do {
		
			type String COL = tsetptr.lnptr
			set TRNSET = ln.@COL
		}

		// If not transet from LN, and Product pointer, get it
		if TRNSET.isNull(), 'tsetptr.prodptr.isNull() do {
		
			type String COL = tsetptr.prodptr
			type RecordPRODDFTL proddftl = Db.getRecord("PRODDFTL","TYPE = :ln.type")
			set TRNSET = proddftl.@COL
		}
		
		// If TRNSET is null or if it contains another pointer, it's an error
		
		// Valid transaction set not defined on this loan or its product type
		if TRNSET="" set ERMSG = $$^MSG(4352)
		else  if Db.isDefined("TRNSETPTR","PTRNAME = :TRNSET") set ERMSG=$$^MSG(4352)
	}
	
	// Call MRPC076B to build out transaction set and return message
	set ERMSG = $$^MRPC076B(TRNSET,.ttx(),BRCD,.return)

 	quit ERMSG

vSIG()	quit "60646^49376^Vanitha Krishnasamy^5112"	// Signature - LTD^TIME^USER^SIZE
