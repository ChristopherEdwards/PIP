LNPTS9(RecordLN ln,RecordTTX ttx)	

	/*
		Loan Late Charge Assessment

	       ORIG:
	       DESC:
	
	---- Revision History -------------------------------------------------
	
	05/15/06 - KELLYP - CR 21249
		   Modified SET section to set LN.DLCAF=0 instead of null. Since
		   DLCAF is a logical field, it must always be defined.
	
	03/08/06 - TITOVE - CR 19832
		   Modified SET section to correctly update LN.DLCAF
		   and prevent null subscript when POPT is null.

	02/26/06 - TITOVE - CR 19733
		   Modified PRBLN section to pass correct arguments to
		   CURRCID^LNPTS8.


	*/

	new ICPA,POPT,PAOI
	
	do INIT(.ln,.ttx)
	do PROC(.ln,.ttx)
	do FILE(.ln,.ttx)
	quit 

	
INIT(RecordLN ln,RecordTTX ttx) // Initialize variables

	new TSO

	set BALINT=ln.balint
	set POPT=ln.popt
	set GRP=ln.grp
	set ICPA=ln.icpa
	set PAOI=ln.paoi
	if ICPA'="" do {
		Type RecordUTBLICPA utblicpa=Db.getRecord("UTBLICPA","KEY=:ICPA")
		set ICPA=utblicpa.prio
		}
	if POPT'="" do {
		Type RecordLNPOPT lnpopt=Db.getRecord("LNPOPT","GRP,POPT")		
		set POPT=lnpopt.popt	
		}
	set TSO=$$FIELDIN^UTSO(ttx.tso,"LCHGADJ")
	set ttx.tso=TSO
	
	quit 
	
PROC(RecordLN ln,RecordTTX ttx)	// Processing

	if $E(ICPA,6) set BALINT=ln.balint+ZAMT
	
	do GL^LNPTSU(.ttx,ZAMT,4)
	do %HSEQ^LNPTSU(.ttx,"*#L#"_ZAMT)
	quit 

	
FILE(RecordLN ln,RecordTTX ttx)	// File data

	do SET(.ln,POPT,TAMT,%EffectiveDate)
	
	set ZAMT=0
	if PAOI=1 do PRBLN(.ln,.ttx)
	
	quit 

	
public SET(RecordLN ln,
	   String POPT,
	   Number AMT,
	   Date %EffectiveDate)

	// Update YTD and Life counters
	type public Date CUVAR2
	type public Number BALINT
	
	type Number FIN1

	// # Assessed Life
	set ln.lcanlf = ln.lcanlf + 1

	// $ Assessed Life
	set ln.papl = ln.papl + AMT

	set FIN1 = $$BOFY^SCADAT(CUVAR2, 1)

	if (CUVAR.yeoff)&(%EffectiveDate < FIN1)&(CUVAR2 '> ((FIN1 + CUVAR.yeoff) - 1)) set ln.papy = ln.papy + AMT
	else  do {
		// # Assessed YTD
		set ln.lcan = ln.lcan + 1

		// $ Assessed YTD
		set ln.paytd = ln.paytd + AMT
		}
	/*
	Late charge assessed tax year-date and prior tax year are NOT defined
	fields in functional spec. for tax year so far, else will be set here.
	*/	

	// Update miscellaneous account fields

	// Int Calc Bal
	set ln.balint = BALINT

	// Late Charge Due
	set ln.lchg = ln.lchg + AMT

	// Chg Assessed Thru
	set ln.lcla = %EffectiveDate

	// Daily Chg Flag
	if ln.bseq do {

		set ln.dlcaf = 0
		
		if POPT.isNull() quit
		
		type RecordLNPOPT lnpopt = Db.getRecord("LNPOPT", "GRP = :ln.grp, POPT = :POPT", 1)
		
		if lnpopt.cdf set ln.dlcaf = 1
		}
	
	quit


PRBLN(RecordLN ln,RecordTTX ttx) // Apply late charges to linked current sub-account for problem loans

	new CURRCID,TSO,SUBTLO

	set CURRCID=$$CURRCID^LNPTS8(ln.cid,%EffectiveDate)
	quit:'CURRCID 
	Type RecordLN ln1=Db.getRecord("LN","CID=:CURRCID")
	set TYPE=ln1.type
	set CRCD=ln1.crcd
	set GLSC=ln1.glsc

	// DRTRPE - Late charge debit
	Type RecordPRODCTL prodctl=Db.getRecord("PRODCTL","TYPE")
	set LTC=prodctl.drtrpe
	set OTC="MCR"

	// LGLL - late charge income
	Type RecordUTBLGLSC utblglsc=Db.getRecord("UTBLGLSC","GLSC")
	set OCID=utblglsc.lgll
	
	set TSO=""
	set TSO=$$FIELDIN^UTSO(TSO,"LCHGADJ")
	if $D(%UserStation) set SUBTLO=%UserStation
	else  set SUBTLO=$$TLO^UTLO

	Type RecordTTX ttxpri
	set ttxpri=Class.new("RecordTTX")
	do POST^LNTRB(.ttxpri,CURRCID,LTC,TAMT,%EffectiveDate,SUBTLO,TSO,,CRCD)
	do POST^LNTRB(.ttxpri,OCID,OTC,TAMT,%EffectiveDate,SUBTLO,TSO,,CRCD)
	quit 

vSIG()	quit "60400^38031^Pat Kelly^3241"	// Signature - LTD^TIME^USER^SIZE
