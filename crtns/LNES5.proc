LNES5	// Escrow payee maintenance
	/*
	       ORIG:  Neal E. Gorman (5053) - 04/26/87

	  ---- Revision History ------------------------------------------------
	
		31/01/06 - Srinivar - CR19137
			   Cleaned up the warnings.
			   Modified the code to conform to PSL standards.
			   
		
		09/18/02 - DATTAR - 49451
			   Converted to PSL
	  ----------------------------------------------------------------------
	 */
	
	do NEW 
	
	quit
	
NEW	//
	do INIT(0)
	quit


UPD	//
        do INIT(1)
        quit
 

INQ     //
        do INIT(2)
        quit


INIT(%ProcessMode) //

	type String VFMQ
	type Number %PG,%PAGE
	
	set %PG=0 
	set %PAGE=1
	type RecordLNTRS1 fLNTRS1
	do VPG(.fLNTRS1)
	quit


VPG(RecordLNTRS1 fLNTRS1) // Page control
	
	type public Boolean ER
	type public Number %PG
	type public String VFMQ
	
	type Boolean FINISH
	
	
	set FINISH=0
	for  do { quit:FINISH=1
	      	if %PG=0 do VPG00 if (VFMQ="Q")!ER set FINISH=1 quit
              	if %PG>0 do VPG01(.fLNTRS1)
        	if "DFQ"[VFMQ do VER(.fLNTRS1) set FINISH=1 quit
                set %PG=%PG+1
                }
	quit		


VPG00	// Set up
	
	type public String IO,%READ,%TAB,VFMQ
	
	set %TAB("TRTYPE")=".TRTYPE1/TBL=[LNTRS]"
	if %ProcessMode set %TAB("PAYID")=".PAYID2/TBL=[LNTRS1]/XPP=D DEFREC^LNES5(TRTYPE,X)"
	else  set %TAB("PAYID")=".PAYID2/TBL=[LNTRS1]:NOVAL/XPP=D DEFREC^LNES5(TRTYPE,X)" 
	if %ProcessMode=2 set %TAB("IO")=$$IO^SCATAB($I)
	set %READ="@@%FN,,,TRTYPE/REQ,PAYID/REQ"
	if %ProcessMode=2 set %READ=%READ_",IO/REQ"

	do ^UTLREAD 

	if VFMQ="Q" quit

	if %ProcessMode=2,IO'=$I do OPEN^SCAIO
	quit


DEFREC(TRTYPE,PAYID) //Check for record exist or not
	
	type public Number %OSAVE
		
	type RecordLNTRS1 lntrs1=Db.getRecord("LNTRS1","TRTYPE=:TRTYPE,PAYID=:PAYID",1)
	if lntrs1.getMode(),'%OSAVE do {
		do Runtime.setErrSTBLER("LNTRS1","RECOF")
	}
	quit


VPG01(RecordLNTRS1 fLNTRS1)	// Screen
	
	type public String TRTYPE,PAYID
	
	set fLNTRS1=Db.getRecord("LNTRS1","TRTYPE=:TRTYPE,PAYID=:PAYID",1)
	do fLNTRS1.setAuditFlag(1)		

	do DRV^USID(%ProcessMode,"LNTRS1",.fLNTRS1)

	quit


ERR	//
	
	type public Boolean ER
	type public String VFMQ
	
	set ER=1 
	do DSPBP^UTLERR

	set VFMQ="Q"
	quit


VER(RecordLNTRS1 fLNTRS1) //
 	
 	type public String VFMQ
 	
 	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit
	do FILE(.fLNTRS1)
	do END 
	quit


FILE(RecordLNTRS1 fLNTRS1) // File data
	
	type public String TRTYPE,PAYID,ET,UTBL()
	
	lock +UTBL("LNTRS",TRTYPE,PAYID):2 
	else  set ET="RECLOC" do ERR quit
	
	// Date Last Due - Remittance
	if '%ProcessMode!(fLNTRS1.remld.isNull()) do F2(.fLNTRS1)
	do fLNTRS1.bypassSave()
	quit


END	//
	
	
	type public String PAYID,RM,%TAB,VFMQ
	type public Boolean ER
	
	kill %TAB lock 
	if ER!(%ProcessMode=2)!(%ProcessMode=4) quit
	set PAYID=$G(PAYID)

	if VFMQ="Q" do {

		// Escrow payee ~p1 not created
		if %ProcessMode=0 set RM=$$^MSG(1009,PAYID) quit

		// Escrow payee ~p1 not modified
		if %ProcessMode=1 set RM=$$^MSG(1011,PAYID) quit

		// Escrow payee ~p1 not deleted
		set RM=$$^MSG(1010,PAYID)
		}
	else  do {
		// Escrow payee ~p1 created
		if %ProcessMode=0 set RM=$$^MSG(1006,PAYID) quit

		// Escrow payee ~p1 modified
		if %ProcessMode=1 set RM=$$^MSG(1008,PAYID) quit

		// Escrow payee ~p1 deleted
		set RM=$$^MSG(1007,PAYID)
		}
	set ER="W"
	quit


F2(RecordLNTRS1 fLNTRS1) //
 
        type public Date NJD
        type public String AF
        
        type String FRE
        type Date JD
        
        // Date Next Due - Remittance
       
        set FRE="-"_fLNTRS1.cremfre
        set JD=fLNTRS1.remnd
        do ^UFRE
 
        // Remittance Annual Factor
        if '%ProcessMode set fLNTRS1.raf=AF
 
        // Date Next Due - Remittance
        set FRE="-"_fLNTRS1.remfre
        set JD=fLNTRS1.remnd
        do ^UFRE

	if fLNTRS1.remld.isNull() set fLNTRS1.remld=NJD
	quit

vSIG()	quit "60303^24274^Srinivasan, Rajesh^3611"	// Signature - LTD^TIME^USER^SIZE
