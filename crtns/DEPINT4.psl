DEPINT4(RecordDEP dep,RecordTTX ttx,RecordTRN trn)	// Decrease Capitalized Interest Available
	/*

	   DESC: Adjusts (decreases) the Interest Available Not Credited field
	   on an account.  The basic tran codes that use this routine are
	   DRIANC, DRIANCD, and DRIANCS.  Routine DEPINT5 is used for
	   increasing Available Interest Not Credited.

	   KEYWORDS: Interest, Available, Adjustment

	   INPUTS:
		. dep  deposit account object		/TYP=RecordDEP

		. ttx  transaction object		/TYP=RecordTTX

		. trn  transaction code			/TYP=RecordTRN

	   RETURNS:

	   RELATED:

	   EXAMPLE:
	   do ^DEPINT4(.dep,.ttx,.trn)
	   do EC^DEPINT4(.dep,.ttx,.trn)

	---- Revision History ------------------------------------------------
	
	11/29/06 - MbuiM - CR 21675
		   Changed reference of INTAVLNC to INTAVLNCR to comply with 
		   GTM version 5.0.

	12/13/02 - ZWITKOWITSM - 51349
		   Correct call to CYRINT^IRA.

	11/14/02 - ZWITKOWITSM - 43583
		   PSL clean-up.

	06/13/02 - ZWITKOWITSM - 43583
		   Converted to PSL.

	----------------------------------------------------------------------
	*/

	new TAMT,%EffectiveDate

	do INIT(.dep,.ttx)
	if ER quit

	// IRA earnings adj
	if dep.ira do CYRINT^IRA(.dep,.ttx,1) if ER quit

	do AUT(.dep)
	if ER quit

	set $P(TAMT,"#",8)=+TAMT

	if dep.iaf do INTAVL(.dep,.ttx)

	do FILE(.dep,.ttx)

	quit


FILE(RecordDEP dep,RecordTTX ttx)	// File Data

	set ttx.tamt=TAMT
	set dep.tld=%SystemDate

	quit


INTAVL(RecordDEP dep,RecordTTX ttx)	// Interest Available Not Credited

	new SAVEC

	set SAVEC=ttx.lnerc
	set $P(SAVEC,"#",1)=$S(TAMT<dep.intavlncr:TAMT,1:dep.intavlncr)
	set ttx.lnerc=SAVEC

	if TAMT<dep.intavlncr set dep.intavlncr=dep.intavlncr-TAMT
	else  set dep.intavlncr=0

	quit


INIT(RecordDEP dep,RecordTTX ttx) // Initialize data

	// Withdrawal of interest/dividend not restricted
	if 'dep.iaf do Runtime.setErrMSG("DEP",2978) quit

	// Not allowed for interest available option 2
	if dep.iaf=1 do Runtime.setErrMSG("DEP",8448) quit

	set TAMT=ttx.tamt
	set %EffectiveDate=ttx.efd

	quit


Public EC(RecordDEP dep,RecordTTX ttx,RecordTRN trn)	// Error Correct

	/*
	   INPUTS:
		. dep  deposit account object		/TYP=RecordDEP

		. ttx  transaction object		/TYP=RecordTTX

		. trn  transaction code			/TYP=RecordTRN

	   EXAMPLE:
		do EC^DEPINT4(.dep,.ttx,.trn)

	*/

	do INIT(.dep,.ttx)

	if dep.iaf=2 set dep.intavlncr=dep.intavlncr+ttx.lnerc

	do FILE(.dep,.ttx)

	quit


AUT(RecordDEP dep)	// Authorization Verification

	// Restriction Flag
	if +dep.rflg do ^UFLG(.dep)

	if dep.iaf=2,TAMT>dep.intavlncr do { quit:ER

		new INTAVLNCR

		set INTAVLNCR=$$^SCARND(dep.intavlncr,0,dep.cid)

		// Transaction Amount of ~p1 is Greater than the Available Amount of ~p2
		do Runtime.setErrMSG("DEP",7817,"TAMT~INTAVLNCR") quit
		}

	if dep.stat set OVR(dep.cid,"OVR","STAT"_+dep.stat)=""

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60598^43478^Marie Mbui^2613"	// Signature - LTD^TIME^USER^SIZE
