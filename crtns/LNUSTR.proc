LNUSTR	  /*
ORIG: GRAY - 04/30/2001
DESC: Convert account's payment string or path

---- Comments --------------------------------------------------------

---- Revision History ------------------------------------------------

       01/21/06 - KumarSS - 19103
       	          Modified VPG01 section to refer the LNPAS2 table instead 
           	  of PAS2 table for the /TAB value for %TAB("PAS").

	08/08/05 - KELLYP - CR 13278
		   Modified entire procedure to conform to current
		   PSL standards & removed pre-2003 revision history.
 */

	type Number %PAGE,%PG,CID,OLNTB
	type String %PAP,%PAS,PAP,PAS,VFMQ

	set %PG=0,%PAGE=1

	do VPG00 if "DFQ"[VFMQ.get() do VER quit
	set %PG = %PG + 1
       
	do VPG01
       
	do VER

	quit
	
	
VPG00	// Set up

	type public Boolean ER
	type public String %PAP,%PAS,VFMQ

	type String %READ,%TAB()
	
	set %TAB("CID")=".CID1/XPP=set %EXT=1,ZCLS=""L"" do ^UACN"
	set %READ="@@%FN,,,CID/REQ"

	do ^UTLREAD 

	if VFMQ="Q" quit

	type ResultSet rs=Db.select("PAS,PAP","LN","CID=:CID")
	
	if rs.next() do {
		set %PAS=rs.getCol(1)
		set %PAP=rs.getCol(2)
		}

	quit
	
	
VPG01	// Screen

	type public Number OLNTB
	type public String %PAP,%PAS,PAP,PAS,VFMQ

	type String %READ,%TAB(),CURR

	set OLNTB=05030
	
	type RecordLNPAS2 lnpas2=Db.getRecord("LNPAS2","TABLE=:%PAS")

	// Current payment string: ~p1
	set CURR="".justify(6)_$$^MSG("4256",lnpas2.distrib)
	
	set %TAB("PAS")=".PAS1/HLP=[LN]PAS/TBL=[LNPAS2]DISTRIB"
	set %TAB("PAP")=".PAP1/HLP=[LN]PAP/TBL=[LNPATH]PATH,[LNPATH]DESC:QU ""[LNPATH]TABLE=<<PAS>>"""
	
	set %READ="@CURR,,PAS/REQ,,PAP/REQ"
	do ^UTLREAD 
	
	if VFMQ="Q" quit
	
	if PAS'=%PAS quit
	if PAP'=%PAP quit
	
	set VFMQ="Q" 
	
	// No change to path or string.  Conversion not processed.
	do Runtime.setErrMSG("LN","1904")
	
	quit
  	
  	
VER	//

	type public String VFMQ

	if VFMQ="Q" do END quit
	do FILE
	do END
	quit
	
	
FILE	// File data

	type public Number CID
	type public String %PAP,%PAS,PAP,PAS

	type RecordLN ln=Db.getRecord("LN","CID=:CID")
	
	do ln.setAuditFlag(1)
	
	if PAP'=%PAP set ln.pap=PAP
	if PAS'=%PAS set ln.pas=PAS
	
	do ln.save()
	
	quit
	
	
END	// End processing

	type public Number CID
	type public String ER,VFMQ
	
	quit:ER
	
	// Account ~p1 not converted
	if VFMQ="Q" do Runtime.setErrMSG("LN",127,CID.get())
	
	// Account ~p1 converted
	if VFMQ'="Q"  do Runtime.setErrMSG("LN",105,CID)
	
	set ER="W"
	
	quit

vSIG()	quit "60288^42832^Sudanthiran S. Kumar^2256"	// Signature - LTD^TIME^USER^SIZE
