REGD	//Private;Reg 'D' Report
	/*
	   ORIG:  Tom Locke - 07/14/86

	  ---- Revision History   ----------------------------------------------
	   12/02/05 - SWARNALP - CR16620
	   	      Code changed to correct run time errors.

	   12/29/03 - CARROLLJ - CR7658
		      Added #ACCEPT prior to setting %SystemDate.

	   06/26/02 - SRIVASTAVAN - 49794
		      Converted to PSL

	   06/18/02 - BECKERJ - 49998	   
	 	      Reverse change made in ARQ 46062. Nonpersonal MMDA's
		      should be displayed under "F.2 Total Nonpers Sav/Time
		      Deps".

	   08/23/01 - MENYAJ - 46062
	              In LOAD section, removed option LN=5, to exclude
	              Nonpersonal MMDA's from being displayed under header
	       	      "F.2 Total Nonpers Sav/Time Deps".

	   09/19/00 - VETSENM - 41933
	              Routine has been modified to set %0 and PC variables
	              in section LOAD to prevent undefined error.

	*/
INIT	//
	type Number PID
	type Date TJD1
		
	set PID=%ProcessID
	
	set XJD=%SystemDate

	#ACCEPT DATE=12/29/03;PGM=John Carroll
	set TJD1=%SystemDate-7
 
	#ACCEPT DATE=12/29/03;PGM=John Carroll
	for TJD1=TJD1:-1 quit:((TJD1#7)=5)
	
	kill %EXT,OLNTB,REGD
	
	do Db.delete("TMPREGD","PID=:PID")
	
	set %ProcessMode=0 
	set PC=1
	
	set RID="SCA008"
	do DRV^URID
	
	quit

Public LOAD	// Preload array
	// This section is called by the report pre-processor for SCA008.
	
	type public Date TJD2
	
	set %ProcessMode=0 
	set PC=1
	do HDG
	for JD=TJD2:1:TJD2+6 set PC=PC+1 do ACCUM

	// Total demand deps
	set XLN=3 for PC=2:1:9 for LN=0,1 do TOT

	// Total savings/time dep
	set XLN=15 for PC=2:1:9 for LN=5,7,10,11 do TOT

FILE	// File local array into ^TMP($J, for report writer
	
	for I=5,7,10,11 kill REGD(I)
	set N="" 
	for  set N=$O(REGD(N)) quit:N=""  do {
		type RecordTMPREGD tmpregd=Class.new("RecordTMPREGD")
		set tmpregd.pid=%ProcessID
		set tmpregd.seq=N
		set tmpregd.desc=$P(REGD(N),"|",1)
		set tmpregd.tutho=$P(REGD(N),"|",2)
		set tmpregd.wtho=$P(REGD(N),"|",3)
		set tmpregd.ththo=$P(REGD(N),"|",4)
		set tmpregd.ftho=$P(REGD(N),"|",5)
		set tmpregd.satho=$P(REGD(N),"|",6)
		set tmpregd.sutho=$P(REGD(N),"|",7)
		set tmpregd.mtho=$P(REGD(N),"|",8)
		set tmpregd.totho=$P(REGD(N),"|",9)
		do tmpregd.bypassSave()
		}
	quit

ACCUM	// Accumulate REG "D" totals
	
	//Demand Deps 
	set LN2=0
	set LN=0 
	set REGD=3 
	for PERS=0,1 do CALC
	
	//ATS and NOW Accts
	set LN2=0 
	set LN=1
	set REGD=1 
	for PERS=0,1 do CALC
	
	//Telephone and PAT
	set LN2=0 
	set LN=1 
	set REGD=2 
	for PERS=0,1 do CALC
	
	//Money Market Accts (personal)
	set LN2=0 
	set LN=8 
	set PERS=0 
	set REGD=5 do CALC
	
	//Money Market Accts (non-personal)
	set LN2=5 
	set LN=8 
	set PERS=1 
	set REGD=5 do CALC
	
	//Other Savings Deps (personal)
	set LN2=0 
	set LN=8 
	set PERS=0 
	set REGD=4 do CALC
	
	//Other Savings Deps (non-personal)
	set LN2=7 
	set LN=8 
	set PERS=1 
	set REGD=4 do CALC
	
	//Personal time deps
	set LN2=0 
	set LN=12 
	set PERS=0 
	set REGD=6 
	for TD=0:1:3 do CALC
	
	//Time deps < 1.5 years
	set LN2=10 
	set LN=12 
	set PERS=1 
	set REGD=6 
	for TD=0,3 do CALC
	
	//Time deps > 1.5 years
	set LN2=11 
	set LN=12 
	set PERS=1 
	set REGD=6 
	for TD=1,2 do CALC
	
	//Time deps > $100,000
	set LN2=0 
	set LN=14 
	set REGD=6 
	for PERS=0,1 for TD=2,3 do CALC
	
	set LN2=0 
	set LN=16 
	set REGD=0 
	for PERS=0,1 do CALC

CSH	// Vault cash
		
	new BRCD,CSH,SJD,TPRF,UID
	set LN=13 
	set (BRCD,UID)=""
	set CSH=0
		

BRCD	//
	
	type ResultSet rs=Db.select("BRCD,UID","TPCASH",,"BRCD,UID")
        if 'rs.isEmpty() while rs.next()  do {
        	set TPRF=0
		set BRCD=rs.getCol(1)
       		set UID=rs.getCol(2)
 		set SJD=JD+1
		type RecordTPCASH tpcash=Db.getRecord("TPCASH","BRCD=:BRCD,UID=:UID,TPD=:JD,CRCD='USD'",1)
		if tpcash.getMode() do {
			set TPRF=tpcash.ectot
			if TPRF="" do {
				type ResultSet tpd=Db.select("TPD,ECTOT","TPCASH","BRCD=:BRCD AND UID=:UID AND TPD>:SJD")
	        		if 'tpd.isEmpty() while tpd.next()  do { quit:TPRF'=""
	               			set SJD=tpd.getCol(1)
					set TPRF=tpd.getCol(2)
	                     		}
				}
			}
		set CSH=CSH+TPRF
   		}
	
	do VAULT 

	do SUM 
	
	quit

VAULT	// Vault cash
	set X=CSH+500 set THO=X\1000
	set $P(REGD(LN),"|",PC)=THO
	quit

CALC	//
	if 'Db.isDefined("REGD","TJD=:JD,PERS=:PERS,REGD=:REGD") quit

	if REGD=6 do {
		type RecordREGDD regdd=Db.getRecord("REGDD","TJD=:JD,PERS=:PERS,REGD=:REGD,CAT=:TD",1)
		if regdd.getMode() set AMT=+regdd.tamt
		}
	
	else  do {
		type RecordREGD regd=Db.getRecord("REGD","TJD=:JD,PERS=:PERS,REGD=:REGD")
                set AMT=+regd.tamt
		}
	
	set X=AMT+500 
	set THO=X\1000
	set $P(REGD(LN),"|",PC)=$P(REGD(LN),"|",PC)+THO
	if LN2 set $P(REGD(LN2),"|",PC)=$P(REGD(LN2),"|",PC)+THO

SUM	// Summary totals for week
	set $P(REGD(LN),"|",9)=$P(REGD(LN),"|",9)+THO
	if LN2 set $P(REGD(LN2),"|",9)=$P(REGD(LN2),"|",9)+THO
	quit

TOT	// 
	set $P(REGD(XLN),"|",PC)=$P(REGD(XLN),"|",PC)+$P(REGD(LN),"|",PC)
	quit

HDG	// Line descriptions
	//I18N=OFF
	
	set REGD(0)="A.1.C Other Demand Deps"
	set REGD(1)="A.2   ATS/NOW/Drafts/Phone/PAT's"
	set REGD(3)="A.3   Total Transaction Accts"
	set REGD(5)="C.2   Nonpersonal MMDAs"
	set REGD(7)="C.4   Nonpersonal Savings"
	set REGD(8)="C.1   Total Other Savings"
	set REGD(10)="D.2   Nonpers Time Deps < 1.5 yrs"
	set REGD(11)="D.3   Nonpers Time Deps > 1.5 yrs"
	set REGD(12)="D.1   Total Time Deps"
	set REGD(13)="E.1   Vault Cash"
	set REGD(14)="F.1   Time Deps > or = $100,000"
	set REGD(15)="F.2   Total Nonpers Sav/Time Deps"
	set REGD(16)="      Unassigned"

	//I18N=ON
	quit

vSIG()	quit "60236^29271^P.R. Swarnalatha^5275"	// Signature - LTD^TIME^USER^SIZE
