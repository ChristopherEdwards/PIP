IRSC99C		/*
	ORIG: Laura Hillanbrand
	DESC: IRS Processing - Form 1099-C Correction
	I18N=QUIT: Excluded from I18N standards.

	---- Comments --------------------------------------------------------
	======================================================================
		*** THIS ROUTINE IS TO BE COMPILED WITH IRSTPMTR ***
		     *** DO NOT RUN THIS ROUTINE STANDALONE ***
	======================================================================
	
	---- Revision History ------------------------------------------------
	
	05/08/06 - TITOVE - CR 21140
		   Modified BRECBLD to set "C" corrected return indicator
		   for a second step of a two-step correction.

	09/28/05 - HAILEYM - CR17143
		Converted to PSL.
		
	*/
	
	// Accept "Dead code" warning during runtime of @IRSTAPE.
	#ACCEPT Date=10/14/2005;PGM=Marie Hailey;CR=17143
	quit
	
	
MTRCNTRL	// Master control

	type public Boolean ER

	type Number PASSFLG
	
	do Db.fastDelete("B1099C")
	
	set PASSFLG=1
	do ARECBLD quit:ER	// Build and write the "A" record - First pass
	do BRECBLD quit:ER	// Build and write the "B" records - First pass
	do CRECBLD quit:ER	// Build and write the "C" record - First pass
	
	set PASSFLG=2
	do ARECBLD quit:ER	// Build and write the "A" record - Second pass
	do BRECBLD quit:ER	// Build and write the "B" records - Second pass
	do CRECBLD quit:ER	// Build and write the "C" record - Second pass
	
	do STORETOT		// Store the TTR report totals
	quit
	
	
ARECBLD	// "A" record builder
	
	type public String AMTIND,FORMCODE,TAMT()
	
	set FORMCODE=4	// IRS form code
	set AMTIND=237	// IRS amount type indicator
	set TAMT(2).piece("|",2)="Amount of Debt Cancelled"
	set TAMT(3).piece("|",2)="Charge-off Interest Amount"
	set TAMT(7).piece("|",2)="Fair Market Value of Property"
	do ARECWRT	// Write the record to tape
	quit
	
	
BRECBLD	// "B" record builder

	type public Date FORMS()
	type public Number AMT(),PASSFLG
	type public String FORM
	
	type Date DATE,JD,XJD
	type Number CID,COTOT,I
	type String ADDR,BIND,CITY,CNTRY,CORRTN,DOCCODE,FORCNTY,FULLADDR,LNAME
	type String NAME,SPECIAL,STATE,TIN,TINTYPE,X,XTIN,ZIP
	
	type DbSet ds
	type RecordB1099C b1099c
	type RecordIRSUPD irsupd
	type RecordM1099 m1099
	
	set XJD=FORMS(FORM).get()
	
	for I=1:1:9,"A","B","C" set AMT(I)=0
	
	set DOCCODE="  "
	set CORRTN=$select(PASSFLG=1:"G",PASSFLG=2:"C")
	
	set b1099c=Class.new("RecordB1099C")
	
	set ds=Db.selectDbSet("IRSUPD","TJD NOT <:XJD AND FTYPE=11")
	while ds.next() do {
		set irsupd=ds.getRecord("IRSUPD")
		
		if irsupd.ccode=1,(PASSFLG=1) quit	// Skip duplicates when writting to tape
		if irsupd.ccode=4,(PASSFLG=1) quit	// Skip "New Entry" for first pass
		if irsupd.ccode=3,(PASSFLG=2) quit	// Skip "change amount" for second pass
		
		if PASSFLG=1 set CID=irsupd.obseq,TIN=irsupd.otin
		if PASSFLG=2 set CID=irsupd.bseq,TIN=irsupd.miscseq
		
		set TINTYPE=$select(TIN?2N1"-"7N:1,TIN?3N1"-"2N1"-"4N:2,1:" ")
		set XTIN=TIN.piece("|",1).translate(" ","-")
		
		set m1099=Db.getRecord("M1099","TAXID=:TIN,SEQ=:CID",1)
		
		set COTOT=m1099.coprin+m1099.coint+m1099.cofee
		
		if PASSFLG=1,(irsupd.ccode=2) set (AMT(2),AMT(3),AMT(7))=0
		else  set AMT(2)=COTOT*100,AMT(3)=m1099.coint*100,AMT(7)=m1099.fmktv*100
		
		if PASSFLG=1 set NAME=irsupd.oname,LNAME=irsupd.olnam	
		if PASSFLG=2 set NAME=m1099.nam,LNAME=m1099.lnam
		set FULLADDR=m1099.ad1_"|"_m1099.ad2_"|"_m1099.ad3_"|"_m1099.city_"|"_m1099.state_"|"_m1099.mzip
		set ADDR=m1099.ad1_" "_m1099.ad2_" "_m1099.ad3
		set CITY=m1099.city
		set STATE=m1099.state
		set ZIP=m1099.mzip.piece("-",1)_m1099.mzip.piece("-",2)
		set CNTRY=m1099.cntry
		set FORCNTY=$select(CNTRY="US":" ",1:1)	// Foreign country indicator
		set BIND=(m1099.bankrup_" ").extract(1)
		set DATE=m1099.td
		
		// ---------  Special Data Entry Section  --------
		//; Blank|544|546
		set SPECIAL="".justify(3)
		//;Bankruptcy Indicator |547
		set SPECIAL=SPECIAL_BIND
		//; Date Cancelled|548|555
		set SPECIAL=SPECIAL_DATE.toString("YEARMMDD")
		//; Debt Description|556|594
		set X=m1099.desc_" "_m1099.desc2_" "_m1099.desc3
		set SPECIAL=SPECIAL_X.justify(39,-1,,1)
		//; Blank|595|662
		set SPECIAL=SPECIAL_"".justify(68)
		//; Special Data Entries|663|722
		set SPECIAL=SPECIAL_"".justify(60)
		//; Blank|723|750
		set SPECIAL=SPECIAL_"".justify(28)
		
		if irsupd.ccode<>1 do BRECWRT	// Skip duplicates
	
		if TINTYPE=2 set X=LNAME
		else  set X=NAME
		do NAMCNTRL^IRSTPMTR set b1099c.bnamcon=X
				
		// Inserting "B" Records into B1099C File.
		/*
		B1099C.BDC date will be in MM/DD/YY format because 1099PRO 
		accept only xx/xx/xx date format.
		*/
		if b1099c.getMode() do b1099c.setMode(0)
		set b1099c.pdate=irsupd.tjd
		set b1099c.btaxid=XTIN
		set b1099c.bseq=m1099.seq
		set b1099c.btintype=TINTYPE
		set b1099c.btin=XTIN
		set b1099c.bcid=CID
		set b1099c.bamt2=AMT(2)/100
		set b1099c.bamt3=AMT(3)/100
		set b1099c.bamt7=AMT(7)/100
		set b1099c.bname=NAME
		set b1099c.baddr1=m1099.ad1
		set b1099c.baddr2=m1099.ad2
		set b1099c.baddr3=m1099.ad3
		set b1099c.bcity=CITY
		set b1099c.bstate=STATE
		set b1099c.bzip=m1099.mzip
		set b1099c.bbi=BIND
		set b1099c.bdc=DATE.toString("MM/DD/YY")
		set b1099c.bdd1=m1099.desc
		set b1099c.bdd2=m1099.desc2
		set b1099c.bdd3=m1099.desc3
		do b1099c.bypassSave()
		}
	quit
	
	
CRECBLD	// "C" record builder
	
	do CRECWRT	// Write the record to tape
	quit


%STOPLOD	// Stop %ZRTNLOD from this point on down

	quit
	
	
TRECWRT		// Dummy line reference for GT.M

	quit
	

ARECWRT		// Dummy line reference for GT.M

	quit
	
	
BRECWRT		// Dummy line reference for GT.M

	quit
	
	
CRECWRT		// Dummy line reference for GT.M

	quit
	
	
STORETOT	// Dummy line reference for GT.M

	quit

vSIG()	quit "60393^38887^Eugene Titov^5439"	// Signature - LTD^TIME^USER^SIZE
