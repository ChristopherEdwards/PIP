I18NMLDX //Counts MLD records/Rebuild I18NMLDX/Fix
 /*
	ORIG: kumarb - 10/21/2005
	DESC: Counts MLD records/Rebuild I18NMLDX/Fix
   
    	---- Revision History --------------------------------------------
        
        10/21/05 - KUMARB - CR 16921
        	Converted to PSL.
        
	------------------------------------------------------------------
	*/
        
	quit

public CHECK(Number fix) // Compars MLD to MLDX to see if there is a mismatch
	// To execute "GTM>D CHECK^I18NMLDX
	
	type public String ER,RM

	type Number c,cx,fx

	if 'fix.exists() set fix=0

	set c=0	// number of records in MLD
	set cx=0	// number of records in MLDX
	set fx=0	// number of records to fix in MLDX

	// Counting records in MLD
	type ResultSet rs=Db.select("SRCPHR,TARGPHR","I18NMLD")
	while rs.next() do {
		set c=c+1
		}

	// Counting records in MLDX
	type ResultSet rs1=Db.select("TARGPHR,SRCPHR","I18NMLDX")
	while rs1.next() do {
		set cx=cx+1
		}

	if cx>c do FIX

	// Number MLD=~p1, Number MLDX=~p2, Number MLDX Fixed=~p3
	set ER="W",RM=$$^MSG(5453,c,cx,fx)

	quit
	
FIX	// Deletes MLDX records not pointing to MLD

	type public Number fix,fx
	
	type String TARGPHR,SRCPHR

	type ResultSet rs=Db.select("TARGPHR,SRCPHR","I18NMLDX")
	while rs.next() do {
		set TARGPHR=rs.getCol("TARGPHR")
		set SRCPHR=rs.getCol("SRCPHR")
		type RecordI18NMLD i18nmld=Db.getRecord("I18NMLD","SRCPHR=:SRCPHR,TARGPHR=:TARGPHR",1)
		if 'i18nmld.getMode() do {
			set fx=fx+1
			if fix do Db.delete("I18NMLDX","TARGPHR=:TARGPHR AND SRCPHR=:SRCPHR")
			}
		}

	quit

BUILD	//  Rebuilds MLDX from MLD

	type public String %i18ngo,ER,RM

	type Number c
	type String TARGPHR,SRCPHR

	// Set %i18ngo=Yes before running
	if %i18ngo.get()'="Yes" set ER="W",RM=$$^MSG(5454) quit

	set c=0	//number of new MLDX records created

	do Db.fastDelete("I18NMLDX")

	// Reading records in MLD
	type ResultSet rs=Db.select("SRCPHR,TARGPHR","I18NMLD")
	while rs.next() do {
		set SRCPHR=rs.getCol("SRCPHR")
		set TARGPHR=rs.getCol("TARGPHR")
		type RecordI18NMLDX i18nmldx=Db.getRecord("I18NMLDX","TARGPHR=:TARGPHR,SRCPHR=:SRCPHR",1)
		set i18nmldx.targphr=$$NRMPHR^I18NNRM(TARGPHR,1)
		do i18nmldx.save()
		set c=c+1
		}

	// Number of MLDX created = ~p1
	set ER="W",RM=$$^MSG(5455,c)

	quit

vSIG()	quit "60218^26796^Balasubramonian Sankar^2110"	// Signature - LTD^TIME^USER^SIZE
