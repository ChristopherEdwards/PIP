COMPFTST	  	/*
	ORIG: spr - 08/30/2005
	DESC: Start/Stop Commission Portfolio Balances

	---- Comments --------------------------------------------------------
	This routine will be used to start the batch process BCHCOMPFTBAL.
	This process will run during the day at a low priority to compute 
	portfolio balances for commissions.  
	
	KEYWORDS:	
	
	EXAMPLE:
		D START^COMPFTST
		D STOP^COMPFTST
	---- Revision History ------------------------------------------------
	
	09/08/05 - SPR - 17008
		   Agent/Commission - General DBI3 system area cleanup.	
		   Converted M to PSL.
		   
	----------------------------------------------------------------------
	*/
 	
 	quit
 	
 	
DAYEND  
	/*
	Start Commission Portfolio Balance Process from dayend
	Same as start except there will be no prompt to user
	*/

        type public String ER,RM
	
	type RecordDDPCTL ddpctl=Db.getRecord("DDPCTL","DDPLIT=""DDP""")
	
	// Invalid network function
	if ddpctl.ddpsys'="HOST" set ER=1,RM=$$^MSG(1409) quit  
	
	// Process ~p1 is still running.  Stop the server process first.
        if $$pid'="" set ER=1,RM=$$^MSG(2240,"BCHCOMPFTBL") quit  

	do JOB(1)
	
	quit
	
	
	//---------------------------------------------------------------------
START   // Start Commission Portfolio Balance process from user function
	//---------------------------------------------------------------------
        
	type public Boolean ER
	type public String RM,VFMQ
	
	type RecordDDPCTL ddpctl=Db.getRecord("DDPCTL","DDPLIT=""DDP""")
	
	// Invalid network function
	if ddpctl.ddpsys'="HOST" set ER=1,RM=$$^MSG(1409) quit  
        
        // Process ~p1 is still running.  Stop the server process first.
        if $$pid'="" set ER=1,RM=$$^MSG(2240,"BCHCOMPFTBL") quit  

        do PRMT(1) quit:VFMQ="Q"
        do JOB(0)
        
        quit

	//---------------------------------------------------------------------
JOB(Boolean DAYEND)     // External entry point to spawn portfolio balance process
	//---------------------------------------------------------------------
       
        type public String ER,prcnam,RM,x
        
	// Process ~p1 is still running.  Stop the server process first.
        if $$pid'="" set ER=1,RM=$$^MSG(2240,"BCHCOMPFTBL") quit  

        type RecordCUVAR cuvar=Db.getRecord("CUVAR")
	        
        set prcnam="COMPFTBL_"_cuvar.ptmdirid
        set x=$$^%ZJOB("^COMPFTBL","PRO="_prcnam,1)

	// If dayend call, skip return message
	if DAYEND quit		

	// ~p1 submitted
        if x set RM($O(RM(""),-1)+1)=$$^MSG(6800,prcnam)  
        // ~p1 not submitted
        else  set RM($O(RM(""),-1)+1)=$$^MSG(6799,prcnam)  

        set ER="W"
        
        quit


	//---------------------------------------------------------------------
pid()   // Return process ID
        //---------------------------------------------------------------------
        
        type public Number SEQ
        type Number pid
               
        type RecordCOMPFTST compftst=Db.getRecord("COMPFTST","SEQ=:SEQ")
        
        set pid=compftst.JOB if pid="" quit ""
        set pid=$$HEXDEC^%ZHEX(pid)
        
        if '$$VALID^%ZPID(pid) quit ""
        
        quit pid


	//---------------------------------------------------------------------
STOP    // Issue a stop message to PROFILE/IBS server(s)
	//---------------------------------------------------------------------
        
        type public String ER,RM,VFMQ
        
        type RecordDDPCTL ddpctl=Db.getRecord("DDPCTL","DDPLIT=""DDP""")
	
	// Invalid network function
	if ddpctl.ddpsys'="HOST" set ER=1,RM=$$^MSG(1409) quit  

	// Monitor is not running
        if $$pid="" set ER=1,RM=$$^MSG(8366) quit  

        do PRMT(0) quit:VFMQ="Q"
        
	// Process Stopped
        set RM=$$^MSG(2238)  // Process stopped
        set ER="W"

        quit
        
        
	//---------------------------------------------------------------------        
PRMT(Number OPT)  // Prompts for startup/shutdown
	//---------------------------------------------------------------------

        /*
        ARGUMENTS:
             . OPT     Option (0=Stop, 1=start)        /TYP=N/LEN=1/REQ
        */
        
        type Number %PAGE,%PG
        type String %READ,%TAB(),CONT,VFMQ

        set %PG=1,%PAGE=1

        set %TAB("CONT")=".CONT2"

        set %READ="@@%FN,,,CONT/REQ"
        
        do ^UTLREAD if VFMQ="Q" quit
        
        if 'CONT set VFMQ="Q"
        
        quit


	//---------------------------------------------------------------------
SETJOB  // Set the job number into a global
	//---------------------------------------------------------------------
	
	type public Number SEQ
	
	type RecordCOMPFTST compftst=Class.new("RecordCOMPFTST")
	
	set compftst.seq=SEQ
	set compftst.job=%ProcessID
	
	quit


	//---------------------------------------------------------------------
REMJOB  // Remove the job number from the global
	//---------------------------------------------------------------------
	
	type public Number SEQ
	
	do Db.delete("COMPFTST","SEQ=:SEQ") 
	
	quit    	
 #OPTION ResultClass ON
Public String vSIG()	quit "60187^37203^Renga SP^4861"	// Signature - LTD^TIME^USER^SIZE
