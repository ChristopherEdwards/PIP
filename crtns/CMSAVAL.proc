CMSAVAL(accts,card,crdtyp,acn)
	/*
	 ---- Description -----------------------------------------------------
		This custom-card validation procedure checks all accounts 
	  linked to a card to see if it passes two criteria tests: 
	
	   1) That there is only one primary account for a card and that 
		this account is a DDA account.
	   2) That a single account cannot be tied to a second card if 
		both cards are owned by the same user.
	
	 	  In the event that either test is failed, an error message is 
	  returned.  If both tests are passed, a null string is returned.
	  ----------------------------------------------------------------------
	
	  ---- Revision History ------------------------------------------------
	  
	  12/12/05 - SPR - 18555
		   Card Management - General DBI3 system area cleanup.
		   
	  ----------------------------------------------------------------------
	*/
	
 type Boolean done
 type Number CID,owner,stat,totprim
 type String accttyp,cardnum,primacct,str,TEMPACCT
 					
 // Check number of primary accounts
 set totprim=0
 set TEMPACCT=""
 for  set TEMPACCT=$O(accts(TEMPACCT)) quit:TEMPACCT=""  do {
	if accts(TEMPACCT)=1 do {
  		set totprim=totprim+1
 		set primacct=TEMPACCT
 	}
 }
 // No primary accounts defined.
 quit:totprim=0 $$^MSG(3951) 
 // Too many primary accounts defined.
 quit:totprim>1 $$^MSG(3955) 
 
 // Check if primary account is a DDA account. 
 set CID=primacct
 type RecordACN acn1=Db.getRecord("ACN","CID=:CID")
 set accttyp=acn1.grp
 // Primary account is not a DDA account
 quit:accttyp'="DDA" $$^MSG(3958) 
 					
 // Get all cards linked to each account
 set done=0,str=""
 for  set TEMPACCT=$O(accts(TEMPACCT)) quit:TEMPACCT=""!done  do {
	type ResultSet rs=Db.select("CRDNUM,ACN,CRDTYP","CRDGRP","CID=:TEMPACCT") 
	for  quit:'rs.next()!done  do {
		set cardnum=rs.getCol("CRDNUM")
		if cardnum'=card  do { 
			set owner=rs.getCol("ACN")
			set crdtyp=rs.getCol("CRDTYP")
			type RecordCRD crd=Db.getRecord("CRD","CRDTYP=:crdtyp,CRDNUM=:cardnum")
                        set stat=crd.stat
                        // Only restr. if active
			if acn=owner,'stat  do {     
				// Diff. card has same
				set str=$$^MSG(3959) 
				//  acct & owner						    
				set done=1
			}
		}
	}
 kill rs
 }
 
 quit str    // Return message or "" if the card passed validation

vSIG()	quit "60254^31995^Renga SP^2232"	// Signature - LTD^TIME^USER^SIZE
