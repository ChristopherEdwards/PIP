IRACON	// Compute Date of Last Contribution to an IRA Plan

	/*
	       ORIG:  LINDA P. KOZAK (4334) - 07/17/86

	  ---- Revision History --------------------------------------------------

	   12/28/05 - DHANALAKSHMI R - 18747
		      Modified the section HIST by removing the ORDER BY from
		      the getRecord statement of HIST table and to correct the
		      compilation error.

	   12/06/05 - DHANALAKSHMI R - 16851
		      Modified the section HIST to correct the runtime error and 
		      added a condition to the while to get the lastest record.
		      PSL clean up.

	   04/05/02 - GORDONT - 49794
		      Converted to PSL.
		      Removed Pre-2000 revision history.

	*/
	do INIT
	quit


INIT
	type public String XIRA01
	type Number ITC
	type String TSO

	set XIRA01=""

	do CID

	quit


	//---------------------------------------------------------------------
Public CID
	//---------------------------------------------------------------------

	type public Number ACN,IRATYP

	type ResultSet rs=Db.select("CID","RELCIF","ACN=:ACN")
	quit:rs.isEmpty()

	while rs.next() do {

		type Number CID
		set CID=rs.getCol(1)

		type RecordDEP dep=Db.getRecord("DEP","CID=:CID",1)

		if 'dep.getMode() quit
		if (dep.ira-IRATYP) quit

		do HIST
		}

	quit


	//---------------------------------------------------------------------
HIST	
	//---------------------------------------------------------------------
	
	type public Number CID
	type public String XIRA01

	type Number BKT,ITC,Y	
 	type Date XTJD
 	type String TAMT,TSEQ,UTSO

	set XTJD=""

	// The following section used to be handled by UHFETCH
	type ResultSet rs=Db.select("TSEQ","HIST","CID=:CID","TJD DESC")

	if rs.isEmpty() quit
	while rs.next() quit:'XTJD.isNull()  do {

		set TSEQ=rs.getCol("TSEQ")
		type RecordHIST hist=Db.getRecord("HIST","CID=:CID,TSEQ=:TSEQ")
		set ITC=hist.itc	
		if '$E(hist.itc)!($E(hist.itc,6))!($E(hist.itc,12)) quit
		if hist.tso="" quit
	
		set UTSO="" do OUT^UTSO(.UTSO,hist.tso)
		if '(UTSO("IRAC").exists()) quit
		set Y=UTSO("IRAC")

		new utbliracon
		type RecordUTBLIRACON utbliracon=Db.getRecord("UTBLIRACON","KEY=:Y")
		set BKT=utbliracon.rsn
		if BKT-1,BKT-2 quit

		set XTJD=hist.tjd
		set TAMT=hist.tamt
		}

	if 'XTJD.isNull(),XTJD>XIRA01 set XIRA01=XTJD_"|"_(+TAMT)
	quit


	//---------------------------------------------------------------------
XIRA01(ACN,IRATYP,TAXYR)	// Assignment Expression for Data item XIRA01 in file IRA
	//---------------------------------------------------------------------

	type String XIRA01
	do INIT
	quit XIRA01

vSIG()	quit "60267^30179^Dhanalakshmi R^2392"	// Signature - LTD^TIME^USER^SIZE
