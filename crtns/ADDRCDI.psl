ADDRCDI		/* ORIG: BURNSM - 07/19/00
	 DESC: Account Mailing Address - CDIs
	
	
	 LIBRARY:
		
		. $$CSZ		 - City, State Zip
		. $$MCSZ		- City, State Country Zip
		. $$ACN			- Customer Number
		. $$ADDRFLG		- Account Address Journal Flag

	------Revision History------------------------------------------------

	08/17/00 - OFALTR 40116
		   Added sections ACN and ADDRFLG to return Customer Number and
		   Account Address Journal Flag.
	
	----------------------------------------------------------------------
	*/

	quit

CSZ(XCID)	// Returns City, State Zip value
	/*
	 The function is used to concatenate the [ACNADDR]CSZ field.
	
	 ARGUMENTS:
	 . XCID		Sequence Number		/TYP=N/REQ
	
	 RETURNS:
	 $$ City, State Zip			/TYP=T
	
	 EXAMPLE:
	 W $$CSZ^ADDRCDI(100)
	*/

	new CITY,STATE,X,ZIP

	set X=Db.getOneRow("CITY,STATE,MZIP","ACNADDR","XCID")
	set CITY=$E($P(X,$C(9),1),1,25)
	set STATE=$P(X,$C(9),2)
	set ZIP=$P(X,$C(9),3)
	quit CITY_", "_STATE_" "_ZIP
	

MCSZ(XCID)       // Returns City, State Country Zip value
	/*
	 The function is used to concatenate the [ACNADDR]MCSZ field.
	
	 ARGUMENTS:
	 . XCID		Sequence Number	 /TYP=N/REQ
	
	 RETURNS:
	 $$ City, State Country Zip		/TYP=T
	
	 EXAMPLE:
	 W $$MCSZ^ADDRCDI(100)
	*/

	new CITY,STATE,CNTRY,X,ZIP
	
	set X=Db.getOneRow("CITY,STATE,CNTRY,MZIP","ACNADDR","XCID")

	set CITY=$E($P(X,$C(9),1),1,25)
	set STATE=$P(X,$C(9),2)
	set CNTRY=$P(X,$C(9),3)
	set ZIP=$P(X,$C(9),4)
	if CNTRY="US" quit CITY_", "_STATE_" "_ZIP
	quit CITY_", "_STATE_" "_CNTRY_" "_ZIP
	
ACN(XCID)       //
	/*
 
	 ARGUMENTS:
	 . XCID	    Account Number /TYP=N/REQ/MECH=VAL
 
	 RETURNS:
	 . $$	      Returns ACN - Customer Number
	*/
 
	quit Db.getOneRow("ACN","ACN","XCID")

ADDRFLG(XCID)       //
	/*
 
	 ARGUMENTS:
	 . XCID	    Account Number /TYP=N/REQ/MECH=VAL
 
	 RETURNS:
	 . $$	      Returns 1 - if Account Address Journal is defined
				   0 - if Account Address Journal is not defined
	*/
 
	new XACN,XTJD
	set XTJD=%SystemDate
	set XACN=$$ACN(XCID) if XACN="" quit 0
	set FLG=Db.isDefined("DTJADDRCHG","XTJD,XACN,XCID")
	quit FLG
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43488^Sanchez SCM Administrator^1937"	// Signature - LTD^TIME^USER^SIZE
