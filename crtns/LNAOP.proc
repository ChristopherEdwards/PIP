LNAOP	//
	/*

	  ---- Revision History ------------------------------------------------

	   09/09/05 - 	Satyanas - CR 16994
	   		Modified the code to PSL standards and removed 
	   		the unscoped warnings.	   	      	
	   
	   10/27/02 - CHHABRIAS - 49451
		      Converted to PSL

	  ----------------------------------------------------------------------

	*/
	
	do NEW
	quit


NEW 	//
	do INIT(0)
	quit


UPD 	//
	do INIT(1)
	quit


INQ 	//
	do INIT(2)
	quit


INIT(%ProcessMode)	//

	type public Number %PG,%PAGE
	type public String VFMQ

	set %PG=0
	set %PAGE=1

	type RecordLNAOP fLNAOP

	do VPG(.fLNAOP)

	quit


VPG(RecordLNAOP fLNAOP)	// Page control

	type public Number %PG
	type public String VFMQ
	type Boolean FINISH

	set FINISH=0
     
        for  do { quit:FINISH

		if %PG=0 do VPG00 if VFMQ="Q" set FINISH=1 quit
		if "DFQ"[VFMQ set FINISH=1 quit

		if %PG>0 do VPG01(.fLNAOP) if VFMQ="Q" set FINISH=1 quit
		if "DFQ"[VFMQ do VER(.fLNAOP) set FINISH=1 quit
		set %PG=%PG+1
		}

        quit	


VPG00	// Set up

	type public String ET,IO,PAYEE,%READ,%TAB(),TYP,UTBL(),VFMQ
	
	set TYP=""
	set %TAB("TYP")=".TYP1/TBL=[STBLAOTYP]"
	
	// Payee currently on file
	set %TAB("PAYEE")=".PAYEE1/XPP=if '%OSAVE,($$CHKLNAOP^LNAOP) set ER=1,RM=$$^MSG(2149)"

	if %ProcessMode set %TAB("PAYEE")=%TAB("PAYEE")_"/TBL=[LNAOP]PAYEE:QU ""[LNAOP]TYP=<<TYP>>"""

	if %ProcessMode=2 set %TAB("IO")=$$IO^SCATAB($I)

	set %READ="@@%FN,,,TYP/REQ,PAYEE/REQ"

	if %ProcessMode=2 set %READ=%READ_",IO/REQ"

	do ^UTLREAD

	if VFMQ="Q" quit

	if %ProcessMode=1 lock +UTBL("AOAMO",TYP,PAYEE):2 else  set ET="RECLOC" do ERR quit

	if %ProcessMode=2,IO'=$I do OPEN^SCAIO

	quit


VPG01(RecordLNAOP fLNAOP)	// Screen

	type public String PAYEE,TYP
	
	set fLNAOP=Db.getRecord("LNAOP","TYP=:TYP,PAYEE=:PAYEE",1)

	do DRV^USID(%ProcessMode,"LNAOP",.fLNAOP)

	quit

ERR	//

	type public Boolean ER
	type public String VFMQ
	
	set ER=1 

	do ^UTLERR

	set VFMQ="Q"

	quit


VER(RecordLNAOP fLNAOP) 	//

	type public String VFMQ

	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit

	if %ProcessMode=3 do {
		do Db.delete("LNAOP","TYP=:TYP AND PAYEE=:PAYEE")
		}
	else  do fLNAOP.save()

	do END

	quit


END	//

	type public Boolean ER
	type public String PAYEE,RM,%TAB,TYP,UTBL(),VFMQ
	
	kill %TAB

	if ER!(%ProcessMode=2)!(%ProcessMode=4) quit

	if %ProcessMode=1 lock -UTBL("AOAMO",TYP,PAYEE):2

	set PAYEE=PAYEE.get()

	if VFMQ="Q" do {

		// Add-on payee ~p1 not created
		if %ProcessMode=0 set RM=$$^MSG(220,PAYEE) quit

		// Add-on payee ~p1 not modified
		if %ProcessMode=1 set RM=$$^MSG(222,PAYEE) quit

		// Add-on payee ~p1 not deleted
		set RM=$$^MSG(221,PAYEE)
		}
	else  do {

		// Add-on payee ~p1 created
		if %ProcessMode=0 set RM=$$^MSG(217,PAYEE) quit

		// Add-on payee ~p1 modified
		if %ProcessMode=1 set RM=$$^MSG(219,PAYEE) quit

		// Add-on payee ~p1 deleted
		set RM=$$^MSG(218,PAYEE)
		}

	set ER="W"

	quit
	
	
RSVEA(GLC,CGL)	//Private [AOAMO]RSVEA - Dealer Reserve Amount
	
	/*
	This function contains the computation for field [AOAMO]RSVEA.
	It was moved here, due to its extreme length.

	ARGUMENTS:
		. GLC	G/L Indicator Credit CID /TYP=N/REQ/MECH=VAL
	
		. CGL	Credit Account Number	 /TYP=N/REQ/MECH=VAL

	RETURNS:
		. $$	RSVEA			 /TYP=T

	*/

	type String C,T

	type RecordLN ln=Db.getRecord("LN","CID=:CGL",1)
	
	set T=ln.type
	set C=ln.crcd
	
	if GLC quit ""
	
	if ln.sbwamt quit ln.sbwamt

	type ResultSet rs=Db.select("SBWAMT","PRODDFTL","TYPE=:T")
	if rs.next() quit rs.getCol("SBWAMT")

	quit ""


CHKLNAOP()	//-------------------------------------

	type ResultSet rs=Db.select("DESC","LNAOP","TYP=:TYP AND PAYEE=:X")

	if rs.isEmpty() quit 0	

	quit 1

vSIG()	quit "60208^31350^Sethy, Satyanarayan^3454"	// Signature - LTD^TIME^USER^SIZE
