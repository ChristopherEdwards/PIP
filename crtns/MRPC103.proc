public MRPC103(String return,Number versn,Number PMT,String ETC,Date EFD,Number ACN,Number SGP,Number CCL)
  /*
   ORIG: KELLYP - 04/05/2000
   DESC: Mass Payment Processor

   ---- Comments --------------------------------------------------------

    This procedure introduces functionality for processing mass payments
    for a customer, a statement group, or a commitment number to PFW.  Using 
    the Mass Payment Application function in PFW Teller, the user can process
    a single payment for all of a customer's accounts, all accounts tied to
    a statement group, or for all of the loans tied to a commitment.

   ---------------------------------------------------------------------- 

   ---- Revision History ------------------------------------------------
	
	10/25/05 - KELLYP - CR 16684
		   Modified entire procedure to conform to current PSL 
		   standards and to call PROC section of LNMPP instead
		   of VER section.  Also removed pre-2003 revision history.
	
	11/10/03 - CARROLLJ - 51630
		   Pass correct parameters to LNMPP.
  */

	type public Boolean ER
	type public Date TPD
	type public String RM

	type Boolean CBF
	type Number BRCD

	set ER=""
	set BRCD=CUVAR.bobr

	if TPD.get().isNull() set TPD=%SystemDate
	
	set SGP=SGP.get()
	set ACN=ACN.get()
	set CCL=CCL.get()

	// In this case, EFD is a value being passed in to this RPC.
	set EFD=$$^SCAJD(EFD)
	
	do POSTPROC() 
	if ER quit $$ERRMSG^PBSUTL(RM.get())	    // Check input parameters

	set CBF=0

	// Builder Loan Commitment
	if 'CCL.isNull() do {
		type RecordLN ccl=Db.getRecord("LN","CID=:CCL")
		type RecordTTX ttx()
		set CBF=ccl.cbf
		if CBF do COMM^LNMPP(.ttx(),.ccl,CCL,0) if 1
		}
	
	// All other mass payments
	if 'CBF do PROC^LNMPP
	
	if ER=1 quit $$ERRMSG^PBSUTL(RM.get())	

	if ER="W" set return=$$V2LV^MSG(RM.get())	    // Return Complete Message

	quit ""


POSTPROC()   // Post Processor Edit Checks for Input Parameters

	type public Boolean ER
	type public Date EFD
	type public Number ACN,CCL,SGP
	type public String ETC,RM

	// Invalid Customer Number
	if 'ACN.isNull(),'Db.isDefined("CIF","ACN=:ACN") set ER=1,RM=$$^MSG(1295) quit

	// Statement group requires customer number
	if (ACN.isNull()),('SGP.isNull()) set ER=1,RM=$$^MSG(2538) quit
	
	// Customer or commitment number required
	if (ACN.isNull()),(CCL.isNull()) set ER=1,RM=$$^MSG(702) quit

	if 'CCL.isNull() do { quit:ER
		type RecordLN ln=Db.getRecord("LN","CID=:CCL",1)

		// Not a commitment account
		if 'ln.cpf set ER=1,RM=$$^MSG(2015) 
		}

	// Edit checks for offset transaction code
	type RecordTRN xtrn=Db.getRecord("TRN","ETC=:ETC")

	// Debit transactions only
	if xtrn.itc.extract(1) set ER=1,RM=$$^MSG(772) quit

	// Miscellaneous offset only
	if xtrn.cls'="M" set ER=1,RM=$$^MSG(1755) quit

	// Offset must contain default G/L account
	if xtrn.acn.isNull() set ER=1,RM=$$^MSG(2092) quit

	// EFD must be same as or before system date (no future dating)
	if EFD>%SystemDate set ER=1,RM=$$^MSG(880) quit
	
	quit

vSIG()	quit "60198^56398^Pat Kelly^2782"	// Signature - LTD^TIME^USER^SIZE
