UTBLACCM		/*
	  ORIG: titove - 04/08/2005
	  DESC: Maintain user tables ACC

	---- Comments --------------------------------------------------------

	---- Revision History ------------------------------------------------

		10/05/06 - Ravindra Rathi - CR 23390
		           Modified the VER section to add a 'do END quit' after 
		           the Db.delete call when %ProcessMode=3 to prevent
		           the else conditon from getting executed,which 
		           was causing the LNRFILER error.
		
		04/14/05 - TITOVE - CR 15089
			   Modified as part of DBI2 project. Removed FILE
			   section.

		09/30/02 - DATTAR - 49451
			   Converted to PSL.

	*/
 
public	NEW     // Insert

        do INIT(0)
        
        quit
 
 
public	UPD     // Update

        do INIT(1)
        
        quit
 
 
public	INQ     // Inquiry

        do INIT(2)
        
        quit
 
 
public	DEL     // Delete

        do INIT(3)
        
        quit
 
 
INIT(%ProcessMode)

	type public Number %PAGE, %PG

        type String OLNTB, VFMQ
 
        set %PG = 0
        set %PAGE = 1
 
        type RecordUTBLACC fUTBLACC
        
        do VPG(.fUTBLACC)
 
        quit
 
 
VPG(RecordUTBLACC fUTBLACC)     // Page control

	type public Number %PAGE, %PG
	type public String VFMQ

	type Boolean FINISH = 0
        
	for  do { quit:FINISH = 1
        	
		if %PG = 0 do VPG00 if 1
 
		else  if %PG > 0 do VPG01(.fUTBLACC)
 
		if "DFQ"[VFMQ do VER(.fUTBLACC) set FINISH = 1 quit
 
		set %PG = %PG + 1
		}
                
	quit
 
 
VPG00   // Set up

	type public Number ER
	type public String %NOPRMT, %READ, %TAB, VFMQ

        if %ProcessMode = 0 do {
 
                // Invalid entry ~p1
                set %TAB("CLS") = "[UTBLACC]CLS/XPP=I ""*M""[X S ER=1,RM=$$^MSG(5791,X)"
                set %TAB("AC") = "[UTBLACC]AC/XPP=D ACPST^UTBLACCM(X)"
                }
        else  do {
 
                // Invalid entry ~p1
                set %TAB("CLS") = "[UTBLACC]CLS/XPR=D CLS^UTBLACCM/XPP=I ""*M""[X S ER=1,RM=$$^MSG(5791,X)"
                set %TAB("AC") = "[UTBLACC]AC/TBL=[UTBLACC]:QU ""[UTBLACC]CLS=<<CLS>>"""
                if %ProcessMode = 2 set %TAB("IO") = $$IO^SCATAB($I)
                }
 
        set %READ = "@@%FN,,,CLS/REQ,AC/REQ"
        set %NOPRMT = "N"
        
        if %ProcessMode = 2 set %READ = %READ_",IO/REQ"
        
        do ^UTLREAD
 
        if VFMQ = "Q" set ER = 1 quit
 
        quit
 
 
ACPST(Number AC)       // Prevent attempt to insert identical record

	type public String CLS, ET

	// Record already on file
        if '%ProcessMode,Db.isDefined("UTBLACC","CLS=:CLS,AC=:AC") set ET = "RECOF" do ERR quit

        quit
 
 
CLS     // Get CLS for UTBLGLSCR
 
	type public String CLS()

        type ResultSet rs = Db.select("CLS","UTBLACC")
        
        while rs.next() set CLS(rs.getCol("CLS")) = ""
 
        quit
 
 
VPG01(RecordUTBLACC fUTBLACC)   // Screen

	type public Number AC
	type public String CLS, ER, IO, VFMQ

	type String SID

        set SID = $S(CLS="D":"UTBLACCD",1:"UTBLACC")

        set fUTBLACC = Db.getRecord("UTBLACC", "CLS = :CLS, AC = :AC", 1)
        
        do DRV^USID(%ProcessMode,SID,.fUTBLACC)
 
        if (%ProcessMode = 2),(IO '= $I) do OPEN^SCAIO if ER set VFMQ = "Q" quit
 
        quit
 
 
ERR     //

	type public String ER, VFMQ

	set ER = 1
        
	do DSPBP^UTLERR
 
	set VFMQ = "Q"
 
	quit
 
 
VER(RecordUTBLACC fUTBLACC) //

	type public Number AC
	type public String CLS, VFMQ

	if (%ProcessMode = 2) ! (%ProcessMode = 4) ! (VFMQ = "Q") do END quit
 
	if (%ProcessMode = 3) do Db.delete("UTBLACC","CLS=:CLS AND AC=:AC") do END quit
	        
	else  do fUTBLACC.save()
        
	do END
 
	quit
 
 
END	

	type public Number ER
	type public String RM, VFMQ

	if ER.get() ! (%ProcessMode = 2) ! (%ProcessMode = 4) quit
 
        if (VFMQ = "Q") do {
 
                // Record not created
                if (%ProcessMode = 0) set RM = $$^MSG(6709,"Record") quit
 
                // Record not modified
                if (%ProcessMode = 1) set RM = $$^MSG(6710,"Record") quit
 
                // Record not deleted
                set RM = $$^MSG(6711,"Record")
                }
                
        else  do {
 
                // Record created
                if (%ProcessMode = 0) set RM = $$^MSG(6712,"Record") quit
 
                // Record modified
                if (%ProcessMode = 1) set RM = $$^MSG(6713,"Record") quit
 
                // Record deleted
                set RM = $$^MSG(3028,"Record")
                }
 
        set ER = "W"
 
        quit

vSIG()	quit "60544^26910^Ravindra Rathi^4339"	// Signature - LTD^TIME^USER^SIZE
