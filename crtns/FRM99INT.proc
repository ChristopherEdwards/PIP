public FRM99INT	// Prepare Interest Reporting Information
	/*
	ORIG: Kathie Jalbuena (7070) - 11/20/87
	DESC: This procedure takes interest information from M1099 and
	      DEP and stores the data to be reported in a REP127 table.

	INPUTS:
		. CORR	Correction run flag		/TYP=L/NOREQ

		. LRD	Last Run Date			/TYP=D/NOREQ

		. MULTI	Separate multiple form flag	/TYP=L/NOREQ
			Specifies whether to separate those customers with
			multiple 1099-INT form from those with only one form.

	I18N=QUIT: Excluded from I18N standards.
	
	---- Revision History ------------------------------------------------
	
	11/07/06 - SWARNALP - CR 23635
		   The following changes are made in this procedure as part of 
		   the Year 2006 programming changes for magnetic media 
		   reporting of IRS 1098, 1099 & 5498 forms supported by 
		   Profile.
		   .	Modified section BOND to include the following changes:
		   	O    Records with the transaction types 8, 9 and 10 
		   	     are made to be reportable and the records having 
		             no interest paid, no withholding, no forfeiture 
		             and no interest on private activity bonds with or 
		             without required IRS cutoff condition are made 
		             non-reportable.
		        O    Interest Paid (M1099.INT) amount is set to 
		             REP127.INTBOND when the Transaction Type (M1099.TYPE) 
		             is either equal to 8 - Clean Renewal Bonds or 
		             9 - Gulf Bonds, if not it will be set to 
		             REP127.TAXEXINT when the Transaction Type (M1099.TYPE)
		             is equal to 10 - State or Local Bonds and will be set 
		             to REP127.INT for other bond transaction types (1,2,3 
		             or 4). The data stored in REP127.INTBOND will 
		             be reported under "Interest Earnings" column, the 
		             data in REP127.TAXEXINT in "Tax Exempt Interest" and 
		             the data in REP127.INT will be reported under 
		             "Savings Bonds" column in the reports SCA127 and 
		             SCA143.
			O    Int on Pvt Activity Bonds (M1099.INTPAB) is set to 
			     REP127.INTPAB when the Transaction Type (M1099.TYPE) 
			     is 10 - State or Local Bonds so as to get displayed 
			     in reports SCA127 and SCA143.		       
		     	     		   	
	05/26/06 - TITOVE - CR 21509
		   Scoped TYPCIF array as public in IRSUPD section to make
		   sure same records do not process multiple times. In BOND 
		   section, added ORDER BY clause to ensure the latest record
		   will be processed last. Performed light clean up.

	05/15/06 - DESHPANDE S K - CR 21167
		   Modified sections ACN and BOND, set variable ZIP9 to CIF.MZIP9.
		   
	04/04/06 - TITOVE - CR 20591
		   Removed CIF and M1099 tags. Modified IF ELSE condition in
		   CID section to prevent executing both due to reset of $T
		   value. Corrected form type selection in CORR section. 
		   Removed unused arguments from several section calls.

	12/30/05 - SWARNALP - CR 18751
		   Removed the IRSXSEQ table reference from the section 
		   FRM1042S as the table IRSXSEQ itself is deleted as the 
		   functionality is no longer supported
		   
	09/22/05 - HAILEYM - CR17143
		   Converted to PSL.
		
	*/	
	type public Boolean CORR,MULTI
	type public Date LRD
	
	type Boolean CTOFA
	type Date BEGYR,ENDYR
	type Number CTOF,SBN
	type String EIN,PSBN
	
	type RecordUTBLIRS utblirs

	set CORR=CORR.get(),LRD=LRD.get(),MULTI=MULTI.get()
	
	// Beginning of tax year
	set BEGYR=$$BOTY^SCADAT($$BOTY^SCADAT(%SystemDate,1)-1,1)
	// End of tax year
	set ENDYR=$$EOTY^SCADAT(BEGYR,1)
	
	set EIN=CUVAR.EIN
	set CTOFA=CUVAR.CTOF1099
	set SBN=CUVAR.SBINSTNO
	if 'SBN.isNull() set PSBN=SBN_"-"
	
	set utblirs=Db.getRecord("UTBLIRS","KEY='1099-INT'",1)

	set CTOF=utblirs.fctoff
	
	do Db.fastDelete("REP127","JOB=:%ProcessID")
	
	// Correction Reporting Run
	if CORR do CORR(LRD) quit
	
	// Original Reporting Run
	
	// File M1099 data into REP127 table
	type ResultSet rs=Db.select("DISTINCT TAXID","M1099")
	
	while rs.next() do BOND(rs.getCol("TAXID"), 0)
	
	// Collate through all of the accounts in the system. Save data from any
	// taxable interest accounts into REP127 table.
	type ResultSet rs1=Db.select("ACN","CIF")
	
	while rs1.next() do ACN(rs1.getCol("ACN"), 0)
	
	quit
	

BOND(String TAXID,
     Number CCODE)

	// Process bond transactions for one taxid

	type public Boolean CORR,CTOFA
	type public Date BEGYR,ENDYR
	type public Number CTOF

	type Number ACN,FOR,INT,INTBOND,INTPAB,MF,TAXEXINT,WITH
	type String ZIP9
	
	type DbSet ds
	type RecordCIF cif
	type RecordM1099 m1099
	type RecordREP127 rep127
	
	set (INT,FOR,INTBOND,INTPAB,TAXEXINT,WITH)=0
	set ds=Db.selectDbSet("M1099","TAXID=:TAXID","SEQ ASC")
	while ds.next() do {
		set m1099=ds.getRecord("M1099")
		
		if (m1099.formtyp<>1)!((m1099.type>4)&(m1099.type<8)) quit
		
		if m1099.td<BEGYR!(m1099.td>ENDYR) quit
		
		if ((m1099.type=1)!(m1099.type=2)!(m1099.type=3)!(m1099.type=4)) set INT=INT+m1099.int
		
		// Clean Renewal Bonds and Gulf Bonds
		if ((m1099.type=8)!(m1099.type=9)) set INTBOND=INTBOND+m1099.int
		
		set WITH=WITH+m1099.intwh
		
		// State or Local Bonds
		if m1099.type=10 do {
			set TAXEXINT=TAXEXINT+m1099.int
			set INTPAB=INTPAB+m1099.intpab
			}
		}
	
	if 'CORR,(INT+INTBOND+TAXEXINT+FOR+WITH+INTPAB)=0 quit		// Nothing to report
	
	// Use the address of the last transaction
	set ACN=+m1099.relacn 

	if ACN do {
		set cif=Db.getRecord("CIF","ACN=:ACN",1)
		if 'cif.getMode() set ZIP9=0,MF=1 quit
		
		set ZIP9=cif.mzip9 
		set MF=cif.mf
		}
	else  set ZIP9=m1099.mzip,MF=m1099.mf
	
	if ZIP9?5N set ZIP9=ZIP9_"-0000"
	if ZIP9.isNull() set ZIP9=0
	if MF.isNull() set MF=1
	
	// Don't report if it meets IRS cutoff condition and no withholding,
	// forfeiture or interest on private activity bonds
	if 'CORR,CTOFA,(INT+INTBOND+TAXEXINT)<CTOF,(FOR+WITH+INTPAB)=0 quit
	
	set rep127=Class.new("RecordREP127")
	set rep127.job=%ProcessID
	set rep127.taxid=TAXID
	set rep127.acn=ACN
	set rep127.cid=0
	set rep127.int=INT
	set rep127.for=FOR
	set rep127.with=WITH
	set rep127.intbond=INTBOND
	set rep127.intpab=INTPAB
	set rep127.taxexint=TAXEXINT
	do rep127.bypassSave()

	quit
	

ACN(Number ACN,
    Number CCODE)

	// Process accounts owned by this customer

	type public Boolean CORR,CTOFA
	type public Number CTOF
	
	type Number CID,MF,TOTFOR,TOTINT,TOTWITH
	type String ACNCORR(),ACNDTL(),TAXID,ZIP9
	
	type RecordCIF cif
	type RecordREP127 rep127
	
	set cif=Db.getRecord("CIF","ACN=:ACN",1)
	
	set ZIP9=cif.mzip9 
	if ZIP9?5N set ZIP9=ZIP9_"-0000"
	if ZIP9.isNull() set ZIP9=0
	
	set TAXID=cif.taxid 
	if TAXID.isNull() set TAXID=0
	
	if cif.mf.isNull() set MF=1
	else  set MF=cif.mf
	
	type DbSet ds = Db.selectDbSet("RELCIF", "ACN=:ACN")
	
	while ds.next() do {
		
		type RecordRELCIF relcif = ds.getRecord("RELCIF")
		
		do CID(ACN,relcif.cid,.ACNDTL(),.ACNCORR(),CCODE,+relcif.s1099)
		}
	
	// Calculate total interest
	set CID=""
	set (TOTINT,TOTWITH,TOTFOR)=0
	for  set CID=ACNDTL(CID).order() quit:CID.isNull()  do {
		
		set TOTINT=TOTINT+ACNDTL(CID).piece("|",1)
		set TOTFOR=TOTFOR+ACNDTL(CID).piece("|",2)
		set TOTWITH=TOTWITH+ACNDTL(CID).piece("|",3)
		}
	
	if 'CORR,(TOTINT+TOTFOR+TOTWITH)=0 quit		// Nothing to report
	
	if 'CORR,CTOFA,TOTINT<CTOF,'TOTWITH,'TOTFOR quit
	
	set rep127=Class.new("RecordREP127")
	set rep127.job=%ProcessID
	set rep127.taxid=TAXID
	set rep127.acn=ACN
	
	set CID=""
	for  set CID=ACNDTL(CID).order() quit:CID.isNull()  do {
		
		if rep127.getMode() do rep127.setMode(0)     // Reset into Insert mode

		set rep127.cid=CID
		set rep127.int=ACNDTL(CID).piece("|",1)
		set rep127.for=ACNDTL(CID).piece("|",2)
		set rep127.with=ACNDTL(CID).piece("|",3)
		/*
		INTPAB (Int on Pvt Activity Bonds) and TAXEXINT (Tax-exempt Interest) 
		are applicable only for bond type of transactions.
		*/
		set rep127.intpab=0
		set rep127.taxexint=0
		
		do rep127.bypassSave()
		}
	quit
	

CID(Number ACN,			// Customer number
    Number CID,			// Account number
    String ACNDTL(),		// Account data
    String ACNCORR(),		// Correction data
    Number CCODE,		// Correction Code
    Boolean SEPT)		// Separate 1099 Reporting flag
    
	// Accumulate interest, withholding and penalty for this account

	type public Boolean CORR, CTOFA
	type public Number CTOF
	
	type Number FOR, INT, WITH
	
	type RecordDEP dep

	set dep = Db.getRecord("DEP", "CID = :CID", 1)
	
	/* 
	  Deposit account is not eligible if it has any of the following characteristics:
	  -  escrow account
	  -  retirement account
	  -  does not belong to the same (primary) owner
	  -  exempt from IRS 1099 reporting
	  -  savings bond type of interest (Original Issue Discount Account Flag is set)
	*/
	if (dep.grp = "ESC") ! dep.ira ! (dep.acn <> ACN) ! (+dep.irsexm) ! dep.oid quit
	
	// Loan account - need to see whether it has any linked escrow account,
	// in which case get the interest/witholding/penalty data (if any) from it
	if 'dep.getMode() do ESC(CID, .FOR, .INT, .WITH)
	
	if dep.getMode() do {
		 
		set INT = dep.ipty
		set WITH = dep.bwpty
		set FOR = dep.penpty
		}
	
	if (INT < 0) set INT = 0	// Don't report negative interest
	
	// Don't report if it meets IRS cutoff condition and no withholding
	// or forfeiture
	if 'CORR,'CTOFA,(INT < CTOF),((FOR + WITH) = 0) quit
	
	if ((FOR + WITH + INT) = 0) quit	// Nothing to report
	
	// If RELCIF.S1099 flag is set, separate 1099 reporting is required
	set ACNDTL(CID) = INT_"|"_FOR_"|"_WITH_"|"_SEPT
	
	if CORR,(CCODE = 2) set ACNCORR(CID) = "0|0|0|"_SEPT
	
	quit
	

CORR(Date LRD)	// Collates IRSUPD for 1099-INT Corrections Section

	type Date EFD
	type String TYPCIF(,)

	type DbSet ds
	type RecordIRSUPD irsupd
	
	set EFD=LRD-1
	
	set ds=Db.selectDbSet("IRSUPD","TJD>:EFD AND FTYPE<3")
	while ds.next() do {
		set irsupd=ds.getRecord("IRSUPD")
		
		do IRSUPD(.irsupd)
		}
	quit
	

IRSUPD(RecordIRSUPD irsupd)	// Detail processing of IRSUPD

	type public String TYPCIF(,)
	type RecordACN acn
	
	// Correction forms for this taxid and form type have already been processed
	if $$PROCESSED(irsupd.ftype, irsupd.ocif, .TYPCIF(,)) quit
	
	// This is a correction to a BOND 1099-INT
	if (irsupd.ftype = 2) do BOND(irsupd.miscseq, irsupd.ccode) quit
	
	set acn = Db.getRecord("ACN", "CID = :irsupd.miscseq", 1)
	
	do ACN(+acn.acn, irsupd.ccode)

	quit
	

public PROCESSED(Number TYPE,
	  String CIF,
	  String TYPCIF(,))		//NOREQ/REF:W
		 
	// Check if tax id for this form type have already been processed

	// No CIF and it is a bond
	if CIF.isNull(), (TYPE = 2) quit 0
	
	// No CIF associated with this account
	if CIF.isNull() quit 1
	
	// Not processed
	if 'TYPCIF(TYPE,CIF).exists() set TYPCIF(TYPE,CIF)="" quit 0
	
	// Processed
	quit 1
	

ESC(Number CID,
    Number FOR,				//REF:W
    Number INT,				//REF:W
    Number WITH)			//REF:W	
    
	// Process escrow account associated with this loan account
	type DbSet ds
	type RecordLNBIL0 lnbil0
	
	set (INT,FOR,WITH)=0
	
	set ds=Db.selectDbSet("LNBIL0","CID=:CID")
	
	while ds.next() do {
		
		set lnbil0=ds.getRecord("LNBIL0")
		
		if lnbil0.pe01da do DEP(lnbil0.pe01da,.FOR,.INT,.WITH)
		if lnbil0.pe02da do DEP(lnbil0.pe02da,.FOR,.INT,.WITH)
		if lnbil0.pe03da do DEP(lnbil0.pe03da,.FOR,.INT,.WITH)
		if lnbil0.pe04da do DEP(lnbil0.pe04da,.FOR,.INT,.WITH)
		if lnbil0.pe05da do DEP(lnbil0.pe05da,.FOR,.INT,.WITH)
		if lnbil0.pe06da do DEP(lnbil0.pe06da,.FOR,.INT,.WITH)
		if lnbil0.pe07da do DEP(lnbil0.pe07da,.FOR,.INT,.WITH)
		if lnbil0.pe08da do DEP(lnbil0.pe08da,.FOR,.INT,.WITH)
		if lnbil0.pe09da do DEP(lnbil0.pe09da,.FOR,.INT,.WITH)
		if lnbil0.pe10da do DEP(lnbil0.pe10da,.FOR,.INT,.WITH)
		if lnbil0.pe11da do DEP(lnbil0.pe11da,.FOR,.INT,.WITH)
		if lnbil0.pe12da do DEP(lnbil0.pe12da,.FOR,.INT,.WITH)
		if lnbil0.pe13da do DEP(lnbil0.pe13da,.FOR,.INT,.WITH)
		if lnbil0.pe14da do DEP(lnbil0.pe14da,.FOR,.INT,.WITH)
		if lnbil0.pe15da do DEP(lnbil0.pe15da,.FOR,.INT,.WITH)
		if lnbil0.pe16da do DEP(lnbil0.pe16da,.FOR,.INT,.WITH)
		if lnbil0.pe17da do DEP(lnbil0.pe17da,.FOR,.INT,.WITH)
		if lnbil0.pe18da do DEP(lnbil0.pe18da,.FOR,.INT,.WITH)
		if lnbil0.pe19da do DEP(lnbil0.pe19da,.FOR,.INT,.WITH)
		if lnbil0.pe20da do DEP(lnbil0.pe20da,.FOR,.INT,.WITH)
		}
	quit
	
	
DEP(Number ECID,
    Number FOR,				//REF:W
    Number INT,				//REF:W
    Number WITH)			//REF:W

	// Payment Element - Deposit Account
		
	type RecordDEP dep
	
	set dep=Db.getRecord("DEP","CID=:ECID",1)
	
	if 'dep.getMode() quit
	
	if dep.oid quit		// Savings bond type of interest
	if dep.irsexm quit	// Exempt from IRS1099 reporting
	
	set INT=INT+dep.ipty
	set WITH=WITH+dep.bwpty
	set FOR=FOR+dep.penpty
	
	quit

vSIG()	quit "60585^12561^P.R. Swarnalatha^12120"	// Signature - LTD^TIME^USER^SIZE
