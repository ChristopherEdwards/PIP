public	FRM1042S
	/*
	ORIG: titove - 10/25/2005
	DESC: This routine takes 1042-S information and stores the
	      items to be reported in REP1042S table.
	
	INPUTS:
		. CORR	Correction run flag		/TYP=L/NOREQ
			This flag is used in procedure IRSO42S

		. LRD	Last Run Date			/TYP=D/NOREQ


	RETURNS:
		. ^TMP1	Raw data for interest reporting
	
	EXAMPLE:
		do ^FRM1042S
		

	I18N=QUIT: Excluded from I18N standards.

	---- Revision History ------------------------------------------------
	
	05/15/06 - DESHPANDE S K - CR 21167
		   Modified section ACN, the variable ZIP9 is set to CIF.MZIP9
		   
	03/29/06 - MugilvannanCS - CR - 20354
		   . Modified the select condition for the table TAX1042 to fetch
		     the record where REVFLG=0
		   . Modified the section FORM to populate the value for 4th,5th
		     and 6th piece in the TMP1 array.
		     
	12/30/05 - SWARNALP - CR 18751
		   Removed the IRSXSEQ table reference from the section 
		   FRM1042S as the table IRSXSEQ itself is deleted as the 
		   functionality is no longer supported

	10/26/05 - TITOVE - CR 17287
		   Converted into PSL. Removed tags ACNDTL, PROCESSED and
		   pre-2005 revision history. Moved several "quit" conditions
		   from T1042S section to before the call to it. Optimized
		   selection of records from TAX1042 table by adding more
		   conditions to it.
		   
	08/19/05 - Hillanbrand - CR16492
		   Brought in P01Etrade version of this routine which includes
		   changes for project ARQ#30980 1042-S Corrections, 
		   ARQ#47224 2001 Specification for Filing Form 1042-S and
		   CR13081 2004 Specifications for Filing Form 1042-S. This routine
		   contains the changes for the projects and is ready for the
		   conversion to a procedure.
	 
	07/13/05 - TAMBIP -CR15827
		   Retrofit 47224. To populate TMP1 array with IRSEXM value for first
		   line on form section FORM was modified.

	*/

	type public Boolean CORR
	type public Date LRD

	type Boolean CTOFA
	type Date BEGYR, ENDYR
	type Number CTOF, SAVCID, SBN, YR
	type String EIN, TYPCIF(,,)

	// Inputs from procedure IRSO42S and reports SCA1042B and SCA1042C
	set CORR = CORR.get()
	set LRD = LRD.get()
	
	set SAVCID = ""

	// Find the interest/ dividend reporting year
	set BEGYR = $$BOTY^SCADAT($$BOTY^SCADAT(%SystemDate,1)-1,1) // Beginning of tax year
	set ENDYR = $$EOTY^SCADAT(BEGYR,1)          	// End of tax year
	set YR = ENDYR.year()				// Year part of tax year

	set EIN = CUVAR.ein
	set CTOFA = CUVAR.ctof1099
	set SBN = CUVAR.sbinstno
	
	type RecordUTBLIRS utblirs = Db.getRecord("UTBLIRS", "KEY='1042-S'", 1)
	
	if 'utblirs.getMode() set CTOF = ""
	else  set CTOF = utblirs.fctoff

	do Db.delete("REP1042S","JOB=:%ProcessID")

	// Correction Reporting Run
	if CORR do CORR(LRD) quit
	
	// Original Reporting Run
	type DbSet ds = Db.selectDbSet("TAX1042", "TAXYR=:YR AND REVFLG=0 AND VDATE IS NULL AND CDATE IS NULL", "CID ASC")
	
	while ds.next() do {
		
		type RecordTAX1042 t1042 = ds.getRecord("TAX1042")
		
		do T1042S(.t1042)
		}
	
	// Process last CID
	if 'SAVCID.isNull() do ACN(SAVCID)
	
	quit
	
	
T1042S(RecordTAX1042 t1042)

	type public Boolean CORR
	type public Number SAVCID
	type public String ACNDTL(,,,)
	
	type String FR = ""

        if t1042.irsexm.isNull()!(t1042.irsexm = 1)!((t1042.irsexm = 0)&(t1042.nr '= "CA")) quit
        
        // Voided, Corrected or Original
	set FR = $S(t1042.vdate:"V",t1042.cdate:"C",1:"O")
	
	if SAVCID.isNull() set SAVCID = t1042.cid
	
	if (SAVCID '= t1042.cid) do { quit
	
		do ACN(SAVCID) 
		
		set SAVCID = t1042.cid
		}
	
	set ACNDTL(FR,t1042.cid,t1042.nr,t1042.bwpct,t1042.whexr).piece("|",1) = ACNDTL(FR,t1042.cid,t1042.nr,t1042.bwpct,t1042.whexr).get().piece("|",1) + t1042.ipyd
	set ACNDTL(FR,t1042.cid,t1042.nr,t1042.bwpct,t1042.whexr).piece("|",2) = ACNDTL(FR,t1042.cid,t1042.nr,t1042.bwpct,t1042.whexr).get().piece("|",2) + t1042.nbwa
	set ACNDTL(FR,t1042.cid,t1042.nr,t1042.bwpct,t1042.whexr).piece("|",3) = ACNDTL(FR,t1042.cid,t1042.nr,t1042.bwpct,t1042.whexr).get().piece("|",3) + t1042.nrwacy
		
	set ACNDTL(FR,t1042.cid,t1042.nr,t1042.bwpct,t1042.whexr).piece("|",11) = t1042.irsexm

	if t1042.vdate set ACNDTL(FR,t1042.cid,t1042.nr,t1042.bwpct,t1042.whexr).piece("|",15) = t1042.vdate
	else  if t1042.cdate set ACNDTL(FR,t1042.cid,t1042.nr,t1042.bwpct,t1042.whexr).piece("|",16) = t1042.cdate

	quit


ACN(CID)	// Process accounts owned by this ACN

	type public Boolean CORR
	type public Number FN
	type public String ACNDTL(,,,), OTIN
	
	type Number ACN, MF
	type String TAXID, ZIP9

	type RecordDEP dep = Db.getRecord("DEP", "CID = :CID")
	
	set ACN = dep.acn

	type RecordCIF cif = Db.getRecord("CIF", "ACN = :ACN")
	
	set ZIP9 = cif.mzip9 
	if ZIP9?5N set ZIP9 = ZIP9_"-0000"
	if ZIP9.isNull() set ZIP9 = 0
	
	set TAXID = cif.taxid
	
	set MF = cif.mf
	if MF.isNull() set MF = 1	// Regular mail flag - default

	// Need to get Recipient Code
	type RecordRELCODE1 rcode1 = Db.getRecord("RELCODE1", "REL = :dep.acnrelc")
	
	set FN = 0

	if 'CORR do FORM("O",CID,TAXID,rcode1.rcode.piece($C(9),1))
	
	if CORR do {
		
		do FORM("V",CID,OTIN,rcode1.rcode.piece($C(9),1))
		
		do FORM("C",CID,TAXID,rcode1.rcode.piece($C(9),1))
		}
	
	do FILE
	
	kill ACNDTL

	quit
	
	
FORM(String FR,			// Type of Form Run
	Number CID,		// Account Number
	String TID,		// Tax ID
	String RCD)		// Tax Recipient Code

	// Loop through ACNDTL array to build TMP1 array
	
	type public Boolean CORR
	type public Number FN
	type public String ACNDTL(,,,), TMP1(,)

	type Number BWPC, CNT, WHEXR
	type String NR

	set CNT = 0
	set NR = ""
	for  set NR = ACNDTL(FR,CID,NR).order() quit:NR.isNull()  do {
		
		set BWPC = ""
		for  set BWPC = ACNDTL(FR,CID,NR,BWPC).order() quit:BWPC.isNull()  do {
			
			set WHEXR = ""
			for  set WHEXR = ACNDTL(FR,CID,NR,BWPC,WHEXR).order() quit:WHEXR.isNull()  do {
				
				set CNT = CNT + 1
				
				// Second line on form
				if CNT#2 = 0 do { quit
					set TMP1(CID,FN).piece("|",7) = BWPC
					set TMP1(CID,FN).piece("|",8) = WHEXR
					set TMP1(CID,FN).piece("|",9) = NR
					set TMP1(CID,FN).piece("|",10) = ACNDTL(FR,CID,NR,BWPC,WHEXR).piece("|",1)
					set TMP1(CID,FN).piece("|",11) = ACNDTL(FR,CID,NR,BWPC,WHEXR).piece("|",2)
					set TMP1(CID,FN).piece("|",12) = ACNDTL(FR,CID,NR,BWPC,WHEXR).piece("|",3)
					set TMP1(CID,FN).piece("|",18) = ACNDTL(FR,CID,NR,BWPC,WHEXR).piece("|",11)
					}
					
				// First line on new form
				set FN = FN + 1 
				set TMP1(CID,FN) = BWPC_"|"_WHEXR_"|"_NR_"|"_ACNDTL(FR,CID,NR,BWPC,WHEXR)
				set TMP1(CID,FN).piece("|",13) = RCD
				set TMP1(CID,FN).piece("|",14) = ACNDTL(FR,CID,NR,BWPC,WHEXR).piece("|",11)
				if FR = "V" set TMP1(CID,FN).piece("|",15) = 1
				if FR = "C" set TMP1(CID,FN).piece("|",16) = 1
				set TMP1(CID,FN).piece("|",17) = TID
				}
			}
		}
				
	quit


FILE	// Loop through TMP1 and file

	type public String TAXID, TMP1(,)
	type public Number ACN, CID, MF
	
	type Number FN = ""

	for  set FN = TMP1(CID,FN).order() quit:FN.isNull()  do {
		
		type RecordREP1042S rep = Db.getRecord("REP1042S", "JOB=:%ProcessID,MF=:MF,TAXID=:TAXID,ACN=:ACN,CID=:CID,FN=:FN", 1)
		
		set rep.%bwpct1 = TMP1(CID,FN).piece("|",1)
		set rep.whexr1 = TMP1(CID,FN).piece("|",2)
		set rep.nr1 = TMP1(CID,FN).piece("|",3)
		set rep.ipyd1 = TMP1(CID,FN).piece("|",4)
		set rep.nbwa1 = TMP1(CID,FN).piece("|",5)
		set rep.nrwacy1 = TMP1(CID,FN).piece("|",6)
		set rep.%bwpct2 = TMP1(CID,FN).piece("|",7)
		set rep.whexr2 = TMP1(CID,FN).piece("|",8)
		set rep.nr2 = TMP1(CID,FN).piece("|",9)
		set rep.ipyd2 = TMP1(CID,FN).piece("|",10)
		set rep.nbwa2 = TMP1(CID,FN).piece("|",11)
		set rep.nrwacy2 = TMP1(CID,FN).piece("|",12)
		set rep.rcode1 = TMP1(CID,FN).piece("|",13)
		set rep.irsexm = TMP1(CID,FN).piece("|",14)
		set rep.vdflg = TMP1(CID,FN).piece("|",15)
		set rep.crflg = TMP1(CID,FN).piece("|",16)
		set rep.ctaxid = TMP1(CID,FN).piece("|",17)
		set rep.irsexm2 = TMP1(CID,FN).piece("|",18)

		do rep.save()
		}
		
	quit


CORR(Date LRD)	// Collates ^IRSUPD for 1042-S Corrections

	type Date EFD = LRD - 1
	
	type DbSet ds = Db.selectDbSet("IRSUPD","TJD>:EFD AND FTYPE=15")
	
	while ds.next() do {
		
		type RecordIRSUPD irsupd = ds.getRecord("IRSUPD")
		
		do IRSUPD(.irsupd)
		}
	quit
	

IRSUPD(RecordIRSUPD irsupd)	// Detail processing of ^IRSUPD

	type public Date LRD
	type public Number SAVCID, YR
	type public String TYPCIF(,,)
	
	type String OTIN = irsupd.otin

	// Check if correction for this taxid and form type already processed
	if TYPCIF(irsupd.ftype,irsupd.ocif,irsupd.miscseq).data() quit
	
	set TYPCIF(irsupd.ftype,irsupd.ocif,irsupd.miscseq) = " "
	
	type DbSet ds = Db.selectDbSet("TAX1042", "TAXYR=:YR AND CID=:irsupd.miscseq AND REVFLG IS NULL")
	
	while ds.next() do {
		
		type RecordTAX1042 t1042 = ds.getRecord("TAX1042")
		
		if t1042.vdate,(t1042.vdate < LRD) quit
	
		if t1042.cdate,(t1042.cdate < LRD) quit
	
		if (irsupd.ccode '= 2),'t1042.vdate,'t1042.cdate quit
		
		do T1042S(.t1042)
		}

	// Process last CID
	if 'SAVCID.isNull() do ACN(SAVCID)

	quit

vSIG()	quit "60403^47599^Shriram Deshpande^8666"	// Signature - LTD^TIME^USER^SIZE
