CIFFUNCS		/*CIFFUNCS	Library;CIF Filer Functions

	   ORIG: RUSSELL - 03/14/96
	   DESC: Loan Filer Functions
	
	   KEYWORDS: Loans
	
	   EXAMPLE:
	   Text of example (line one)
	
	   LIBRARY:
	   . TCMTFM   - Comment field for maintenance (DEP,LN,CIF)
	         . TCMT     - Comment field for maintenance (other files)
	         . CIFHBLD  - Common call to CIF file history
	         . FMLD     - Update File Maintenance Last Date field

	
	---------- Revision History -------------------------------------------
	
	12/21/06 - SWARNALP - CR 24539
		   Added FMTABLE as the 7th argument in the line tag of section 
		   TCMTFM and as the 8th argument in the line tag of section 
		   TCMT to get the value passed from the journal mapping.  Also 
		   changed the logicto set the values in appropriate places in 
		   TCMT variable delimited by ":" to make sure that the values 
		   are stored in right location in case if any of the parameter 
		   is passed as null.
                   
        11/17/06 - Mugilvannan - 24126
	   	   Modified the section FMLD to update the column CIF.USERID to 
	   	   identifies the teller that did the last file maintenance update.
	   	   
	   	   
	01/17/05 - HILLANBRAND - 13686
		   Removed subsection TAXID. CIF triggers were modified to call
		   directly into the procedure EXT^VERTIN.

	05/16/03 - ZWITKOWITSM - 51349
		   Correct use of non-required parameters in section TCMTFM,
		   TCMT, and CIFHBLD to prevent undefined errors.

	12/04/02 - CARROLLJ - 51349
		   Remove linetag TYPE.  The check on description in PRODCTL
		   is not needed since it's a required field. 

	09/16/02 - SCOTTC - 43583
		Code review changes from PSL conversion.  Removed setting of TYPE in TAXID 
		section.  It wasn't used in VERTIN or anywhere else.

	02/01/02 - MALHOTRAV - 47777
		Modified section NAM. The following code was causing
		an undefined error during conversion -
		Q $E(FNM,1,FL-(LEN-40))_$S(MI'="":" "_MI,)_" "_LNM_" "_SFX
		This has been modified to fix the problem.

	01/03/02 - GOLATOS - 43583
		Converted to PSL.
	
	*/

	//Do not enter from top
	quit	

public	TCMTFM(KEY,CLS,COLUMN,OLD,NEW,TEFD,FMTABLE)	// Comment field for file maint

	/*
	   Returns appropriate comment field (TCMT) for file maintenance
	   activity for use by the LN file journal for DTJFM
	
	ARGUMENTS:
	. KEY  Account number or 	/TYP=T/REQ
	     customer number
	
	. CLS  Account class  		/TYP=T/REQ
	
	. COLUMN Column name  		/TYP=T/REQ
	
	. OLD  Old value		/TYP=T/REQ
	
	. NEW  New value		/TYP=T/REQ
	
	. TEFD  Effective date		/TYP=D/NOREQ
	
	. FMTABLE  File Maintenance Table  /TYP=T/NOREQ
	
	   RETURNS:
	. $$  Comment field		/TYP=T
	
	   EXAMPLE:
	set X=$$TCMTFM^CIFFUNCS(CID,"L","STAT",1,4,56565,"STBLSTATL")
	
	*/

	new TCMT,TABLE
	// Table name
	set TABLE=$S(CLS="D":"DEP",CLS="L":"LN",1:"CIF")
	if CLS="CIFEXT" set TABLE=CLS
	// Comment
	set TCMT="["_TABLE_"]"_COLUMN_":"_OLD_":"_NEW  
	if TABLE'="CIF" set TCMT=KEY_TCMT      
	if TEFD.get() set TCMT=TCMT_":"_$$DAT^%ZM(TEFD)
	set TCMT.piece(":",6)=FMTABLE.get()
	
	quit TCMT
	
public	TCMT(KEY,TABLE,COLUMN,OLD,NEW,TEFD,COMMENT,FMTABLE)	//Comment field for file maint

	/*
	   Returns appropriate comment field (TCMT) for file maintenance
	   activity for use by the LN file journal for DTJFM
	
	ARGUMENTS:
	. KEY  Account number or	  /TYP=T/REQ
	     customer number
	
	. TABLE  Table name 		  /TYP=T/REQ
	
	. COLUMN Column name		  /TYP=T/REQ
	
	. OLD  Old value		  /TYP=T/REQ
	
	. NEW  New value		  /TYP=T/REQ
	
	. TEFD     Effective date		  /TYP=D/NOREQ
	
	. COMMENT  Additional comment	  /TYP=T/NOREQ
	
	. FMTABLE  File Maintenance Table /TYP=T/NOREQ
	
	   RETURNS:
	. $$  Comment field		/TYP=T
	
	   EXAMPLE:
	set X=$$TCMT^CIFFUNCS(CID,"CIFFSD","IRN",12,13,56565,,"STBLSTATL")
	
	*/

	new TCMT
	// Comment
	set TCMT=KEY_"["_TABLE_"]"_COLUMN_":"_OLD_":"_NEW      
	if TEFD.get()>%SystemDate do {
		// TEFD info
		set TCMT=TCMT_":"_$$DAT^%ZM(TEFD)   
		if COMMENT.get()'="" set TCMT=TCMT_" "_COMMENT
		}
	// Extra comment field
	else  if COMMENT.get()'="" set TCMT=TCMT_":"_COMMENT
	set TCMT.piece(":",6)=FMTABLE.get()     
	quit TCMT
	

public	CIFHBLD(TCMT,VPAR)	//Common call to CIF history filer

	/*
	   This code provides a common interface to applications that must
	   file into the CIF history file
	
	   ARGUMENTS:
	         . TCMT          Comment                 /TYP=T/REQ
	
	         . VPAR          Filer switches          /NOREQ/MECH=REFARR:R
	
	              TRIGBRF,VALREQ,VALDD,TRIGAFT,UPDATE,VALRI,INDEX,JOURNAL
	
	   INPUTS:
	         . SYSTEM        TJD,EFD,%UID
	
	         . ACN           Customer number          /TYP=N/REQ
	
	   EXAMPLE:
	         D CIFHBLD^CIFFUNCS($$^MSG(1234),"/NOVALDD/NOVALRI")
	
	*/
	new cifh,SEQ
	type RecordCIFH cifh=Class.new("RecordCIFH")
	do cifh.setAuditFlag(1)
	set cifh.seq=Db.nextVal("CIFH","ACN")
	set cifh.acn=ACN
	set cifh.tcmt=TCMT 
	set cifh.tjd=%SystemDate
	set cifh.tlo=%UserStation
	set cifh.hdate=%CurrentDate
	set cifh.htime=%CurrentTime
	set cifh.uid=%UserID

	do cifh.save(VPAR.get())

	quit 


public	FMLD(ACN)	// Update File Maintenance Last Date field

	/*
	ARGUMENTS:
	
	. ACN Customer Number		/TYP=N/REQ/MECH=VAL
	
	EXAMPLE:
	
	do FMLD^CIFFUNCS(ACN)

	*/

	type RecordCIF cif=Db.getRecord("CIF","ACN")
	set cif.fmld=%SystemDate
	set cif.userid=%UserID
	do cif.bypassSave()

	quit 
	

public	NAM(FNM,MNM,LNM,SFX)	// Default Customer Full Name

	/*
	This code will generate the Customer Full Name based on the
	structured name fields First, Middle, Last, and Suffix.
	
	Since it will be possible for the total length of these components
	to exceed 40 characters, middle name will be replaced by an initial.
	If the length still exceeds 40, the first name will be truncated.
	
	ARGUMENTS:
	. FNM   First Name		/TYP=T/NOREQ

	. MNM   Middle Name		/TYP=T/NOREQ

	. LNM   Last Name		/TYP=T/NOREQ

	. SFX   Suffix			/TYP=T/NOREQ
	
	RETURNS:
	. $$    '< 40: FNM MNM LNM SFX
		< 40: FNM MI LNM SFX (FNM may be truncated)

	*/

	new FL,ML,LL,SL,I,LEN,MI,NAM,SPACE
	
	set FNM=$G(FNM) set MNM=$G(MNM) set LNM=$G(LNM) set SFX=$G(SFX)
	set FL=$L(FNM) set ML=$L(MNM) set LL=$L(LNM) set SL=$L(SFX)
	set SPACE=$S(FL>0:1,1:0)+$S(ML>0:1,1:0)+$S(SL>0:1,1:0)
	
	// Total length not greater than 40
	if FL+ML+LL+SL+SPACE'>40 quit $S(FNM'="":FNM_" ",1:"")_$S(MNM'="":MNM_" ",1:"")_LNM_$S(SFX="":"",1:" "_SFX)
	
	// Length exceeds 40 - Try it with just a middle initial
	set MI=$E(MNM) set LEN=FL+$L(MI)+LL+SL+SPACE
	if ML if LEN'>40 quit $S(FNM'="":FNM_" ",1:"")_MI_" "_LNM_$S(SFX="":"",1:" "_SFX)
	
	// The suffix is causing the problem - Truncate first name
	quit $E(FNM,1,FL-(LEN-40))_$S(MI'="":" "_MI,1:"")_" "_LNM_" "_SFX
	

vSIG()	quit "60624^22488^P.R. Swarnalatha^6367"	// Signature - LTD^TIME^USER^SIZE
