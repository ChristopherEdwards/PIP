SCAV100	//
  /*
 ORIG: chhabris - 07/31/2005
 DESC: PAGE HEADING DRIVER FOR DEC VT-200

 ---- Comments --------------------------------------------------------
 	CALLED BY: 
	    CALLS:  ^%TRMVT,^%ZUSE
	     DESC:  
	
	 GLOBALS -  
	     READ:
	
	    INPUT:
	        PN  - PAGE NUMBER (DEFAULT TO 1)
	        RN  - ROUTINE NAME
	        %RD - RUN DATE
	        %ED - EFFECTIVE DATE
	
	   OUTPUT:
	        QUIT -  0 (CONTINUE)  1 (QUIT)



 ---- Revision History ------------------------------------------------
	07/29/2005 - CHHABRIS - CR16677
		     Converted to PSL.
		     Also removed the revision history prior to 2004.
 ----------------------------------------------------------------------

 */
 
	quit


START	//

	type public Number %HI,IORM,PN,$X,$Y
	type public String CONAM,HDG(),$I,%MSKD,RN,SCROLL,SKHD,%TO,QUIT
	type public Date %ED,%RD
		
	set:'PN.data() PN=1
	set:'%RD.data() %RD=$$DAT^%ZM(%CurrentDate,%MSKD.get()) set:'%ED.data() %ED=%RD
	//
	if PN = 1 kill %HI set PN = PN + 1
	set:'%HI.data() QUIT=""
	set:'IORM.data() IORM=79
	set:'SKHD.data() SKHD=""

	if 'SCROLL.data() set SCROLL=""
	if PN = -1 do EXIT quit
	
	if %HI.data() do WAIT quit
	
	// Clear screen and switch to 132 column mode
	write $$CLEAR^%TRMVT
	if IORM=132 write $$SCR132^%TRMVT

	set $X=0,$Y=0
	do TERM^%ZUSE($I,"WIDTH="_(IORM+1)_"/ECHO")
	
	//  Run Date: 
	write CONAM.get(),?(IORM-20),$$^MSG("4904"),%RD

	// Effective: 
	write !,RN.get(),?(IORM-20),$$^MSG("3689"),%ED.get()
	
	do HDG
	quit
	
HDG //
	
	type public Number %HI
	type public String HDG(),IORM
	type Number N
	
	set N=""
	for %HI=1:1 set N=HDG(N).order() quit:N.isNull()  write !,HDG(N)
	
	// Draw graphic line, lock region, position cursor
	write !,$$GREN^%TRMVT,$$LINE^%TRMVT(IORM),!
	set %HI = %HI + 3
	//
	write $$LOCK^%TRMVT(%HI,24),$$CUP^%TRMVT(1,%HI-1)
	set $Y=%HI-2,$X=0
	quit
	
	
WAIT	//

	type public String QUIT,X
	type public Number %HI
	
	// Press RETURN to continue or <Q>uit: 
	write !,$$BTMXY^%TRMVT,$$^MSG("4409")
	do ESCN
	set QUIT=X="Q"
	//
	write $C(13),$$LINDEL^%TRMVT,$$CUP^%TRMVT(1,23)
	set $X=0,$Y=%HI
	if QUIT do EXIT
	quit
	
	
EXIT	//
	type public Number %HI,IORM,PN
	type public String HDG(),SCROLL,SKHD
	
	if '%HI.data() kill PN quit
	
	// ---------- Unlock region
	do TERM^%ZUSE(0,"FLUSH")

	// End of report .... Press RETURN to continue: 
	if PN=-1 write $$^MSG("4819") do ESCN
	
	// Unlock region, clear screen, switch to 80 colmn mode
	write $$LOCK^%TRMVT,$$CLEAR^%TRMVT,$$SCR80^%TRMVT
	do TERM^%ZUSE($I,"ECHO/ESCAPE")

	kill %HI,IORM,HDG,PN,SKHD,SCROLL lock
	quit


ESCN	//
	type Number LEN
	type public Number %TO,ZB
	type public String X

	do TERM^%ZUSE($I,"ECHO/ESCAPE")

	if %TO read X else  set ZB=29 quit
	set LEN=$ZB.length()
	set ZB=$ZB.ascii(LEN)
	set:ZB=126 ZB=+$ZB.extract(3,9)
	quit:X'?.E1L.E
	set X=$$UPPER^%ZFUNC(X)
	quit

vSIG()	quit "60178^29155^Sanjay Chhrabria^2677"	// Signature - LTD^TIME^USER^SIZE
