DEPPEN0(RecordDEP dep,RecordTTX ttx,RecordTRN trn)	//;PBS - DEP - V2.0 - Decrease Penalty YTD
	/*
	       ORIG:
	       DESC:
	
	  ---- Revision History ------------------------------------------------

            07/27/05 - SkariahV- CR16679
	               Removed #WARN and #OPTIMIZE directives.
	
	  
	    01/21/03 - Erik Scheetz - 51349
	    	    Made general changes for the following:
		    	1. Cleaned up Parameter mismatches
		    	2. Cleaned up code by using new PSL methods/properties.
		    	3. Added #WARN/#OPTIMIZE compiler switches
	    		
	    09/26/02 - Erik Scheetz - 43583
		      PSL conversion cleanup.

	    6/04/02 - Erik Scheetz - 43583 
		      Convert to PSL, moved from routine to DQ Procedure
		      Definition. Remove pre-2001 revision history
	
	*/

	
	type Public Number ER
	
	//Amount exceeds year-to-date penalty
	if dep.pytd-ttx.tamt<0!(dep.pentytd<0) do Runtime.setErrMSG("DEP",271) quit:ER

	if dep.ira do CYRPEN^IRA(.dep,.ttx) quit:ER	 

	do AUT(.dep)
	quit:ER	 

	do FILE(.dep,.ttx,.trn)
	quit

FILE(RecordDEP dep,RecordTTX ttx,RecordTRN trn) // File Data

	type Public Number TYR,YR
	
	set dep.bal=dep.bal+ttx.tamt		
	set dep.tld=%SystemDate			
	
	// Update Pr.Fin.Year indicator
	set dep.balcol=dep.balcol+ttx.tamt	
	set YR=$$PYFLAG^DEPINT(.dep,.ttx)	

	// Update Pr.Tax Year indicator
	set TYR=$$PTYFLAG^DEPINT(.dep,.ttx)	
	
	if YR set dep.pytd=dep.pytd-ttx.tamt
	else  set dep.ppy=dep.ppy-ttx.tamt

	if TYR set dep.pentytd=dep.pentytd-ttx.tamt
	if 'TYR,'trn.pcfd35 set dep.penpty=dep.penpty-ttx.tamt
	
	set dep.ppl=dep.ppl-ttx.tamt
	
	do ^UPDSTAT(.dep,.trn)
	quit 

public EC(RecordDEP dep,RecordTTX ttx,RecordTRN trn) 	// Error Correct

	type Public Number ER
	
	if ttx.tso["REV#" do ^UPDCCLD(.dep,.ttx)

	if dep.ira do CYRPEN^IRA(.dep,.ttx) quit:ER 

	do FILE(.dep,.ttx,.trn)
	quit
	
AUT(RecordDEP dep) // Authorization Verification

	type Public Number CID,OVR(,,)
	
	// Restriction Flag
	if dep.rflg do ^UFLG(.dep) 	

	// Account Status
	if dep.stat set OVR(CID,"OVR","STAT"_dep.stat)=""
	
	quit 
 #OPTION ResultClass ON
Public String vSIG()	quit "60108^21027^Viji Skariah^1865"	// Signature - LTD^TIME^USER^SIZE
