LNUSFV		/*
	   Secondary frequency verification utility

	       ORIG:  Neal E. Gorman (5053) - 08/14/88
	  CALLED BY:  Routine: LNPPC3, Screen LNUCPF
	      CALLS:  ^UFRE
	   PROJ #'S:
	       DESC:

	              Secondary Frequency Verification Utility

	              The purpose of this utility is to provide a means to
	              verify that:

	              1)  Secondary frequencies (i.e. interest, payment change,
	                  reamortization) correspond to primary frequencies
	                  (usually master payment frequencies).  All secondary
	                  frequency "hits" fall on those of the primary freq-
	                  uency.

	              2)  Secondary next dates (i.e. next interest change date)
	                  falls on a regular "hit" of the primary frequency.

	              Sample Calls - @LNS015 - Mortgage Maintenance (^LNFIL1
	                                       calls ^LNPPC3)
	   GLOBALS -
	       READ:
	        SET:

	      INPUT:

	   Variables: None


	     OUTPUT:

	   Variables:  ER - Error Flag
	               RM - Error Message (if appropriate)

	   Parameters: None

	              Note:  Routine verifies propriety of D2 and F2

	---- Revision History ------------------------------------------------

	03/09/06 - AlagarsamyS - CR 19088
		   Added a parameter in getRecord to avoid error.
	
        07/26/05 - SkariahV- CR16679
	           Removed #WARN and #OPTIMIZE directives.
	              
	05/26/04 - KELLYP - CR 8446
		   Modified entire procedure to conform to standards and 
		   fixed several errors in the post-conversion logic.

	07/11/02 - CARROLLJ - 43583
		   PSL conversion cleanup.

	02/18/02 - CARROLLJ - 43583
		   Convert to PSL.
	----------------------------------------------------------------------

	*/
	quit

Public VER(D1,F1,P1,D2,F2,P2)	// Verification of secondary frequency

	/*
	Aruguments:

	Parameters  D1 - Start date for primary frequency array
		    F1 - Primary Frequency (usually master pymt frequency)
		    P1 - Text describing 1st frequency (for error message)

		    D2 - "Hit" Date on Secondary Frequency
		    F2 - Secondary Frequency
		    P2 - Text describing 2nd frequency (for error message)
	*/

	type Date JD,NJD
	type Number AF,PCHM
	type String FRE,MFRE,NGFRE

	type Public Number CID,ER
	type Public String ET,RM

	// master payment
	if $G(P1)="" set P1=$$^MSG(4209)

	// Only consider PCHM with a loan account
	type RecordLN ln=Db.getRecord("LN","CID",1)
	set PCHM=ln.pchm
	if F1=""!(F2=""),(PCHM'=2!(PCHM'=3)) set ER=2 set ET="LNUSFV3" do ^UTLERR quit
	if F1=""!(F2="") set ER=1 set ET="LNUSFV3" do ^UTLERR quit

	if '$G(D1) do D1(.ln)
	if 'D2 set D2=D1
	if '$D(MFRE(D1,F1)) do MFRE if AF>364 quit
	set JD=D2 do VER1
	if ER set ET="LNUSFV1" do ^UTLERR quit

	set FRE=F2 
	set JD=D2 
	set (D2,JD)=$$NJD^UFRE(JD,FRE) quit:ER  do VER1
	if ER set ET="LNUSFV2" do ^UTLERR
	quit


VER1	// Verification

	type Public Date D1,D2,JD,NJD
	type Public String F1,FRE,MFRE,NGFRE
	type Public Number ER

	set NJD=$O(MFRE(D1,F1,"")) 
	set FRE=NGFRE
	if D2<NJD for  set JD=NJD set NJD=$$NJD^UFRE(JD,FRE) quit:ER  set MFRE(D1,$E(FRE,2,99),NJD)="" if D2'<NJD quit
	set NJD=$O(MFRE(D1,F1,""),-1) 
	set FRE=F1
	if D2>NJD for  set JD=NJD set NJD=$$NJD^UFRE(JD,FRE) quit:ER  set MFRE(D1,FRE,NJD)="" if D2'>NJD quit

	if '$D(MFRE(D1,F1,D2)) set ER=1
	quit

D1(RecordLN ln)	// Determine start date if not provided

	type Public Date D1

	Type RecordLNBIL1 lnbil1=Db.getRecord("LNBIL1","CID=:ln.cid,SCHSEQ=:ln.schseq")
	set D1=lnbil1.cdpd if D1 quit

	// Scheduled Payment - Next Date
	set D1=ln.schnd if D1 quit

	// Distribution 1 Next Due
	set D1=ln.dist1nd if D1 quit

	set D1=%SystemDate
	quit

MFRE	// Builds master payment frequency array

	type Public Number AF,ER
	type Public Date D1,JD,NJD
	type Public String AF,F1,FRE,MFRE,NGFRE

	set JD=D1 
	set NGFRE="-"_F1

	set NJD=$$NJD^UFRE(JD,NGFRE,.AF) quit:ER 
	if AF>364 quit
	for  set JD=NJD set NJD=$$NJD^UFRE(JD,F1) quit:ER  set MFRE(D1,F1,NJD)="" if %SystemDate+366<NJD quit
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60333^29199^Sivakumar Alagarsamy^3881"	// Signature - LTD^TIME^USER^SIZE
