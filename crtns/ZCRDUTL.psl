ZCRDUTL	  /*
	ORIG: rickardsb - 02/07/2007
	DESC: Card Management Utility

	---- Comments --------------------------------------------------------

	---- Revision History ------------------------------------------------

	08/27/07 - Brandon Rickards - CR 28685
		Modified section STAT to return '05' Response Code for Hot status
		and '36' for card status 2,3,5,10
		Modified section LMTCHK to use NULL limit values as unlimited

	07/10/07 - Brandon Rickards - CR 28162
		Modified section STAT to return '04' Response Code for Restricted
		Cards.

	06/19/07 - Brandon Rickards - CR 27711
		Modified section STAT to approve Status 15 (Active without PIN).
		Replaced each getMode() with Db.isDefined.

	06/07/07 - Brandon Rickards - CR 27424
		Modified section LMTCHK to quit with Response Code "T7"
		for Cash Advance exceeding daily limit.
		Modified section STAT to use Db.isDefined rather than getMode
		to determine if a valid Record entry exists.

	05/11/07 - Brandon Rickards - CR 27068
		Created procedure.

 */

	// No top level entry
	quit 

STAT(CRDNUM,CRDTYP,CRDSTAT,EXPDT)
	/*
	Validate Card Status

	ARGUMENTS:
		CRDNUM		Card Number
		CRDTYP		Card Type
		CRDSTAT		Card Status
		EXPDT		Expiration Date

	  RETURNS:
		$$ Returns Response Code
		CRDSTAT		Return current Card Status
		EXPDT		Returns Expiration Date
	*/
	set (CRDSTAT,EXPDT)=""

	// RSPCD "14"=Invalid Card Number
	if 'Db.isDefined("CRD","CRDTYP=:CRDTYP,CRDNUM=:CRDNUM") quit "14"
	type RecordCRD crd=Db.getRecord("CRD","CRDTYP=:CRDTYP,CRDNUM=:CRDNUM")

	// Card Status
	set CRDSTAT=crd.stat
	// Card Expiration Date
	set EXPDT=crd.expdt

	// Valid Status: 0=Active & 15=Active without PIN
	if CRDSTAT=0!(CRDSTAT=15) quit "00"

	// 1=Hot (RSPCD "05"=Do Not Honor)
	if CRDSTAT=1 quit "05"
	// 2=Order Pending,3=Ordered,5=Cancelled,10=Damamged
	if CRDSTAT=2!(CRDSTAT=3)!(CRDSTAT=5)!(CRDSTAT=10) quit "36"
	// 6=Expiration Pending,12=Expired (RSPCD "54"=Expired Card)
	if CRDSTAT=6!(CRDSTAT=12) quit "54"
	// 8=Lost (RSPCD "41"=Lost Card)
	if CRDSTAT=8 quit "41"
	// 9=Stolen (RSPCD "43"=Stolen Card, pick up)
	if CRDSTAT=9 quit "43"

	// All other Statuses are invalid, quit "05" - Do Not Honor
	quit "05"

LMTCHK(CRDNUM,CRDTYP,TRNTYPE,TAMT)
	/*
	Check Daily Card Limits

	ARGUMENTS:
		CRDNUM		Card Number
		CRDTYP		Card Type
		TRNTYPE		Transaction Type (ATM,POS,Cashback)
		TAMT		Transaction Amount

	  RETURNS:
		$$ Returns Sufficient limits=0, Negative Response Code otherwise
	*/
	// RSPCD "14"=Invalid Card Number
	if 'Db.isDefined("CRD","CRDTYP=:CRDTYP,CRDNUM=:CRDNUM") quit "14"

	type RecordCRD crd=Db.getRecord("CRD","CRDTYP=:CRDTYP,CRDNUM=:CRDNUM")

	// RSPCD "61"=Exceeds withdrawal amount limit
	if TRNTYPE="ATM",(crd.atmravl'=""),((crd.atmravl-TAMT)<0) quit "61"
	if TRNTYPE="CSHBK",(crd.csbravl'=""),((crd.csbravl-TAMT)<0) quit "61"
	if TRNTYPE="POS",(crd.posravl'=""),((crd.posravl-TAMT)<0) quit "T7"
	if (crd.crdavl'=""),((crd.crdavl-TAMT)<0) quit "61"

	quit 0

LMTUPD(CRDNUM,CRDTYP,TRNTYPE,TAMT)
	/*
	Update Daily Card Limits


	ARGUMENTS:
		CRDNUM		Card Number
		CRDTYP		Card Type
		TRNTYPE		Transaction Type (ATM,POS,Cashback)
		TAMT		Transaction Amount
	*/
	if 'Db.isDefined("CRD","CRDTYP=:CRDTYP,CRDNUM=:CRDNUM") quit
	type RecordCRD crd=Db.getRecord("CRD","CRDTYP=:CRDTYP,CRDNUM=:CRDNUM")
	do crd.setAuditFlag(1)

	/*
	If the last transaction date stored in CRD.TLD is not equal to 
	the system date then CRD.TLD will be updated with the system date.
	In addition, the withdrawal amounts stored in CRD.TWTLD,CRD.ATMTWTLD,
	CRD.POSTWTLD, and CRD.CSBTWTLD will be reset to zero.
	*/
	if crd.tld'=%SystemDate do {
		set crd.tld=%SystemDate
		set crd.twtld=0
		set crd.atmtwtld=0
		set crd.postwtld=0
		set crd.csbtwtld=0
	}

	if TRNTYPE="ATM" do {
		set crd.atmtwtld=crd.atmtwtld+TAMT
		set crd.twtld=crd.twtld+TAMT
	}
	else  if TRNTYPE="CSHBK" do {
		set crd.csbtwtld=crd.csbtwtld+TAMT
		set crd.twtld=crd.twtld+TAMT
	}
	else  do {
		set crd.postwtld=crd.postwtld+TAMT
		set crd.twtld=crd.twtld+TAMT
	}

	do crd.save()

	quit
	
	
 #OPTION ResultClass ON
Public String vSIG()	quit "60869^84795^Brandon Rickards^3861"	// Signature - LTD^TIME^USER^SIZE
