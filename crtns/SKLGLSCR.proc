SKLGLSCR //; Maintain user tables GLSC_2
	/*
	       ORIG:  Michael Berlin 06/09/98

	  ---- Revision History ------------------------------------------------

	    01/17/05 - KINI - CR13991
	    	       Modified END section: replaced with correct error 
	    	       message on record delete.
	    	       Modified FILE section to delete correct UTBLGLSCR
	    	       record.
	    	       Modified VPG01 section to display G/L Set Code 
	    	       description.	    	       

	    08/13/04 - KELLYP - CR 11521
	    	       Re-converted to PSL to fix various errors from previous
	    	       conversion and to conform to current PSL standards.

	    09/20/02 - DATTAR - 49451
		       Converted to PSL

	 ----------------------------------------------------------------------
	*/
	
	quit
	

NEW	//
	do INIT(0)
	quit


UPD	//
	do INIT(1)
	quit


INQ	//
	do INIT(2)
	quit


DEL	//
	do INIT(3)
	quit


INIT(%ProcessMode)  //

	type RecordUTBLGLSCR fUTBLGLR

	do VPG(.fUTBLGLR)

	quit


VPG(RecordUTBLGLSCR fUTBLGLR)	// Page control

	type Public String ER

	type Boolean FINISH
	type Number %PAGE,%PG
	type String AC,CLS,GLSC,IO,OLNTB,VFMQ
	
	set %PG=0 
	set %PAGE=1
	set FINISH=0

	for  do { quit:FINISH
		if %PG=0 do VPG00 if ER set FINISH=1 quit
		if %PG>0 do VPG01(.fUTBLGLR)
		if "DFQ"[VFMQ do VER(.fUTBLGLR) set FINISH=1 quit
		set %PG=%PG+1
		}
	quit


VPG00	// Set up

	type Public String AC,ER,GLSC,IO,VFMQ
	
	type String %NOPRMT,%READ,%TAB()

	if %ProcessMode=0 do {
		set %TAB("GLSC")="[UTBLGLSC]GLSC/TBL=[UTBLGLSC]/XPP=S CLS=$$CLSGLSC^SKLGLSCR(X)"
		set %TAB("AC")="[UTBLACC]AC/TBL=[UTBLACC]"
		}
	else  do {
		set %TAB("GLSC")="[UTBLGLSCR]GLSC/TBL=GLSC(/XPR=D CLSGLSCR^SKLGLSCR/XPP=S CLS=$$CLSGLSC^SKLGLSCR(X)"
		set %TAB("AC")="[UTBLGLSCR]AC/TBL=[UTBLGLSCR]:QU ""[UTBLGLSCR]GLSC=<<GLSC>>"""
		if %ProcessMode=2 set %TAB("IO")=$$IO^SCATAB($I)
		}

	set %READ="@@%FN,,,GLSC/REQ,AC/REQ" set %NOPRMT="N"
	if %ProcessMode=2 set %READ=%READ_",IO/REQ"
	do ^UTLREAD 
	
	if VFMQ="Q" set ER=1
	
	quit


CLSGLSC(GLSC) // Get GLSC class

	type Public String GLSC

	if 'Db.isDefined("UTBLGLSC","GLSC") quit ""

	type RecordUTBLGLSC fUTBLGLS=Db.getRecord("UTBLGLSC","GLSC=:GLSC")

	quit fUTBLGLS.cls


CLSGLSCR // Get GLSC for UTBLGLSCR

	type Public String GLSC

	type ResultSet rs=Db.select("GLSC","UTBLGLSCR","AC>0")
	while rs.next() set GLSC(rs.getCol("GLSC"))=""

	quit


VPG01(RecordUTBLGLSCR fUTBLGLR) // Screen

	type Public String AC,CLS,ER,GLSC,IO,VFMQ
	
	type String SID

	set fUTBLGLR=Db.getRecord("UTBLGLSCR","GLSC=:GLSC,AC=:AC",1)

	type RecordUTBLACC fUTBLACC=Db.getRecord("UTBLACC","CLS=:CLS,AC=:AC")

	if '%ProcessMode set fUTBLGLR.desc=fUTBLACC.desc

	set SID=$S(CLS="D":"UTBLGLSCRP",1:"UTBLGLSCR")
	if %ProcessMode=2,IO'=$I do OPEN^SCAIO if ER set VFMQ="Q" quit

	do DRV^USID(%ProcessMode,SID,.fUTBLGLR,.fUTBLACC)

	quit


	
VER(RecordUTBLGLSCR fUTBLGLR) //

	type Public String VFMQ

	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit

	do FILE(.fUTBLGLR)

	do END

	quit


FILE(RecordUTBLGLSCR fUTBLGLR) // File data
	
	type Public String GLSC
	
	if %ProcessMode=3 do Db.delete("UTBLGLSCR","GLSC=:GLSC AND AC=:AC") quit
	
	do fUTBLGLR.save()
	
	quit


END	//

	type Public String ER,RM,VFMQ

	if (ER.get())!(%ProcessMode=2)!(%ProcessMode=4) quit

	if VFMQ="Q" do {
		// Record not created
		if %ProcessMode=0 set RM=$$^MSG(4794) quit

		// Record not modified
		if %ProcessMode=1 set RM=$$^MSG(4795) quit

		// Record not deleted
		set RM=$$^MSG(4796)
		}
	else  do {
		// Record created
		if %ProcessMode=0 set RM=$$^MSG(6712,$$^MSG(2326)) quit

		// Record modified
		if %ProcessMode=1 set RM=$$^MSG(2334) quit

		// Record deleted
		set RM=$$^MSG(3028,$$^MSG(2326))
		}
	set ER="W"
	quit

vSIG()	quit "59940^58712^Irina Kin^3484"	// Signature - LTD^TIME^USER^SIZE
