VERTIN		 /*  ---------------  Revision History -------------------------

	  05/16/06 - SWARNALP - CR 21172
	  	     Modified EXT section to stop validating the TAX ID further 
	  	     and quit out of the section only if the TAX ID is null for 
	  	     the TIN/SIN options 0 or 4.
	  	     
	  01/27/04 - HILLANBRAND 13686
	  	     Per code review.  Change TINCO to cuvar.tinco and
	  	     removed double error message comments.

	  01/18/04 - HILLANBRAND
	  	     Remove prior years revision history. Removed sections not needed
	  	     and coded per PSL standards
	  
	*/

	
	/*
 	   Tax ID Format Verification - Verifies valid tax-id's.

	*/

	quit


EIN(DUP,TIN)	// Employer Identification Number 
	
	// (If it's wrong format) OR (it's not duplicate AND invalid EIN)
	if '(TIN?2N1"-"7N)!('DUP&($F("00|07|08|09|17|18|19|28|29|49|78|79|89",$P(TIN,"-",1)))) quit 1

	quit 0
	
	
SSN(DUP,TIN)	// Social Security Number   
	
	// (If it's wrong format) OR (it's not duplicate AND invalid SSN)
	if '(TIN?3N1"-"2N1"-"4N)!('DUP&($P(TIN,"-",3)="0000")) quit 1

	else  quit 0
	
	quit
	

EXT(RecordCIF cif) // Call from CIF post processors (Input=ACN)  
	
	
	type Boolean DUP
	type public String ER,RM
	
	type RecordCUVAR cuvar=Db.getRecord("CUVAR")
	
	if cif.taxid.isNull(),'cuvar.taxreq!(cuvar.taxreq=4) quit
 
	if cif.taxid="" do { quit
		set ER=1
		// Required 
		set RM=$$^MSG(2385)      		  
		}

	set DUP=$$DUP(cif.taxid,cif.co,cif.acn)
 	
	if cuvar.taxreq'>2  do { quit:ER            
		if cif.pers=0 set ER=$$SSN(DUP,cif.taxid)
		else  if cif.pers=1 set ER=$$EIN(DUP,cif.taxid)
		if ER do ERROR(cuvar.taxreq) quit 
		}

	else  do {
		if cuvar.taxreq'=5 do {              		
			if cuvar.ccntry="PT" do PT(cif.taxid)
			else  if cuvar.ccntry="CA" do SIN(cif.taxid)
			}

		 else  do {
			new x
			if cif.pers=0 set x=$$SSN(DUP,cif.taxid)
			if cif.pers=1 set x=$$EIN(DUP,cif.taxid)
			
			do SIN(cif.taxid)
			
			//Must be in US or Canadian format
			if x>0,ER>0 set RM=$$^MSG(3928) 
			else  set ER=0,RM=""			
		
			}
		}
	
	quit 
	
	
EXT1(IEIN)	// Call from @CUV001 CUVARGEN1 screen post processors  
	
	type Boolean DUP
	type public String ER,RM
	
	
	type RecordCUVAR cuvar=Db.getRecord("CUVAR")

	if IEIN="",'cuvar.taxreq!(cuvar.taxreq=4) quit
 
	

	if IEIN="" do {  quit
		set ER=1
		// Required 
		set RM=$$^MSG(2385)       		
		}

	set ER=""
	set DUP=1
	
				
	if cuvar.taxreq'>2 do { quit:ER     			// US Customer
	
			set ER=$$EIN(DUP,IEIN)
			if ER do ERROR(cuvar.taxreq) quit 
			}

	else  do {

		if cuvar.taxreq'=5 do {     			
			
			if cuvar.ccntry="PT" do PT(IEIN)
			else  if cuvar.ccntry="CA" do SIN(IEIN)
			}

		else  do {
		
			do SIN(IEIN)
			if $$EIN(DUP,IEIN)>0,ER>0 set RM=$$^MSG(3928)
 							
			else  do {

				set ER=0 
				set RM=""
				}
			}	
		}


	quit 
	
DUP(TIN,CO,ACN) // Duplicate tax-ID returns 1-Allowed duplicate. 0-Not allowed 
	

	type public String ER,RM
	type Boolean DUP
	type String DUPTIN,X,XTIN1,XTIN2
	
	set (XTIN1,XTIN2)=""
	
	type RecordCUVAR cuvar=Db.getRecord("CUVAR")
	
	if Db.isDefined("UTBLDUPTIN","TIN") set XTIN1=TIN

	if cuvar.duptin!(XTIN1'="") do {  quit:ER 0
		// Allow Duplicate TIN based on a company code.

		//Do all of the following checks only if CO is defined.
		if (cuvar.tinco=1)!(('cuvar.tinco)&(CO="")) quit

		// Any CIF's with this TIN
		set X=$$ACNFRTID^XALPHA(TIN,CO)

		// TAXID is in Duplicate TIN based on a company code table.
		if CO'="",X'="",X'=ACN,Db.isDefined("UTBLTINCO","CO,TIN") set XTIN2=TIN
		if XTIN2'="" quit

		// Tax ID already assigned to CIF ~p1
		if X'="",X'=ACN do { 
			set ER=1 
			//TIN/SIN already assigned to CIF ~p1
			set RM=$$^MSG(2603,X)	
		}
	}
	else  do {  quit 0
		//Any CIF's with this TIN
		set X=$$ACNFRTID^XALPHA(TIN)	

		if X'="",X'=ACN do {
			set ER=1
			// Tax ID already assigned to CIF ~p1
			set RM=$$^MSG(2603,X)
		}
	}

	quit 1
	
PT(TIN)	// Verify tax ID format for Portuguese Tax ID

	type public String ER,RM
	type String I,REM,Y,Z
	
	set ER=0 
	set RM=""
	
	
	if $L(TIN)'=9 do {
		set ER=1
		// Invalid tax ID number (must be 9 characters)
		set RM=$$^MSG(1489) quit
		}

	if $E(TIN)="C" do { quit
 		if $E(TIN,2)'=1 
		set ER=1
		// Invalid tax ID number (2nd character must be '1') 
		set RM=$$^MSG(1487)  			
		}
	
	set Y=0
	for I=1:1:8 set Y=Y+($E(TIN,I)*(10-I))
	set REM=Y#11 
	set Z=$S(REM'<2:11-REM,1:0)
	
	

	if Z'=$E(TIN,9) do {
		set ER=1 
		// Invalid tax ID number (bad check digit)
		set RM=$$^MSG(1488) quit        	
		}	

	quit 	

	
VERFMT(TIN)	/*
	   Verify format only without regard to ACN, etc.  
	   Called from post processors, e.g., in M1099

	*/
	
	type public String ER,RM
	
	type RecordCUVAR cuvar=Db.getRecord("CUVAR")
	
	//If taxid is not required quit
	if TIN="",'cuvar.taxreq!(cuvar.taxreq=4) quit

	if TIN=""  set RM=$$^MSG(2385) quit        

	if TIN?3N1"-"2N1"-"4N set ER=$$SSN(0,TIN) if ER do ERROR(cuvar.taxreq) quit 
	if TIN?2N1"-"7N set ER=$$EIN(0,TIN) if ER do ERROR(cuvar.taxreq) quit 

	quit 
	

SIN(TIN)	// Verify SIN number for Canadian users.  
	
	type public String ER,RM
	type Number A,B,C,I,S
	type String SIN

	set (A,B,C,I,S)=0
	if (TIN?3N1" "3N1" "3N) set SIN=$P(TIN," ",1)_$P(TIN," ",2)_$P(TIN," ",3)
	else  set SIN=TIN

	// SIN Check Digit Calculation

	// Get all odd digits from right to left 
	for I=2:2:8 do {
		set A=$E(SIN,I)*2 
		if A>9 set A=A-9
		set B=B+A 
		set A=0
		}
	
	set C=$E(SIN,1)+$E(SIN,3)+$E(SIN,5)+$E(SIN,7)
	set B=(B+C)#10
	if B=0 set S=$E(SIN,1,8)_0
	else  set S=$E(SIN,1,8)_(10-B)

	

	if SIN'=S  do { quit
		set ER=1
		// Invalid SIN number 
		set RM=$$^MSG(918)      		
		}
		
	quit 
	
	
	
	
ERROR(TAXREQ)	// Error
	
	type Public String ER,RM
	
	//Warning - invalid U.S. tax ID
	if TAXREQ=2 do { quit
		set ER="W" 
		set RM=$$^MSG(2966)         	        
		}
	
	// Invalid TIN/SIN  
	set RM=$$^MSG(1490)             		

	quit 

	
 #OPTION ResultClass ON
Public String vSIG()	quit "60401^35102^P.R. Swarnalatha^5464"	// Signature - LTD^TIME^USER^SIZE
