RSKAVL		/*
	Risk Analysis Available Balance Formula
	---- Revision History ------------------------------------------------
	 
	 08/10/05 - NATRAJAH -16674
	 	    File has been retrofitted from Bilbo verion V70DEV.
	 	    
	 03/21/03 - CARROLLJ - 51349
		    Removed the use of from array for database independence 
		    project.

	 01/12/2000 - DOUGAN - 31126
	 	    Temporary change until the conversion of transaction 
		    processing into PSL: Removed dep object from argument list 
		    of RSK linetag, and instead instantiated it and copied %A 
		    array into it, inside the linetag.

	 10/06/99 - DOUGANM - 33890
		    Converted M code into PSL

	 10/04/99 - MOTENJ - 33455
                    Remove the building of arrays from computed data items
                    called from LNCDI,ACNCDI,LNCO3.

	 11/30/98 - JARVISG - 30501
                    Modification of BALAVL data item.
	
	 10/22/98 - CARROLLJ - 49816:001
	            Changed the risk analysis calculation to pass in NET amount
	            from DEPPO.
	----------------------------------------------------------------------
	*/

	quit

RSK(CID,NET)    
	
	new dep,NONSD,PRI,RAMT,RSKSEQ,SDFUN,TRNSEQ,TSO,UNALHLD
	
	if $G(CID)="" quit 0
	set RAMT=$G(NET)
	
	type RecordDEP dep=Db.getRecord("DEP","CID")

	set BALAVL=dep.balavl
	set GCRAVL=dep.gcravl
	
	// Subtract available balance from the remaining amount
	if BALAVL>0 set RAMT=RAMT-BALAVL
	set %SystemDate=Db.getOneRow("TJD","CUVAR")
		
	set UNALHLD=dep.chkhld-dep.alcamt	//Unallocated check hold
	
	/*
	If transaction amount to be covered by Risk is greater than unallocated 
	holds, then risk cannot cover the transaction.  Quit with zero.
	*/
	if RAMT>UNALHLD quit 0
	
	/*
	GCR coverage can only cover the amount that is in Same day 
	GCRavailable when there are same day deposits
	*/
	if dep.netdep do {
		set NONSD=UNALHLD-dep.netdep	// Non Same Day Funds
		if NONSD<0 set NONSD=0		// No non-same day available
		if NONSD>RAMT quit		// Non same day funds cover
		set SDFUN=RAMT-NONSD
	
		/*
		Find how much GCR will be allowed to cover.  No same day 
		funds available. Can not use GCR to cover transaction.
		*/
		if 'dep.sdavl set GCRAVL=0 quit 
	
		/*
		If same day available is less than what is needed for same 
	 	day funds that are needed, can not use GCR to cover transaction.
		*/
		if dep.sdavl<SDFUN set GCRAVL=0 quit 
	
		}
	
	set RSKAVL=dep.gcravl+dep.dcl+dep.secavl

	if UNALHLD<RSKAVL set RSKAVL=UNALHLD

	quit RSKAVL
 #OPTION ResultClass ON
Public String vSIG()	quit "60122^35802^Hari Natrajan^2371"	// Signature - LTD^TIME^USER^SIZE
