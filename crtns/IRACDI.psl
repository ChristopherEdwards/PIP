IRACDI		/* 
	 ORIG: ofaltr - 06/29/2000
	 DESC: Computed Fields for IRA
	 
	  LIBRARY
	
	  $$AGE70H	Date Customer Turns 70.5
	  $$NOTDSDT	Notice Distribution Start Date
	  $$FIRSTACCC	First Account
	  $$NOTRDT	Notice Response Date
	
	 ---- Comments --------------------------------------------------------
	 
	 ---- Revision History-------------------------------------------------

        16/11/05 - DHANALAKSHMI R - 16851
	           Modified the section DISBURSE by implementing the project
	           CR1869DS Multiple Distribution Codes for IRS Form 1099-R.
	           
        09/09/05 - DHANALAKSHMI R - 16851
	           Modified the section FIRSTACC by replacing usage of XRSPPLID 
	           with DEP and also modified the settings of XSTAT.
	           PSL clean up to avoid few warnings.
	           
	09/20/00 - MURRAY - 34101
	           Added computed filed for BENMCSZ.  With the City length
	           increased to 40, this insures that BENMCSZ remains 50 
	           characters in length.

	09/06/00 - REEDD - 31426:006
		   Added section DISBURSE to return the total disbursement
		   amount for each benificiary.  This amount will include
		   each year that has a disbursement amount.

	07/25/00 - OFALTR - 40116
		Added section NOTRDT to return Notice Response Date.

	*/

	quit	 
	
AGE70H(XACN)	//Age Customer Turns 70.5
	/*
        The function is used to compute [IRA]AGE70H field.
        
        ARGUMENTS:
	
	  ACN		Customer Number		/TYPE=N/REQ/MECH=VAL
        
         RETURNS: Age Customer Turns 70.5
        
         EXAMPLE: $$AGE70H^IRACDI(ACN)
	*/
	
        type Number XAGE
        type Date XDOB

	type RecordCIF cif=Db.getRecord("CIF","ACN=:XACN")
	set XDOB=cif.dob

        if 'XDOB quit ""
	set XAGE=$$ADDMJD^SCADAT(XDOB,846,1)

        quit XAGE
        	

NOTDSDT(YEAR)	//Notice Distribution Start Date
	/*
        The function is used to compute [IRA]NOTDSDT field.
        
        ARGUMENTS:
	  YEAR		Current Year		/TYPE=D/REQ/MECH=VAL
        
         RETURNS: Notice Distribution Start Date
        
         EXAMPLE: $$NOTDSDT^IRACDI(YEAR)
	*/

	new DATE
	set DATE=$$FDAT^%ZM("0401"_(YEAR+1),"MMDDYEAR")
	quit DATE
	
FIRSTACC(XACN,XRPASEQ)	//First Account 
	/*
        The function is used to compute [IRA]FIRSTACC field.
        
        ARGUMENTS:
	
	  ACN		Customer Number		/TYPE=N/REQ/MECH=VAL
        
         RETURNS: First Account
        
         EXAMPLE: $$FIRSTACC^IRACDI(ACN,RPASEQ,TAXYR)
	*/

	new QUIT,XCID,XSTAT
	new rs

	set QUIT=0

	type ResultSet rs=Db.select("CID,STAT","DEP","ACN=:XACN AND RPASEQ=:XRPASEQ")
	if rs.isEmpty() quit ""
	while rs.next() quit:QUIT=1  do {

		set XCID=rs.getCol("CID")
		set XSTAT=rs.getCol("STAT")
		if XSTAT=0 set QUIT=1 quit
		}
	
	quit XCID

NOTRDT()	// Notice Response Date

	quit (%SystemDate+10)

	//----------------------------------------------------------------------
DISBURSE(ACN,RPASEQ,BENSEQ)

	type Number DISBAMT

	set DISBAMT=0

	type DbSet ds=Db.selectDbSet("IRABEN1","ACN=:ACN AND RPASEQ=:RPASEQ AND BENSEQ=:BENSEQ")
	if ds.isEmpty() quit DISBAMT

	while ds.next() do {

		type RecordIRABEN1 iraben1=ds.getRecord("IRABEN1")

		set DISBAMT=DISBAMT+iraben1.tamt+iraben1.tamt59+iraben1.tamt60+iraben1.tamt68+iraben1.tamt75+iraben1.tamt85+iraben1.tamt87
		}

	quit DISBAMT

BENMCSZ(ACN,RPASEQ,BENSEQ)

	type String BENMCSZ
	
	type RecordIRABEN iraben=Db.getRecord("IRABEN","ACN=:ACN,RPASEQ=:RPASEQ,BENSEQ=:BENSEQ")
	set BENMCSZ=(iraben.bencity).extract(1,35)_", "_iraben.benstate_" "_iraben.benzip
	
	quit BENMCSZ
 #OPTION ResultClass ON
Public String vSIG()	quit "60225^40046^Dhanalakshmi R^3382"	// Signature - LTD^TIME^USER^SIZE
