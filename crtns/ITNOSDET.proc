public ITNOSDET
  /*
ORIG: kellyp - 09/14/2005
DESC: Detailed Projected Nostro Report

---- Comments --------------------------------------------------------

	This program creates the ITNOSDETAIL records for the Treasury 
	Projected Nostro Detail Report (SCA441).  This is essentially
	the same as a standard projected nostro report (15 day or on-
	line), except that it is designed to show the individual 
	postings which make up a balance for a given day.

---- Revision History ------------------------------------------------

	09/14/05 - KELLY - CR 17050
		Converted to PSL.
 */
 
 	type Number BAL,BALAMT,CID
	type String ITNOSDEF()
	
	// Delete the temp table for the report
	do Db.delete("ITNOSDETAIL","JOB=:%ProcessID")
	
	// Build the list of Nostro accounts
	do BLDDEF^ITNOSR2(.ITNOSDEF())
	
	set CID=""
	
	for  set CID=ITNOSDEF(CID).order() quit:CID.isNull()  do {
		
		set BAL=$$OPENBAL^ITNOSR2(CID,%SystemDate)
		
		type DbSet ds=Db.selectDbSet("ITNOSTRO","CID=:CID AND DTJD>:%SystemDate","CID,DTJD,TSEQ")
		while ds.next() do {
			
			type RecordITNOSTRO itnostro=ds.getRecord("ITNOSTRO")
			type RecordITNOSDETAIL itnosdet=Class.new("RecordITNOSDETAIL","JOB=:%ProcessID,CID=:CID,TJD=:itnostro.dtjd")

			set BALAMT=$S(itnostro.itc1:itnostro.tamt*(-1),1:itnostro.tamt)

			set itnosdet.seq=Db.nextVal("ITNOSDETAIL","JOB=:%ProcessID,CID=:CID,TJD=:itnostro.dtjd")
			set itnosdet.drcr=itnostro.itc1
			set itnosdet.tamt=itnostro.tamt
			set itnosdet.balbef=BAL
			set itnosdet.crcd=$S('itnostro.crcd.isNull():itnostro.crcd,1:%SystemCurrency)
			set itnosdet.bal=BAL+BALAMT
			set itnosdet.dealid=$$FIELD^UTSO(itnostro.tso,"DEALID")
			do itnosdet.save()
			}
		}
	quit
	

vSIG()	quit "60157^57761^Pat Kelly^1616"	// Signature - LTD^TIME^USER^SIZE
