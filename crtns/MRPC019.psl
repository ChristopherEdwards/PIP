MRPC019(RETURN,VERSN,CID,%EffectiveDate,AMOUNT,FEES)	//Public;MRPC call for Anticipated Earnings Status
	/*

	   KEYWORDS: RPC

	   ARGUMENTS:
	         . RETURN         Anticipated Earnings Details    /TYP=T/REQ/MECH=REFNAM:W

	         . VERSN          ^MRPC019 version number
	                          current version=1               /TYP=T/REQ/MECH=VAL

	         . CID            Account Number                  /TYP=T/REQ/MECH=VAL

	         . %EffectiveDate Effective Date                  /TYP=T/REQ/MECH=VAL

	         . AMOUNT         Withdrawal Amount               /TYP=T/MECH=VAL/DEFAULT=DEP.BAL

	   RETURNS:
	         . $$    Error Message           /TYP=T
	                 Null=No Error

	   RELATED:
	         . $$PBSMRPC - MRPC Class Driver

	   EXAMPLE:
	         S RM=$$^MRPC018(.VAL,1,CID,%EffectiveDate,AMOUNT)

	  ---- Revision History ------------------------------------------------
	  
	  11/29/06 - MbuiM - CR 21675
		   Changed reference of INTAVLNC to INTAVLNCR to comply with 
		   GTM version 5.0.
	  
	  11/17/06 - Mugilvannan - 24126
	   	      Modified %EffectiveDate to default to cuvar.tjd if
	   	      %EffectiveDate is not defined.
	   	      
	   03/24/06 - ALAGARSS - 20052
	   	      Modified CLSINI^DEPDBS call to pass trn object.
	   
	   11/07/03 - CARROLLJ - 51630
		      Modify EXTERN^BCHFEEUT call to pass dep object.

	   05/07/03 - CARROLLJ - 51349
			Removed the use of %a array.

	   04/16/2002 - DATTAR - ARQ 49794
		        Converted to PSL

	  ----------------------------------------------------------------------
	*/

	new CRT,ETC,FEEACT,FEEAMT,FEETOT,ITC,MDT,NET,PTYPE,SAVACR
	new RPAFEE,TAB,TRN23,WTH
	
	// Check Version
	// Version number of client message is not compatible with server
	if $G(VERSN)'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))

	// Invalid account ~p1
	if 'Db.isDefined("ACN","CID=:CID") quit $$ERRMSG^PBSUTL($$^MSG(1259,CID))

	// Check Account Number & Code ("D"=Deposit,"L"=Loan)
	type RecordACN acn=Db.getRecord("ACN","CID=:CID")
	if acn.cls'="D" set ET="INVLDCLS" do ^UTLERR quit $$ERRMSG^PBSUTL($G(RM))

	type RecordDEP dep=Db.getRecord("DEP","CID=:CID")
	
	// Set EFD to cuvar.tjd when EFD is NULL
	type RecordCUVAR cuvar=Db.getRecord("CUVAR")
	if %EffectiveDate.isNull() set %EffectiveDate=cuvar.tjd
	
	// Check Julian Date
	if %EffectiveDate>%SystemDate do { if $G(ER) quit $$ERRMSG^PBSUTL($G(RM))
		set MDT=dep.mdt quit:MDT=""
		
		// Account matures on ~p1
		if %EffectiveDate>MDT do {
			set ER=1 
			set RM=$$^MSG(71,$$DAT^%ZM(MDT,$G(%MSKD)))
			}
		}

	// Set Principal to Amount
	// Default balance
	if '$G(AMOUNT) set AMOUNT=dep.bal
	set PRIN=$G(AMOUNT)
	do EXEC^DEPAES

	// Calculate service fees
	if $G(FEES) do {
		set (FEEAMT,FEEACT)=0
		set PTYPE=dep.type
		quit:$G(PTYPE)=""
		type RecordPRODCTL prodctl=Db.getRecord("PRODCTL","TYPE=:PTYPE") 
		set ETC=prodctl.drtrci
		quit:$G(ETC)=""

		
		type RecordTRN trn=Db.getRecord("TRN","ETC=:ETC")

		// Internal Transaction Code
		set ITC=trn.itc

		set SAVACR=ACR
		do CLSINI^DEPDBS(.dep,.trn)

		set ACR=SAVACR
		set SVBALCL=+BAL+INTAVLNCR

		// Substract service charges
		set TRN23=trn.pcf

		// returns FEEAMT
		if $E(TRN23,10) set FEEAMT=$$FEE10^DEPDBS(.dep,.trn,.FEEAMT) quit:$G(ER)

		if $E(TRN23,9) do {

			// returns FEEACT
			do EXTERN^BCHFEEUT(.dep,"0",$S($G(%EffectiveDate):%EffectiveDate,1:%SystemDate)) quit:$G(ER)

			// RPA Type
			set RPAFEE=cuvar.rpafee
			if FEEACT,dep.ira set FEEACT=FEEACT+$$RPACRT^IRAWH(.dep,RPAFEE,FEEACT)
			}
		set FEETOT=FEEAMT+FEEACT
		}

	if $G(ER) quit $$ERRMSG^PBSUTL($G(RM),$G(ET))
	set RETURN=""
	set TAB=$C(9)

	// Set Return
	// Amount of Withdrawal, Effective Date, Anticipated Earnings, Penalty Amount, Anticipated Disbursements
	set RETURN=PRIN_TAB_%EffectiveDate_TAB_ACR_TAB_PEN_TAB_INTDSB_TAB

	// Anticipated Chk. Amount, Adjustment Amount, Message, Earnings & Penalty Amount
	// Positive Accrued Int/Div
	set RETURN=RETURN_$P(dep.aecamt,"#",1)_TAB_$G(ADJ)_TAB_$G(MSG)_TAB_$G(NET(1))_TAB

	// Earnings Only Amount, Withholding Earn. Only Amount, Penalty Only Amount, Withholding Penalty Only Amount
	set RETURN=RETURN_$G(WTH(1))_TAB_$G(NET(2))_TAB_$G(WTH(2))_TAB_$G(NET(3))_TAB
	set RETURN=RETURN_$G(WTH(3))_TAB_$G(FEETOT)_$C(13,10)
	set RETURN=$$V2LV^MSG(RETURN)

	quit ""
 #OPTION ResultClass ON
Public String vSIG()	quit "60598^41333^Marie Mbui^4026"	// Signature - LTD^TIME^USER^SIZE
