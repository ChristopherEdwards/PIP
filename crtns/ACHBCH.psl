ACHBCH  /*
   	ORIG: CHENARDP - 10/03/2001
	DESC: ACH Batch Record Maintenance

	
	 ---- Revision History ------------------------------------------------
	12/16/03 - CARROLLJ - 7239
		   Removed dead code from PP01 linetag.

	02/18/2002 - VinayaK - 49451
		   Converted to PSL
	
         ----------------------------------------------------------------------
       */	

	do NEW
	quit
	
	
NEW	//
	do INIT(0)
	quit

UPD	//
	do INIT(1)
	quit

INQ	//
	do INIT(2)
	quit

DEL	//
	do INIT(3)
	quit


INIT(%ProcessMode)	//

	type public String OLNTB,VFMQ
	type Number %PAGE,%PG
	
	type RecordACH1 fACH1

	set %PG=0 
	set %PAGE=1
	kill OLNTB,VFMQ
	do VPG(.fACH1)

	quit


VPG(RecordACH1 fACH1)	// Page control
	
	type public Number %PG
	type public String VFMQ
	type Boolean FINISH
	
	set FINISH=0

	for  do { quit:FINISH
		
		if %PG=0 do VPG00 if VFMQ="Q" set FINISH=1 quit
		if "DFQ"[VFMQ do VER(.fACH1) set FINISH=1 quit
		
		 if %PG>0 do VPG01(.fACH1) if VFMQ="Q" set FINISH=1 quit 
		 if "DFQ"[VFMQ do VER(.fACH1) set FINISH=1 quit 
		 set %PG=%PG+1
		}
	quit


VPG00  // Page 1 setup

	type public Boolean ER
	type public String %NOPRMT,%READ,%TAB,VFMQ
	type String IO
	
	set %TAB("COID")=".COID1/HLP=[ACH1]COID/TBL=[ACH]/XPP=D PP01^ACHBCH"
	set %TAB("PTYPE")=".PTYPE1/HLP=[ACH1]PTYPE/TBL=[ACH1]/XPP=D PP02^ACHBCH"
	if %ProcessMode=2 set %TAB("IO")=$$IO^SCATAB($I)

	set %READ="@@%FN,,,COID/REQ,PTYPE/REQ" 
	if %ProcessMode'=3 set %NOPRMT="N"
	if %ProcessMode=2 set %READ=%READ_",,IO/REQ" set IO=$I

	do ^UTLREAD 

	if VFMQ="Q" set ER=1 quit 
	quit 
	

VPG01(RecordACH1 fACH1)	// Input screen

	type public String COID,PTYPE
	type String SID

	if %ProcessMode=0 do {
		set fACH1=Class.new("RecordACH1")
		set fACH1.COID=COID
		set fACH1.PTYPE=PTYPE
		}
	else  set fACH1=Db.getRecord("ACH1","COID=:COID,PTYPE=:PTYPE")

	set SID="ACHBCH"

	do DRV^USID(%ProcessMode,SID,.fACH1)

	quit 
	
PP01	// Company ID post-processor
	
	type public Boolean ER,%OSAVE
	type public String I(),RM,X
	
	quit:X.isNull() 
	quit:%OSAVE 

	set I(3)=I(3)_":NOVAL"            
	if X?1"1"9N!(X?1"3"9N)!(X?1"9"9AN) quit 

	set ER=1 
	set RM="Invalid format - use format 1nnnnnnnn or 3nnnnnnnn or 9aaaaaaaaa"

	quit 
	

PP02	// Payment type post-processor
	/*
	   If %O=3 (delete), warn if detail records exist
	   and prohibit deletion if trxns may be pending.
	
	*/

	type public Boolean ER,%OSAVE
	type public String COID,I(),RM,X
	type Date Z
	
	quit:X.isNull() 

	if %OSAVE=0,Db.isDefined("ACH1","COID=:COID,PTYPE=:X") do { quit
		set ER=1 
		set RM="Record already exists"
		}

	set:%OSAVE=0 I(3)="" 
	quit:%OSAVE'=3 

	// No detail records
	type ResultSet rs=Db.select("SEQ","ACH2","COID=:COID AND PTYPE=:X")
	if rs.isEmpty() quit

	type RecordACH1 ach1=Db.getRecord("ACH1","COID=:COID,PTYPE=:X")
	set Z=ach1.lead+ach1.edate

	if Z<%SystemDate do { quit
		set ER=1
		set RM="Transactions may still be pending"
		}

	set ER="W" 
	set RM="Warning - detail records will also be deleted"
	quit 
	

VER(RecordACH1 fACH1)	//
	
	type public String COID,PTYPE,VFMQ
	
	if %ProcessMode=2!(VFMQ="Q") do END quit

	if %ProcessMode=3 do {
		do Db.delete("ACH1","COID=:COID AND PTYPE=:PTYPE")
		do Db.delete("ACH2","COID=:COID AND PTYPE=:PTYPE")
		}
	else  do {
		do fACH1.save()
	
		//Added the code to insert the data to ACH table.
		
		type RecordACH ach=Db.getRecord("ACH","COID=:COID",1) 
		if 'ach.getMode() do {
			type RecordACH ach=Class.new("RecordACH")
			set ach.coid=COID
			set ach.desc=PTYPE
			do ach.save()
			}
		}
	do END 
	quit
	

END	// ----------------------------
	
	type public String ER
	type public String COID,PTYPE,RM,VFMQ
	
	quit:ER!(%ProcessMode=2)!(%ProcessMode=4)
	
	if VFMQ="Q" do {
		if %ProcessMode=0 set RM="ACH originating company "_COID_", payment type "_PTYPE_" not created" quit 
		if %ProcessMode=1 set RM="ACH originating company "_COID_", payment type "_PTYPE_" not modified" quit 
		set RM="ACH originating company "_COID_", payment type "_PTYPE_" not deleted"
		}
	else  do {
		if %ProcessMode=0 set RM="ACH originating company "_COID_", payment type "_PTYPE_" created" quit 
		if %ProcessMode=1 set RM="ACH originating company "_COID_", payment type "_PTYPE_" modified" quit 
		set RM="ACH originating company "_COID_", payment type "_PTYPE_" deleted"
		}
	set ER="W"
	quit 
 #OPTION ResultClass ON
Public String vSIG()	quit "60200^25647^Sethy, Satyanarayan^4065"	// Signature - LTD^TIME^USER^SIZE
