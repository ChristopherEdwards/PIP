public SBRCTMS(RecordTTX ttx,RecordTRN trn)
  /*
ORIG: STATTOND - 05/25/2001
DESC: Shared Branch Misc. Recipt Printing
	
This procedure places the name of the credit union (acquirer),
and the address of the credit union (acquirer)into misc field 2 of 
tlr_journal for receipt printing by the generic forms enhancement.

---- Revision History ------------------------------------------------

	08/16/05 - KELLYP - CR 16880
		Modified entire procedure to use parameters available 
		from MSCRPLY^PBSTSSP.  Previous versions had access
		to variables that were laying around but P04 does not 
		and has to reference the ttx/trn objects directly. 
		Also converted entire procedure to conform to current 
		PSL standards and removed pre-2003 revision history.

	12/03/03 - KELLYP - 48514
		Corrected section of code that removed the account number from
		the SBCID1 and SBCID2 TSO fields.  Account number should be 
		returned in full "CIF#=ACCT#" format.  Also cleaned up new list.
 */

	type Number BRCD,FCID,SBISO,TCID
	type String BRSCZ,HNAM,MNAM,RETURN

	if trn.sbtrntyp.isNull() quit ""

	type RecordSTBLSBTSD sbtsd=Db.getRecord("STBLSBTSD","TRNTYP=:trn.sbtrntyp",1)
 	if 'sbtsd.getMode() quit ""

	set BRCD=ttx.brcd
	
	if BRCD.isNull() quit ""

	type RecordUTBLBRCD ubrcd=Db.getRecord("UTBLBRCD","BRCD=:BRCD",1)
	if 'ubrcd.getMode() quit ""

	set BRSCZ=ubrcd.brstate_"  "_ubrcd.brcntry_"  "_ubrcd.brzip	

	set FCID=$$FIELD^UTSO(ttx.tso,"SBCID1")
	set TCID=$$FIELD^UTSO(ttx.tso,"SBCID2")
	set MNAM=$$FIELD^UTSO(ttx.tso,"SBMNAM")
	set SBISO=$$FIELD^UTSO(ttx.tso,"SBISO") 
	
	type ResultSet rs=Db.select("NAM","UTBLSBCU","ISOBIN=:SBISO")
	if rs.next() set HNAM=rs.getCol("NAM")
	else  set HNAM=""	

	set RETURN=$C(9)_ubrcd.brad1_$C(9)_ubrcd.brad2_$C(9)_ubrcd.brcity_$C(9)_BRSCZ_$C(9)_HNAM_$C(9)_MNAM_$C(9)_FCID_$C(9)_TCID

	quit RETURN

vSIG()	quit "60130^35111^Pat Kelly^1739"	// Signature - LTD^TIME^USER^SIZE
