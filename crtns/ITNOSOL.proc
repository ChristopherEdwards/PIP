public ITNOSOL
  /*
ORIG: kellyp - 09/14/2005
DESC: On Line Nostro Creation File

---- Comments --------------------------------------------------------

	This program creates the ITNOSOL records for the Treasury 
	Projected Nostro Summary Report (SCA439).  The report goes
	three business days out from TJD and shows projected Nostro
	balances in all Nostro accounts for those dates.

---- Revision History ------------------------------------------------

	09/14/05 - KELLY - CR 17050
		Converted to PSL.

 */

	type public Date D1,D2,D3,START
	
	type Date END
	
	set D1=$$NBD^UNBD(START+1,1,0,"")	// T+1 
	set D2=$$NBD^UNBD(START+1,2,0,"")	// T+2
	set D3=$$NBD^UNBD(START+1,3,0,"")	// T+3
	set END=D3+1

	// Delete the temp table for the report
	do Db.delete("ITNOSOL","PID=:%ProcessID")
	
	// Create the ITNOSFIL records
	do CRFILE^ITNOSR2(START,END)
 
	// Build the ITNOSOL records for the report
	do BLDDTL

	quit
	
public PP
	// Post processor for START field on SCA439 query screen

	type public Boolean ER
	type public Date X
	
	if X.isNull() quit
	
	// Start date must be the same as or after the system date
	if X<%SystemDate do Runtime.setErrMSG("ITNOSOL","2532") quit:ER
	
	// Date must be a valid business day
	if 'X.isBusDate() do Runtime.setErrMSG("ITNOSOL","8093") quit:ER

	quit
	
BLDDTL
	// Build the ITNOSOL detail records for the report

	type public Date D1,D2,D3,START

	type Date LDATE
	type Number CID
	type String CRCD

	type ResultSet rs=Db.select("CRCD,CID","ITNOSFIL","T=:%ProcessID","CRCD,CID")
	while rs.next() do {
		set CRCD=rs.getCol("CRCD")
		set CID=rs.getCol("CID")
		
		type RecordITNOSOL itnosol=Db.getRecord("ITNOSOL","PID=:%ProcessID,CRCD=:CRCD,CID=:CID",1)
		set itnosol.d1=D1
		set itnosol.d2=D2
		set itnosol.d3=D3
		set itnosol.start=START
		
		for LDATE=D3,D2,D1 do DATES(.itnosol)
		
		do itnosol.save()
		}
	quit

DATES(RecordITNOSOL itnosol)

	type public Date D1,D2,D3,LDATE,START
	type public Number CID
	type public String CRCD
	
	type Number BAL
	
	if (LDATE<START)!('LDATE.isBusDate()) quit
	
	type RecordITNOSFIL itnosfil=Db.getRecord("ITNOSFIL","T=:%ProcessID,CRCD=:CRCD,CID=:CID,PRJ=:LDATE",1)
	
	if itnosfil.getMode() set BAL=itnosfil.openbal
	else  do {
		type Date TMPDT
		type ResultSet rs=Db.select("OPENBAL","ITNOSFIL","T=:%ProcessID AND CRCD=:CRCD AND CID=:CID AND PRJ<:LDATE","PRJ DESC")
		if rs.isEmpty() set BAL=$$OPENBAL^ITNOSR2(CID,"")
		else  if rs.next() set BAL=rs.getCol("OPENBAL")
		}

	if LDATE=D1 set itnosol.t1=BAL
	else  if LDATE=D2 set itnosol.t2=BAL
	else  if LDATE=D3 set itnosol.t3=BAL
	
	quit

vSIG()	quit "60157^57786^Pat Kelly^2456"	// Signature - LTD^TIME^USER^SIZE
