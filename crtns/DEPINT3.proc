DEPINT3(RecordDEP dep,RecordTTX ttx,RecordTRN trn,CR)	// Residual Interest Adjustment
	/*
	   ORIG: SMITHC - 05/10/96

	   DESC: Adjusts the Residual Interest field on an account.  The basic
	   tran codes that use this routine are DRID and IRID.

	   KEYWORDS: Interest, Adjustment

	   INPUTS:   

		. dep  deposit account object		/TYP=RecordDEP

		. ttx  transaction object		/TYP=RecordTTX

		. trn  transaction code			/TYP=RecordTRN 

		. CR   Credit/Debit flag

	   RETURNS:

	   RELATED:

	   EXAMPLE:
	         do ^DEPINT3(.dep,.ttx,.trn,0)

	---- Revision History ------------------------------------------------
	
	07/17/06 - DESHPANDE S K - CR 20748
		   Replaced Transaction variables (TRNPRIM)
		   with Transaction Stored Values.
	
	11/14/02 - ZWITKOWITSM - 43583
		   PSL clean-up.

	----------------------------------------------------------------------
	*/

	new TAMT,%EffectiveDate

	do INIT(.ttx)
	do AUT(.dep, ttx.getStoredValue("isPrimary")) quit:ER
	do RESINT(.dep,.ttx)
	do FILE(.dep)

	quit


FILE(RecordDEP dep)

	set dep.tld=%SystemDate

	// Update residual interest file for debits only
	if CR-dep.trb=0 do RESFILE^BCHINTPS(+TAMT,dep.cid,%SystemDate)

	quit


RESINT(RecordDEP dep,RecordTTX ttx)	// Residual Interest

	if CR-dep.trb=0 set dep.resint=dep.resint+TAMT
	else  set dep.resint=dep.resint-TAMT

	set $P(TAMT,"#",10)=+TAMT
	set ttx.tamt=TAMT

	quit


INIT(RecordTTX ttx)	// Initialize data

	set TAMT=ttx.tamt
	set %EffectiveDate=ttx.efd

	quit


AUT(RecordDEP dep, Boolean ISPRIM)	// Authorization Verification

	// Restriction Flag
	if dep.rflg do ^UFLG(.dep)

	if ISPRIM,(CR-dep.trb),(TAMT>dep.resint) do { quit

		// Rounded residual interest
		new RNDRI

		set RNDRI=$$^SCARND(dep.resint,0,dep.cid)

		// Transaction Amount of ~p1 is Greater than the Available Amount of ~p2
		do Runtime.setErrMSG("DEP",7817,"TAMT~RNDRI")
		}

	// Account not coded to track residual int/div
	if '(CR-dep.trb),dep.negbalop<2 do Runtime.setErrMSG("DEP",414)

	if dep.stat set OVR(dep.cid,"OVR","STAT"_+dep.stat)=""

	quit


Public EC(RecordDEP dep,RecordTTX ttx,RecordTRN trn,CR)	// Error Correct
	/*
	   INPUTS:

		. dep  deposit account object		/TYP=RecordDEP

		. ttx  transaction object		/TYP=RecordTTX

		. trn  transaction code			/TYP=RecordTRN

		. CR   Credit/Debit Flag

	   EXAMPLE:
	         do ^DEPINT3(.dep,.ttx,.trn,0)

	*/

	new AMT,EFD,TAMT

	do INIT(.ttx)

	if CR-dep.trb=0 set AMT=TAMT
	else  set AMT=-TAMT

	set dep.resint=dep.resint+AMT

	// Reset deferred interest file
	if TSO["DEFINT#" do DEFRESET^DEPINT(.ttx,2,AMT) quit:ER

	do FILE(.dep)

	quit

vSIG()	quit "60464^46453^Shriram Deshpande^2376"	// Signature - LTD^TIME^USER^SIZE
