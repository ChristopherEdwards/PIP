public	RFLG
	/*

	---- Revision History ------------------------------------------------
	04/10/06 - GIRIDHARANL - CR 19591
		   Added variable OPT to capture value of %ProcessMode before 
		   ^UTLREAD is called. Changed section PP to use the new variable.
		   Also in section VER, changed the if - else structure to two if 
		   statements to prevent both if and else being executed in the same
		   run
		   
	01/22/06 - TIT0VE - CR 19098
		   Added PR section to build a look-up array of existing
		   restriction values that would include descriptions.

	04/28/05 - TITOVE - CR 15221
		   Modified as part of DBI2 project.   

	09/04/02 - JANGIRA - 49451
		   Converted to PSL.

	----------------------------------------------------------------------
	*/
	
	// Insert/Update

	do INIT(1)
	
	quit


public	INQ 	// Inquiry

	do INIT(2)
	
	quit
	

public	DEL	// Delete

	do INIT(3)
	
	quit
	

INIT(%ProcessMode)

	type Number %PAGE, %PG, OLNTB, ZNW
	type String GRP, RFLG, VFMQ
	type String OPT
	
	set %PG = 0 
	set %PAGE = 1
	set OPT = %ProcessMode
	
	type RecordUTBLRFLG fUTRFLG
        do VPG(.fUTRFLG)

        quit


VPG(RecordUTBLRFLG fUTRFLG)	// Page control

	type public Number %PG, ER
	type public String VFMQ

	type Boolean FINISH = 0

        for  do { quit:FINISH
        	
                if (%PG = 0) do VPG00 if ER!(VFMQ = "Q") set FINISH = 1 quit
                
                if (%PG > 0) do VPG01(.fUTRFLG)
                
                if "DFQ"[VFMQ do VER(.fUTRFLG) set FINISH = 1 quit
                
                set %PG = %PG + 1
                }
                
        quit


VPG00 	// Set up

	type public Number %TAB, ER
	type public String %NOPRMT, %READ, VFMQ

	set %TAB("GRP") = ".GRP1/TBL=[STBLLIBS]NAME"
	set %TAB("RFLG") = ".RFLG1/TBL=RFLDESC(/XPR=D PR^RFLG/XPP=D PP^RFLG"

	if (%ProcessMode = 2) set %TAB("IO") = $$IO^SCATAB($I)

	set %READ = "@@%FN,,,GRP/REQ,RFLG/REQ"
	
	set %NOPRMT = "N"
	
	if (%ProcessMode = 2) set %READ=%READ_",IO/REQ"
	
	do ^UTLREAD 
	
	if (VFMQ = "Q") set ER = 1 quit

	if 'Db.isDefined("UTBLRFLG","GRP = :GRP AND RFLG = :RFLG") set %ProcessMode = 0

	quit


PR      // UTLREAD pre-processor
 
	type public String GRP, RFLDESC()
 
	type DbSet ds = Db.selectDbSet("UTBLRFLG","GRP=:GRP")
 
	while ds.next() do {
 
		type RecordUTBLRFLG rflg = ds.getRecord("UTBLRFLG")
                
		set RFLDESC(rflg.rflg) = rflg.desc
		}
 
	quit


PP	// UTLREAD post-processor

	type public Number ER, ZNW
	type public String GRP, I(), X, OPT

	kill ZNW
	
	if X.isNull() quit
	
	if (OPT = 3) do { if ER quit
		
		type ResultSet rs = Db.select("UCLS", "UTBLRFL1", "GRP = :GRP AND RFLG = :X")

		// Delete the authorizations before deleting the restriction
		if 'rs.isEmpty() do Runtime.setErrMSG("UTBLRFL1",8424) quit:ER
		}
		
	if (%ProcessMode > 1) quit
	
	if Db.isDefined("UTBLRFLG", "GRP = :GRP AND RFLG = :X") quit
	
	set I(3) = ""
	
	set ZNW = 1
	
	quit


VPG01(RecordUTBLRFLG fUTRFLG) // Restrict screen

	type public Number ER
	type public String GRP, RFLG, VFMQ

	set fUTRFLG = Db.getRecord("UTBLRFLG", "GRP = :GRP, RFLG = :RFLG", 1)

	if (%ProcessMode = 2) do OPEN^SCAIO if ER set VFMQ = "Q" quit

	do DRV^USID(%ProcessMode,"UTBLRFLG",.fUTRFLG)
	
	quit


VER(RecordUTBLRFLG fUTRFLG)

	type public Number ZNW
	type public String GRP, RFLG, VFMQ

	if (%ProcessMode = 2) ! (VFMQ = "Q") do END quit

	if (%ProcessMode = 1) ! (%ProcessMode = 0) set fUTRFLG.tares = 1
	
	if (%ProcessMode = 3) , fUTRFLG.fares do {
		
		set %ProcessMode = 1
		set fUTRFLG.tares = 0
		set fUTRFLG.trig1 = "" 
		set fUTRFLG.trig2 = ""
		set fUTRFLG.trig3 = "" 
		set fUTRFLG.trig4 = ""
		}
		
	if ZNW.get() set %ProcessMode = 0
        	
	if (%ProcessMode = 3) do Db.delete("UTBLRFLG","GRP=:GRP AND RFLG=:RFLG")
        
        if '(%ProcessMode = 3) do fUTRFLG.save()

	do END
	
	quit


END

	type public Number ER, ZNW
	type public String RFLG, RM, VFMQ

	if ER ! (%ProcessMode = 2) ! (%ProcessMode = 4) quit
	
	set ZNW = ZNW.get()

	if (VFMQ = "Q") do {

		// User-defined restriction ~p1 not created
		if ZNW set RM = $$^MSG(2886,RFLG.get()) quit

		// User-defined restriction ~p1 not modified
		if (%ProcessMode = 1) set RM = $$^MSG(2888,RFLG.get()) quit

		// User-defined restriction ~p1 not deleted
		set RM = $$^MSG(2887,RFLG.get())
		}
	else  do {

		// User-defined restriction ~p1 created
		if ZNW set RM = $$^MSG(2883,RFLG) quit

		// User-defined restriction ~p1 modified
		if (%ProcessMode = 1) set RM = $$^MSG(2885,RFLG) quit

		// User-defined restriction ~p1 deleted
		set RM = $$^MSG(2884,RFLG)
		}
		
	set ER = "W"
	
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60365^61060^Lakshmi Giridharan^4348"	// Signature - LTD^TIME^USER^SIZE
