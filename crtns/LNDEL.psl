LNDEL	//Loan Delinquency Report Driver
	/*

	---- Revision History ------------------------------------------------
	
	03/06/06 - HILLANBRAND - 19939
	           In the VPG00 section, added all the tmp tables 
	           that this function builds to be deleted.  This corrects
	           a unique constraint violation on an insert to rep404 
	           when the same user session executes the function LNDL01
	           more than once.

	03/03/05 - HILANBRAND - 13281
		   Corrected VAR section to set VAR="DRC"_J instead
		   of VAR="VAR"_J.
	
	05/06/02 - VETSENM - 49794
	           Converted to PSL.            	

	*/

	// Report on all categories in ^CUVAR("DELQ")
	set SINGLE=0
	// Must fetch categories
	kill DRC
	// Must fetch option
	kill LNDRBY


INIT	// Entry point from ^LNDEL3 which can prompt for the above variables.
	set %PG=0 
	set %PAGE=1
	kill VFMQ,OLNTB

	// Not just participation loans.
	if '$D(PART) set PART=0
	
VPG	// Page control
	if %PG=0 do VPG00 do VPG0 quit
	if %PG>0 do VPG0 quit

VPG0 	//
	if "DFQ"[VFMQ quit
	set %PG=%PG+1
	do VPG 
	quit

VPG00	// Set up

	set JOB=%ProcessID

	type RecordCUVAR cuvar=Db.getRecord("CUVAR")

	// Report by Days, Months or Payments 
	if '$D(LNDRBY) set LNDRBY=cuvar.lndrby

	//   Report Method code
	set LNDRM=cuvar.lndrm

	//  Report Driver Routine
	set RPTNAM=cuvar.rptnam

	// Minimum reportable amount
	set MINAMT=cuvar.minamt

	// Build array of reporting categories, if not already supplied.
	if '$D(DRC) do DRC(.cuvar)
	set DRC=$O(DRC(""),-1)

	set DRCTEXT=""

	// DAY
	if LNDRBY="D" set DRCTEXT=$$^MSG(7032)
	// MONTH
	if LNDRBY="M"  set DRCTEXT=$$^MSG(7033)
	// PAYMENT
	if LNDRBY="P" set DRCTEXT=$$^MSG(5694)

	// Unknown (invalid) report option
	if DRCTEXT="" set ER=1 quit

	kill TEXT

	// I18N=OFF
	set TEXT(1)=$S(LNDRM=2:"NCUA",1:"")
	// I18N=ON
	
	// ~p1 Loan Delinquency Report
	set TEXT(1)=$$^MSG(4028,TEXT(1))
	set TEXT(1)=$J("",40-($L(TEXT(1))\2))_TEXT(1)

	//This function will determine delinquencies and print reports
	set TEXT(2)=$$^MSG(4024)

	//for all loan types using the following delinquency categories:
	set TEXT(3)=$$^MSG(4025)
	set TEXT(4)=""

	set TEXT=5 for DRC=1:1:DRC do TEXT
	set TEXT=TEXT+1 set TEXT(6)=""

	set %READ="@TEXT(1),"

	set J=1 
	for  set J=$O(TEXT(J)) quit:J=""  do {  
		set %READ=%READ_",@TEXT("_J_")"
		}

	set %TAB("IO")=$$IO^SCATAB($I)
	set %READ=%READ_",,,IO/REQ"

	do ^UTLREAD 
	if "Q"[VFMQ  quit
	if ER quit

	kill TEXT
	do Db.fastDelete("LNDELQ1","JOB=:JOB")
	do Db.fastDelete("LNDELQ2","JOB=:JOB")
	do Db.fastDelete("LNDELQ21","JOB=:JOB")
	do Db.fastDelete("LNDELQ2B","JOB=:JOB")
	do Db.fastDelete("LNDELQ2B1","JOB=:JOB")
	do Db.fastDelete("LNDELQ2C","JOB=:JOB")
	do Db.fastDelete("LNDELQ2C1","JOB=:JOB")
	do Db.fastDelete("LNDELQNI","JOB=:JOB")
	do Db.fastDelete("LNDELQNP","JOB=:JOB")
	do Db.fastDelete("LNDELQP","JOB=:JOB")
	do Db.fastDelete("LNDELT1","JOB=:JOB")
	do Db.fastDelete("LNDELT2","JOB=:JOB")
	do Db.fastDelete("LNDELT3","JOB=:JOB")
	do Db.fastDelete("LNDELT4","JOB=:JOB")
	do Db.fastDelete("LNTDELQ","JOB=:JOB")
	do Db.fastDelete("REP404","PID=:JOB")
	

	// Create table of text descriptions of the categories, DRCN()
	do DRCN

	set ER=0

	if LNDRM=1 do ^LNDEL1 if ER do ERR quit

	if LNDRM=2 do ^LNDEL2 if ER do ERR quit

	// Invalid report method ~p1
	if LNDRM'=1,LNDRM'=2 set ER=1 set RM=$$^MSG(1445,LNDRM) do ERR quit

	// Delinquency reports ...
	set RID=RPTNAM 
	do ^URID 
	if PGM="" set ET="INVLDRPT" do ERR quit
	
	do OPEN^SCAIO quit:ER

	new DBOPT,LIBS,RN

	set LIBS="SYSDEV"
	set DBOPT=5
	
	type RecordDBTBL dbtbl=Db.getRecord("DBTBL","%LIBS=:LIBS,DBOPT=:DBOPT,ID=:RID")
	set RN=dbtbl.desc

	set %NOOPEN=1 
	do V0^@PGM 
	kill %NOOPEN
	
	do CLOSE^SCAIO

	do Db.fastDelete("LNDELQ1","JOB=:JOB")

	set VFMQ="Q"

	quit


ERR	//
	set ER=1 
	do DSPBP^UTLERR
	set VFMQ="Q"
	quit
	

END	//
	kill %TAB,%ProcessMode,VFMQ,ER,ET,RM
	kill DRC,DRCN,JOB,LNDRBY,LNDRM,MINAMT,RPTNAM
	kill TEXT,DRCTEXT
	quit


TEXT	//========== Construct TEXT() for headings

	new BLANK,P1,P2,TMP

	set BLANK="     "
	if '$D(TEXT(TEXT)) set TEXT(TEXT)=""

	set P1=$P(DRC(DRC),"|",1) 
	set P2=$P(DRC(DRC),"|",2)

	if P1=P2 set TMP=$J(P1,3)

	// ~p1 TO ~p2
	else  set TMP=$$^MSG(359,$J($P(DRC(DRC),"|",1),3),$J($P(DRC(DRC),"|",2),3))

	set TMP=TMP_" "_DRCTEXT_$S(P2=1:"",1:"S")

	if ($L(TEXT(TEXT))+$L(TMP)+2)>75 set TEXT=TEXT+1 set TEXT(TEXT)=""

	if TEXT(TEXT)="" set TEXT(TEXT)=BLANK_TMP
	else  set TEXT(TEXT)=TEXT(TEXT)_", "_TMP

	quit

DRC(RecordCUVAR cuvar)	//Delinquency Reporting Categories
	
	/*

	   DRC = number of categories
	   DRC(sequence) = minimum | maximum, for ranges days/months/payments.

	   Search every piece for possible categories since the user can file
	   empty sections in the reporting category list.

	*/

	new I,J,K,L

	kill DRC 
	set DRC=0
	set VAR="DRC"

	for J=1:1:20 set VAR="DRC"_J do {
		set I=cuvar.@VAR
		if I'="" do DRC1
		}

	quit

DRC1	//

	set K=$P(I,"-",1) 
	set L=$P(I,"-",2) 

	if L="" set L=K

	set DRC=DRC+1 
	set DRC(DRC)=K_"|"_L

	quit

DRCN	//Delinquency Reporting Category Names

	new I,K,L
	kill DRCN,DRC(0) 
	set DRC=""

	// Maximum of 20 categories
	for DRC=1:1:20 do DRCN1

	quit

DRCN1	//

	if '$D(DRC(DRC)) set DRCN(DRC)="" quit

	set K=$P(DRC(DRC),"|",1) 
	set L=$P(DRC(DRC),"|",2)

	// AT LEAST ~p1
	if K=L set DRCN(DRC)=$$^MSG(4026,K)

	// ~p1 TO ~p2
	else  set DRCN(DRC)=$$^MSG(359,K,L)

	set DRCN(DRC)=DRCN(DRC)_" "_DRCTEXT_$S(L=1:"",1:"S")

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60330^62285^Laura Hillanbrand^5116"	// Signature - LTD^TIME^USER^SIZE
