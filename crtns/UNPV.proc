UNPV	//;PBS - U - V3.0 - Generic Net Present Value Calculator
	/*
	       ORIG:  Chuck Hardy (6721) - 03/06/87

	   This utility computes the net present value of a series of cash flows
	   based upon an interest rate.

	   Requires IRN and CF array by sequence 0-999 for number of cash flows
	   CF(SEQ)=$ amount of each cash flow | # of cash flows for that amount |
	   frequency for each flow (standard UFRE convention).

	   CF(0) must equal the initial investment.

	   Sign convention is very important to the proper operation of this
	   calculation.  Cash flows which are received must be positive, and
	   those expended must be negative.  i.e. a typical loan transaction
	   (from the lender's viewpoint) would have an inital cash outflow of
	   the loan amount.  This amount would be negative.  The monthly pay-
	   ments would be positive (as they are being received by the lender.)

	   KEYWORDS: Calculations, Interest, Deposits, Loans

	   INPUTS:
	       . CF     Cash Flow Array                /TYP=T/REQ/MECH=REFARR:R
	                As defined above, the CF array conatins the cash flows
	                for which an NPV is to be computed

	       .IRN     Nominal Interest Rate          /TYP=$/REQ
	                The interest rate for which the NPV is to be computed.

	       . AF     Annual Factor                  /TYP=N/REQ
	                The number of times per year that cash flows are
	                occurring.  If flows may be made at any time, then AF
	                must equal 365.

	                If FRE is defined AF is not required.

	       . FRE    Cash flow frequency            /TYP=T/REQ
	                The frequency at which cash flows occur.  If flows may
	                occur at any time, then FRE must equal 1DA.

	                If AF is defined FRE is not required.

	       . ODYS   Odd Days (May be fractional)  /TYP=N/NOREQ
	                Specifies the number of days in the first period between
	                CF(0) and the first cash flow which exceed (+) or are
	                less than (-) the standard cash flow frequency.

	       . BASIS  Calculation Basis             /TYP=N/NOREQ
	                If ODYS is defined, this specifies the number of days
	                per year that should be used for that odd period's
	                calculation.

	                The default value is 360.

	       . NCR    Negative cash flow borrowing  /TYP=$/NOREQ
	                rate (not tested)

	       . DEC    # decimal places              /TYP=N/NOREQ
	                Specifies the decimal precision of the value of IRR.
	                The tolerance of the computation (MARGIN) is based on
	                this value.  The greater the decimal precision, the
	                greater the accuracy of the calculation.

	                The default value is 5.

	   RETURNS:
	       . NPV    The net present value of the  /TYP=$
	                cash flow series

	 ---- Revision History -------------------------------------------------

	 10/19/05 - S.Krishnan - CR16885
		    Eliminated all the deprecated features
		    Converted the ^UFRE calls to .nextFreqDate() method.
		      
	 05/17/02 - KRISHNAG - ARQ# 49794
                    Converted to PSL

	*/

	if '$D(IRN) set ER=1 set NPV="" set ET="INVLDANS" do ^UTLERR quit

	set IRN=IRN/100

	new CNTR,EXP,FV,IR,JD,MARGIN,N,NEG,NUM,OPER,OPX,PV,PT,SIR,SEQ,TNUM
	type Date NJD,JD

	set (OPX,ER,CNTR,TNUM,NPV)=0 
	set MARGIN=.01 
	set SEQ=""

	if '$D(AF),'$D(FRE) do { quit
		set ER=1
		set ET="INVLDFRE" 
		do END
		}

	if '$G(AF) do {
		set JD=+%CurrentDate
		set NJD=JD.nextFreqDate(FRE)
		}

	if 'AF set ER=1 do END quit

	if '$D(NCR) new NCR set NCR=0

	if NCR,NCR>1 set NCR=NCR/100

	if '$D(BASIS) new BASIS set BASIS=360

	if '$D(ODYS) new OPER,ODYS set (OPER,ODYS)=0 do NPV quit

	set OPER=ODYS/(BASIS/AF)
	do NPV
	quit

NPV	// Calculate the net PV for the cash flows at the stated IRN
	
	set SEQ=$O(CF(SEQ)) 

	if SEQ="" do END quit

	set PT=+CF(SEQ) 
	set NUM=+$P(CF(SEQ),"|",2)

	if 'SEQ set NPV=NPV+PT do NPV quit

	if 'PT set TNUM=TNUM+NUM do NPV quit

	set IR=IRN/AF

	// FMIRR
	if NCR if ((PT<0)&(NEG<0))!((PT>0)&(NEG>0)) set IR=(NCR*NEG)/AF

	set EXP=$$LNX^%ZFUNC(1+IR)*NUM

	set EXP=1/$$EXP^%ZFUNC(EXP)

	set PV=PT*((1-EXP)/IR)

	if 'TNUM do { quit
		if OPER do I1 quit 

		set NPV=NPV+PV 
		set TNUM=TNUM+NUM 

		do NPV
		}	

	set N=TNUM 
	set PT=0 
	set FV=PV
	
	do I
	quit

I	//
	
	set EXP=$$LNX^%ZFUNC(1+IR)*N

	set EXP=1/$$EXP^%ZFUNC(EXP)

	set PV=PT*((1-EXP)/IR)+(FV*EXP)

	if OPX quit

	do I1
	quit

I1	//
	
	if OPER do {
		set (N,OPX)=1
		set SIR=IR
		new IR
		set IR=SIR*OPER
		set PT=0
		set FV=PV

		do I

		set OPX=0
		}

	set NPV=NPV+PV
	set TNUM=TNUM+NUM

	do NPV 
	quit

END	//
	set NPV=$$^SCARND(NPV,0,"",$G(%SystemCurrency),2)

	set IRN=IRN*100
	quit

vSIG()	quit "60199^27243^S. Krishnan^4561"	// Signature - LTD^TIME^USER^SIZE
