MSRVLOC	//Private; Mass Service Item Location creation
	/*
	       ORIG:  Seth A. Savitsky - 08/04/94
	       DESC:  This routine is called by the maintenance functions
	              associated with the mass creation of Service Item
	              locations.

	REVISION HISTORY
	------------------------------------------------------------------

	05/09/05 - KELLYP - CR 15872
		Rewrote procedure to use one object array instead of 
		multiple local arrays.  Also modified entire procedure 
		to conform to current PSL standards and removed pre-2003 
		revision history.

	------------------------------------------------------------------
	*/

	quit


public NEW 	// Create Service Item Locations

	do PROC(1)

	quit


PROC(Number %ProcessMode)

	type String SRVCAT,SRVTYP,VFMQ

	do QUERY

	if VFMQ="Q" do END quit

	do SCREEN
	
	quit


QUERY 	// Query for service item category and type

	type String %READ,%TAB()

	set %TAB("SRVCAT")="[UTBLSRVLOC]SRVCAT/TBL=[UTBLSRVCAT]"
	set %TAB("SRVTYP")="[UTBLSRVLOC]SRVTYP/TBL=[UTBLSRVTYP]"

	set %READ=";@@%FN/REV/CEN;;SRVCAT/REQ;SRVTYP/REQ"

	do ^UTLREAD

	quit


SCREEN	// Call SRVMLSCR Screen

	type public String SRVCAT,SRVTYP,VFMQ

	type RecordUTBLSRVLOC srvloc()
	type Number I
	
	set I=1
	
	// Initialize srvloc records
	type DbSet ds=Db.selectDbSet("UTBLSRVLOC","SRVCAT=:SRVCAT AND SRVTYP=:SRVTYP","SRVLOC ASC")
	while ds.next() do {
		set srvloc(I)=ds.getRecord()
		set I=I+1
		}
	
	type RecordUTBLSRVCAT srvcat=Db.getRecord("UTBLSRVCAT","SRVCAT=:SRVCAT")

	do DRV^USID(%ProcessMode,"SRVMLSCR",.srvloc(),.srvcat)
	
	if VFMQ="Q" do END quit
	
	do FILE(.srvloc())
	
	do END

	quit


FILE(RecordUTBLSRVLOC srvloc())	// File service item location records

	type public String SRVCAT,SRVTYP

	type Number I

	// Delete all UTBLSRVLOC records for the category and type specified
	do Db.delete("UTBLSRVLOC","SRVCAT=:SRVCAT AND SRVTYP=:SRVTYP")
	
	set I=""
	
	// Save UTBLSRVLOC records from screen
	for  set I=srvloc(I).order() quit:I.isNull()  do {
		// Force create mode since Db.delete destroyed old records
		do srvloc(I).setMode(0)
		set srvloc(I).srvcat=SRVCAT
		set srvloc(I).srvtyp=SRVTYP
		do srvloc(I).save()
		}

	quit


END	// End of Processing

	type public String ER,RM,VFMQ

	set ER="W"

	// Service/Check Item Locations created
	if VFMQ'="Q" set RM=$$^MSG(7433)

	// Service/Check Item Locations not created
	else  set RM=$$^MSG(7434)

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60036^42237^Pat Kelly^2229"	// Signature - LTD^TIME^USER^SIZE
