public TRREFNO(String SOURCE,String REFNO,String KEYS)	// Transaction Reference Number Generator
	/*
	   Origin: Matt Lessig 07/06/93
	   Description: Generic Transaction Reference Number Generator
	    This routine genrates the transaction reference number
	    used by fx deals, outgoing/incoming foreign pmts, SWIFT serries
	    9 messages and domestic payments/collections.

	   Arguments:
	   . SOURCE Source of transaction	/TYPE=T/REQ
	      VALUE		Description
	     	1 		foreign exchange deals
	     	2 		outgoing foreign payments
	     	3 or 'SWIFT' 	incoming foreign payments
		4 		SWIFT series 9 messages
		5 		domestic payments/collections

	   . REFNO  Transaction reference number /TYPE=T/NOREQ

	   . KEYS  Keys to transaction record /TYPE=T
	     DEAL(NOINT) or EFTPAY(CID,SEQ)

	  ---- Revision History -------------------------------------------------
	   
	   12/01/05 - KELLYP - CR 18292
	   	      Modified entire procedure to conform to current PSL 
	   	      standards.  Also modified the TR* sections to properly
	   	      update the TRREF table which they weren't doing before.  
	   	      This prevents a problem where all NDPO's were being 
	   	      assigned the same reference number (eftpay.refno).  Also
	   	      removed pre-2003 revision history.
	*/

	type public Boolean ER
	type public String TRREF()

	type Boolean SWIFT
	type Date DATE
	type Number YEAR
	type String DD,MM,YY

	// Transaction Reference Number file locked
	lock +TRREF(SOURCE):3 else  do Runtime.setErrMSG("TRREF","7630") quit:ER

	set SWIFT=0

	// called from SWIFTEFT
	if SOURCE="SWIFT" do {
		set SWIFT=1
		set SOURCE=3
		}

	do VARDEC

	// foreign exchange deals
	if SOURCE[1 do TRFX

	// outgoing clean payments
	if SOURCE[2 do TROUT

	// incoming clean payments
	if SOURCE[3 do TRIN

	// 9XX MESSAGES
	if SOURCE[4 set REFNO=$$950

	// Domestic payments/collections
	if SOURCE[5 do TREFT

	lock -TRREF(SOURCE)
	
	quit


VARDEC	// Variable declaration

	type public Date DATE
	type public Number YEAR
	type public String DD,MM,YY

	// Today's system date
	set DATE=$$DAT^%ZM(%SystemDate,"MM/DD/YEAR")

	// Month
	set MM=DATE.piece("/",1)

	// Date
	set DD=DATE.piece("/",2)

	// Year (long)
	set YEAR=DATE.piece("/",3)

	// Year (abrev)
	set YY=YEAR.extract(3,4)

	quit


TRFX	// Generator for foreign exchange deals
	/* The function is used to generate the next transaction reference
	   number for foreign exchange deals.  The format for the 
	   transaction refernece number is YYYYMMDDSS9999.

	Inputs:
 
	. SOURCE 	Reference Number source code	/TYP=T/REQ
 
	Outputs:
 
	. REFNO		The next available transaction 	/TYP=T
 			reference number in format
			YYYYMMDDSS9999

	*/

	type public Boolean ER
	type public Number YEAR
	type public String DD,DLINDX(,),MM,REFNO,SOURCE

	type RecordTRREF trref=Db.getRecord("TRREF","TYPE=:SOURCE",1)

	if 'trref.getMode() set REFNO=YEAR_MM_DD_SOURCE_"0001"
	else  set REFNO=trref.refno+1

	if (YEAR_MM_DD)'=REFNO.extract(1,8) set REFNO=YEAR_MM_DD_SOURCE_"0001"

	set trref.refno=REFNO

	do trref.bypassSave()

	// Transaction Reference Number file locked
	lock +DLINDX("REF",REFNO):0 else  do Runtime.setErrMSG("DLINDX","7630") quit:ER

	quit


TROUT	// Generator for outgoing clean payments
	/* The function is used to generate the next transaction reference
	   number for outgoing clean payments.  The format for the 
	   transaction refernece number is 999999YYMM.

	Inputs:
 
	. SOURCE 	Reference Number source code	/TYP=N/REQ
 
	Outputs:
 
	. REFNO		The next available transaction 	/TYP=S
 			reference number in format
 			999999YYMM
	*/

	type public Boolean ER
	type public String EFTREF(),MM,REFNO,SOURCE,YY

	type Number LASTM,LASTY

	type RecordTRREF trref=Db.getRecord("TRREF","TYPE=:SOURCE",1)

	if 'trref.getMode() do { quit
		set REFNO="100000"_YY_MM
		set trref.refno=REFNO
		do trref.bypassSave()
		}

	set LASTY=trref.refno.extract(7,8)

	// Still in same year
	if LASTY=YY do {
		set LASTM=trref.refno.extract(9,10)

		//still in same month
		if MM=LASTM set REFNO=trref.refno.extract(1,6)+1
		//start new month
		if MM>LASTM set REFNO="100000"
		}

	// Start new year
	if YY'=LASTY set REFNO="100000"

	// Upper bound
	if REFNO=150000 set REFNO="100000"

	set REFNO=REFNO_YY_MM

	set trref.refno=REFNO
	do trref.bypassSave()

	// Transaction Reference Number file locked
	lock +EFTREF(REFNO):0 else  do Runtime.setErrMSG("EFTREF","7630") quit:ER

	quit


TRIN	// Generator for incoming clean payments
	/* The function is used to generate the next transaction reference
	   number for incoming clean payments.  The format for the 
	   transaction refernece number is 999999YYMM.

	Inputs:
 
	. SOURCE 	Reference Number source code	/TYP=N/REQ
 
	Outputs:
 
	. REFNO		The next available transaction 	/TYP=S
 			reference number in format
 			999999YYMM
	*/

	type public Boolean ER,SWIFT
	type public String EFTREF(),MM,REFNO,SOURCE,YY

	type Number LASTM,LASTY

	type RecordTRREF trref=Db.getRecord("TRREF","TYPE=:SOURCE",1)

	if 'trref.getMode() do { quit
		set REFNO="150000"_YY_MM
		set trref.refno=REFNO
		do trref.bypassSave()
		}

	set LASTY=trref.refno.extract(7,8)

	// Still in same year
	if LASTY=YY do {
		set LASTM=trref.refno.extract(9,10)

		// Still in same month
		if MM=LASTM set REFNO=trref.refno.extract(1,6)+1

		// Start new month
		if MM>LASTM set REFNO="150000"
		}

	// Start new year
	if YY'=LASTY set REFNO="150000"

	// Upper bound
	if REFNO=200000 set REFNO="150000"

	set REFNO=REFNO_YY_MM

	set trref.refno=REFNO
	do trref.bypassSave()

	// Transaction Reference Number file locked
	if 'SWIFT lock +EFTREF(REFNO):0 else  do Runtime.setErrMSG("EFTREF","7630") quit:ER

	quit


TREFT	// Generator for domestic payments & collections

	type public String REFNO,SOURCE

	type RecordTRREF trref=Db.getRecord("TRREF","TYPE=:SOURCE",1)

	set REFNO=trref.refno+1
	set trref.refno=REFNO
	
	do trref.bypassSave()

	quit


public 950()	// Generator for next sequence reference number
	/* The function is used to generate the next transaction reference
	   number for SWIFT 9 series messages.  The format for the 
	   transaction refernece number is yymmdd8nnnnnnnn.

	Returns:
 
	. REFNO		The next available transaction 	/TYP=S
 			reference number in format
 			yymmdd8nnnnnnnn
	*/

	type public Boolean ER
	type public String DD,MM,SEQ,TRREF(),YY

	type String REFNO,SOURCE

	lock +TRREF(4):2 else  do Runtime.setErrMSG("TRREF","7630") quit:ER ""

	set SOURCE=4

	type RecordTRREF trref=Db.getRecord("TRREF","TYPE=:SOURCE",1)
	
	if 'trref.getMode() set trref.refno=1
	
	set REFNO=trref.refno

	do VARDEC

	if REFNO.extract(1,6)'=(YY_MM_DD) do {
		set REFNO=YY_MM_DD_8
		set SEQ="00000001"
		}
	else  do {
		set SEQ=REFNO.extract(8,15)+1
		set SEQ="00000000".extract(1,8-SEQ.length())_SEQ
		}

	set REFNO=REFNO.extract(1,7)_SEQ

	set trref.refno=REFNO
	do trref.bypassSave()

	lock -TRREF(4)

	quit REFNO

vSIG()	quit "60235^45397^Pat Kelly^6487"	// Signature - LTD^TIME^USER^SIZE
