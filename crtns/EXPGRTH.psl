EXPGRTH	/* 	Expected Growth Table Maintenance -  Screen Driver

	       ORIG: Vince Arpa  6/25/98
	       INIT:
	       DESC:

	  ---- Revision History ------------------------------------------------
	   10/25/02 - DATTAR - 49451
	              Converted to PSL           	    

	   04/30/01 - GORDONT - 40339
 	              Changed LOAD,FILE sections to check for Expected Growth %.

	 -------------------------------------------------------------------------
	*/

NEW	//

	do INIT(0) 
	quit 


UPD	//

	do INIT(1) 
	quit 


INQ	//

	do INIT(2) 
	quit 


DEL	//

	do INIT(3) 
	quit	


INIT(%ProcessMode) //

	new DUP,ERFLAG,GTB,MAX,OLNTB,VFMQ

	set %PG=0 
	set %PAGE=1 
	set ANT=0 
	set DFT=0
	set MAX=13

	type RecordUTBLEXPGRTHT fEXPGTHT
	do VPG(.fEXPGTHT)

	quit	


VPG(RecordUTBLEXPGRTHT fEXPGTHT)	// Page control

        new FINISH
        set FINISH=0
        for  do { quit:FINISH
		if %PG=0 do VPG00 if ER set FINISH=1 quit

	        if %PG>0 do VPG01(.fEXPGTHT) 

                if "DFQ"[VFMQ do VER(.fEXPGTHT) set FINISH=1 quit

                set %PG=%PG+1
                }

        quit
 
	
VPG00	//Set up screen for Expected Growth Table

	set %TAB("GTBL")="[UTBLEXPGRTHT]GTBL/HLP=[UTBLEXPGRTHT]GTBL/TBL=[UTBLEXPGRTHT]/XPP=D POSGTBL^EXPGRTH"
	if %ProcessMode=2 set %TAB("IO")=$$IO^SCATAB($I)
	
	set %READ="@@%FN,,,GTBL/REQ" set %NOPRMT="F"
	if %ProcessMode=2 set %READ=%READ_",,IO/REQ"
	do ^UTLREAD 

	if VFMQ="Q" set ER=1 quit 
	if %ProcessMode=2,IO'=$I do OPEN^SCAIO

	quit 
	

VPG01(RecordUTBLEXPGRTHT fEXPGTHT) //Screen Set Up for data entry

	new A,%MODS,%REPEAT

	set %MODS=(MAX*(%PG-1))+1
	set %REPEAT=MAX

	set fEXPGTHT=Db.getRecord("UTBLEXPGRTHT","GTBL=:GTBL",1)

	// Call function to load expected growth table
	if %ProcessMode>0 do LOAD 
	else  set GTB(1)="1|||"
	
	if %ProcessMode'=2 do {
		for I=$O(GTB(""),-1)+1:1:(MAX*10) set GTB(I)=I_"|||"
		}

	do DRV^USID(%ProcessMode,"UTBLEXPGRTH",.fEXPGTHT)
	
	quit 
	
	
LOAD	// Load rate array for screen from current most index
	new EOPBAL,EXPGRTH,I,SEQ

	set SEQ=1

	type ResultSet rs=Db.select("EOPBAL,EXPGRTH,EXGRPCT","UTBLEXPGRTH","GTBL=:GTBL")
	while rs.next() do {

		// Sequence
		set $P(GTB(SEQ),"|",1)=SEQ 
	
		// Minimum Balance Tier
		set $P(GTB(SEQ),"|",2)=rs.getCol("EOPBAL")

		// Expected Growth
		set $P(GTB(SEQ),"|",3)=rs.getCol("EXPGRTH")
  
		// Expected Growth % (ARQ 40339)
		set $P(GTB(SEQ),"|",4)=rs.getCol("EXGRPCT")

		// Used to check for duplicates
		set DUP(rs.getCol("EOPBAL"))=""              

		set SEQ=SEQ+1
		}

	quit 
	
	
POSGTBL	// Post processor for Growth Table prompt
	
	if X="" quit
	if %OSAVE quit

	// Expected Growth Table ~p1 already exists
	if Db.isDefined("UTBLEXPGRTHT","GTBL=:X") set ER=1 set RM=$$^MSG(3359,GTBL)
	set I(3)=""

	quit 
	

VER(RecordUTBLEXPGRTHT fEXPGTHT) //
	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit

	do FILE(.fEXPGTHT) 

	do END 

	quit

	      
FILE(RecordUTBLEXPGRTHT fEXPGTHT) // File data
	new cnt,i
		
	if %ProcessMode=3 do {
		do Db.delete("UTBLEXPGRTHT","GTBL=:GTBL")
		}
	else  do fEXPGTHT.save() 

	// Loop through the GTB array and set each balance tier accordingly
	do Db.delete("UTBLEXPGRTH","GTBL=:GTBL")

	if %ProcessMode=3 quit

	set cnt=""
	for  set cnt=$O(GTB(cnt)) quit:cnt=""  do {
		new EOPBAL
		set EOPBAL=$P(GTB(cnt),"|",2)
		if $G(EOPBAL)="" quit

		type RecordUTBLEXPGRTH expgrt=Db.getRecord("UTBLEXPGRTH","GTBL=:GTBL,EOPBAL=:EOPBAL",1)
		set expgrt.expgrth=$P(GTB(cnt),"|",3)
		set expgrt.exgrpct=$P(GTB(cnt),"|",4)     
		do expgrt.save()
		}
	quit 
	
	
END	//  Display return Messages
	if $G(ER)!(%ProcessMode=2)!(%ProcessMode=4) quit
		
	if VFMQ="Q" do {
		// Expected growth table ~p1 not created
		if %ProcessMode=0 set RM=$$^MSG(3355,GTBL) quit    

		// Expected growth table ~p1 not modified
		if %ProcessMode=1 set RM=$$^MSG(3356,GTBL) quit    

		// Expected growth table ~p1 not deleted
		set RM=$$^MSG(3358,GTBL)    
		}
	else  do {
		// Expected growth table ~p1 created
		if %ProcessMode=0 set RM=$$^MSG(3352,GTBL) quit     

		// Expected growth table ~p1 modified
		if %ProcessMode=1 set RM=$$^MSG(3353,GTBL) quit     

		// Expected growth table ~p1 deleted
		set RM=$$^MSG(3354,GTBL)        
		}

	set ER="W"
	quit 
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43517^Sanchez SCM Administrator^3942"	// Signature - LTD^TIME^USER^SIZE
