TTXDIS		//; FORMAT INCLEARING CONTROL REPORT
	/*
	       ORIG: Michael Winigrad
	
	  --------------Revision History----------------------------------------
	
	03/29/06 - SATYANAS - CR19687
		   Changed the Code to write the data into the output File
		   while exectuing the Function TTXINI. 
		   Replced the declaration of public IO IO by declaring  
		   public String IO. Removed the call OPEN^SCAIO, because it 
		   is already open the IO. Removed the call of CLOSE^SCAIO, 
		   because it is closing the file in the EXIT section of TTXFMT OR SCAHDG 
		   procedure. Replaced set %NOCLOSE=1 by set VRWOPT("NOCLOSE") = 1.
	
	12/22/05 - PRAKASHJ - CR17423
		   Removed PSL deprecited features and warnings.
		   
	12/29/03 - CARROLLJ - CR7658
		   Corrected precedence errors.


	06/09/03 - Dan Russell - 51351
		   Removed use of schema class methods and replaced
		   references to temporary table TMPBCHDT with TMPRPT3.
		   
	01/31/02 - Scottc - 43583
		   Convert to PSL.  Remove old history.

	  -----------------------------------------------------------------------
	*/

	type public Number PN
	type public String IOHDG
	
	if 'IOHDG.exists() set IOHDG="^SCAHDG"
	
	do RPT1 
	do RPT2 
	do RPT3
	
	set PN=-1 
	do @IOHDG
	quit 
	
ENT	//------------------ Entry point for standalone operation ------------
	
	type Number X
	type public Number PN
	type public String %ED,CONAM,POP,IOHDG
	type public IO IO
	
	set:'%ED.exists() %ED="" 
	#ACCEPT DATE=12/29/03;PGM=John Carroll
	set:'$D(%InputTimeOut) %InputTimeOut=60
	
	if 'CONAM.exists() set CONAM=CUVAR.conam
		
	for  do TAPENUM quit:(X=1)!(X="Q")
	if X=1 quit

	//   Output control report to
	write !!,$$^MSG(5114) 
	kill POP 
	do ^SCAIO       
	if 'IOHDG.exists() set IOHDG="^SCAHDG"
	
	do RPT1 
	do RPT2 
	do RPT3
	use IO 
	
	set PN=-1 
	
	do @IOHDG
	quit 

TAPENUM	 // Get file number	
	type Number TAB
	type public String TAPENUM,X
	
	set X=0
	use 0 write !!,$$^MSG(1091) 
	read TAPENUM 
	if TAPENUM.isNull() set X=1 quit 


	// File number ~p1
	if TAPENUM="?" do {
		use 0 write !!
		set TAB=0
		type ResultSet rs=Db.select("TAPENUM","INCTAPE")
		if rs.isEmpty() set TAPENUM="" quit
		while rs.next() do {
			set TAPENUM=rs.getCol("tapenum")
			set TAB=TAB+10 
			write TAPENUM,?TAB 
			if TAB>70 do {
				set TAB=0 
				write !
				}
			}
		use 0 write !!,$$^MSG(1091) 
		read TAPENUM
		}

	// Invalid file number
	if TAPENUM.isNull() set X=1 quit
	if 'Db.isDefined("INCTAPE","TAPENUM") write " "_$$^MSG(1338) quit
	set X="Q"
	quit
	
RPT1	//----------------------- Batch report -------------------------------

	type public Number BCHNUM,ER,IORM,PN,TC,TD,TI,QUIT
	type public String ET,IO,IOHDG,PGM,RID,RN,TAPENUM,VRWOPT()
	type String %NOBANNER
	
	// Batch Report for File Number ~p1
	set RN=$$^MSG(5105,TAPENUM)            
	set RID="BTTXDIS"
	set QUIT=0 
	do ^URID 
	if PGM.isNull() do { quit
		set ER=1 
		set ET="INVLDRPT" 
		do ^UTLERR 
		}

	set VRWOPT("NOCLOSE") = 1 
	set %NOBANNER=1         
	do V0^@PGM
	
	set $Y=0 

	set PN=1 
	set IORM=132 
	do @IOHDG
	set (TI,TD,TC)=0
	type ResultSet rs=Db.select("BCHNUM","INCTAPEB","TAPENUM=:TAPENUM")
	if rs.isEmpty() quit
	while rs.next() do { quit:QUIT
		set BCHNUM=rs.getCol("BCHNUM") 
		do PNT1
		}
	quit 
	
PNT1	
	type public Number BCHNUM,QUIT,TAPENUM,TC,TD,TI,RN
	type public String IOHDG,IOSL
	
	if $Y'<IOSL do @IOHDG 
	if QUIT quit 
	
	type RecordINCTAPEB inctap=Db.getRecord("INCTAPEB","TAPENUM,BCHNUM")

	if (inctap.inst.isNull())!(inctap.efdt.isNull())!(inctap.bchcnt.isNull()) write !,BCHNUM.justify(5),?5,$$^MSG(5103) quit
	if (inctap.bchdr.isNull())!(inctap.bchcr.isNull()) write !,BCHNUM.justify(5),?5,$$^MSG(5103) quit

	write !,BCHNUM.justify(5),?12,inctap.efdt,?34,inctap.uid,?46,(inctap.trandate).toString("MM/DD/YEAR")
	write ?63,((inctap.bchcnt).toString()).justify(5) 
	set TI=TI+inctap.bchcnt
	write ?72,(inctap.bchdr/100).roundDec(2,,12) 
	set TD=TD+inctap.bchdr
	write ?88,(inctap.bchcr/100).roundDec(2,,12) 
	set TC=TC+inctap.bchcr

	// File rejected
	write ?103,$S(inctap.sts=0:$$^MSG(5109),inctap.sts=1:$$^MSG(5107),1:"")  

	quit
	
RPT2	//----------------------- Teller Report --------------------------------

	// Teller Report for File Number ~p1

	type public Number BCHNUM,ER,PN,RN,TAPENUM,TB,TC,TD,TI,QUIT
	type public String ET,IORM,IOHDG,IOSL,PGM,REC(),RID,VRWOPT(),ZDIR
	type public IO IO
	type String BCHCNT,BCHDR,BCHCR,CNT,HDG,J,ZEFD,ZUID

	set RN=$$^MSG(5116,TAPENUM)            //Teller Report for File Number ~p1
	
	set RID="TTXDIS" 
	do ^URID 
	if PGM.isNull() do { quit
		set ER=1 
		set ET="INVLDRPT" 
		do ^UTLERR 
		}

	set VRWOPT("NOCLOSE") = 1
	do V0^@PGM
	
	do Db.fastDelete("TMPRPT3","PID=:%ProcessID")

	type ResultSet rs=Db.select("BCHNUM","INCTAPEB","TAPENUM=:TAPENUM")
	if rs.isEmpty() quit
	while rs.next() set BCHNUM=rs.getCol("bchnum") do {
		type RecordINCTAPEB inctap
		set inctap=Db.getRecord("INCTAPEB","TAPENUM,BCHNUM")

		if (inctap.inst.isNull())!(inctap.efdt.isNull())!(inctap.uid.isNull()) quit
		set ZDIR=inctap.inst
		set ZEFD=inctap.efdt
		set ZUID=inctap.uid
		set J=%ProcessID
		
		type RecordTMPRPT3 tmprpt3=Db.getRecord("TMPRPT3","PID=:%ProcessID,KEY1=:ZDIR,KEY2=:ZEFD,KEY3=:ZUID",1)
		if tmprpt3.getMode() do {	
			set REC=tmprpt3.data
			set REC.piece("|",1)=REC.piece("|",1)+1			// Count
			set REC.piece("|",2)=REC.piece("|",2)+inctap.bchcnt	// Batch count
			set REC.piece("|",3)=REC.piece("|",3)+inctap.bchdr	// Debits
			set REC.piece("|",4)=REC.piece("|",4)+inctap.bchcr	// Credits
			set tmprpt3.data=REC

			do tmprpt3.bypassSave()
			}
		}	

	set $Y=0 
	set PN=1 
	set IORM=132 
	do @IOHDG
	set (TB,TI,TD,TC)=0

	
	type DbSet ds=Db.selectDbSet("TMPRPT3","PID=:%ProcessID")

	while rs.next() do { quit:QUIT

		if $Y'<IOSL do @IOHDG if QUIT quit
		
		type RecordTMPRPT3 tmprpt3=ds.getRecord("TMPRPT3")

		set ZDIR=tmprpt3.key1
		set ZEFD=tmprpt3.key2
		set ZUID=tmprpt3.key3
		set REC=tmprpt3.data
		set CNT=REC.piece("|",1)
		set BCHCNT=REC.piece("|",2)
		set BCHDR=REC.piece("|",3)
		set BCHCR=REC.piece("|",4)
		
		write !,ZDIR,?22,$$DAT^%ZM(ZEFD),?39,ZUID,?55,CNT.justify(5) 
		set TB=TB+CNT
		write ?69,BCHCNT.justify(5) 
		set TI=TI+BCHCNT
		write ?78,(BCHDR/100).roundDec(2,,12) 
		set TD=TD+BCHDR
		write ?94,(BCHCR/100).roundDec(2,,12) 
		set TC=TC+BCHCR		
		}

	do Db.fastDelete("TMPRPT3","PID=:%ProcessID")

	quit 
	
RPT3	//----------------------- File totals ----------------------------------

	// Summary Report for File Number ~p1
	type public Number TAPENUM,PFILCNT,PFILCR,PFILDR
	type public String ERRMSG,IOHDG
	type String STS

	type public IO IO
	type Number RN,PN,IORM

	set RN=$$^MSG(5115,TAPENUM)            
	set $Y=0 
	set PN=1 
	set IORM=132 
	do @IOHDG
	
	type RecordINCTAPE inctape=Db.getRecord("INCTAPE","TAPENUM")

	// Source:
	write !,$$^MSG(4341),inctape.src      
	// Origin:
	write !,$$^MSG(5113),inctape.origin      
	// Transmission date:
	if inctape.trandate>0 write !,$$^MSG(5124),$$DAT^%ZM(inctape.trandate) 
	// Transmission time:
	write !,$$^MSG(5125),inctape.trantime      
	// Total batch count:
	write !,$$^MSG(5117),inctape.bchcnt      
	// Total item count:
	write !,$$^MSG(5120),inctape.filcnt    
	// Total file debits:
	write !,$$^MSG(5119),(inctape.fildr/100).roundDec(2,,12)     
	// Total file credits:
	write !,$$^MSG(5118),(inctape.filcr/100).roundDec(2,,12)     
	// Operator comments:
	write !,$$^MSG(5112),inctape.opercmt      
	// Total number of rejected batches:
	write !!,$$^MSG(5121),inctape.rejbch   
	// Total rejected debits:
	write !,$$^MSG(5123),(inctape.rejdr/100).roundDec(2,,12)    
	
	// Total rejected credits:
	write !,$$^MSG(5122),(inctape.rejcr/100).roundDec(2,,12)

	type ResultSet rs=Db.select("STS","INCTAPEB","TAPENUM=:TAPENUM")
	while rs.next() do {
		set STS=rs.getCol("sts")
		//File rejected
		if ('STS.isNull()),(STS=0) write !!,$$^MSG(5109)    
		quit
		}
	// set I18n=off
	write !
	// Physical File Data
	if PFILCNT.exists(),PFILDR.exists(),PFILCR.exists() do {
		write !,"Physcial File Data"
		write !,"Error Message: "_ERRMSG.get()
		write !,"Physical File Total Record Count: "_PFILCNT
		write !,"Physical File Total Debits: "_(PFILDR/100).roundDec(2)
		write !,"Physical File Total Credits: "_(PFILCR/100).roundDec(2)
		}
	// set I18n=on
	quit 

vSIG()	quit "60354^39522^Sethy, Satyanarayan^7869"	// Signature - LTD^TIME^USER^SIZE
