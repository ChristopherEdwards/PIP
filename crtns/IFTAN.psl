IFTAN(String CID)
	/*
	 ORIG: VANITHAK - 01/12/2007
	 DESC: Inter-Bank Funds Transfer Account Number Calculation(IFTAN)

	 ---- Comments --------------------------------------------------------

	 IFTAN is a unique number assigned by financial institutions in 
	 Mexico to each account and used by the Mexican clearing house 
         CECOBAN for the purposes of identifying the account in the
	 inter-bank funds transfers.

	 ---- Revision History ------------------------------------------------
	 
	 07/05/2007 - VanithaK - CR28007
	              Modified the changes made for CR 27879 so that there 
	              is no zero fill for the Region code which is less than 
	              three digits. 
	 
	 06/28/2007 - VANITHAK CR 27879
	   	      Modified to get the Region code from UTBLBRCD so that
	   	      whenever the region code associated with a branch of an
	   	      account changes, it will be used in the calculation
	   	      of IFTAN.
	 	      
	 01/24/2007 - VANITHAK CR 24939
	 	      Created for Inter-Bank Funds Transfer Account number
	 	      calculation.

	*/
	type String BANKID, CH1, CH2, CHKSUM, IDLEN, LASTCH, REGCODE, TOTALCH, TOTAL, UNQID
	type Number ER=0, I, J, K, LASTDIGIT
	
	set UNQID = ""
	type RecordACN acn=Db.getRecord("ACN","CID=:CID",1)
	if 'acn.getMode() quit UNQID
	
	// get bank id
	#IF 'CUVAR.BANKID.isNull()
	set BANKID = CUVAR.BANKID.toString()
	#ELSE
	set BANKID = ""
	#ENDIF
	
	// get region code
	type RecordUTBLBRCD utblbrcd = Db.getRecord("UTBLBRCD","BRCD=:acn.boo",1)
	set REGCODE = utblbrcd.region
		
	set CHKSUM="371"
	set TOTALCH=0,TOTAL=0

	// fill zeros if not in required length
	if (BANKID.length()<3) set BANKID = BANKID.justify(3,1,"0",0)
	
	if (CID.length()<11) set CID = CID.justify(11,1,"0",0)

	// truncate if more than required length
	if (CID.length()>11) set CID = CID.justify(11,,,1)

	set UNQID = BANKID_REGCODE_CID
	set IDLEN=UNQID.length()

	// calculate check digit
	for I=1:3:IDLEN do { quit:ER
		set K=I
		for J=1:1:3 do {
			set CH1=UNQID.extract(K)
			set CH2=CHKSUM.extract(J)
			set TOTALCH = CH1*CH2

			if (TOTALCH.length()>1) do {
				set LASTDIGIT=TOTALCH.extract(2)
				set TOTAL = TOTAL+LASTDIGIT
			}
			else  set TOTAL = TOTAL+TOTALCH
			set K=K+1
		}
	}

	if (TOTAL.length()) > 1 do {
		set LASTCH=10-TOTAL.extract(TOTAL.length())
		if (LASTCH=10) set UNQID = UNQID_"0"
		else  set UNQID = UNQID_LASTCH
	}
	else  set UNQID = UNQID_TOTAL
	
	quit UNQID
 #OPTION ResultClass ON
Public String vSIG()	quit "60816^50007^Vanitha Krishnasamy^2341"	// Signature - LTD^TIME^USER^SIZE
