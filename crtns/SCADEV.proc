SCADEV	/*	Set up device security.

	 ---- Revision History ------------------------------------------------
	  
	  05/16/06 - MBUIM - 21180
	  	     Modified VPG section to file change and quit if VFMQ="F".
	  	     An undefined %PG was returned when FILE was selected as 
	  	     the logic continued to the next screen instead of filing.  
	  	  
	  08/16/02 - DATTAR - 49451
		     Converted to PSL
	
	  ----------------------------------------------------------------------
	*/

NEW 	//

	do INIT(0)
	quit


UPD	//

	do INIT(1)
	quit


INQ 	//

	do INIT(2)
	quit


DEL	//

	do INIT(3)
        quit


INIT(%ProcessMode)	//

	type Number %PAGE, %PG, INST, OLNTB, RSTCT, ZPG

	set %PG=0 
	set ZPG=0 
	set %PAGE=1
	set INST=CUVAR.sbinstno
	set RSTCT=0
	type RecordDEVICE fDEVICE
	do VPG(.fDEVICE)
	quit


VPG(RecordDEVICE fDEVICE) // Page control

	type public Boolean ER, FOUND
 
        type public Number %PG, %ZPG
        type public String VFMQ
 
	set FOUND=0
	for  do { quit:FOUND=1
        	if %PG=0 do VPG00(.fDEVICE) if ER!(VFMQ="Q") set FOUND=1 quit
        	if %PG=1 do VPG01(.fDEVICE) if ER!(VFMQ="Q") set FOUND=1 quit
        	if VFMQ="F" do VER(.fDEVICE) set FOUND=1 quit
	        if %PG>1 do VPG02(.fDEVICE) if ER!(VFMQ="Q") set FOUND=1 quit
                if "DFQ"[VFMQ do VER(.fDEVICE) set FOUND=1 quit
                set %PG=%PG+1
		set ZPG=%PG
                }
        quit	
        
VPG00(RecordDEVICE fDEVICE) //Set up

	set %TAB("DEVNAME")=".IO4/HLP=[DEVICE]DEVNAME/TBL=[DEVICE]/XPP=D PP00^SCADEV"

	set %READ="@@%FN,,,DEVNAME/REQ" 
	set %NOPRMT="N"
	
	do ^UTLREAD 
	
	if VFMQ="Q" quit

	// Don't set up restrictions if no institution number
	if INST="" quit
	set I=0 
	set UCLS="" 
	set %REPEAT=15

	set fDEVICE=Db.getRecord("DEVICE","DEVNAME=:DEVNAME",1)

	do SETUP(.fDEVICE)
	
	set X=I\15 
	set %PAGE=$S(X=0:2,I#15:X+2,1:X+1)
	quit


VPG01(RecordDEVICE fDEVICE) // Device Screen

	do DRV^USID(%ProcessMode,"SCADEV1",.fDEVICE)

	quit


VPG02(RecordDEVICE fDEVICE) // Screen

	do LOAD

	do DRV^USID(%ProcessMode,"SCADEV2",.fDEVICE)

	if VFMQ="Q" quit

	do SAVE
	kill UCLS
	quit


SETUP(RecordDEVICE fDEVICE)	// Set up userclass array
	type ResultSet rs=Db.select("UCLS","SCAU0","UCLS>:UCLS")
	while rs.next() do { quit:UCLS=""
		set UCLS=rs.getCol("UCLS")
		set I=I+1
		set J=2
		set $P(SVUCLS(I),"|",1)=UCLS
		do DEFAULT(.fDEVICE)

		if 'rs.next() set UCLS="" quit
		set UCLS=rs.getCol("UCLS")
		set J=5
		set $P(SVUCLS(I),"|",4)=UCLS
		do DEFAULT(.fDEVICE)
		}

	quit


LOAD	// Load data for screen

	set INDX=(%PG-2)*15
	for I=1:1:15  do { quit:X=""
		set X=$G(SVUCLS(I+INDX)) 
		if X="" quit
		set UCLS(I)=X
		}
	quit


SAVE	// Save data from screen

	set INDX=(ZPG-2)*15
	for I=1:1:15 do {
		set X=$G(UCLS(I)) 
		set SVUCLS(I+INDX)=X
		}
	quit


DEFAULT(RecordDEVICE fDEVICE) //

	type RecordDEVICER devicer=Db.getRecord("DEVICER","DEVNAME=:DEVNAME,INST=:INST,UCLS=:UCLS",1)

	if '%ProcessMode do { quit
		set $P(SVUCLS(I),"|",J)=0 
		set $P(SVUCLS(I),"|",J+1)=0 
		}

	if devicer.getMode()=1 do {
		if devicer.rest="",fDEVICE.rest do { quit 
			set $P(SVUCLS(I),"|",J)=1 
			set $P(SVUCLS(I),"|",J+1)=0
			}

		if devicer.rest do { quit
			set $P(SVUCLS(I),"|",J)=1 
			set $P(SVUCLS(I),"|",J+1)=1 
			}
		
		}

	set $P(SVUCLS(I),"|",J)=0 
	set $P(SVUCLS(I),"|",J+1)=0
	quit


ERR	//

	set ER=1 

	do ^UTLERR

	set VFMQ="Q"
	quit


PP00	// Device post-processor

	// Record already exists
	if '%OSAVE,Db.isDefined("DEVICE","DEVNAME=:X") do { quit
		set ER=1 
		set RM=$$^MSG(2327) 
		}

	// Record not found
	if %OSAVE,'Db.isDefined("DEVICE","DEVNAME=:X") do { quit
		set ER=1 
		set RM=$$^MSG(2335)
		}
	if '%OSAVE set I(3)=""
	quit


RSTRCT	// Restrict access

	new TUCLS
	set TUCLS=$P(SVUCLS(I),"|",J)
	do Db.delete("DEVICER","DEVNAME=:DEVNAME AND INST=:INST AND UCLS=:TUCLS")
	set RSTCT=1
	quit


PERMIT	// Permit access

	new TUCLS
	set TUCLS=$P(SVUCLS(I),"|",J)
	type RecordDEVICER devicer=Db.getRecord("DEVICER","DEVNAME=:DEVNAME,INST=:INST,UCLS=:TUCLS",1)
	set devicer.rest=0
	do devicer.save()
	quit


ROUT	// Restrict output only

	new TUCLS
	set TUCLS=$P(SVUCLS(I),"|",J)
        type RecordDEVICER devicer=Db.getRecord("DEVICER","DEVNAME=:DEVNAME,INST=:INST,UCLS=:TUCLS",1)
        set devicer.rest=0
	do devicer.save()
	set RSTCT=1
	quit


CHECK	//

	set J=1 
	if $P(SVUCLS(I),"|",1)'="",$P(SVUCLS(I),"|",3) do ROUT
	if $P(SVUCLS(I),"|",1)'="",$P(SVUCLS(I),"|",2),'$P(SVUCLS(I),"|",3) do RSTRCT
	if $P(SVUCLS(I),"|",1)'="",'$P(SVUCLS(I),"|",2),'$P(SVUCLS(I),"|",3) do PERMIT
	set J=4 
	if $P(SVUCLS(I),"|",4)'="",$P(SVUCLS(I),"|",6) do ROUT
	if $P(SVUCLS(I),"|",4)'="",$P(SVUCLS(I),"|",5),'$P(SVUCLS(I),"|",6) do RSTRCT
	if $P(SVUCLS(I),"|",4)'="",'$P(SVUCLS(I),"|",5),'$P(SVUCLS(I),"|",6) do PERMIT
	quit


VER(RecordDEVICE fDEVICE) 	//

	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit
	do FILE(.fDEVICE)
	do END
	quit


FILE(RecordDEVICE fDEVICE)	// File data

	if %ProcessMode=3 do Db.delete("DEVICE","DEVNAME=:DEVNAME") quit
	for I=1:1 quit:$G(SVUCLS(I))=""  do CHECK
	if RSTCT set fDEVICE.rest=1
	else  do {
		set fDEVICE.rest=0
		do fDEVICE.save()
		if INST'="" do Db.delete("DEVICER","DEVNAME=:DEVNAME AND INST=:INST")
		}
	quit


END	//

	if ER!(%ProcessMode=2)!(%ProcessMode=4) quit

	if VFMQ="Q" do {
		// Device ~p1 not created
		if %ProcessMode=0 set RM=$$^MSG(826,DEVNAME) quit

		// Device ~p1 not modified
		if %ProcessMode=1 set RM=$$^MSG(828,DEVNAME) quit

		// Device ~p1 not deleted
		set RM=$$^MSG(827,DEVNAME)
		}
	else  do {
		// Device ~p1 created
		if %ProcessMode=0 set RM=$$^MSG(823,DEVNAME) quit

		// Device ~p1 modified
		if %ProcessMode=1 set RM=$$^MSG(825,DEVNAME) quit
		
		// Device ~p1 deleted
		set RM=$$^MSG(824,DEVNAME)
		}
	set ER="W"
	quit

vSIG()	quit "60401^60808^Marie Mbui^5400"	// Signature - LTD^TIME^USER^SIZE
