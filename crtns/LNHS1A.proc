public	LNHS1A	// Loan History File Builder
	/*
	       ORIG:  David Caliendo (5527) - 02/18/87

	---- Revision History ------------------------------------------------
	
	12/22/05 - TITOVE - CR 18597
		   Corrected overall logic and brought up to latest PSL
		   standards as part of DBI3 project.

	05/06/05 - KinI - 15524
		   Removed lnbil1.bp1 references and related variables as its 
		   sub-fields are referenced directly per DBI standards.
	
	*/
	type public Number CID, TSEQ
	type public String ET

	type Number P(,), SEQ, XP(,), XXP(,)

	type RecordLN ln = Db.getRecord("LN", "CID = :CID")
	
	type RecordHIST hist = Db.getRecord("HIST", "CID = :CID, TSEQ = :TSEQ")

	// Used to call HISTL^UHFETCH
	// Get lower level of history for satisfied bills
	type DbSet ds = Db.selectDbSet("HISTSB","CID=:CID AND SLN=:TSEQ")

	// Payment support file incomplete for this transaction
	if ds.isEmpty() set ET = "PSFINC" do ERR^MRPC021 quit

	while ds.next() do {
		
		type RecordHISTSB histsb = ds.getRecord("HISTSB")
		
		do LOOP(.histsb)
		}
	
	do SORT

	do LOOP2(.ln,.hist)

	quit


LOOP(RecordHISTSB histsb)

	/*
	
	ARGUMENTS:
		. histsb        Transaction History Object  TYP=RecordHISTSB/REQ

	*/
	type public Number P(,), XP(,)
	type public String LNHS1A()

	type Number A, D, I
	type String AVAR, B, BVAR, DVAR

	for I = 1:1:5 do { quit:B.isNull()
		
		set AVAR = "pe0"_I_"bseq"
		set BVAR = "pe0"_I_"ea"
		set DVAR = "pe0"_I_"amt"
		
		set A = +histsb.@AVAR
		set B = histsb.@BVAR
		set D = +histsb.@DVAR
				
		// No more payment elements for this HISTSB.SLSEQ
		if B.isNull() quit
	
		if B = "M" set LNHS1A(4).piece("|",7) = LNHS1A(4).piece("|",7) + D

		if 'P(A,B).exists() set P(A,B) = 0
		if 'XP(B,A).exists() set XP(B,A) = 0
		
		set XP(B,A) = XP(B,A) + D
		set P(A,B) = P(A,B) + D
		}

	quit


LOOP2(RecordLN ln,	// Loan Account Object
      RecordHIST hist)	// Transaction History Object

	type public Number BDD(), CID, P(,), TBIL, XXP(,)
	type public String LNHS1A(), LNHS1B(), LNHS1C()
	
	type Number A, B, BPA, C, D, I, NUM, ONP, T, Y
	type String M, N, X

	for I = 1,2,3 set LNHS1C(I) = ""
	
	set NUM = 0
	for  set NUM = P(NUM).order() quit:NUM.isNull()  do {
		
		set M = "" 
		set T = 0
		
		for  set M = P(NUM,M).order() quit:M.isNull()  set T = T + P(NUM,M)

		type RecordLNBIL1 lnbil1 = Db.getRecord("LNBIL1", "CID = :CID, SCHSEQ = :NUM")

		set BDD(lnbil1.cdpd) = "" 
		set TBIL = TBIL + 1
		set LNHS1C(1) = LNHS1C(1)_lnbil1.cbcd_"|"
		set LNHS1C(2) = LNHS1C(2)_lnbil1.cdpd_"|"
		set LNHS1C(3) = LNHS1C(3)_T_"|"
		}

	set (C,ONP,BPA) = 0
	set N = ""
	for  set N = XXP(N).order() quit:N.isNull()  do {
		
		// Don't display LCHG or UNAPF in repeat region
		if "L/F"[N quit
		
		set (A,B,D) = 0 
		set C = C + 1 
		set LNHS1B(C) = ""
		
		set M = ""		
		for  set M = XXP(N,M).order() quit:M.isNull()  do {
			
			if M set A = A + XXP(N,M),LNHS1B(C).piece("|",M + 3) = XXP(N,M)
			
			set D = D + XXP(N,M)
			
			if 'M,(N.extract() '= "M") set B = B + XXP(N,M)
			}
			
		set LNHS1B(C) = N_"|"_D_"|"_B_"|"_LNHS1B(C).piece("|",4,99)
		set ONP = ONP + B 
		set BPA = BPA + A
		}

	set LNHS1A(4).piece("|",1) = ONP
	set LNHS1A(4).piece("|",2) = BPA
	set Y = 0
	set X = hist.tamt

	// Extract pieces of HIST.TAMT
	// Undisbursed
	if ln.aruf set X.piece("#",2) = X.piece("#",2) + X.piece("#",5)
	// Add-on
	if ln.iam set X.piece("#",3) = X.piece("#",3) + X.piece("#",8)

	for I = 2:1:5 set LNHS1A(4).piece("|",I + 1) = +X.piece("#",I)
	
	set LNHS1A(4).piece("|",8) = X.piece("#",7)

	// Find all MCHG and UNAPF amounts
	set LNHS1A(4).piece("|",7) = X.piece("#",6)
	set LNHS1A(4).piece("|",11) = X.piece("#",9)

	// Get first sequence in Internal Bill Detail record for this CID
	set Y = P(0).order()
	
	// No Internal Bill Detail records for this CID
	if Y.isNull() quit
	
	type RecordLNBIL1 lnbil1 = Db.getRecord("LNBIL1", "CID = :CID, SCHSEQ = :Y")
	
	// Payment Due Date
	set LNHS1A(4).piece("|",9) = lnbil1.cdpd

	// Get last sequence in Internal Bill Detail record for this CID
	set Y = P("").order(-1)
	
	type RecordLNBIL1 lnbil2 = Db.getRecord("LNBIL1", "CID = :CID, SCHSEQ = :Y")
	
	// Payment Due Date
	set LNHS1A(4).piece("|",10) = lnbil2.cdpd

	quit


SORT	

	type public Number P(,), XP(,), XXP(,)
	
	type Number I, M, NUM, Z()
	type String N

	set NUM = 0
	for I = 1:1 set NUM = P(NUM).order() quit:NUM.isNull()  set Z(NUM) = I
	
	if 'P(0,"").order().isNull() set Z(0) = 0
	
	set (N,M) = ""
	for  set N = XP(N).order() quit:N.isNull()  do {
		
		for  set M = XP(N,M).order() quit:M.isNull()  set XXP(N,Z(M)) = XP(N,M)
		}

	kill XP

	quit

	

vSIG()	quit "60260^55239^Eugene Titov^4329"	// Signature - LTD^TIME^USER^SIZE
