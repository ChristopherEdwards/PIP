QRYTYPE(String CLS,String GRP,String TYPELIST())
	/*
	ORIG:  Allan R. Mattson (6915) - 09/11/86
	PROCEDURE ID: QRYTYPE
	DESC: Account Type Query
	
	---- Comments --------------------------------------------------------

	Allow screen selection of a list of types for the specified class and
	group.

	ARGUMENTS:
		. CLS		Class		/TYP=T/REQ/NONULL/MECH=VAL

		. GRP		Group list	/TYP=T/NOREQ/MECH=VAL
				comma separated

		. TYPELIST	List of selected type	/REQ/MECH=REFARR:W

	RETURNS:
		. ER		Error flag	/TYP=L
		
		. RM		Error message	/TYP=T	

	------ Revision History ------------------------------------------------
	
	11/02/04 - RussellDS - CR13011
		   Made VFMQ public since callers examine it on return.
		   
		   Fixed error in DFT section related to construction of WHERE
		   clause.

	12/18/03 - CARROLLJ - CR7239
		   Correct precedence errors.

	04/29/03 - Dan Russell - 51351
		   Fix calls to screen QRYTYPE.  Clean up code.

	07/20/02 - YENDAPALLIS -43583
		   PSL conversion clean up.

	08/12/02 - Dan Russell - 49794
		   Restructured to simplify.  Added parameters to eliminate Q
		   array and to allow list of returned values.

	04/02/02 - YENDAPALLIS - 43583
		   Converted to PSL 
	----------------------------------------------------------------------------
	*/

	type Public Number ER=0
	type Public String RM,VFMQ

	type Number ITEMCNT,%MODS,N,%PAGE,%PG,%REPEAT,RPTSIZE
	type String R()

	do DFT(CLS,GRP.get())

	set RPTSIZE=18

	// Get data
	set %PG=1
	set ITEMCNT=R("").order(-1)
	set %PAGE=(((ITEMCNT-1)\RPTSIZE))+1

	for  do { quit:VFMQ="F"!(VFMQ="Q")
		if %PG=%PAGE do {		// Last page
			set %REPEAT=ITEMCNT#RPTSIZE
			if '%REPEAT set %REPEAT=RPTSIZE
			}
		else  set %REPEAT=RPTSIZE

		do DRV^USID(0,"QRYTYPE")
		if '(VFMQ="F"!(VFMQ="Q")) do {
			set %PG=%PG+1
			set %MODS=((%PG-1)*RPTSIZE)+1
			}
                }

	quit:"Q"[VFMQ

	// Move data into return array
	set N="" 
	for  set N=R(N).order() quit:N=""  do {
		type Number I
		type String X

		for I=2,4,6 do {
			if R(N).piece("|",I) do {
				set X=R(N).piece("|",I-1)
				if X'="" set TYPELIST(X)=""
				}
			}
		}

	if 'TYPELIST.data() do {
		set ER=1
		// No product types selected
		set RM=$$^MSG(1976)
		}
	
	quit


DFT(String CLS,	// Private - Create default info
	String GRP)

	type Public Number %REPEAT
	type Public String R()

	type Number I,TYPE
	type String TMPARRAY

	// Store in a temporary array so that the display will appear in TYPE 
	// order, rather than GRP order.
	
	if CLS="*" do {
		type ResultSet prodrs=Db.select("TYPE","PRODDFTC")
		while prodrs.next() do {
			set TYPE=prodrs.getCol(1)
			set TMPARRAY(TYPE)=TYPE_"|1"
			}
		}

	else  do {
		if GRP="" do {
			type ResultSet rs=Db.select("distinct TYPE","ACN","CLS=:CLS")
        		while rs.next() do {
                		set TYPE=rs.getCol(1)
				set TMPARRAY(TYPE)=TYPE_"|1"
			}
		}
		else  do {
			type String IN,WHERE,X

			set IN=""
			for I=1:1 set X=GRP.piece(",",I) quit:X=""  set IN=IN_"'"_X_"',"
			
			set WHERE="CLS=:CLS AND GRP IN ("_IN.extract(1,IN.length()-1)_")"
			
			#ACCEPT DATE=10/15/02;PGM=Suneetha Yendapalli
			type ResultSet rs=Db.select("distinct TYPE","ACN",WHERE)
        		while rs.next() do {
                		set TYPE=rs.getCol(1)
				set TMPARRAY(TYPE)=TYPE_"|1"
			}
		}
	}

	// Create R array in manner expected by the rest of the routine.
	set TYPE=""
	for I=1:1 set TYPE=TMPARRAY(TYPE).order() quit:TYPE=""  do {
		set R(((I-1)\3)+1).piece("|",(((I-1)#3)*2)+1)=TMPARRAY(TYPE)
	}

	set %REPEAT=R("").order(-1)
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43598^Sanchez SCM Administrator^3317"	// Signature - LTD^TIME^USER^SIZE
