MRPC046(return,versn,frdat,todat,incfnl,bal,iacm,irn,intfre,nxtintdt,lstintdt)	//
	/*
	   ORIG: Neal Gorman - 02/18/97

	   Returns an interest amount computed over a specified date range for
	   a client-specified set of processing characteristics.

	   [SCATBL5]RPCID=46

	   KEYWORDS: Client/Server, Deposit Utilities, Loan Utilities

	   ARGUMENTS:
	  	 . return    					 /TYP=T/REQ
	        						 /MECH=REFNAM:W

	  	 . versn 	 ^MRPC046 version number 	 /TYP=N/REQ
	    			 Current version = 1 		 /MECH=VAL

	  	 . frdat 	 Beginning date in calculation	 /TYP=D/REQ
	    			 date range  			 /MECH=VAL

	  	 . todat	 Final date in calculation	 /TYP=D/REQ
	    			 range   			 /MECH=VAL

	   	 . incfnl	 Flag indicating whether or not	 /TYP=L/NOREQ
	    			 final date should be included	 /MECH=VAL

	  	 . bal 		 Principal amount 		 /TYP=N/REQ
	        						 /MECH=VAL

	   	. iacm 		 Interest accrual method 	 /TYP=T/REQ
	    			 TABLE: STBLIACM  		 /MECH=VAL

	  	. irn 		 Interest rate  		 /TYP=N/REQ
	        						 /MECH=VAL

	  	 The following parameters are required if the first position
	  	 of IACM is equal to "0".  This is because the "standard
	  	 period" methods calculate an interest amount for the full
	  	 period and then multiply by the fraction of the period
	  	 elapsed.

	  	 . intfre	 Interest frequency 		 /TYP=F/NOREQ
	        						 /MECH=VAL

	  	 . nxtintdt	 Next interest date 		 /TYP=D/NOREQ
	        						 /MECH=VAL

	  	 . lstintdt	 Last interest date 		 /TYP=D/NOREQ
	        						 /MECH=VAL


	   RETURNS:
	  	 . $$ 		 Error message  		 /TYP=T
	    			 Null = No error

	 	 . intamt	 Interest amount

	   RELATED:
	  	 . $$^PBSMRPC - MRPC Service Class Driver


	  ---- Revision History ------------------------------------------------
	01/03/03 - GRAY - 51349
		   Corrected %GTM-E-LABELUNKNOWN,VERINP+18^MRPC046 error from
		   occuring.  The linetag BTCHEDT was removed from the 
		   routine UFRE.  The call was changed to call into EDT
		   instead.
	
	04/15/02 - Srivastavan - 49794
		   Converted to PSL
		
	*/
	new ER,int,RM

	// Version number of client message is not compatible with server
	if $G(versn)'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))

	set intfre=$G(intfre)
	set nxtintdt=$G(nxtintdt)
	set lstintdt=$G(lstintdt)

	// Verify input received from client
	set ER=0 do VERINP quit:ER $$ERRMSG^PBSUTL($G(RM),$G(ET))

	// Change final date is client instruction is to exclude
	if '$G(incfnl) set todat=todat-1

	set int=$$^UIC(bal,frdat,todat,iacm,irn,0,,intfre,,nxtintdt,lstintdt)
	if 'ER set int=$$^SCARND(int,,,,5)

	if ER quit $$ERRMSG^PBSUTL($G(RM),$G(ET))
	set return=$$V2LV^MSG(int,"",0)
	quit ""


VERINP	// Verify input data

	set ER=0

	// If first digit of IACM is equal to 0, interest frequency and last/next
	// dates are required.
	if '$E(iacm) do { quit:ER

		if intfre=""!(nxtintdt="")!(lstintdt="") do { quit

			set ER=1
			
			// Response required with int/div method of ~p1
			set RM=$$^MSG(2402,iacm)
			}

		// Verify frequency syntax
		set X=intfre,JD=lstintdt
		do EDT^UFRE
		if ER do {

			kill ET
			new jd 
			set jd=$$DAT^%ZM(lstintdt)

			// Frequency of "~p1" is invalid with beginning date ~p2
			set RM=$$^MSG(1613,intfre,jd)
			}
		}

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43585^Sanchez SCM Administrator^3021"	// Signature - LTD^TIME^USER^SIZE
