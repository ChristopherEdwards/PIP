EXPCIFH	
	/* ------------------------- Revision History --------------------------

	09/14/05 - KumarSS - CR 16667
		   Removed expos from argument list, since calling programs 
		   do not pass in expos object and instead created it within
		   the UPDCIFH linetag.
		   Passed EXPOS record object as parameter.
	*/

	quit


	//---------------------------------------------------------------------
public UPDCIFH(RecordEXPOS expos,	// EXPOS record object
               Number OLDCOMEXP,	// Old Committed Exposure
               Number OLDOUTEXP,	// Old Outstanding Exposure
               String OLDCRCD,		// Old Currency Code
               Date OLDMDT,		// Old Maturity Date
               Number %ProcessMode)	// Process Mode
        //---------------------------------------------------------------------
        
       	// File an entry into the customer history file.

	type public Number RECID
	type Number SEQH
	type String TCMT
	
	type ResultSet rs=Db.select("SEQ","CIFH","ACN=:expos.acn","SEQ DESC") 
	if rs.isEmpty() set SEQH=1
	else  set SEQH=rs.next(),SEQH=rs.getCol("SEQ")+1

	// Create Exposure Record
	if %ProcessMode=0 do {

		// Exposure record created
		set TCMT=$$^MSG(2816)_" RECID "_RECID
					
		do FILECIFH(expos.acn,SEQH,TCMT)
		}

	// Delete Exposure Record
	if %ProcessMode=3 do {

		// Exposure record deleted
		set TCMT=$$^MSG(2818)_" RECID "_RECID

		do FILECIFH(expos.acn,SEQH,TCMT)
		}

	// Modify exposure record
	if %ProcessMode=1 do {

		if (OLDCOMEXP'=expos.comexp) do {

			// Changed ~p1 from ~p2 to ~p3
			set TCMT=$$^MSG(3014,"EXPOS.COMEXP",OLDCOMEXP,expos.comexp)
			
			do FILECIFH(expos.acn,SEQH,TCMT)
			set SEQH=SEQH+1
			}

		if (OLDOUTEXP'=expos.outexp) do {

			// Changed ~p1 from ~p2 to ~p3
			set TCMT=$$^MSG(3014,"EXPOS.OUTEXP",OLDOUTEXP,expos.outexp)
			
			do FILECIFH(expos.acn,SEQH,TCMT)
			set SEQH=SEQH+1
			}

		if (OLDCRCD'=expos.crcd) do {

			if OLDCRCD="" set OLDCRCD="NULL"
			
			// Changed ~p1 from ~p2 to ~p3
			set TCMT=$$^MSG(3014,"EXPOS.CRCD",OLDCRCD,expos.crcd)
			
			do FILECIFH(expos.acn,SEQH,TCMT)
			set SEQH=SEQH+1
			}

		if (OLDMDT'=expos.mdt) do {

			if OLDMDT="" set OLDMDT="NULL"

			// Changed ~p1 from ~p2 to ~p3
			set TCMT=$$^MSG(3014,"EXPOS.MDT",OLDMDT,expos.mdt)
			
			do FILECIFH(expos.acn,SEQH,TCMT)
			}
		}

	quit


	//---------------------------------------------------------------------
FILECIFH(Number ACN,			// Customer Number
         Number SEQH,			// Sequence Number
         String TCMT)			// Transaction Comment
	//---------------------------------------------------------------------

	type RecordCIFH cifh=Class.new("RecordCIFH")
	
	set cifh.acn=ACN
	set cifh.seq=SEQH
	set cifh.tcmt=TCMT
	
	do cifh.save()
	quit

vSIG()	quit "60186^21761^Sudanthiran S. Kumar^2603"	// Signature - LTD^TIME^USER^SIZE
