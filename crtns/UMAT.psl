UMAT(RecordDEP dep,RecordTTX ttx)
 	/*
 	 PROCEDURE ID:	UMAT
 	     Original:	Allan Mattson
		 Date:	10/2/2002 - 3:33:15 PM
 	         Desc:	Maturing Time Deposit

	---- Comments --------------------------------------------------------

	ARGUMENTS:
		. dep	Deposit account object		TYP=RecordDEP/REQ
		. ttx	Currently processed transaction	TYP=RecordTTX/REQ
						
	INPUT: 
		. PRM		Penalty Reduction Method	TYP=N
		. ADJACR	Accrual adjustment (RECALC)	TYP=N
		. ADJAWT	Accrued Withholding Tax)	TYP=N
		. NTAX		
		. TAXRATE

	OUTPUT: 
		. ADJ  Accrual adjustment (grace period)	TYP=N
		. ADJW Withholding tax accrual adjustment (grace period) TYP=N
		. BWA  Backup withholding			TYP=N
		. INT  Interest amount				TYP=N
		. PEN  Penalty amount				TYP=N

	----- Revision History ----------------------------------------------
	
	06/20/06 - SANTHUMS - CR 21652
		   When renewal Grace Period is in effect both if and else 
		   statement are getting executed. So Modified UMAT section
		   to add structured do for if statement. 
	
	06/05/06 - SANTHUMS - CR 21314
	  	   Passed PRIN as a new parameter to ^DEPPEN procedure.
                   
	10/3/2002 - Vitaliy Antonov - 43583
		Converted To PSL.

	03/05/02 - MATTSONA - 45067
		Set PRM=1 in section INIT if an account is in the Renewal
		Grace Period so that Backup Withholding Amount will be
		calculated correctly.  Also put an Else in front of line
		calling to ^DEPPEN so that penalty is not calculated during
		the Renewal Grace Period.
		Previously, if an account had Backup Withholding and a
		closeout transaction was done during the Renewal Grace
		Period, Backup Withholding would not be calculated.

	1/09/02  - SCHWARTZA - 46105:2
		AWTI was set to the correct piece.
	
	-----------------------------------------------------------------------
	*/

	new ACR,AWT,IRN,TAXRATE

	set IRN=dep.irn

	// Penalty Reduction Method
	if $G(PRM)="" set PRM=dep.prm
	if PRM="" set PRM=1

	/* Only set ADJACR to zero if it is not already set. EFD transactions
	call RECALC first and ADJACR will be set if there is an adjustment.
	GOPT adjustment is the grace period calculated adjustment plus
	adjustment from recalc. */
	if '$G(ADJACR) set ADJACR=0
	if '$G(ADJAWT) set ADJAWT=0

	/* INT represent the interest paid as of the effective date. 
	RECALC adjusts the account */
	set INT=$$^SCARND(dep.posacr,0,dep.cid)

	set (ADJ,ADJW,BWA,PEN)=0

	/* Moved calculation of backup withholding (tax) amount from next line
	to after PEN is calculated.  BWA is calculated on interest net of 
	penalty. */
	set TYPE=dep.type
	type RecordPRODCTL prodctl=Db.getRecord("PRODCTL","TYPE=:TYPE",1)
	set GRCDYS=prodctl.grcdys
	if GRCDYS="" set GRCDYS=CUVAR.GRACE

	if $G(%EffectiveDate)="" set %EffectiveDate=%SystemDate

	/* Renewal Grace Period in effect
	Reset PRM so it is in effect ignored (so BWA calculates correctly) */
	if dep.dlr,dep.dlr+GRCDYS+1>%EffectiveDate do {
		set PRM=1 
		do GRACE(.dep)
		}
	// Notice Account Flag
	else  if dep.mdt>%EffectiveDate!(dep.notice) do ^DEPPEN(.dep,.ttx,PRIN)

	if PEN do {
		if (PEN>(INT+ADJ)),((PEN-INT-ADJ)'<.01) quit 
		if (PEN<(INT+ADJ)),((INT+ADJ-PEN)'<.01) quit 

		/* If the difference between interest and penalty is less
		than 3, the penalty was designed to wipe out the accrual.
		Therefore set penalty equal to accrual plus adjustment. */
		set PEN=$$^SCARND((INT+$G(ADJ)),0,dep.cid)
		}

	/* Only call account backup withholding routine if not an IRA.
	Set int equal to INT less the adjustment to ensure that the
	withholding amount is calculated correctly. */

	// Backup Withholding
	if INT,'dep.ira,dep.bwf do {
		if PRM=1 do {
			if dep.awtp set BWA=dep.awt
			else  do ^DEPBW(.dep,.ttx,INT,.BWA,.NTAX,CUVAR.BWAPGM,dep.awti,IRN,.TAXRATE)
			}
		if PRM>1 do {
			if dep.awtp set BWA=dep.awt-$G(ADJW)
			else  do {
				new CPEN,INT1
				set CPEN=$E($G(CTL),2)
				if 'CPEN,$G(CTL) set INT1=INT+$G(ADJ) quit:'INT1
				else  set INT1=INT-PEN+$G(ADJ) quit:'INT1
				if INT1'>0 set INT1=0 quit
				do ^DEPBW(.dep,.ttx,INT1,.BWA,.NTAX,CUVAR.BWAPGM,dep.awti,IRN,.TAXRATE)
				}
			}
		}
	set BWA=$$^SCARND(BWA,0,dep.cid)

	quit


GRACE(RecordDEP dep)	// Grace processing
	// Adjust accrued according to Grace Period Proc Option

	// 0 = No Adjustment to Accrued
	if 'dep.gopt quit

	set IACM=dep.iacm set IRCB=dep.ircb

	// 1 = W/D Amount Accrues at Previous Rate - Adjust Accrued
	if dep.gopt=1 do GOPT1(.dep,.ADJ,.ADJW) quit
	// 2 = W/D Amount Accrues at 0.00% - Adjust Accrued
	if dep.gopt=2 do GOPT2(.dep,.ADJ,.ADJW) quit
	// 3 = W/D Amount Accrues at Passbook Rate - Adjust Accrued
	if dep.gopt=3 do GOPT3(.dep,.ADJ,.ADJW) quit

	quit


GOPT1(RecordDEP dep,ADJ,ADJW) 
	/* W/D amt accrues from DLR to W/D date @ IRLM
	Grace Period Proc Option = 
	1 - 'W/D Amount Accrues at Previous Rate - Adjust Accrued'

	Arguments:
		. dep	Deposit account object	TYP=RecordDEP/REQ
		. ADJ	Accrual adjustment (grace period)	
						TYP=N/MECH=REFVAL/REQ
		. ADJW	Withholding tax accrual adjustment (grace period)
						TYP=N/MECH=REFVAL/REQ
	*/

	// Interest Rate At Last Maturity
	if dep.irn-dep.irlm=0 quit

	// Calculate accrued - current rate
	set IRN=dep.irn

	do GRACE^DEPAES(.dep,.ACR)

	set (ACR(1),ADJ)=ACR
	set (AWT(1),ADJW)=dep.awt

	// Calculate accrued - rate at last maturity
	set IRN=dep.irlm
	do GRACE^DEPAES(.dep,.ACR)

	set ADJ=$$^SCARND(ACR-ADJ,0,dep.cid)
	set ADJ=ADJ+$$^SCARND(-ADJACR,0,dep.cid)

	set ADJW=$$^SCARND(dep.awt-ADJW,0,dep.cid)
	set ADJW=ADJW+$$^SCARND(-ADJAWT,0,dep.cid)

	quit


GOPT2(RecordDEP dep,ADJ,ADJW)	
	/* W/D amt accrues zero from DLR to W/D date
	Grace Period Proc Option = 
	2 - "W/D Amount Accrues at 0.00% - Adjust Accrued" */

	set IRN=+dep.irn
	do GRACE^DEPAES(.dep,.ACR)

	set IRN=0

	set (ACR(1),ADJ)=$$^SCARND(ACR,0,dep.cid)
	set ADJ=-ADJ
	set ADJ=ADJ+$$^SCARND(-ADJACR,0,dep.cid)

	set (AWT(1),ADJW)=$$^SCARND(dep.awt,0,dep.cid)
	set ADJW=-ADJW
	set ADJW=ADJW+$$^SCARND(-ADJAWT,0,dep.cid)

	quit


GOPT3(RecordDEP dep,ADJ,ADJW) 
	/* W/D amt accrues at passbook rate
	Grace Period Proc Option = 
	3 - "W/D Amount Accrues at Passbook Rate - Adjust Accrued" */

	if dep.irn-CUVAR.PBKIRN=0 quit

	set IRN=dep.irn
	do GRACE^DEPAES(.dep,.ACR)

	set (ACR(1),ADJ)=ACR
	set (AWT(1),ADJW)=dep.awt

	set IRN=CUVAR.PBKIRN
	do GRACE^DEPAES(.dep,.ACR)

	set ADJ=$$^SCARND(ACR-ADJ,0,dep.cid)
	set ADJ=ADJ+$$^SCARND(-ADJACR,0,2)

	set ADJW=$$^SCARND(dep.awt-ADJW,0,dep.cid)
	set ADJW=ADJW+$$^SCARND(-ADJAWT,0,2)

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60436^22339^Sunitha Santhumayor^6171"	// Signature - LTD^TIME^USER^SIZE
