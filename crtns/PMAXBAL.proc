PMAXBAL	 // 
 // ORIG: SKLYUTD - 10/12/1999
 // DESC: Previous Largest Loan Balance
 // 
 // ---- Comments --------------------------------------------------------
 // 
 // ---- Revision History ------------------------------------------------
 // 
 // 07/06/00 - KESTELMANN - 40971
 //            Added code to quit if loan modeler is running.
 //
 // dummy quit for compiler
 quit

EXEC(ACN,CID,NEWHABL1)	// process the checks
 if $g(LNMDLR)=1 quit   // ARQ 40971
 new HABL1,PMAXBAL,XCID,XXCID,XHABL1,stop,PCID,NCID,prev,next
 set ACN=$get(ACN),CID=$get(CID),NEWHABL1=$get(NEWHABL1)
 set stop=0
 if (ACN="")!(CID="")!(NEWHABL1="") quit

 new cif
 type RecordCIF cif
 set cif=Db.getRecord("CIF","ACN")
 set PMAXBAL=cif.pmaxbal

 if PMAXBAL=NEWHABL1 quit	// no change is needed.
 // new balance is more - update and quit
 if PMAXBAL<NEWHABL1 set cif.pmaxbal=NEWHABL1 do cif.bypassSave() quit
 
 new rs,habl
 type ResultSet rs
 set rs=Db.select("CID,HABL1","LN","ACN=:ACN")

 if rs.isEmpty() quit
 for  quit:'rs.next()  do {
	set HABL1=rs.getCol(2)
	if HABL1="" set HABL1=0
	set XCID=rs.getCol(1)
	set habl(HABL1,XCID)=HABL1
 }
 
 set HABL1="" 
 for  set HABL1=$o(habl(HABL1),-1) quit:(HABL1="")!(stop=1)  do {
	set XCID=""
	for  set XCID=$o(habl(HABL1,XCID),-1) quit:(XCID="")!(stop=1)  do {
		
		// Only decrease PMAXBAL if the account with the
        	// highest HABL1 is the account that is being processed
        	// and the amount of the new HABL1 is less then before. 
		if (XCID=CID)&(HABL1>NEWHABL1)&(PMAXBAL=HABL1) do {

			set XHABL1=""
		// Check if accounts with the same HABL1 exist.
			set PCID=$o(habl(HABL1,XCID),-1) // previous
			set prev=$d(habl(HABL1,PCID))    
			set NCID=$o(habl(HABL1,XCID))	 // next accts	
			set next=$d(habl(HABL1,NCID))		
			if ('prev),('next) set XHABL1=$o(habl(HABL1),-1) 
			else  set XHABL1=HABL1

			if NEWHABL1'<XHABL1 set PMAXBAL=NEWHABL1,stop=1
			else  set PMAXBAL=XHABL1,stop=1
		}
	}
 }
 if stop=1 set cif.pmaxbal=PMAXBAL do cif.bypassSave() quit
 quit

vSIG()	quit "60215^67264^Irina Kin^1930"	// Signature - LTD^TIME^USER^SIZE
