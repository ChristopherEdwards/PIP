LNPPC3	//

	/*
	Post processors to verify secondary frequencies

	   ORIG:  Neal E. Gorman (5053) - 09/06/88

	   NOTE:  New calls to this routine should make use of the *F line
	          tags and parameter passing.  The older line tags should
	          be phased out over time.

	---- Revision History ------------------------------------------------

	05/26/04 - KELLYP - CR 8446
		   Fixed logic error in FREVERF section to prevent undefined 
		   error from occurring.

	02/13/02 - ARCILLAZ - 43583
		   Converted to PSL. Removed old revision history.

	*/
	quit

Public CHK(RecordLN ln)	// Mass check - called by LNNEW

	// If ER=2 then the error is serious enough to terminate loan create.
	do INIT1(.ln)

	// Distribution 1 Next Due
	set X=ln.intfre if $L(X) do INTFRE(.ln,ln.dist1nd) quit:ER

	// Distribution 1 Next Due
	set X=ln.ichnd if $L(X) do ICHND(.ln,ln.dist1nd) quit:ER

	// Distribution 1 Next Due
	set X=ln.pcfre if $L(X) do PCFRE(.ln,ln.dist1nd) quit:ER

	// Distribution 1 Next Due
	set X=ln.pchnd if $L(X) do PCHND(.ln,ln.dist1nd) quit:ER

	// P&I Payment Change Next Date
	set X=ln.rafre if $L(X) do RAFRE(.ln,ln.pchnd) quit:ER

	// P&I Payment Change Next Date
	set X=ln.rand if $L(X) do RAND(.ln,ln.pchnd) quit:ER
	quit


INTFRE(RecordLN ln,D1)	//

	do INIT1(.ln)
	set D1=$G(D1)

	// Int/Div Index
	if $L(X),'$L(ln.index) set ET="LNPPC3A" set ER=2 do ^UTLERR quit

	// Check that dates correspond only if PCM 2 (Interest Calculation) is 1
	// (Standard Formula Using IRN)
	if $E(PCM,2)'=1 quit

	// interest change
	set P2=$$^MSG(4208)

	// Distribution 1 Frequency
	set F1=ln.dist1fre
	do FREVER(.ln)
	quit


Public INTFREF(X,D1,pcmval,F1,schld)	//Validate interest freq against master

	/*
	   ARGUMENTS:
	   X		LN.INTFRE	/REQ
	   D1		LN.DIST1ND	/REQ
	   pcmval	LN.PCMVAL	/REQ
	   F1		LN.DIST1FRE	/REQ
	   schld	LN.SCHLD 	/NOREQ
	Pass only if variable CPF was defined in caller

	Check that dates correspond only if PCM 2 (Interest Calculation) is 1
	(Standard Formula Using IRN)
	*/

	// Int calc = accrual, dates needn't correspond
	quit:$E(pcmval,2)'=1

	// Interest/Dividend change
	set P2=$$^MSG(4208)
	do FREVERF($G(schld))
	quit


PCFRE(RecordLN ln,D1)	//

	set D1=$G(D1)
	if '$E(PCM),$L(X) set ER=2 set ET="NPINAPF" do ^UTLERR quit

	// payment change
	set P1="" 
	set P2=$$^MSG(4210)

	// Distribution 1 Frequency
	set F1=ln.dist1fre

	// Compare to master payment frequency
	do FREVER(.ln) quit:ER

	// GEM's and IEM's
	if ln.pchm>1 quit

	// Int/Div Change - Next Date
	set F1=ln.intfre 
	set D1=ln.ichnd
	
	// Compare to int change freq
	do FREVER(.ln) quit:'ER
	set ET="PCCFR2" do ^UTLERR
	quit


Public PCFREF(X,D1,F1,pchm,intfre,ichnd,schld)	// Validate PCFRE to master

	/*
	   Validate payment change frequency against master

	ARGUMENTS:
	   X		LN.PCFRE	/REQ
	   D1		LN.DIST1ND	/REQ
	   F1		LN.DIST1FRE	/REQ
	   pchm		LN.PCHM		/REQ
	   intfre	LN.INTFRE	/REQ
	   ichnd	LN.ICHND	/REQ
	   schld	LN.SCHLD	/NOREQ
	   Pass only if variable CPF was defined in caller

	*/

	// payment change
	set P1="" 
	set P2=$$^MSG(4210)

	// Compare to master pmt freq
	do FREVERF($G(schld)) quit:ER

	// GEM's and IEM's
	if pchm>1 quit
	set F1=intfre 
	set D1=ichnd

	// Compare to int change freq
	do FREVERF($G(schld))

	// Payment change dates do not correspond to interest change dates
	if ER set ET="PCCFR2" do ^UTLERR
	quit


RAFRE(RecordLN ln,D1)	//

	do INIT1(.ln)
	set D1=$G(D1)
	if '$E(PCM),$L(X) set ER=2 set ET="NPINARF" do ^UTLERR quit

	// re-amortization
	set P1="" 
	set P2=$$^MSG(4211)

	// Distribution 1 Frequency
	set F1=ln.dist1fre

	// Compare to master payment frequency
	do FREVER(.ln) quit:ER

	// payment change
	set F1=ln.pcfre 
	set D1=ln.pchnd 
	set P1=$$^MSG(4210)

	// Compare to int change freq
	do FREVER(.ln) quit:'ER
	set ET="PCCFR2" do ^UTLERR
	quit


Public RAFREF(X,D1,F1,pcfre,pchnd,schld)	// Validate RAFRE to master

	/*
	   Validate re-amortization frequency against master

	ARGUMENTS:
	   X		LN.RAFRE 	/REQ
	   D1		LN.DIST1ND 	/REQ
	   F1		LN.DIST1FRE	/REQ
	   pcfre	LN.PCFRE	/REQ
	   pchnd	LN.PCHND 	/REQ
	   schld	LN.SCHLD	/NOREQ
	Pass only if variable CPF was defined in caller

	*/

	// re-amortization
	set P1="" 
	set P2=$$^MSG(4211)

	// Compare to master pmt freq
	do FREVERF($G(schld)) quit:ER
	set F1=pcfre 
	set D1=pchnd

	// payment change
	set P1=$$^MSG(4210)

	// Compare to int change freq
	do FREVERF($G(schld))

	// Payment change dates do not correspond to interest change dates
	if ER set ET="PCCFR2" do ^UTLERR
	quit


Public D2FRE(RecordLN ln,D1)	//

	set D1=$G(D1)

	// 2nd payment
	set P2=$$^MSG(4205)

	// Distribution 1 Frequency
	set F1=ln.dist1fre

	// Payment schedule in use
	if F1="*" quit
	if X="*" set ET="LNUCPF3" set ER=1 do ^UTLERR quit
	do FREVER(.ln)
	quit


Public D3FRE(RecordLN ln,D1)	//

	set D1=$G(D1)

	// 3rd payment
	set P2=$$^MSG(4206)

	// Distribution 1 Frequency
	set F1=ln.dist1fre

	// Payment schedule in use
	if F1="*" quit
	if X="*" set ET="LNUCPF3" set ER=1 do ^UTLERR quit
	do FREVER(.ln)
	quit


Public D4FRE(RecordLN ln,D1)	//

	set D1=$G(D1)

	// 4th payment
	set P2=$$^MSG(4207)

	// Distribution 1 Frequency
	set F1=ln.dist1fre

	// Payment schedule in use
	if F1="*" quit
	if X="*" set ET="LNUCPF3" set ER=1 do ^UTLERR quit
	do FREVER(.ln)
	quit


ICHND(RecordLN ln,D1)	//

	do INIT1(.ln)
	set D1=$G(D1)

	// Check that dates correspond only if PCM 2 (Interest Calculation) is 1
	// (Standard Formula Using IRN)
	if $E(PCM,2)'=1 quit

	// interest change
	set P2=$$^MSG(4208)

	// Distribution 1 Frequency
	set F1=ln.dist1fre

	// Int/Div Check Frequency
	set F2=ln.intfre
	do NDVER

	quit


ICHNDF(X,D1,pcmval,F1,F2)	// Validate interest change date against master

	/*
	ARGUMENTS:
	   X		LN.ICHND 	/REQ
	   D1		LN.DIST1ND	/REQ
	   pcmval	LN.PCMVAL 	/REQ
	   F1		LN.DIST1FRE 	/REQ
	   F2		LN.INTFRE 	/REQ

	   Check that dates correspond only if PCM 2 (Interest Calculation) is 1
	   (Standard Formula Using IRN)
	*/

	// Int calc = accrual, dates needn't correspond
	quit:$E(pcmval,2)'=1

	// Interest/Dividend change
	set P2=$$^MSG(4208)
	do NDVERF
	quit


PCHND(RecordLN ln,D1)	//

	do INIT1(.ln)
	set D1=$G(D1)
	if '$E(PCM),$L(X) set ER=2 set ET="NPINAPD" do ^UTLERR quit

	// interest change
	set P1=$$^MSG(4208) 

	// payment change
	set P2=$$^MSG(4210)

	// Payment Change Frequency
	set F1=ln.intfre 
	set F2=ln.pcfre

	// Int/Div Change - Next Date
	set D1=$S($L(ln.ichnd):ln.ichnd,1:D1)

	// Distribution 1 Frequency
	if ln.pchm>1 set D1=ln.dist1nd set F1=ln.dist1fre set P1=""
	do NDVER
	quit


Public PCHNDF(X,D1,F1,F2,ichnd,dist1fre,pchm)	// Validate pmt chg date to master

	/*
	ARGUMENTS:
	    X		LN.PCHND 	/REQ
	    D1		LN.DIST1ND	/REQ
	    F1		LN.INTFRE	/REQ
	    F2		LN.PCFRE	/REQ
	    ichnd 	LN.ICHNG	/REQ
	    dist1fre	LN.DIST1FRE	/REQ
	    pchm	LN.PCHM		/REQ
	*/

	// Interest/Dividend change
	set P1=$$^MSG(4208)

	// payment change
	set P2=$$^MSG(4210)
	if ichnd'="",pchm'>1 set D1=ichnd
	if pchm>1 set F1=dist1fre set P1=""
	do NDVERF
	quit


RAND(RecordLN ln,D1)	//

	do INIT1(.ln)
	set D1=$G(D1)
	if '$E(PCM),$L(X) set ER=2 set ET="NPINARD" do ^UTLERR quit

	// interest change
	set P1=$$^MSG(4208) 

	// re-amortization
	set P2=$$^MSG(4211)

	// Re-amortization Frequency
	set F1=ln.intfre 
	set F2=ln.rafre

	// Int/Div Change - Next Date
	set D1=$S($L(ln.ichnd):ln.ichnd,1:D1)

	// Distribution 1 Frequency
	if ln.pchm>1 set D1=ln.dist1nd set F1=ln.dist1fre set P1=""
	do NDVER
	quit


Public RANDF(X,D1,F1,F2,ichnd,dist1fre,pchm)	// Validate re-amort date to master

	/*
	ARGUMENTS:
	    X		LN.PCHND	/REQ
	    D1		LN.DIST1ND	/REQ
	    F1		LN.INTFRE	/REQ	
	    F2		LN.RAFRE	/REQ
	    ichnd	LN.ICHNG	/REQ
	    dist1fre	LN.DIST1FRE	/REQ
	    pchm	LN.PCHM		/REQ
	*/

	// Interest/Dividend change
	set P1=$$^MSG(4208)

	// re-amortization
	set P2=$$^MSG(4211)
	if ichnd'="",pchm'>1 set D1=ichnd
	if pchm>1 set F1=dist1fre set P1=""
	do NDVERF
	quit


Public D2ND(RecordLN ln,D1)	//

	set D1=$G(D1)
	do DISTND(.ln) quit:ER

	// 2nd payment
	set P2=$$^MSG(4205)

	// Distribution 1 Frequency
	set F1=ln.dist1fre quit:F1="*"

	// Distribution 2 Frequency
	set F2=ln.dist2fre
	do NDVER
	quit


Public D3ND(RecordLN ln,D1)	//

	set D1=$G(D1)
	do DISTND(.ln) quit:ER

	// 3rd payment
	set P2=$$^MSG(4206)

	// Distribution 1 Frequency
	set F1=ln.dist1fre quit:F1="*"

	// Distribution 3 - Frequency
	set F2=ln.dist3fre
	do NDVER
	quit


Public D4ND(RecordLN ln,D1)	//

	set D1=$G(D1)
	do DISTND(.ln) quit:ER

	// 4th payment
	set P2=$$^MSG(4207)

	// Distribution 1 Frequency
	set F1=ln.dist1fre quit:F1="*"

	// Distribution 4 - Frequency
	set F2=ln.dist4fre
	do NDVER
	quit


INIT1(RecordLN ln)	// Initialize variables if not previously defined

	// Payment Calculation Method
	Type RecordSTBLPCM stblpcm=Db.getRecord("STBLPCM","KEY=:ln.pcm")
	set PCM=stblpcm.pcmp
	quit


DISTND(RecordLN ln)	// Verify that next date is not less than [LN]DFP

	if X="" quit
	set %DS=X 
	set %JD=$$^SCAJD(%DS) quit:ER  
	set X=%JD

	// Date of First Payment
	if X<ln.dfp set ER=1 set ET="DUEDFP" do ^UTLERR quit

	// check that date does not exceed MDT
	set MDT=ln.mdt if 'ln.mdt do CMDT^LNPPC1(.ln)
	if ln.mdt,(X>ln.mdt) set ER=1 set ET="DUEMDT" do ^UTLERR quit
	quit


FREVER(RecordLN ln)	// Secondary frequency verifications

	// NOTE:  FREVERF has similar code to support *F line tags
	quit:'$L(X)  do EDT^UFRE quit:ER

	// No master payment frequency
	if '$L(F1) quit
	set NGFRE="-"_F1
	if $G(CPF) set NJD=$$NJD^UFRE(ln.schld,NGFRE) quit:ER
	set D1=NJD

	// master payment
	if $G(P1)="" set P1=$$^MSG(4209)
	do VER^LNUSFV(D1,F1,P1,"",X,P2)
	quit


FREVERF(schld)	//Private; Secondary frequency verifications for *F line tags

	/*
	   Similar code to FREVER above, but without reference to CPF and LN(58)
	   Does not re-check X -- assumed valided already by data entry

	*/

	type String NGFRE
	type Date NJD
	type Public String ER,F1,P1,P2,X
	type Public Date D1

	// No master payment frequency
	if '$L(F1) quit
	set NGFRE="-"_F1
	
	// CR 8446 - Set D1=NJD only if schld is defined
	if $G(schld) do { quit:ER
		set NJD=$$NJD^UFRE(schld,NGFRE) quit:ER
		set D1=NJD
		}

	// master payment
	if $G(P1)="" set P1=$$^MSG(4209)
	do VER^LNUSFV(D1,F1,P1,"",X,P2)
	quit


NDVER	// Next date verifications

	// NOTE:  NDVERF has similar code to support *F line tags
	quit:'$L(X)  
	set %DS=X 
	set %JD=$$^SCAJD(%DS) quit:%JD<0

	if '$L(F2) set F2=F1

	// master payment
	if $G(P1)="" set P1=$$^MSG(4209)
	do VER^LNUSFV(D1,F1,P1,%JD,F2,P2)
	quit


NDVERF	//Private; Next date verifications for *F line tags

	// Similar code to NDVER above

	new %DS,%JD
	quit:'$L(X)  set %DS=X set %JD=$$^SCAJD(%DS) quit:%JD<0

	if '$L(F2) set F2=F1
	set NGFRE="-"_F1

	// master payment
	if $G(P1)="" set P1=$$^MSG(4209)
	do VER^LNUSFV(D1,F1,P1,%JD,F2,P2)
	quit


Public PCVER(RecordLN ln)	// Verify that non-P+I loans do not have payment change data.

	do INIT1(.ln)
	if $E(PCM) quit

	// Payment Change Frequency
	if $L(ln.pcfre) set ER=2 set ET="NPINAPF" quit

	// P&I Payment Change Next Date
	if $L(ln.pchnd) set ER=2 set ET="NPINAPD" quit

	// P&I Payment Change Date Offset
	if $L(ln.pcoff) set ER=2 set ET="NPINAPO" quit

	// Re-amortization Frequency
	if $L(ln.rafre) set ER=2 set ET="NPINARF" quit

	// Re-amortization - Next Date
	if $L(ln.rand) set ER=2 set ET="NPINARD" quit
	quit

vSIG()	quit "59886^43540^Sanchez SCM Administrator^10719"	// Signature - LTD^TIME^USER^SIZE
