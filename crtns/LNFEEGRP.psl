LNFEEGRP	//;PBS -  - V2.0 - Loan Fee Group Maintenance
	/*
	       ORIG:
	  ---- Revision History ------------------------------------------------
	   09/27/02 - TELIV - 49451
		      Converted to PSL 	

	  ---------------------------------------------------------------------
	*/


COPY	//

	do INIT(5)
	quit


INIT(%ProcessMode)	//

	new OLNTB,VFMQ

	set %PG=0 
	set %PAGE=$S(%ProcessMode=5:0,1:1)

	do VPG00 

	if ER=1!(VFMQ="Q") quit
	
	if "DFQ"[VFMQ do VER 

	quit


VPG00	// Set up

	new %TAB
	set %TAB("IO")=$$IO^SCATAB($I)
	set %TAB("FEEGRP")=".LNFEEGRP/TBL=[UTBLLNFEEGRP]"

	if %ProcessMode=5 set %TAB("FEECPY")=".LNFEEGRP1/TBL=[UTBLLNFEEGRP]/XPP=D PPFEE^LNFEEGRP"
	set %READ="@@%FN,,,FEEGRP/REQ"

	if %ProcessMode=5 set %READ=%READ_",FEECPY/REQ"

	do ^UTLREAD 

	if VFMQ="Q" quit

	quit


PPFEE	// Fee group post-processor

	if X="" quit

	if %OSAVE'=0,%OSAVE'=5 set ER=1 quit

	// Record already exists
	if Db.isDefined("UTBLLNFEEGRP","FEEGRP=:X") do { quit
			set ER=1 
			set RM=$$^MSG(2327) 
			}

	set I(3)=""
	quit


VER	// Verification

	if VFMQ="Q" do END quit
	do FILE

	do END

	quit


FILE	// File data
	
	new FEETYP
	set FEETYP=""

	if %ProcessMode=5 do {
		type RecordUTBLLNFEEGRP ufeegrpt=Db.getRecord("UTBLLNFEEGRP","FEEGRP=:FEEGRP")		
		type RecordUTBLLNFEEGRP ufeegrp=Db.getRecord("UTBLLNFEEGRP","FEEGRP=:FEECPY",1)

		do ufeegrp.setAuditFlag(1)

		set ufeegrp.desc=ufeegrpt.desc

		do ufeegrp.save()
		
		type ResultSet rs=Db.select("FEETYP,DESC","LNFEEGRP1","FEEGRP=:FEEGRP")
		while rs.next() do {
			set FEETYP=rs.getCol("FEETYP")
			type RecordLNFEEGRP1 ufeegrp1=Db.getRecord("LNFEEGRP1","FEEGRP=:FEECPY,FEETYP=:FEETYP",1)
			do ufeegrp1.setAuditFlag(1)
			set ufeegrp1.desc=rs.getCol("DESC")
			do ufeegrp1.save()
			}
		}

	quit


END	// End of processing

	if ER!(%ProcessMode=2)!(%ProcessMode=4) quit

	if VFMQ="Q" do {

		// Loan fee group ~p1 not copied to ~p2
		set RM=$$^MSG(1632,FEEGRP,FEECPY)
		}
	else  do {

		// Loan fee group ~p1 copied to ~p2
		set RM=$$^MSG(1628,FEEGRP,FEECPY)
		}

	set ER="W"

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43535^Sanchez SCM Administrator^1873"	// Signature - LTD^TIME^USER^SIZE
