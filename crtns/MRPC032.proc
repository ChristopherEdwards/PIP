MRPC032(RETURN,VERSN,CID,FD,TD)	//Public
	/*

	   DESC: Loan Balance Change Summary Report.
	   KEYWORDS: MRPC

	   ARGUMENTS:
	  	 . RETURN Loan balance details  /TYP=T/REQ/MECH=REFNAM:W
	                  in PB5 format

	  	 . VERSN  MRPC032 version number /TYP=N/REQ/MECH=VAL
	                  current version=1

	  	 . CID  Loan Account Number /TYP=N/REQ/MECH=VAL
	  	 . FD           From Date  /TYP=D/REQ/MECH=VAL
	  	 . TD           To Date   /TYP=D/REQ/MECH=VAL

	   RETURNS:
	   	 . $$     Error Message  /TYP=T

	   RELATED:
	         . $$^PBSMRPC - MRPC Service Class Driver

	   EXAMPLE:
	         S RM=$$^MRPC032(.value,1,7000000253,56922,57022)

	  -------Revision History------------------------------------------
	 
	 05/02/06 - KinI - 20978
	 	    Modified to add WIRN parameter to UAVB call. Without this
	 	    "No change activity" message was returned even for 
	 	    qualified accounts.	 	    

	 08/04/03 - CARROLLJ - 51629
		    Modified call to INIT^UAVB to remove second parameter that
		    was passed.

	 04/10/2002 - SRIVASTAVAN - 49794
         	 Converted to PSL

	*/
	
	type public Boolean ER
	type public String ET, RM
	
	type Date date, N, X, Y
	type String rec, RCS(), WIRN()
	type Number I, seq
	
	// Version number of client message is not compatible with server
	if VERSN.get()'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))
	
	type RecordACN acn=Db.getRecord("ACN","CID=:CID",1)
	
	// Invalid account ~p1
	if acn.getMode()=0 quit $$ERRMSG^PBSUTL($$^MSG(1259,CID))
	
	// Invalid Account Class
	if acn.cls'="L" set ET="INVLDCLS" do ^UTLERR quit $$ERRMSG^PBSUTL(RM.get())

	// From Date Error Checking
	do FDERR^UAVB3
	if ER.get() quit $$ERRMSG^PBSUTL(RM.get())

	// To Date Error Checking
	do TDERR^UAVB3
	
	if ER.get() quit $$ERRMSG^PBSUTL(RM.get())

	// Calc change summary
	set N=FD do INIT^UAVB(.acn,CID,FD,TD,1,,,,.WIRN)
	
	if ER.get() quit $$ERRMSG^PBSUTL(RM.get(),ET.get())

	// Remove any duplicate entries from WIRN.  They may have been added
	// back in by ^UAVB0 when it processed the bill record entries.
	set X=""
	set X=WIRN(X).order()
	set Y=X
	for  set X=WIRN(X).order() quit:X.isNull()  do {
		if WIRN(X).piece("|",1)=WIRN(Y).piece("|",1),WIRN(X).piece("|",2)=WIRN(X).piece("|",2) kill WIRN(X)
		else  set Y=X
		}

	set seq=0 
	set date=""

	for  set date=WIRN(date).order() quit:(date.isNull())!(date>TD)  do {
		set seq=seq+1
		set RCS(seq)=date_"|"_WIRN(date).piece("|",1,19)_"|"_WIRN(date).piece("|",21,23)
		}

	// No rate change activity
	if 'seq set ER=1 set RM=$$^MSG(1977) quit $$ERRMSG^PBSUTL(RM.get())

	// Create formatted output string ( col<tab>col<tab>...<CR,LF>...)

	set I="" 
	set rec=""
	for  set I=RCS(I).order() quit:I.isNull()  set rec=rec_RCS(I).translate("|",$C(9))_$C(13,10)

	set RETURN=$$V2LV^MSG(rec)
	
	quit ""
	

vSIG()	quit "60388^63658^Irina Kin^2642"	// Signature - LTD^TIME^USER^SIZE
