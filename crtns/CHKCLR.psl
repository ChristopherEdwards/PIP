CHKCLR	//;CHECK REGISTER CHECK CLEAR FUNCTION
	/*
	       ORIG:  Jill Bulkin (6838) - 05/01/86
	       DESC:  Sets up file with check load information (high/low check #)
	              and check type

	---- Revision History ------------------------------------------------
	  
	02/15/07 - RussellDS - CR25382
		   Eliminated use of obsoleted routine DBSQRYA.
		   
		   Removed old revision history.
	 -----------------------------------------------------------------------
	*/
	
	set %ProcessMode=0 
	do VINIT 
	quit

VINIT	// init variables
	type Boolean FINISH
	
	set (%PG,%PAGE)=1 
	set ER=0
	kill OLNTB,VFMQ
	
	set FINISH=0
	
	for  do { quit:FINISH		
		
		do VPG01 if ER set FINISH=1 quit
	
		if "DFQ"[$G(VFMQ)!$G(ER) do VER set FINISH=1 
	
		}
	
	quit

VPG01	//
	set %TAB("CO")=".CO2/TBL=[UTBLCHKBANK]"
	set %TAB("CKTYP")=".CHKTYP1/TBL=[UTBLCHKS]/XPP=D POS^CHKCLR"

	// Invalid date ~p1
	set %TAB("DATE")=".DATE4/XPP=Q:X=""""  S X=$$^SCAJD(X) I X<0 S ER=1,RM=$$^MSG(1308)"

	set %TAB("CKNO")=".CKNO2/TBL=[CHKREG1]/HLP=[CHKREG1]CKNO/XPP=D EXT^DBSQRY"

	set %READ="@@%FN,,,CO/REQ,CKTYP/REQ,DATE/REQ,CKNO/REQ"
	do ^UTLREAD 
	if VFMQ="Q" quit

	quit


POS	// Check Type Post-Processor

	type public Number GL
	
	if X="" quit
	
	type RecordUTBLCHKS utblchks=Db.getRecord("UTBLCHKS","CHKS=:X")
	set GL=utblchks.cid

	// Type ~p1 has no G/L account in CHKS user table
	if GL="" set ER=1 set RM=$$^MSG(2770,X) quit
	
	type ResultSet chkreg1=Db.select("GL","CHKREG1","CO=:CO AND GL=:GL")

	// No checks in check register
	if chkreg1.isEmpty() set ER=1 set RM=$$^MSG(1912)
		
	quit


VER	// verify and process

	type public Number GL
	type public String CKNO
	
	type Number CKN, STATUS
	type String DQQRY(), WHERE

	if VFMQ="Q"!(ER) do VEXIT quit
	
	set DQQRY(1) = "[CHKREG1]CKNO "_CKNO
	set WHERE = $$WHERE^SQLCONV(.DQQRY(), "CHKREG1")
	set WHERE = "CO=:CO AND GL=:GL AND ("_WHERE_")"

	#ACCEPT Date=02/15/07; Pgm=RussellDS; CR=25356; Group=Dynamic
	type ResultSet rs=Db.select("CKNO","CHKREG1", WHERE)
	while rs.next() do {
		
		set CKN=rs.getCol("CKNO")
		
		type RecordCHKREG1 chkreg1=Db.getRecord("CHKREG1","CO=:CO,GL=:GL,CKNO=:CKN")
		
		// already cleared/voided
		set STATUS=chkreg1.status
		if "12"[STATUS quit
		do FILE(.chkreg1)
	}
	
	do VRPT
	
	quit


VRPT	// report

	set RID="CHKCLR" 
	do ^URID 
	if PGM="" set ET="INVLDRPT" do DSPBP^UTLERR do VEXIT quit
	do ^@PGM
	
	do VEXIT
	
	quit


VEXIT	// Exit function

	if "24"[%ProcessMode!(ER) quit

	set ER="W"

	// Check clear not completed
	if VFMQ="Q" set RM=$$^MSG(522) quit

	// Check clear completed
	set RM=$$^MSG(521)
	quit


FILE(RecordCHKREG1 chkreg1)	// File data

	set chkreg1.status=1
	set chkreg1.clear=DATE
	do chkreg1.bypassSave()
	

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60712^46291^Dan Russell^2525"	// Signature - LTD^TIME^USER^SIZE
