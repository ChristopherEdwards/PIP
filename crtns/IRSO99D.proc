IRSO99D		/*
	ORIG: Vince Arpa 1/23/98
	DESC: IRS Processing - Form 1099-DIV Original
	I18N=QUIT: Excluded from I18N standards.

	---- Comments --------------------------------------------------------
	======================================================================
		*** THIS ROUTINE IS TO BE COMPILED WITH IRSTPMTR ***
		     *** DO NOT RUN THIS ROUTINE STANDALONE ***
	======================================================================
	
	---- Revision History ------------------------------------------------
	
	05/15/06 - DESHPANDE S K - CR 21167
		Modified section CIFAD. Changed the address variables to use 
		mailing address instead of perm address.
		
	09/23/05 - HAILEYM - CR17143
		Converted to PSL.
		
	*/
	
	// Accept "Dead code" warning during runtime of @IRSTAPE.
	#ACCEPT Date=10/14/2005;PGM=Marie Hailey
	quit
	
	
MTRCNTRL	// Master control
	
	type public Boolean ER
	type public String EIN,INSTEIN,PAYERNAM,Q()
	type public Number FAMT(),FCNT
	
	type Number BONDS,REG
	type String BOND,I
	
	set FCNT=""
	for I=1:1:9,"A","B","C","D","E" set FAMT(I)=""
	
	set REG=Q(1).data()
	set BONDS=0
	set BOND=""

	do Db.fastDelete("REP099D","JOB=:%ProcessID")
	do Db.fastDelete("B1099D")
	do ^FRM99DIV

	set INSTEIN=EIN.get()
	if INSTEIN="" set INSTEIN=CUVAR.EIN  
	set PAYERNAM=CUVAR.CNAME

	do ARECBLD quit:ER	// Build and write the "A" record
	do BRECBLD quit:ER	// Build and write the "B" record
	do CRECBLD quit:ER	// Build and write the "C" record
	do STORETOT		// Store the TTR report totals
	
	do Db.fastDelete("REP099D","JOB=:%ProcessID")
	quit
	
	
ARECBLD	// "A" record builder
	
	type public String TAMT()

	type String AMTIND,FORMCODE
	
	set FORMCODE=1	// IRS form code
	set AMTIND="1A"	// IRS amount type indicator
	set TAMT(1).piece("|",2)="Total ordinary dividends"
	set TAMT("A").piece("|",2)="Federal Income Tax Withheld"
	do ARECWRT	// Write the record to tape
	quit
	
	
BRECBLD	// "B" record builder
	
	type public Number AMT(),FAMT(),FCNT
	
	type Number ACN,AMT1,AMTA,CID,I,SEQ
	type String ADDR,CITY,CNTRY,CORRTN,DOCCODE,FORCNTY,FULLADDR,LNAME,NAME
	type String SPECIAL,STATE,TIN,TINTYPE,X,XTIN,XZIP,ZIP
	
	type DbSet ds
	type RecordB1099D b1099d
	type RecordREP099D rep099d
	
	for I=1:1:9,"A","B","C","D","E" set AMT(I)=0
	
	set DOCCODE="  "	// Document specific code
	set CORRTN=" "		// Corrected return indicator
	
	set b1099d=Class.new("RecordB1099D")
	set b1099d.pdate=CUVAR.TJD
		
	set ds=Db.selectDbSet("REP099D","JOB=:%ProcessID")
	while ds.next() do {
		set rep099d=ds.getRecord("REP099D")

		set TIN=rep099d.taxid
		set ACN=rep099d.acn
		set CID=rep099d.cid
		
		set TINTYPE=$S(TIN?2N1"-"7N:1,TIN?3N1"-"2N1"-"4N:2,1:" ")
		set XTIN=TIN.piece("|",1).translate(" ","-")
		
		if CID=0 do M1099AD(TIN)	// M1099 ADDRESS
		if CID do CIFAD(TIN,ACN)	// CIF ADDRESS
		
		set AMT(1)=rep099d.orddiv*100	// Total Ordinary Dividends
		set AMT("A")=rep099d.fedinc*100	// Federal Income Tax Withheld
		
		do BRECWRT			// Write the record to tape

		// Inserting "B" Records into B1099D File.
	
		set AMT1=AMT(1)/100
		set AMTA=AMT("A")/100

		if CID=0 set b1099d.key=XTIN,b1099d.bseq=SEQ
		if CID set b1099d.key=ACN,b1099d.bseq=CID
		
		if TINTYPE=2 set X=LNAME
		else  set X=NAME
		do NAMCNTRL^IRSTPMTR set b1099d.bnamcon=X
		
		if b1099d.getMode() do b1099d.setMode(0)
		set b1099d.btintype=TINTYPE
		set b1099d.btin=XTIN
		set b1099d.bcid=CID
		set b1099d.bamt1=AMT1
		set b1099d.bamta=AMTA
		set b1099d.bname=NAME
		set b1099d.baddr1=ADDR.piece(" ",1)
		set b1099d.baddr2=ADDR.piece(" ",2)
		set b1099d.baddr3=ADDR.piece(" ",3)
		set b1099d.bcity=CITY
		set b1099d.bstate=STATE
		set b1099d.bzip=XZIP
		do b1099d.bypassSave()
		
		// Calculate number of records and tax form report totals.
		set FCNT=FCNT+1
		set FAMT(1)=FAMT(1)+AMT1	// Ordinary Dividend
		set FAMT("A")=FAMT("A")+AMTA	// Federal Tax
		}
	quit
	
	
CIFAD(String TIN,Number ACN)	// CIF ADDRESS

	type public String ADDR,CITY,CNTRY,FORCNTY,FULLADDR,LNAME,NAME,SPECIAL
	type public String STATE,XZIP,ZIP
	
	type RecordCIF cif
	
	set (ADDR,CITY,CNTRY,FULLADDR,LNAME,NAME,STATE,XZIP,ZIP)=""
	set FORCNTY=" "
	
	// Special data position 544-750
	//; Second TIN Notice(optional)|544
	set SPECIAL="".justify(1)
	//; Blank|545|546
	set SPECIAL=SPECIAL_"".justify(2)
	//; Foreign Country or U.S. Possession|547|586
	set SPECIAL=SPECIAL_"".justify(40)
	//; Blank|587|662
	set SPECIAL=SPECIAL_"".justify(76)
	//; Special data entries|663|722
	set SPECIAL=SPECIAL_"".justify(60)
	//; State Income Tax Withheld|723|734
	set SPECIAL=SPECIAL_"".justify(12,,"0")
	//; Local Income Tax Withheld|735|746
	set SPECIAL=SPECIAL_"".justify(12,,"0")
	//; Combined Federal/State Code|747|748
	set SPECIAL=SPECIAL_"".justify(2)
	//; Blank|749|750
	set SPECIAL=SPECIAL_"".justify(2)
	
	set cif=Db.getRecord("CIF","ACN=:ACN",1)
	if 'cif.getMode() quit
	
	set NAME=cif.nam
	set LNAME=cif.lnm
	set FULLADDR=cif.mcity_"|"_cif.mstate_"|"_cif.mzip_"|"_cif.mcntry_"|"_cif.mad1_"|"_cif.mad2_"|"_cif.mad3_"|"_cif.mad4_"|"_cif.mloc
	set ADDR=cif.mad1_" "_cif.mad2_" "_cif.mad3
	set CITY=cif.mcity
	set STATE=cif.mstate
	set ZIP=cif.mzip.piece("-",1)_cif.mzip.piece("-",2)
	set CNTRY=cif.mcntry
	set FORCNTY=$S(CNTRY="US":" ",1:1)	// foreign country indicator
	set XZIP=cif.mzip
	
	quit
	
	
M1099AD(String TIN)	// ADDRESS FOR 1099DIV BONDS

	type public Number SEQ
	type public String ADDR,CITY,CNTRY,FORCNTY,FULLADDR,LNAME,NAME,SPECIAL
	type public String STATE,XZIP,ZIP
	
	type DbSet ds
	type RecordM1099 m1099
	
	set (ADDR,CITY,CNTRY,FULLADDR,LNAME,NAME,SPECIAL,STATE,XZIP,ZIP)=""
	set FORCNTY=" " 
	
	set ds=Db.selectDbSet("M1099","TAXID=:TIN","SEQ DESC")
	if ds.isEmpty() quit
	if ds.next() do {
		set m1099=ds.getRecord("M1099")
		
		set SEQ=m1099.seq
		set NAME=m1099.nam
		set LNAME=m1099.lnam
		set FULLADDR=m1099.ad1_"|"_m1099.ad2_"|"_m1099.ad3_"|"_m1099.city_"|"_m1099.state_"|"_m1099.mzip
		set ADDR=m1099.ad1_" "_m1099.ad2_" "_m1099.ad3
		set CITY=m1099.city
		set STATE=m1099.state
		set ZIP=m1099.mzip.piece("-",1)_m1099.mzip.piece("-",2)
		set CNTRY=m1099.cntry
		set FORCNTY=$S(CNTRY="US":" ",1:1)	// Foreign country indicator
		set XZIP=m1099.mzip
		}
	quit
	

CRECBLD	// "C" record builder
	
	do CRECWRT	// Write the record to tape
	quit


%STOPLOD	// Stop %ZRTNLOD from this point on down

	quit
	
	
TRECWRT		// Dummy line reference for GT.M

	quit
	

ARECWRT		// Dummy line reference for GT.M

	quit
	
	
BRECWRT		// Dummy line reference for GT.M

	quit
	
	
CRECWRT		// Dummy line reference for GT.M

	quit
	
	
STORETOT	// Dummy line reference for GT.M

	quit

vSIG()	quit "60403^47759^Shriram Deshpande^6274"	// Signature - LTD^TIME^USER^SIZE
