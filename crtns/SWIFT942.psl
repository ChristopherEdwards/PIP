SWIFT942	 // 
 // ORIG: VETSENM - 12/07/1999
 // DESC: Incoming SWIFT 942 Message Server
 // 
 /* ---- Comments --------------------------------------------------------

  This is the MT942 SWIFT message server.  This server will use the entries in
  the sw942 file definition to determine whether or not an account should 
  have an MT942 SWIFT message generated at the current time.  The times 
  used in the sw942 table are required to be in 15 minuted increments.
  
    ---- Revision History ------------------------------------------------

  */
 new XDATE set XDATE=%SystemDate
 new XTIME set XTIME=%CurrentTime
 // 900 seconds in a 15 minute increment (15*60)
 if XTIME#900'=0 set XTIME=((XTIME\900)+1)*900

 new stopflg
 set stopflg=0  // initialize to run

 new KEY set KEY=1 	// Define literal key for SWINTSRV file
 
 for  quit:stopflg="STOP"  do {
	set stopflg=Db.getOneRow("MT942STP","SWINTSRV","KEY")
	quit:stopflg="STOP"

	if %SystemDate'=XDATE set XDATE=%SystemDate
	if %CurrentTime'>XTIME hang 30 quit

	new rs
	type ResultSet rs
        set rs=Db.select("SWDATE,SWTIME,CID,STATUS","SW942","SWDATE=:XDATE AND SWTIME=:XTIME")
        if rs.isEmpty() do { quit 
		set XTIME=XTIME+900
		if XTIME=86400 set XTIME=0
		hang 30
		}

	while rs.next()  do {
		set ER=0,RM=""
                new DATE,TIME,CID
                set DATE=rs.getCol(1)
                set TIME=rs.getCol(2)
                set CID=rs.getCol(3)
 		set STATUS=rs.getCol(4)
		if STATUS'="" quit
		set ER=$$ext^SW942GEN(CID,DATE,TIME) 
		if $g(ER) do ERR quit

		do Db.update("SW942","STATUS=1","SWDATE=:XDATE AND SWTIME=:XTIME AND CID=:CID")

                set DEPREC=Db.getOneRow("ACN,MT942G1,MT942G2","DEP","CID")
                new XACN,mt942g1,mt942g2
                set XACN=$P(DEPREC,$C(9),1)
		set mt942g1=$P(DEPREC,$C(9),2)
                set mt942g2=$P(DEPREC,$C(9),3)

		if mt942g1="",mt942g2="" do {
                        new CIFREC
                        set CIFREC=Db.getOneRow("MT942G1,MT942G2","CIF","XACN")
                    	set mt942g1=$P(CIFREC,$C(9),1)
			set mt942g2=$P(CIFREC,$C(9),2)
			}

		quit:mt942g1=""

		if mt942g1=XTIME,mt942g2'="" do {
			if Db.isDefined("SW942","XDATE,mt942g2,CID") quit
			do Db.insert("SW942","SWDATE,SWTIME,CID",":XDATE,:mt942g2,:CID")
			}
		else  do {
			new CAL,NEXTDATE
			set CAL=Db.getOneRow("NBDC","ACN","CID")
			if CAL="" set CAL="IBS"
			set NEXTDATE=$$NBD^UNBD(XDATE+1,1,0,CAL)
			if Db.isDefined("SW942","NEXTDATE,mt942g1,CID") do { quit
				do Db.update("SW942","STATUS=''","SWDATE=:NEXTDATE AND SWTIME=:mt942g2 AND CID=:CID")
				}
			do Db.insert("SW942","SWDATE,SWTIME,CID",":NEXTDATE,:mt942g1,:CID")
			}
		}
	set XTIME=XTIME+900
	hang 60
	}
	quit

 // ----------------------------------------------------------------------
ERR // Logs error in exception file
 // ----------------------------------------------------------------------
 
 if '$D(ET) set ET=$G(RM)
 // SWIFT Message Processing Error
 do LOG^UTLEXC("SWIFT_942","*",$$^MSG(7371),CID,$G(%ZTSEQ),ET,"")
 kill ET,%ZTSEQ
 
 quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43614^Sanchez SCM Administrator^2941"	// Signature - LTD^TIME^USER^SIZE
