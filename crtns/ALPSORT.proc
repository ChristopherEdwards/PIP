ALPSORT
   /* 
   
   ---- Comments --------------------------------------------------------
   
   This utility is used to set entries in the Dayend file for all product 
   types that are set up for Hierarchy (Priority) Automatic Loan Payment 
   processing.  It is called by priority auto loan payments (BCHAUTPMT2 and 
   BCHAUTPMT3).
   
   
   ---- Revision History ------------------------------------------------

	04/06/06 - KELLYP - CR 20625
		   Modified the PROC section to select from LN based on
		   SCHLD instead of SCHND.  BCHAUTPMT2 and BCHAUTPMT3 run
		   in the EOD portion of the Dayend on the scheduled 
		   payment due date but the date would have already been
		   bumped by the BOD portion of the previous Dayend.

	03/02/06 - KELLYP - CR 19841
		   Modified entire procedure to conform to current PSL
		   standards.  Eliminated unnecessary var's and code.

	01/26/04 - Erik Scheetz - CR7798
		   Modified code in the PROC section to collate through LN
		   rather than DAYENDSCH (obsoleted).  Collation remains the
		   same due to index definition.
   */
	quit

public PROC(Number CTL)	// Process Priority ALP Sort

	/* 
	 CTL		Process Control
			0 - Priority ALP processing
			1 - Priority ALP Retry Processing
	*/

	do Db.delete("AUTPMTSORT")

	// Priority ALP processing
	if 'CTL do {  quit
		type ResultSet rspri=Db.select("CID","LN","SCHLD=:%SystemDate AND DIST1FRE IS NOT NULL")
		while rspri.next() do SET(rspri.getCol("CID"))
		}
		
	// Priority ALP retry processing
	else  do {
		type ResultSet rsretry=Db.select("CID","DAYENDALPRTY","TJD=:%SystemDate")
		while rsretry.next() do SET(rsretry.getCol("CID"))
		}

	quit
	

SET(Number CID)	// Set up AUTPMTSORT record if necessary

	type Number PRI

	type RecordLN ln=Db.getRecord("LN","CID=:CID")
	
	if ln.auptcid.isNull() quit			// No ALP account defined
	if 'Db.isDefined("DEP","CID=:ln.auptcid") quit	// Invalid deposit account

	type RecordUTBLALPPRI ualppri=Db.getRecord("UTBLALPPRI","TYPE=:ln.type",1)
	set PRI=ualppri.pri
	if PRI.isNull() set PRI=999

        type RecordAUTPMTSORT autpmts=Db.getRecord("AUTPMTSORT","AUPTCID=:ln.auptcid,PRI=:PRI,CID=:CID",1)
        if 'autpmts.getMode() do autpmts.bypassSave()
 
	quit

vSIG()	quit "60361^40086^Pat Kelly^2134"	// Signature - LTD^TIME^USER^SIZE
