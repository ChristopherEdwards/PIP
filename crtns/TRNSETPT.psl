TRNSETPT	  	/*
	ORIG: KELLYP - 05/03/2000
	DESC: Transaction Set Pointer Procedure

	---- Comments --------------------------------------------------------
	This procedure is called by the TRNSETPTRXX functions (pointer 
	creation, maintenance, and deletion). A dummy entry is written
	to UTBLTRNSET during pointer creation and is removed during
	pointer deletion. 	
	---- Revision History ------------------------------------------------
	
	03/30/06 - ARPAV - 20481
		   Removed global lookup table references.  Changed 
		   'dummy' entry into UTBLTRNSET to use a trnfield of CID.

	11/21/05 - SPR - 18268
	
		   Transaction Support Data/Transaction Sets -General DBI3
		   system area cleanup.
	
	----------------------------------------------------------------------
 	*/
 
	quit	// Dummy quit for compiler


Public PTRPP    // Post-processor for PTRNAME field on query screen when %O=0
	
	type public String X
	type Number seq
	type String ptr
	
	set ptr=X	
	
		
	type RecordTRNSETPTR trnsetptr=Db.getRecord("TRNSETPTR","PTRNAME=:ptr",1)
	
	// Transaction set pointer ~p1 already exists
	if trnsetptr.getMode() do Runtime.setErrMSG("TRNSETPTR",4343,ptr) quit
		
	set seq=0
	
	type ResultSet rs=Db.select("seq","UTBLTRNSET","TRNSET=:ptr AND SEQ>=:seq")
	if rs.next() set seq=rs.getCol("seq")
	
	// ~p1 defined as a transaction set
	if seq'=0 do Runtime.setErrMSG("UTBLTRNSET",4344,ptr)
	quit


Public PROC 	// Entry point for TRNSETPTRXX functions
	
        type public String ER,PTRNAME,VFMQ
        type String %NOPRMT,%READ,SID,%TAB

	// Set up query screen
        set %READ="@@%FN,,,PTRNAME",%NOPRMT="N"
	if %ProcessMode=0 set %TAB("PTRNAME")=".PTRNAME/XPP=D PTRPP^TRNSETPT/LEN=12/DES=""Pointer Name""/REQ"
	else  set %TAB("PTRNAME")="[TRNSETPTR]PTRNAME/DES=""Pointer Name""/REQ/TBL=[TRNSETPTR]"	
	do ^UTLREAD
	
	if PTRNAME.get()="" quit

	set SID="TRNSETPTR"
	
	type RecordTRNSETPTR fTRNSETP=Db.getRecord("TRNSETPTR","PTRNAME=:PTRNAME",1)
		
	
	do DRV^USID(%ProcessMode,SID,.fTRNSETP)

	if VFMQ="Q" do {  quit
		// Transaction set pointer ~p1 not created
		if %ProcessMode=0 set ER="W" do Runtime.setErrMSG("UTBLTRNSET",4346,PTRNAME) quit
		// Transaction set pointer ~p1 not modified
		if %ProcessMode=1 set ER="W" do Runtime.setErrMSG("UTBLTRNSET",4348,PTRNAME) quit
		// Transaction set pointer ~p1 not deleted
		if %ProcessMode=3 set ER="W" do Runtime.setErrMSG("UTBLTRNSET",4350,PTRNAME) quit
		}

	if %ProcessMode=0 do NEWPTR(.fTRNSETP) quit
	if %ProcessMode=1 do UPDPTR(.fTRNSETP) quit
	if %ProcessMode=3 do DELPTR quit

	quit
   

Public NEWPTR(RecordTRNSETPTR fTRNSETP)	// Create Transaction Set Pointer Record
	
	type public String ER,PTRNAME
	type Number fixedval,seq
	type String trnfield

	type RecordTRNSETPTR ptrrec=Class.new("RecordTRNSETPTR")

	set ptrrec.ptrname=PTRNAME
	set ptrrec.lnptr=fTRNSETP.lnptr
	set ptrrec.prodptr=fTRNSETP.prodptr
	do ptrrec.save()

	// Dummy entry for UTBLTRNSET	
 	set seq=2
	set trnfield="CID"
  	set fixedval=0
  	
  	type RecordUTBLTRNSET utbltrnset=Class.new("RecordUTBLTRNSET")
  	
  	set utbltrnset.trnset=PTRNAME
  	set utbltrnset.seq=seq
  	set utbltrnset.trnfield=trnfield
  	set utbltrnset.fixedval=fixedval
  	
  	do utbltrnset.save()
  	
	// Transaction set pointer ~p1 created
        set ER="W" do Runtime.setErrMSG("UTBLTRNSET",4345,PTRNAME)

	quit


Public UPDPTR(RecordTRNSETPTR fTRNSETP)	// Update Transaction Set Pointer Record
	
	type public String ER,PTRNAME
	
	type RecordTRNSETPTR ptrrec=Db.getRecord("TRNSETPTR","PTRNAME=:PTRNAME")
	set ptrrec.lnptr=fTRNSETP.lnptr
	set ptrrec.prodptr=fTRNSETP.prodptr

	do ptrrec.save()

	// Transaction set pointer ~p1 modified
	set ER="W" do Runtime.setErrMSG("TRNSETPTR",4347,PTRNAME)

	quit


Public DELPTR   // Delete Transaction Set Pointer Record
	
	type public String ER,PTRNAME
	type Number SEQ
	type String TRNFIELD

	do Db.delete("TRNSETPTR","PTRNAME=:PTRNAME")

        set SEQ=2
        set TRNFIELD="ETC"
	do Db.delete("UTBLTRNSET","TRNSET=:PTRNAME AND SEQ=:SEQ AND TRNFIELD=:TRNFIELD")

	// Transaction set pointer ~p1 deleted
        set ER="W" do Runtime.setErrMSG("UTBLTRNSET",4349,PTRNAME)

	quit	
 #OPTION ResultClass ON
Public String vSIG()	quit "60354^56873^Vincent Arpa^3967"	// Signature - LTD^TIME^USER^SIZE
