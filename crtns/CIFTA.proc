CIFTA	 /*
    ORIG: GRAY - 01/14/2000
    DESC: Transfer Accounts Between Customers
    
    ---- Comments --------------------------------------------------------
 
    ---- Revision History ------------------------------------------------

	11/27/02 - BECKERJ - 49968:01
		   Added code to section CNT to set up CIFTA if "Customer
		   DOD required to transfer acct" error is received in
		   ^UCIFM.  Also added this error to section ERRDSC.
		   Remove Pre-2000 Revision History.

	02/26/02 - GRAY - 43583
		   The changes done under ARQ#46330 did not set up the new
		   error description for error #7 in ERRDSC.
		   Also removed locking of the globals ^ACN and ^IRA in 
		   section CNT.
	
	09/12/01 - HALPINJ - 46330
		   Added code to section CNT to check to make sure if the
		   account is an IRA account that the plan the account
		   will be transferred to is of the same type as the account.
		   Also added an error to the ERRDSC section in the event
		   that the account type and plan type are different.

	06/25/01 - APPLEYARDM - 44458:001

		   Newed vfkey for each account in section CNT before the 
		   account is processed. This prevents referential integrity 
		   errors on one account from affecting all subsequent 
		   accounts when transferring multiple accounts. 

 */

	quit
	

Public FILE(CTL)	// File data
	/*
		CTL	Position	Description
	
			1		Indicator (0/1) of whether (1) or
					not (0) report file should be built.
					Report file is not built when called
					externally from RPC
	
	 Called internally and by routine MRPC048
	*/

	set CTL=$G(CTL)
	
	set CNT=""
	for  set CNT=$O(CIFTA(CNT)) quit:CNT=""  do CNT
	quit
	
CNT	// Process each account 
	
	new CID,CLS,IRA,X
	
	//MCA 6/25/01
	new vfkey
	
	set CID=$P(CIFTA(CNT),"|",5),ER=0
	if 'CID quit
	
	if CIFTA(CNT) do {
		set IRA=0
		set CLS=Db.getOneRow("CLS","ACN","CID")
		if CLS="D" set IRA=Db.getOneRow("IRA","DEP","CID")
		if IRA do {
			set RPASEQ=Db.getOneRow("RPASEQ","DEP","CID")
			if Db.isDefined("IRATYPE","NACN,RPASEQ") do {
				set NIRA=Db.getOneRow("IRATYP","IRATYPE","NACN,RPASEQ")
				if NIRA'=IRA set $P(CIFTA(CNT),"|",9)=7,$P(CIFTA(CNT),"|",1)=0
				}
			}
		if 'CIFTA(CNT) quit
	
		new CTL
	
		do ^UCIFM
		if ER do {
			set $P(CIFTA(CNT),"|",1)=0
			if ER=6 set $P(CIFTA(CNT),"|",9)=6 quit
			if ER=8 set $P(CIFTA(CNT),"|",9)=8 quit	// JMB - 49968:01
			set $P(CIFTA(CNT),"|",9)=5
			}
		}
	if 'CIFTA(CNT),$P(CIFTA(CNT),"|",9)="" set $P(CIFTA(CNT),"|",9)=3
	
	// Quit if CLT variable indicates that report file should not be built
	if '$E(CTL,1) quit
	
	/*
	Create reporting array in following format:
	REPTA(transfered indicator,CLS,GRP,TYPE,CID)=REL|ROL|STAT|ERROR FLAG
	error flags (piece 9):
	1 - Second customer previously linked
	2 - Insufficent history for IRA
	3 - Account not transferred by user request
	4 - Account locked by another user
	5 - System error
	6 - Frequency error.  Statement end-of-day index file not created.
	7 - Account RPA type must match plan type
	8 - Customer DOD required to transfer acct
	*/
	
	set X=$P(CIFTA(CNT),"|",9)'=""
	
	set REPTA(X,$P(CIFTA(CNT),"|",2),$P(CIFTA(CNT),"|",3),$P(CIFTA(CNT),"|",4),CID)=$P(CIFTA(CNT),"|",6,9)
	
	quit
	
Public ERRDSC	//Build file of error descriptions
	
	// Called internally and by routine MRPC048
	
	// Second customer previously linked.
	set ERRDSC(1)=$$^MSG(3149)
	
	// Insufficent history for IRA.
	set ERRDSC(2)=$$^MSG(3146)
	
	// Account not transferred by user request.
	set ERRDSC(3)=$$^MSG(7364)
	
	// Account locked by another user.
	set ERRDSC(4)=$$^MSG(4051)
	
	// System error.  See error log report.
	set ERRDSC(5)=$$^MSG(3150)
	
        // Invalid frequency - Dayend statement record not created.
        set ERRDSC(6)=$$^MSG(7365)
	
	// Account RPA type must match plan type
	set ERRDSC(7)=$$^MSG(5064)
	
	// Customer DOD required to transfer acct
	set ERRDSC(8)=$$^MSG(5157)		// JMB - 49968:01
	quit

vSIG()	quit "59906^40690^Laura Hillanbrand^3750"	// Signature - LTD^TIME^USER^SIZE
