ACHINC // Utilities to format ACH input into ^ACNINC	
	/*
	       ORIG:  DAN - 18 MAR 1992
	  CALLED BY:
	      CALLS:
	       DESC:  Utilities which can be used by inclearing reformatters
	              to set up ^ACHINC file.

	   GLOBALS -
	       READ:
	        SET:

	      INPUT:
	     OUTPUT:

	  EXT ENTRY:

	   I18N=QUIT: Excluded from I18N standards.

	  ---- Revision History ------------------------------------------------
	   
	   04/04/02 - SHVACHKINAD - 49794
		      Converted to PSL.
				
	   01/09/01 - Scott - 42919
	              Modified BATCH section.  File was being parsed 
		      incorrectly.

	*/

	// No entry from top
	quit

FILE(rec,id)	// Return file level for [ACHINCF] and new file id
	/*
	   Call by:  set X=$$FILE^ACHINC(REC,.FILEID) where REC is a file type
	   data record.  X is returned in format which can be set into
	   ^ACHINC(ID)=X
	*/

	new %DS,%JD,TIME,TRANDATE,TRANTIME,X

	// Not a file header record
	if $E(rec)'=1 set id="" quit ""
	set TRANDATE=$E(rec,24,29) 
	set TRANTIME=$E(rec,30,33)
	// Standard file ID for IBS
	set id=TRANDATE_TRANTIME

	set %DS=$E(rec,26,27)_"/"_$E(rec,28,29)_"/"_$E(rec,24,25)
	set %JD=$$^SCAJD(%DS) 
	set TIME=TRANTIME/100*3600+(TRANTIME#100*60)
	set X=$$DAT^%ZM(%JD)_" formatted "_$$DAT^%ZM(%CurrentDate)_" "_$$TIM^%ZM
	set X=X_"|1|"_$E(rec,2,3)_"|"_$E(rec,4,13)_"|"_$E(rec,14,23)
	set X=X_"|"_%JD_"|"_TIME_"|"_$E(rec,34)_"|"_+$E(rec,35,37)
	set X=X_"|"_+$E(rec,38,39)_"|"_$E(rec,40)_"|"_$$RTB^%ZFUNC($E(rec,41,63))
	set X=X_"|"_$$RTB^%ZFUNC($E(rec,64,86))_"|"_$$RTB^%ZFUNC($E(rec,87,94))

	quit X

FILETR(rec,orec)	// Update file record with trailer totals
	/*
	   Call by:  set X=$$FILETR(REC,OREC) where REC is the file trailer
	   record, OREC is the formated file header from FILE^ACHINC.
	   X is returned as OREC with new values in it.
	*/

	// Debit total
	set $P(orec,"|",14)=$E(rec,32,43)/100
	// Credit total
	set $P(orec,"|",15)=$E(rec,44,55)/100

	quit orec

BATCH(rec)	// Return batch level for [ACHINCB]
	/*
	   Call by:  set X=$$BATCH^ACHINC(REC) where REC is a batch header
	   record.  X is returned in format which can be set into
	   ^ACHINC(FILEID,BCHSEQ)=X
	*/

	new %DS,%JD,X

	// Not a batch header record
	if $E(rec)'=5 quit ""

	set %DS=$E(rec,72,73)_"/"_$E(rec,74,75)_"/"_$E(rec,70,71)
	set %JD=$$^SCAJD(%DS)
	set X="5|"_$E(rec,2,4)_"|"_$$RTB^%ZFUNC($E(rec,5,20))
	set X=X_"|"_$$RTB^%ZFUNC($E(rec,21,40))_"|"_$$RTB^%ZFUNC($E(rec,41,50))
	set X=X_"|"_$$RTB^%ZFUNC($E(rec,51,53))_"|"_$$RTB^%ZFUNC($E(rec,54,63))
	set X=X_"|"_$$RTB^%ZFUNC($E(rec,64,69))_"|"_%JD_"|"_+$E(rec,79)
	set X=X_"|"_$$RTB^%ZFUNC($E(rec,76,78))_"|"_$E(rec,80,87)_"|"_+$E(rec,88,94)

	quit X

BATCHTR(rec,orec)	// Update batch record with trailer totals
	/*
	   Call by:  set X=$$BATCHTR(REC,OREC) where REC is the batch trailer
	   record, OREC is the formated batch header from BATCH^ACHINC.
	   X is returned as OREC with new values in it.
	*/

	// Debit total
	set $P(orec,"|",14)=$E(rec,21,32)/100
	// Credit total
	set $P(orec,"|",15)=$E(rec,33,44)/100

	quit orec

DETAIL(rec)	// Return detail level for [ACHINCD]
	/*
	   Call by:  S X=$$DETAIL^ACHINC(REC) where REC is a detail record.
	   X is returned in format which can be set into
	   ^ACHINC(FILEID,BCHNO,SEQ)=X
	*/

	new X

	// Not a detail record
	if $E(rec)'=6 quit ""

	set X="6|"_$E(rec,2,3)_"|"_$E(rec,4,11)_"|"_$E(rec,12)
	set X=X_"|"_$$RTB^%ZFUNC($E(rec,13,29))_"|"_($E(rec,30,39)/100)
	set X=X_"|"_$$RTB^%ZFUNC($E(rec,40,54))_"|"_$$RTB^%ZFUNC($E(rec,55,76))
	set X=X_"|"_$$RTB^%ZFUNC($E(rec,77,78))_"|"_$E(rec,79)
	set X=X_"|"_$E(rec,80,94)

	quit X

ADDENDA(rec,orec)	// Return addenda level for [ACHINCD]
	/*
	   Call by:  S X=$$ADDENDA^ACHINC(REC,OREC) where REC is the addenda
	   recordd, OREC is the formatted detail record from DETAIL^ACHINC.
	   X is returned as OREC with the new values in it.
	*/

	new %DS,TYPCODE

	set TYPCODE=$E(rec,2,3)
	set $P(orec,"|",12,13)=$E(rec)_"|"_TYPCODE

	// Standard Addenda Record
	if TYPCODE="05" do {
		set $P(orec,"|",14)=$$RTB^%ZFUNC($E(rec,4,83))
		set $P(orec,"|",15)=+$E(rec,84,87)
		}
	// Return Addenda Record
	if TYPCODE="99" do {
		set $P(orec,"|",16)=$E(rec,4,6)
		set $P(orec,"|",17)=+$E(rec,7,21)
		set %DS=$E(rec,24,25)_"/"_$E(rec,26,27)_"/"_$E(rec,22,23)
		set $P(orec,"|",18)=$$^SCAJD(%DS)
		set $P(orec,"|",19)=$E(rec,28,35)
		set $P(orec,"|",14)=$E(rec,36,79)
		}

	quit orec

vSIG()	quit "59886^43488^Sanchez SCM Administrator^4177"	// Signature - LTD^TIME^USER^SIZE
