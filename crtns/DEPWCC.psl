DEPWCC(RecordDEP dep, RecordTTX ttx, RecordTRN trn)	// Transaction processor for waiving service charges
	/*
	ORIG:  Dan S. Russell (2417) - 08/12/86
	PROJ #'S:  1584
	DESC:  Called by any tran code that wants to examine waive service
	       charge data item (WCC).  Will dispatch to DEPPO if no
	       service charge waiver in effect.

	---- Revision History ------------------------------------------------
	
	08/24/05 - RussellDS - CR16911
		   Converted to PSL.
	
	----------------------------------------------------------------------
	*/
	
	type public Date TPD
	type public String BRCD, RM
	
	type Number TAMT, WCC, XCID
	type String ITC, TC
	
	set WCC = dep.wcc
	
	if (WCC '> 0) do ^DEPPO(.dep, .ttx, .trn) quit	// No waivers
	
	type RecordSTBLWCC stblwcc = Db.getRecord("STBLWCC", "WCC=:WCC")
	
	// Check charge waived - ~p1
	set RM = $$^MSG(520, stblwcc.desc)

	// If one time waive only, eliminate flag
	if (WCC = 1) set dep.wcc = 0

	// Set up error correct to reverse this transaction, plus offsetting
	// suspense transaction.
	
	type RecordTTX ttx1 = ttx.copy()
	
	set TAMT = ttx.tamt
	
	// Flag original as error corrected
	set ITC = ttx.itc
	set ITC = ITC.extract(1, 5)_"00000".extract(1, 5 - ITC.length())_1_ITC.extract(7, 99)
	set ttx.itc = ITC
	
	set ttx1.itc = ITC
	set ttx1.tamt = -TAMT
	set ttx1.tso = ""
	set ttx1.tcmt = $$^MSG(3518)	// Waive check charge
	
	do TRNSINGL^TRNDRV(.ttx1, .dep, TPD, BRCD, 5)

	// Suspense TAMT as TSDR
	type RecordSCAU scau = Db.getRecord("SCAU", "UID=:%UserID")
	
	set TC = "TSDR"
	set XCID = scau.tsdr
	if XCID.isNull() set XCID = CUVAR.GLTS
	
	type RecordTRN trn2 = Db.getRecord("TRN","ETC=:TC")
	
	set ITC = trn2.itc
	
	type RecordTTX ttx2 = Class.new("RecordTTX")

	set ttx2.cid = XCID
	set ttx2.itc = ITC
	set ttx2.etc = TC
	set ttx2.tamt = TAMT
	set ttx2.efd = %EffectiveDate.get()
	set ttx2.tlo = ttx.tlo
	set ttx2.tso = "GLO#"_dep.cid
	set ttx2.tcmt = "WAIVE CHECK CHARGE ACCT "_dep.cid
	set ttx2.crcd = ttx.crcd

	do TRNSINGL^TRNDRV(.ttx2, .dep, TPD, BRCD, 5)
	
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60139^42442^Dan Russell^1851"	// Signature - LTD^TIME^USER^SIZE
