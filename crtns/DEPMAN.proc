DEPMAN	//;PBS -  - V2.0 - Time Deposit Manual Rollover
	/*

	       ORIG:  Allan R. Mattson (6915) - 08/29/86
	       DESC:

	      INPUT:
	     OUTPUT:

	  ---- Revision History ------------------------------------------------

	   02/27/06 - PUTTASWH - 19718
		      Converted to PSL.
	  ----------------------------------------------------------------------
	*/

	type public String ET,%NET

	if '%NET do { quit
		set ET="NETWORK"
		do ERR
		}

	do INIT(1)
	quit


INIT(Number %ProcessMode)	// Process Mode

	type String VFMQ
	type public Number %PG,%PAGE

	set %PG=0
	set %PAGE=1
	type RecordROLLOVR ROLL
	type RecordDEP DEP
	type RecordCIF CIF

	do VPG(.ROLL,.DEP,.CIF)
	quit


VPG(RecordROLLOVR ROLL,		// Record ROLLOVR
	RecordDEP DEP,		// Record DEP
	RecordCIF CIF)     	// Record CIF
	// Page control

	type public Number %PG,%PAGE
	type public String ER
	type public String VFMQ
	type Number FINISH

	set FINISH=0
	for  do { quit:FINISH
		if %PG=0 do VPG00 if ER set FINISH=1 quit
		if %PG>0 do VPG01(.ROLL,.DEP,.CIF)
		if "DFQ"[VFMQ do VER(.DEP) set FINISH=1 quit
		set %PG=%PG+1
		}
	quit


VPG00	// Set up

	type public String %NOPRMT,%READ,%TAB,VFMQ
	type public String ER
	
	set %TAB("CID")=".CID1/XPP=D POSCID^DEPMAN"
	set %READ="@@%FN,,,CID/REQ"
	set %NOPRMT="N"

	do ^UTLREAD

	if VFMQ="Q" set ER=1 quit
	
	quit


POSCID	// Account number post-processor

	type public Number GRCDYS,%EXT,X
	type public String ER,RM
	type Date MDT
	type String CLS,TYPE

	set CLS="D"
	set GRCDYS=""
	set %EXT=1

	do ^UACN

	if ER quit
	if X.isNull() quit

	type RecordDEP dep=Db.getRecord("DEP","CID=:X")
	set MDT=dep.mdt

	// Not matured
	if %SystemDate<MDT do Runtime.setErrMSG("DEP",2044) quit

	set TYPE=dep.type

	type ResultSet rs=Db.select("GRCDYS","PRODCTL","TYPE=:TYPE")
	if rs.next() set GRCDYS=rs.getCol("GRCDYS")

	if GRCDYS.isNull() do {
	
		type RecordCUVAR cuvar=Db.getRecord("CUVAR")
		set GRCDYS=cuvar.grace
		
		}

	// Exceeds renewal grace period
	if MDT+GRCDYS<%SystemDate do Runtime.setErrMSG("DEP",1034) 

	quit


VPG01(RecordROLLOVR ROLL,	// Record ROLLOVR
	RecordDEP DEP,		// Record DEP
	RecordCIF CIF)		// Record CIF
	// Screen

	type public Number CID
	type Number ACN

	set ROLL=Db.getRecord("ROLLOVR","CID=:CID",1)
        set DEP=Db.getRecord("DEP","CID=:CID")
        set ACN=DEP.acn
	set CIF=Db.getRecord("CIF","ACN=:ACN")

	do DRV^USID(%ProcessMode,"ROLLOVR",.ROLL,.DEP,.CIF)

	quit


ERR	//
	type public Boolean ER
	type public String VFMQ

	set ER=1

	do DSPBP^UTLERR

	set VFMQ="Q"
	quit 


VER(RecordDEP DEP)	// Record DEP

	type public Number CID
	type public String VFMQ
	type public String ER
	type String PGM,XECUTE

	if (%ProcessMode=2)!(%ProcessMode=4)!(VFMQ="Q") do END quit	
	
	do DEP.save()		// File data 
	
	if ER quit

	set PGM=$$GET^UBCHID("BCHROLL")

	if PGM.isNull() quit

	set XECUTE="D MANUAL^"_PGM_"(CID)"

	#ACCEPT DATE=04/06/02;PGM=CHHABRIAS
	xecute XECUTE

	do END
	quit
	
	
END	//
	type public Number CID
	type public String ER,RM,VFMQ	

	if ER!(%ProcessMode=2)!(%ProcessMode=4) quit

	// Account ~p1 not renewed
	if VFMQ="Q" set RM=$$^MSG(141,CID)
	
	// Account ~p1 renewed
	else  set RM=$$^MSG(156,CID)
	set ER="W"

	quit

vSIG()	quit "60367^44271^Laura Hillanbrand^2938"	// Signature - LTD^TIME^USER^SIZE
