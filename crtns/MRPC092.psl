MRPC092(RETURN,VERSN,SRC,ACN,BATCH)

	/* Procedure ID: MRPC092
	   DESC: Anticipated PAT report. This procedure returns the anticipated
		 (Currenct) rates and amounts for a PAT batch.
	   ORIG: Smita Agarwal
	   DATE: 12/14/99
	
	   KEYWORDS: RPC
	
	   ARGUMENTS:
	   . RETURN  Report			/TYP=T/REQ/MECH=REFNAM:W
	
	   . VERSN  version number		/TYP=N/REQ/MECH=VAL
	     current version=1
	
	   . SRC  Source			/TYP=N/REQ/MECH=VAL
	
	   . ACN  Customer No			/TYP=T/REQ/MECH=VAL

	   . BATCH Batch 			/TYP=T/REQ/MECH=VAL
	
	   RETURNS:
	   . $$     Error Message		/TYP=T
	     Null= No Error
	
	   . RETURN Report			/TYP=N
	
	   RELATED:
	   . $$^PBSMRPC - MRPC Service Class Driver
	
	   EXAMPLE:
	   S RM=$$^MRPC092(RETURN,1,SRC,ACN,BATCH)

	---- Revision History ------------------------------------------------
	
	02/17/06 - PUTTASWH - CR 17988
		   Modified the call to CALC^PATCALC section to 
		   match the parameters.
		   
	12/14/05 - PUTTASWH - CR 18157
		   Modified the code according to DBI standards.
		   
	02/28/02 - GRAY - 43583
		   Replaced global reference with Db.getOneRow method and 
		   performed a minor cleanup of this procedure.
	

	*/	
	
	
	// Version number of client message is not compatible with server
	if VERSN.get()'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))     
	
	new CID,DC,ETC,I,PAT,PATG,QUIT,SEQ,TYPE
	
	do CALC^PATCALC(SRC,ACN,BATCH,.PATG())
	if ER.get() quit $$ERRMSG^PBSUTL(RM.get())
	
	set TAB=$C(9)
	
	// Set Return
	
	set RETURN=""
	set SEQ=""
	set QUIT=0
	
	for I=1:1 do { quit:QUIT
	
		set SEQ=PATG(SEQ).order()
		if SEQ="" set QUIT=1 quit
	
		set PAT=PATG(SEQ).get()
		set CID=PAT.piece("|",2)
		
		type RecordACN acn=Db.getRecord("ACN","CID=:CID",1)
		if acn.getMode() set TYPE=acn.type
		else  set TYPE="G/L"
	
		set ETC=PAT.piece("|",1)
		type RecordTRN trn=Db.getRecord("TRN","ETC=:ETC",1)
		set DC=trn.dc
	
		// Sequence, account no, type
		set RETURN=RETURN_SEQ_TAB_CID_TAB_TYPE_TAB
	
		// transaction code, currency code, anticipated rate
		set RETURN=RETURN_PAT.piece("|",1)_TAB_PAT.piece("|",18)_TAB_PAT.piece("|",19)_TAB
	
		// amount, base currency
		type RecordPAT1 pat1=Db.getRecord("PAT1","SRC=:SRC,ACN=:ACN,BATCH=:BATCH",1)
		set RETURN=RETURN_PAT.piece("|",3)_TAB_pat1.crcd_TAB
	
		// base amount
		set RETURN=RETURN_$S(DC:PAT.piece("|",20),1:-(PAT.piece("|",20)))_$C(13,10)
		}
	
	set RETURN=$$V2LV^MSG(RETURN)
	
	quit ""
 #OPTION ResultClass ON
Public String vSIG()	quit "60313^26693^Hema Puttaswamy^2249"	// Signature - LTD^TIME^USER^SIZE
