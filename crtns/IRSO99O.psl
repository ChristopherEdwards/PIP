IRSO99O		/*
	ORIG: Michael Winigrad (6969)
	DESC: IRS Processing - Form 1099-OID Original
	I18N=QUIT: Excluded from I18N standards.

	---- Comments --------------------------------------------------------
	======================================================================
		*** THIS ROUTINE IS TO BE COMPILED WITH IRSTPMTR ***
		     *** DO NOT RUN THIS ROUTINE STANDALONE ***
	======================================================================
	
	---- Revision History ------------------------------------------------
	
	07/09/07 - NATRAJAH - CR 27921
		   . Modified BRECBLD section to set the field position 547-585 
		     of the variable SPECIAL to dep.irn/year of maturity date.
		   . Added code for inserting Payee "B" record into B1099OID table. 
		   . The conditional check has modified to quit the process only
		     if DEP.IPTY < CUTOFF is true.
		   . Modified setting of FAMT array to store the correct value.	

	05/15/06 - DESHPANDE S K - CR 21167
		   Modified Section BRECBLD to set address variables to mailing 
		   address instead of permanent address.
	
	09/26/05 - HAILEYM - CR17143
		Converted to PSL.
		
	*/
	
	// Accept "Dead code" warning during runtime of @IRSTAPE.
	#ACCEPT Date=10/14/2005;PGM=Marie Hailey
	quit
	
	
MTRCNTRL	// Master control

	type public Boolean ER
	type public Number FAMT(),FCNT
	
	type Number I
	
	set FCNT=""
	for I=1:1:8 set FAMT(I)=""
	
	do ARECBLD quit:ER	// Build and write the "A" record
	do BRECBLD quit:ER	// Build and write the "B" records for Interest Earnings
	do CRECBLD quit:ER	// Build and write the "C" record
	do STORETOT		// Store the TTR report totals
	quit
	
	
ARECBLD	// "A" record builder

	type public String TAMT()
	
	type String AMTIND,FORMCODE
	
	set FORMCODE="D"	// IRS form code
	set AMTIND=1234		// IRS amount type indicator
	set TAMT(1).piece("|",2)="Total Original Issue Discount"
	set TAMT(2).piece("|",2)="Other Periodic Interest"
	set TAMT(3).piece("|",2)="Early Withdrawal Penalty"
	set TAMT(4).piece("|",2)="Federal Income Tax Withheld"
	do ARECWRT	// Write the record to tape
	quit
	
	
BRECBLD	// "B" record builder

	type public Number AMT(),CUTOFF,FAMT(),FCNT,YEAR
	
	type Date MDT
	type Number ACN,BSEQ,CID,I,OID
	type String ADDR,CITY,CNTRY,CORRTN,DOCCODE,FORCNTY,FULLADDR,NAME,LNAME
	type String NAMCON,SPECIAL,STATE,TIN,TINTYPE,X,ZIP
	
	type DbSet ds
	type RecordCIF cif
	type RecordDEP dep
	type RecordOID lyoid,oid
	
	set DOCCODE="  "	// Document specific code
	set CORRTN=" "		// Corrected return indicator
	
	for I=1:1:9 set AMT(I)=0
	
	set ds=Db.selectDbSet("DEP","OID>0")
	while ds.next() do {
		set dep=ds.getRecord("DEP")
		set CID=dep.cid
		
		if dep.irsexm quit	// exempt 1099 reporting
		
		set oid=Db.getRecord("OID","CID=:CID,TAXYR=:YEAR",1)
		if 'oid.getMode() quit
		
		set lyoid=Db.getRecord("OID","CID=:CID,TAXYR=:(YEAR-1)",1)
		if 'lyoid.getMode() set OID=oid.ial
		else  set OID=oid.ial-lyoid.ial
		
		if dep.ipty<CUTOFF quit

		set cif=Db.getRecord("CIF","ACN=:dep.acn")

		set TIN=cif.taxid
		set TINTYPE=$S(TIN?2N1"-"7N:1,TIN?3N1"-"2N1"-"4N:2,1:" ")
		
		set NAME=cif.nam
		set LNAME=cif.lnm
		set FULLADDR=cif.mcity_"|"_cif.mstate_"|"_cif.mzip_"|"_cif.mcntry_"|"_cif.mad1_"|"_cif.mad2_"|"_cif.mad3_"|"_cif.mad4_"|"_cif.mloc
		set ADDR=cif.mad1_" "_cif.mad2_" "_cif.mad3
		set CITY=cif.mcity
		set STATE=cif.mstate
		set ZIP=cif.mzip.piece("-",1)_cif.mzip.piece("-",2)
		set CNTRY=cif.mcntry
		set FORCNTY=$S(CNTRY="US":" ",1:1)	// foreign country indicator
		
		set MDT=dep.mdt
		set SPECIAL="".justify(3)_((dep.irn_"/"_MDT.year())_"".justify(39)).extract(1,39)
		
		set AMT(1)=OID*100		// Total OID 
		set AMT(2)=dep.ipty*100		// Interest earnings
		set AMT(3)=dep.penpty*100	// Early withdrawal penalty
		set AMT(4)=dep.bwpty*100	// Federal income tax withheld
		
		set ACN=cif.acn
		type ResultSet rs = Db.select("BSEQ","B1099OID","PDATE=:%SystemDate AND KEY=:ACN","BSEQ DESC")
		if rs.next() set BSEQ = rs.getCol("BSEQ") + 1
		else  set BSEQ = 1
	
		type RecordB1099OID b1099oid = Db.getRecord("B1099OID","PDATE=:%SystemDate,KEY=:ACN,BSEQ=:BSEQ", 1)

		if TINTYPE=2 set X=LNAME
		else  set X=NAME
		do NAMCNTRL^IRSTPMTR set NAMCON=X
		
		set b1099oid.bnamcon=NAMCON
		set b1099oid.btintype = TINTYPE
		set b1099oid.btin = TIN.translate(" ","-")
		set b1099oid.bcid = CID
		set b1099oid.bamt1 = AMT(1)/100
		set b1099oid.bamt2 = AMT(2)/100
		set b1099oid.bamt3 = AMT(3)/100
		set b1099oid.bamt4 = AMT(4)/100
		set b1099oid.bname = NAME
		set b1099oid.baddr1 = cif.mad1
		set b1099oid.baddr2 = cif.mad2
		set b1099oid.baddr3 = cif.mad3
		set b1099oid.bcity = cif.mcntry
		set b1099oid.bstate = cif.mstate
		set b1099oid.bzip = cif.mzip

		do BRECWRT	// Write the record to tape

		do b1099oid.save()
		
		// Calculate number of records and tax form report totals.
		set FCNT=FCNT+1
		set FAMT(1)=FAMT(1)+(AMT(1)/100)	// Total OID
		set FAMT(2)=FAMT(2)+(AMT(2)/100)	// Interest earnings
		set FAMT(3)=FAMT(3)+(AMT(3)/100)	// Early withdrawal penalty
		set FAMT(4)=FAMT(4)+(AMT(4)/100)	// Federal income tax withheld
		}
	quit
	
	
CRECBLD	// "C" record builder
	
	do CRECWRT	// Write the record to tape
	quit


%STOPLOD	// Stop %ZRTNLOD from this point on down

	quit
	
	
TRECWRT		// Dummy line reference for GT.M

	quit
	

ARECWRT		// Dummy line reference for GT.M

	quit
	
	
BRECWRT		// Dummy line reference for GT.M

	quit
	
	
CRECWRT		// Dummy line reference for GT.M

	quit
	
	
STORETOT	// Dummy line reference for GT.M

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60843^23635^Hari Natrajan^5247"	// Signature - LTD^TIME^USER^SIZE
