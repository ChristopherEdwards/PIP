MRPC030(RETURN,VERSN,CID,EFD,OPTION)	//Public
	/*

	   DESC: Single loan update functions

	   KEYWORDS: MRPC

	   ARGUMENTS:
	   	. RETURN 	Status    		/TYP=T/REQ/MECH=REFNAM:W
	                  	0 = Failure  1 - Success
	   	. VERSN  	MRPC030 version number 	/TYP=N/REQ/MECH=VAL
	                  	current version=1
	   	. CID  		Loan Account Number 	/TYP=N/REQ/MECH=VAL
	   	. EFD           Effective Date  	/TYP=D/MECH=VAL
	   	. OPTION        Function option  	/TYP=N/REQ/MECH=VAL
	     			 1 = Bring Loan to Current Status
	                         2 = Scheduled Payment Date Roll
	     			 3 = Payment Billing Process
	                         4 = Payment Change Offset Process
	                         5 = Interest Change Offset
	                         6 = Payment Change Date Process
	                         7 = Interest Change Date Process
	                         8 = Earnings Analysis
	   RETURNS:
		 . $$     Error Message  		/TYP=T

	   RELATED:
	         . $$^PBSMRPC - MRPC Service Class Driver

	   EXAMPLE:
	         S RM=$$^MRPC030(.value,1,7000000253,,1)

	  -------Revision History------------------------------------------

	   04/07/05 - KinI - 14385
	   	Modified to comply with DBI standards.
	   	Modified LNBOD calls to pass additional parameters.
		Modified PROCESS section to accept additional parameters.
		
	   03/12/03 - CARROLLJ - 51349
		Pass ln object to LNDE1.

           04/10/02 - TELIV - 49794
                Converted to PSL

	*/

	type public String ER, ET, RM
	type Date TPD
	type String VFMQ

	// Version number of client message is not compatible with server
	if VERSN.get()'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))

	type RecordLN ln=Db.getRecord("LN","CID=:CID",1)
	// Invalid account
	if ln.getMode()=0 quit $$ERRMSG^PBSUTL($$^MSG(1259,CID))

	// Invalid Account Class
	if ln.cls'="L" set ET="INVLDCLS" do ^UTLERR quit $$ERRMSG^PBSUTL(RM.get())

	if EFD="" set EFD=%SystemDate
	set TPD=%SystemDate
	set VFMQ="F"
	
	do PROCESS(.ln,OPTION,EFD)

	if ER.get() quit $$ERRMSG^PBSUTL(RM.get())

	set RETURN=$$V2LV^MSG(1)
	
	quit ""


PROCESS(RecordLN ln,	// LN record
	Number OPTION,	// Function option
	Date EFD)	// Effective Date
	
	type public String ER, RM
	type String DO

	set ER=0
	
	// Bring Loan to Current Status
	if OPTION=1 do FILE^LNDE1(.ln) quit

	do {
		// Scheduled Payment Date Roll
		if OPTION=2 set DO="LNSCH" quit

		// Payment Billing Process
		if OPTION=3 set DO="LNBLDB" quit

		// Payment Change Offset Process
		if OPTION=4 set DO="LNPTCHG" quit

		// Interest Change Offset
		if OPTION=5 set DO="LNVRCHG" quit

		// Payment Change Date Process
		if OPTION=6 set DO="LNPMTC" quit

		// Interest Change Date Process
		if OPTION=7 set DO="LNINTC" quit

		// Earnings Analysis
		if OPTION=8 set DO="PABOD" quit
		}

	// Invalid option
	if 'DO.data() do Runtime.setErrMSG("LN",411) quit:ER

	// Validate account
	do VALCID^LNBOD(ln.cid,DO,.EFD) 
	
	if ER.get() quit

	// Validate EFD
	do VALEFD^LNBOD(.ln,EFD,DO,) 
	
	if ER.get() quit

	// Execute function
	set ER=0 do FILE^LNBOD(DO,ln.cid,EFD)

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60003^52594^Irina Kin^2900"	// Signature - LTD^TIME^USER^SIZE
