LNAP1CDI	 // ORIG: OYEGUNZ - 10/20/1999
 // DESC: Loan Application File computed data item
 // 
 // ---- Comments --------------------------------------------------------
 //
 /* ---- Revision History ------------------------------------------------

	12/08/03 - CARROLLJ - CR7239
		   Removed dead code in COLBAL section.

	08/24/00 - SKLYUTD - 41494
		   Modified LVR section to consider that application and
		   application collateral records could be in different
		   currency.  Some minor clean up.

	03/21/00 - KESTELMANN - 37656
		   Add code to calculate computed data items COLBAL and
		   PLDGAMT             

	02/04/00 - KESTELMANN - 36841
		   Set tamtreq=areq in section TAMTREQ.               	


	01/13/00 - SKLYUTD - 33756::030
		Added di,didsc,newv,oldv line tags.  Used for the computed data
		items in lnap1hist file.

 	12/15/99 - SKLYUTD - 33756:05
 		Modified section LNM to new variable cif and use Db.getOneRow
 		method instead of Db.getRecord.
 */

	// dummy quit for the compiler
	quit

TAMTREQ(ACN,APPS)
	if 'Db.isDefined("LNAP1","ACN,APPS") quit
	new addamt,appramt,lnap1,refbal,refflg,tamtreq,areq,rpireb,rpiprem
	type RecordLNAP1 lnap1=Db.getRecord("LNAP1","ACN,APPS")
	set addamt=lnap1.addamt
	set areq=lnap1.areq
 //	set tamtreq=areq+addamt	NK - 33756:33
 	set tamtreq=areq
	quit tamtreq

LNM(SECACN)
	new lnm,cif
	//type RecordCIF cif=Db.getRecord("CIF","SECACN")
	//set lnm=cif.lnm
	set lnm=Db.getOneRow("LNM","CIF","SECACN")
	quit lnm

LVR(ACN,APPS,APPNUM)
	// Calculates loan to value ratio
	new rs1,col,crcd,colval,price,sumval,sumprice
	
	new lanp1
	type RecordLNAP1 lnap1
	set lnap1=Db.getRecord("LNAP1","ACN,APPS")
	new areq,addamt,sumappl,lvr
	set areq=lnap1.areq
	set addamt=lnap1.addamt
	//set sumappl=areq+addamt
	set sumappl=areq
	set lncrcd=lnap1.crcd

	type ResultSet rs1

	set rs1=Db.select("COLL,CRCD,COLVAL,PRICE","LNAP1COL","ACN=:ACN AND APPS=:APPS AND APPNUM=:APPNUM")
	if rs1.isEmpty() quit ""

	set (sumval,sumprice)=0
	for  quit:'rs1.next()  do {
		set colcrcd=rs1.getCol(2)
		if colcrcd="" set colcrcd=%SystemCurrency
		set colval=rs1.getCol(3)
		set price=rs1.getCol(4)
		if colcrcd'=lncrcd do {
			if colval'="" do CAMT^CRCDUTL(lncrcd,colcrcd,.colval)
			if price'="" do CAMT^CRCDUTL(lncrcd,colcrcd,.price)
		}
		if price="" s price=colval

		set sumval=sumval+colval
		set sumprice=sumprice+price
	}

	set lvr=$$COLLVR^LNFUNCS(lncrcd,sumappl,sumappl,colcrcd,sumval,sumprice)

	quit lvr

di(tcmt)	// 
	// This will extract the data item name from the string passed in
	// used for LNAP1HIST.
	quit $E($$di^CIFDBS(tcmt),1,30)

didsc(tcmt)
	// DIDSC in lnap1hist file
	quit $E($$didsc^CIFDBS(tcmt),1,510)

newv(tcmt)
	// NEWVAL in lnap1hist file
	quit $E($$newv^CIFDBS(tcmt),1,40)

oldv(tcmt)
	// OLDVAL in lnap1hist file
	quit $E($$oldv^CIFDBS(tcmt),1,40)

Public PLDGAMT(ACN,APPS,APPNUM,COLL,PRCT,FAMT,XLNCOL) //  Collateral Pledged Amount Secured
 
        /*
        The function is used to compute total pledged amount
        in the loan application collateral record [LNAP1COL]PLDGAMT in the
        pledged account's currency.
 
        ARGUMENTS:
		. ACN     Customer number		/TYP=N/REQ/MECH=VAL
		. APPS    Application sequence number   /TYP=N/REQ/MECH=VAL
		. APPNUM  Application number		/TYP=N/REQ/MECH=VA
                . COLL    Collateral Record             /TYP=N/REQ/MECH=VAL
                . PRCT    Percent Pledged               /TYP=N/REQ/MECH=VAL
                . FAMT    Fixed Amount Pledged          /TYP=N/REQ/MECH=VAL
                . LNCOL   Computed from FILE LNAP1COL    /TYP=L/REQ/MECH=VAL
 
        RETURNS:
                . $$ Pledged Amount Secured
 
        EXAMPLE:
                $$PLDGAMT^LNAP1CDI(COLL,10,0)
        */
 
	if 'Db.isDefined("LNAP1COL","ACN,APPS,APPNUM,COLL") quit
	new data,rs,XCOLACN,XCOLTYP,XCOLVAL,XCOLBAL,XSCOLCD,XPLDGCB

	type RecordLNAP1COL lnap1col
	set lnap1col=Db.getRecord("LNAP1COL","ACN,APPS,APPNUM,COLL")

	set XCOLACN=lnap1col.colacn
	set XSCOLCD=lnap1col.scolcd
	set XCOLVAL=lnap1col.colval
	set XCOLBAL=lnap1col.colbal
	set XPLDGCB=lnap1col.pldgcb

        new rs,XCOLTYP
        type ResultSet rs=Db.select("TYPE","UTBLCOLCD","KEY=:XSCOLCD")
        if rs.isEmpty() set XCOLTYP=""
        else  set XCOLTYP=rs.next(),XCOLTYP=rs.getRow()
 
       if XCOLTYP=70 do {

                if XCOLACN set XCOLVAL=XCOLBAL
                if $G(XLNCOL),XPLDGCB=2 set XCOLVAL=Db.getOneRow("PLDGAVL","DEP","XCOLACN")
                }
 
        quit $$^SCARND((XCOLVAL*$G(PRCT)/100),0,,,2)+$G(FAMT)

Public COLBAL(XCID,PLDGCB) // Collateral Account Ledger Balance
 
        /*
        This function is used to compute [LNAP1COL]COLBAL.  This is the
        balance of the collateral account number [LNAP1COL]COLACN.  For Collateral
        Type of 70 (Pledged Account), the field [LNAP1COL]COLBAL will be
        displayed instead of collateral value [LNAP1COL]COLVAL.
 
        ARGUMENTS:
                . CID  Pledged Account Record   /TYP=N/REQ/MECH=VAL
 
        RETURNS:
                . $$ Balance
 
        EXAMPLE:
                $$COLBAL^LNCDI(40000035)
        */
 
        new BAL
        if $G(XCID)="" quit 0
        if 'Db.isDefined("DEP","XCID") quit 0
        if $G(PLDGCB)=1 set BAL=Db.getOneRow("BALCOL","DEP","XCID")
        if $G(PLDGCB)=2 set BAL=Db.getOneRow("BALAVL","DEP","XCID")
        if '$G(PLDGCB) set BAL=Db.getOneRow("BAL","DEP","XCID")
 
        quit BAL

vSIG()	quit "60211^40730^Irina Kin^5192"	// Signature - LTD^TIME^USER^SIZE
