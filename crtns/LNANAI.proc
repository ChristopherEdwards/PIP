LNANAI	//;PBS - LN - V3.6 - Initialize Nonaccrual Processing
	/*
		ORIG:  Chuck Hardy (6721) - 03/09/89
		DESC:

		INPUT:
		OUTPUT:

		Called by function LNU013 to initialize DAYENDLNNAO for a loan
		product type for the first time.

	  ------Revision History ------------------------------------------------
		03/07/05 - Hillanbrand - 14587
		Corrected %TAB for GRP and scoped throughout. Additional changes
		per code review.

		09/20/02 - SRIVASTAVAN - 49451
		Converted to PSL 

		11/06/00 - TANY - 42122
	        Fixed Undefine variable in TYPE and
	        replaced select^sql with $P(^global) in M routines.

	 ----------------------------------------------------------------------
	*/
	type Number %PAGE, %PG	

	set (%PG,%PAGE)=1

	do VPG

	quit


VPG	// Page control

	type public Number %PG
	type public String VFMQ
	
	type Boolean FINISH
	
	
	set FINISH=0
	for  do { quit:FINISH
		if %PG=1 do VPG01 
		if "DFQ"[VFMQ do VER set FINISH=1 quit
		set %PG=%PG+1
		}
	quit


VPG01	// Screen

	type Number OLNTB
	type String %NOPRMT, %READ, %TAB()
	

	set %NOPRMT="F"
	
	do DRV^USID(2,"LNA")
	
	set OLNTB=10030
	set %TAB("GRP")=".GRP1/TBL=[STBLGRP]GRP:QU ""[STBLGRP]CLS=L"""
	set %TAB("TYPE")=".TYPE2/TBL=[LN]TYPE:DISTINCT:QU ""[LN]GRP=<<GRP>>""/XPP=D TYPE^LNANAI"
	set %READ=",,GRP/REQ,TYPE/REQ"

	do ^UTLREAD

	quit


VER 	//

	type public String VFMQ

	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit
	
	do FILE
	
	do END 
	
	quit


FILE	// File data

	type public Number ANA

	type DbSet rs=Db.selectDbSet("LN","GRP=:GRP AND TYPE=:TYPE AND STAT<>4","GRP,TYPE")
 	while rs.next() do { 
		type RecordLN ln=rs.getRecord("LN")

		if %SystemDate-ln.dist1nd<(ln.anpp-CUVAR.anaoff+1) quit
		
		type RecordDAYENDLNNAO delnnao=Class.new("RecordDAYENDLNNAO")
		set delnnao.ejd=%SystemDate+CUVAR.anaoff
		set delnnao.cid=ln.cid
		set delnnao.auto=ANA
		do delnnao.bypassSave()
		}
	quit


END	//

	type public Number TYPE, X
	type public String ER, RM, VFMQ

	if ER!(%ProcessMode=2)!(%ProcessMode=4) quit
	
	// Non-accrual accounting for product type ~p1 not established
	if VFMQ="Q" set RM=$$^MSG(2005,$G(TYPE))
	
	// Non-accrual accounting for product type ~p1 established
	else  set RM=$$^MSG(2004,TYPE)
	
	set ER="W"
	quit


TYPE	// Check product to see if it has AUTOMATIC NONACCRUAL flag defined
	
	type Public Number ANA, ER, X
	type Public String RM

	type RecordPRODCTL prodctl=Db.getRecord("PRODCTL","TYPE=:X")

	// Product type does not have automatic non-accrual flag set
	if prodctl.ana.isNull() set ER=1,RM=$$^MSG(2249)
	set ANA=prodctl.ana
	quit

vSIG()	quit "59966^60783^Laura Hillanbrand^2377"	// Signature - LTD^TIME^USER^SIZE
