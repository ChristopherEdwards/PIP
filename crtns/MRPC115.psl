MRPC115(RETURN,VERSN,TRNXID,MODE,SRVMSG)
	/*
	 PROCEDURE ID: MRPC115
	 ORIG: SCHEETZE - 04/24/2001
	 DESC: Two Phase Transaction Commit Driver

	---- Comments --------------------------------------------------------

	ARGUMENTS:

	. RETURN	Return Value

	. VERSN		Version Number			/TYP=N/REQ/MECH=REF
			Current version number = 1.

	. TRNXID	Transaction ID			/TYP=T/REQ/MECH=VAL

	. MODE		Processing Mode			/TYP=N/REQ/MECH=VAL
			  1 = Prepare Transaction
			  2 = Commit Transaction
			  3 = Rollback Transaction

	. SRVMSG	Server Message			/TYP=T/NOREQ/MECH=VAL
	
	---- Revision History ------------------------------------------------
	
	08/03/07 - Pete Chenard - CR28605
		   Modified call to Runtime.start to add quotes around the
		   savepoint parameter.
	
	11/07/05 - Sreeram Panyaram
		   Initialized Unscoped Variables for DBI3.
		   
	11/10/03 - CARROLLJ - 51630
		   Correct call to context^PBSTSSP.

	11/15/01 - CARROLLJ - 48179
		   Retrofit UCS to 6.1.


	06/11/01 - Erik Scheetz - 45837
		   Added code in the COMMIT section to pass context
		   (vzcontxt) to the TSSP driver.
	*/

	type Public Boolean ER
	type String vzerr
	
	set ER=0
	set RETURN=""

	// Version number of client message is not compatible with server
	if 'VERSN.get()=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))
	
	set MODE=+MODE.get()
	
	if MODE<1!(MODE>3) quit $$ERRMSG^PBSUTL($$^MSG(4960))
	if MODE=1,Db.isDefined("TRNMSGHDR","TRNXID") quit $$ERRMSG^PBSUTL($$^MSG(4961))

	if MODE=1 set vzerr=$$PREPARE() 
	if MODE=2 set vzerr=$$COMMIT(.RETURN)
	if MODE=3 set vzerr=$$ROLLBACK()

	quit vzerr


PREPARE()	// Prepare Transactions

	
	type Public String CS,SRVMSG,TRNXID,vzcontxt
	type String HDR,MSG,PTR,REC,vzerror,vzrm,valarr
	type Number CIDLIST,i,valcid,XCID

	// TSSP records not present
	if SRVMSG.isNull() quit $$ERRMSG^PBSUTL($$^MSG(2761))

	// Separate Header and Message
	set PTR=$$LV2V^MSG(SRVMSG,.REC)

	// Parse Header
	set PTR=$$LV2V^MSG(REC(1),.HDR)

	// Only allow TSSP message
	if 'HDR(1).get()=1 quit $$ERRMSG^PBSUTL($$^MSG(2761))

	// set vzcontxt from PBSMRPC.
	set vzcontxt=vzcontxt_"/TSSPPREPARE=1"

	// Process Transactions and Rollabck Updates
	set vzerror=$$^PBSTSSP(.vzrm,0,REC(2),1,.vzcontxt)
	do Runtime.rollback()

	// Quit if processing error
	if vzerror quit vzrm.get()

	do CONTEXT^PBSTSSP(vzcontxt,.valarr)
	set CIDLIST=valarr("CIDLIST").get()
	set valcid=0
	for i=1:1:CIDLIST.length(",") do {  quit:valcid
		set XCID=CIDLIST.piece(",",i)
		if Db.isDefined("TRNMSGCID","XCID") set valcid=1
		}

	if valcid quit $$ERRMSG^PBSUTL($$^MSG(63))

	do Runtime.start(CS,"","i")

	// Update transaction message header table
	type RecordTRNMSGHDR tmhdr=Class.new("RecordTRNMSGHDR")
	set tmhdr.trnxid=TRNXID
	set tmhdr.trndate=%CurrentDate
	set tmhdr.trntime=%CurrentTime
	set tmhdr.srvcls=HDR(1)
	set tmhdr.cidlist=CIDLIST

	do tmhdr.bypassSave()

	// Update transaction message detail table
	type RecordTRNMSGDTL tmdtl=Class.new("RecordTRNMSGDTL")
	set tmdtl.trnxid=TRNXID
	for i=1:400:REC(2).length() do {
		set tmdtl.msgseq=i\400+1
		set tmdtl.msgrec=SRVMSG.extract(i,i+399)
		do tmdtl.bypassSave()
		}

	// Update Transaction message account table
	type RecordTRNMSGCID tmcid=Class.new("RecordTRNMSGCID")
	set tmcid.trnxid=TRNXID
	for i=1:1 set tmcid.cid=CIDLIST.piece("~",i) quit:tmcid.cid=""  do tmcid.bypassSave() 
 	
	do Runtime.commit()
	quit ""


COMMIT(return)	// Commit Transactions

		
	type Public String REC,vzcontxt,vzrecord,%TOKEN
	type String ET,msg,PTR,vzerror,vzrm
	type Number i

	if 'Db.isDefined("TRNMSGHDR","TRNXID") quit $$ERRMSG^PBSUTL($$^MSG(4962))
 	
	// Load original message
	type ResultSet rs=Db.select("MSGREC","TRNMSGDTL","TRNXID=:TRNXID")
	if rs.isEmpty() quit $$ERRMSG^PBSUTL($$^MSG(4962))

	set msg=""
	while rs.next() set msg=msg_rs.getCol(1)

	set PTR=$$LV2V^MSG(msg,.REC)

	// Process Transactions (in store and forward mode)
	set vzcontxt=vzcontxt_"/TSSPCOMMIT=1"
	set vzerror=$$^PBSTSSP(.vzrm,1,REC(2),0,vzcontxt)
	if vzerror do {  quit vzrm
		do Runtime.rollback()
		
		type String vzcltokn,vzpkt,vzrec
		
		set vzcltokn=%TOKEN
		set vzrec=vzrecord
		set vzpkt=msg

		// Log error
		set ET="TPC_FAIL"
		do ^UTLERR

		// Save original TSSP message
		set vzerror=$$MSGEXC^PBSSRV(vzrm)

		do REMOVE

		}

	// Remove Transaction Message related tables
	
	do REMOVE
	set return=vzrm
	quit ""

ROLLBACK()	// Rollback Transaction

	do REMOVE

	quit ""


REMOVE	// Remove Transaction Message related tables

	type public String TRNXID
	type Number CID,cidlist,i

	// Remove TRNMSGHDR entry
	
	type RecordTRNMSGHDR trnmsghdr=Db.getRecord("TRNMSGHDR","TRNXID",1)
		if trnmsghdr.getMode() do {
			set cidlist=trnmsghdr.cidlist
			for i=1:1 set CID=cidlist.piece("~",i) quit:CID=""  do Db.delete("TRNMSGCID","CID=:CID")
		}

	do Db.delete("TRNMSGHDR","TRNXID=:TRNXID")
	do Db.delete("TRNMSGDTL","TRNXID=:TRNXID")
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60845^39957^Pete Chenard^4606"	// Signature - LTD^TIME^USER^SIZE
