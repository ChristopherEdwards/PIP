SRVITMTR	// Service Item Fee On-Line Processing
	/*
	  ORIG:  Mindy Gill (8358) - 08/25/92
	  DESC:  This routine has code associated with Service ITEM Fee
	         transactions, for example transaction code SRVPMT for
	         Service Item Fee Payment.

	-------Revision History-----------------------------------------------

	11/14/02 - ZWITKOWITSM - 43583
		   PSL clean-up.

	06/13/02 - ZWITKOWITSM - 43583
		   Converted to PSL.

	-----------------------------------------------------------------------
	*/

	quit


Public FILE(RecordTTX ttx)	// Build OVR array and File

	new EC,OLDAMT,OLDBAL,SRVCAT,SRVID,SRVLOC,SRVTYP,TAMT,UTSO

	do OUT^UTSO(.UTSO,ttx.tso)

	set SRVCAT=$G(UTSO("SRVCAT"))
	if SRVCAT="" quit
	set SRVTYP=$G(UTSO("SRVTYP"))
	if SRVTYP="" quit
	set SRVLOC=$G(UTSO("SRVLOC"))
	if SRVLOC="" quit
	set SRVID=$G(UTSO("SRVID"))
	if SRVID="" quit

	type RecordSRVITM srvitm=Db.getRecord("SRVITM","SRVCAT,SRVTYP,SRVLOC,SRVID")	

	// Must exist to update
	if srvitm.getMode()=0 quit

	set TAMT=+ttx.tamt

	// Error correction or Reversal
	if ttx.itc6!ttx.itc12 set EC=1
	else  set EC=0

	if 'EC,TAMT>srvitm.srvamt set OVR("*","OVR","PMTEXCDS")=""

	// Update Service Item ID file - [SRVITM] and Service Item History file - [SRVHIST].

	// Amount Due
	set OLDAMT=srvitm.srvamt
	// Credit Balance
	set OLDBAL=srvitm.balavl

	// Credit
	if ttx.itc1 do {

		if TAMT>srvitm.srvamt do {
			set srvitm.balavl=srvitm.balavl+(TAMT-srvitm.srvamt)
			set srvitm.srvamt=0
			}

		else  set srvitm.srvamt=srvitm.srvamt-TAMT
		}

	// Debit
	else  set srvitm.srvamt=srvitm.srvamt+TAMT

	// Credit balance < 0
	if srvitm.balavl<0 do {
		set srvitm.srvamt=srvitm.srvamt-srvitm.balavl
		set srvitm.balavl=0
		}

	// Amount Due < 0
	if srvitm.srvamt<0 do {
		set srvitm.balavl=srvitm.balavl-srvitm.srvamt
		set srvitm.srvamt=0
		}

	// Get one value only, or credit balance or amount due
	if srvitm.balavl,srvitm.srvamt do {

		if srvitm.balavl>srvitm.srvamt do {
			set srvitm.balavl=srvitm.balavl-srvitm.srvamt
			set srvitm.srvamt=0
			}

		else  do {
			set srvitm.srvamt=srvitm.srvamt-srvitm.balavl
			set srvitm.balavl=0
			}
		}

	do srvitm.bypassSave()

	if srvitm.balavl'=OLDBAL do SRVHIST("BALAVL",OLDBAL,srvitm.balavl)
	if srvitm.srvamt'=OLDAMT do SRVHIST("SRVAMT",OLDAMT,srvitm.srvamt)

	quit


SRVHIST(DI,OLD,NEW)

	type RecordSRVHIST srvhist=Class.new("RecordSRVHIST")

	set srvhist.srvcat=SRVCAT
	set srvhist.srvtyp=SRVTYP
	set srvhist.srvloc=SRVLOC
	set srvhist.srvid=SRVID
	set srvhist.seq=Db.nextVal("SRVHIST","SRVCAT,SRVTYP,SRVLOC,SRVID")
	set srvhist.tjd=%SystemDate
	set srvhist.tlo=%UserStation
	set srvhist.tcmt=$$TCMTFM^ACNFUNCS("","SRVITM",DI,OLD,NEW,%EffectiveDate)
	set srvhist.time=%CurrentTime
	set srvhist.uid=%UserID

	do srvhist.bypassSave()

	quit


Public EC(RecordTTX ttx)	// Error correct miscellaneous transaction fee

	do FILE(.ttx)

	quit

vSIG()	quit "59886^43611^Sanchez SCM Administrator^2700"	// Signature - LTD^TIME^USER^SIZE
