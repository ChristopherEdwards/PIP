SCA807	 /*
	Procedure ID: SCA807
	ORIG: Varsha Malhotra - 10/02/2001
	DESC: Build Growth Bonus Report

	This procedure is created to send data to the Growth Bonus Report 
	SCA807.


	---- Revision History ------------------------------------------------
	12/29/03 - CARROLLJ - CR7658
		   Removed dead code from CPLAN section.

	10/02/01 - MALHOTRAV - 47733:01
		   Created

	11/07/01 - MALHOTRAV - 47733:02
		   Added quit if commission plan doesn't have a portfolio 
		   linked. In section CPLAN added code to set variable
		   CHECK and also added a call to section CPLAN in the 
		   prompts and queries of the report SCA807. These 
		   modifications are made to fix an undefined error if the 
		   commission plan does not have a portfolio linked.

 */
	do KILLG

	new agntcom,AGENT,CPLAN,compst,compln,pft,pstdt,sca807,sca807a,wfct
	type RecordSCA807 sca807
	type RecordSCA807A sca807a
	type ResultSet compst

	set compst=Db.select("PSTDT,AGENT,CPLAN,CID,SEQ,AMT,CRCD,RATE","DAYENDCOMPST","PSTDT>:FROM AND PSTDT<:TO")
	if compst.isEmpty() quit

	while compst.next() do {
		set PSTDT=compst.getCol(1)
		set AGENT=compst.getCol(2)
		set CPLAN=compst.getCol(3)
		
		if CHECK'="ALL",CPLAN'=CHECK quit

		set CID=compst.getCol(4)
		set SEQ=compst.getCol(5)
		set AMT=compst.getCol(6)
		set CRCD=compst.getCol(7)
		set RATE=compst.getCol(8)
		set agncy=Db.getOneRow("AGENCY","AGENT","AGENT")
		set agntcom=Db.getOneRow("ECOMAMT,COMPYTD","AGENTCOM","AGENT,CPLAN")
		set ECOMAMT=agntcom.piece($C(9),1)
		set COMPYTD=agntcom.piece($C(9),2)
		set compln=Db.getOneRow("GTBL,PFT","UTBLCOMPLN","CPLAN")
		set GTBL=compln.piece($C(9),1)

		set PFT=compln.piece($C(9),2) 
		quit:PFT="" 
		
		type ResultSet pft=Db.select("QEBALLQ","AGENTPFT","AGENT=:AGENT AND PFT=:PFT")   
		if pft.isEmpty() quit

		while pft.next() do {
			set sca807=Class.new("RecordSCA807")
			set QEBALLQ=pft.getCol(1)

			// Keys
			set sca807.agent=AGENT
			set sca807.pstdt=PSTDT
			set sca807.cplan=CPLAN
			set sca807.cid=CID
			set sca807.seq=SEQ

			set sca807.agency=agncy
			set sca807.amt=AMT
			set sca807.crcd=CRCD
			set sca807.rate=RATE
			set sca807.ecomamt=ECOMAMT
			set sca807.compytd=COMPYTD
			set sca807.gtbl=GTBL
			set sca807.pft=PFT
			set sca807.qeballq=QEBALLQ

			do sca807.bypassSave()

			type ResultSet pftq=Db.select("WPFT,WPQRYNUM,WPQRYNAM,WPAGNFAC,QRYBAL","AGENTPFTQ","AGENT=:AGENT AND PFT=:PFT")

			// If no weighted portfolio is linked 
			if pftq.isEmpty() do FILLBLNK quit

			while pftq.next() do {
				set sca807a=Class.new("RecordSCA807A")
				set WPFT=pftq.getCol(1)
				set WPQRYNUM=pftq.getCol(2)
				set WPQRYNAM=pftq.getCol(3)
				set WPAGNFAC=pftq.getCol(4)
				set QRYBAL=pftq.getCol(5)

				set wfct=Db.getOneRow("WFCT","UTBLWPFTQ","WPFT,WPQRYNUM")

				// Keys
				set sca807a.agent=AGENT
				set sca807a.pstdt=PSTDT
				set sca807a.cplan=CPLAN
				set sca807a.cid=CID
				set sca807a.seq=SEQ
				set sca807a.wpft=WPFT
				set sca807a.wpqrynum=WPQRYNUM

				set sca807a.wpqrynam=WPQRYNAM
				set sca807a.wpagnfac=WPAGNFAC

				/* if override weighing factor is not defined 
				use weighing factor */
				if WPAGNFAC="" set sca807a.wpagnfac=wfct
				set sca807a.qrybal=QRYBAL

				do sca807a.bypassSave()
				}
			}
		}

	kill CHECK
		
	quit

	//-----------------------------------------------------------------
CPLAN		/* Post processor for the Query / Prompts. 
		Error message if commission plan is not linked to agent.
		*/ 
	//------------------------------------------------------------------

	// For use when building ^SCA807
	set CHECK=X
	
	// Agent or commission plan not specified
	if QI(1)="ALL"!(X="ALL") quit

	// Commission Plan is defined
	if Db.isDefined("AGENTCOM","AGENT=:QI(1),CPLAN=:X") quit

	kill RM

	// Commission plan ~p1 for ~p1 not defined
	set ER=1,RM=$$^MSG(3378,X_" "_$$^MSG(5507,QI(1)))

	quit

	//-------------------------------------------------------------------
FILLBLNK	// If no weighted portfolio is linked to portfolio
	//--------------------------------------------------------------------
	type RecordSCA807A sca807a
	set sca807a=Class.new("RecordSCA807A")

	// Keys
	set sca807a.agent=AGENT
	set sca807a.pstdt=PSTDT
	set sca807a.cplan=CPLAN
	set sca807a.cid=CID
	set sca807a.seq=SEQ
	set sca807a.wpft=$C(255)		// Null character
	set sca807a.wpqrynum=$C(255)		// Null character

	set sca807a.wpqrynam=""
	set sca807a.wpagnfac=""
	set sca807a.qrybal=""
	set sca807a.wfct=""

	do sca807a.bypassSave()

	quit

	//--------------------------------------------------------------------
KILLG		// Kill Global
	//--------------------------------------------------------------------

	do Db.fastDelete("SCA807")	// Delete the temprory file
	quit	
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43604^Sanchez SCM Administrator^4507"	// Signature - LTD^TIME^USER^SIZE
