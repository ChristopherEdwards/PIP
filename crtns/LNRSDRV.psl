LNRSDRV	  /*
	ORIG: gray - 08/12/2003
	DESC: Loan Provision File Regen and Deletion
 
	---- Comments --------------------------------------------------------
 
	---- Revision History ------------------------------------------------
 
	03/25/05 - TITOVE - CR 15089
		   Modified as part of DBI2 project.

	08/12/03 - GRAY - ARQ# 51351
		   M routine was obsoleted in error.  Placed code that
		   needed to remain for native functions @PROVDEL and
		   @PROVS02 into PSL'd procedure definition.  Originally
		   reported under CR#'s 5535 and 5537.
	*/
 
	quit
 

public	LNRS02  // Loan Provision Regeneration
 
	type Boolean OFFPER=0
	type Date PROVNPDT,REPOST,RERUN
	type Number PROVOFF
	type String %READ,%TAB,VFMQ
	type public Boolean %EXT
	type public Number ER=0

	type RecordCUVAR cuvar=Db.getRecord("CUVAR")

	set PROVOFF=cuvar.provoff
	set PROVNPDT=cuvar.provnpdt
	set RERUN=cuvar.provrcdt
	set REPOST=cuvar.provrpdt
 
	if %SystemDate>(PROVNPDT-PROVOFF),%SystemDate<PROVNPDT set OFFPER=1
 
	set %TAB("RERUN")=".RERUN/XPP=do RERUNPP^LNRSDRV"
	set %TAB("REPOST")=".REPOST/XPP=do REPSTPP^LNRSDRV"
	set %READ="@@%FN,,RERUN,REPOST"
	if '%EXT.get() do ^UTLREAD
	if "Q"[VFMQ quit

 
	if OFFPER do {
		set cuvar.provrcdt=RERUN
		set cuvar.provrpdt=PROVNPDT
		}
 
	else  do {
		set cuvar.provrcdt=RERUN
		set cuvar.provrpdt=REPOST
		}
 
	do cuvar.bypassSave()
 
	quit

 
RERUNPP // Check to make sure that the Re-Calculation date is eqaul to or
	// greater than the system date+1.

	type public Boolean OFFPER
	type public Date PROVNPDT, REPOST, X
	type public Number ER = 0
	type public String %MSKD
 
	if X.isNull() quit
 
	// Must be greater than ~p1
	if X < (%SystemDate + 1) do Runtime.setErrMSG("DAYENDLNRP",1810,%SystemDate.toString(%MSKD.get())) quit:ER
 
	if OFFPER set REPOST = PROVNPDT
 
	quit
 
 
REPSTPP // Check the REPOST date.
	/*
	If rerun date is within the offset period then the REPOST must equal
	the next posting date.  If the rerun date is outside of the offset
	period the REPOST date must be equal to or greater than the RERUN.
	*/
 
	type public Boolean OFFPER
	type public Date PROVNPDT, RERUN, X
	type public Number ER
	type public String %MSKD

	// Must be greater than or equal to ~p1
	if OFFPER,(X '= PROVNPDT) do Runtime.setErrMSG("DAYENDLNRP",8690,PROVNPDT.toString(%MSKD.get())) quit:ER
 
	else  if (X < RERUN) do Runtime.setErrMSG("DAYENDLNRP",8690,PROVNPDT.toString(%MSKD.get())) quit:ER
 
	quit
 
 
public	DEL     // Delete a pending provision file.

	type public Boolean %EXT
	type public String ER, RM, %MSKD

	type Date LPDATE1
	type String %READ, %TAB, VFMQ
 
	set %TAB("LPDATE1") = ".LPDATE1/XPP=do DATEPP^LNRSDRV/REQ=1"

	set %READ = "@@%FN,,LPDATE1"

	if '%EXT.get() do ^UTLREAD
	
	if VFMQ = "Q" quit
 
	do Db.delete("DAYENDLNRP", "PROVNPDT = :LPDATE1")

	// File ~p1 deleted
	set ER = "W",RM = $$^MSG(1095,"DAYENDLNRP")

	quit
 
 
DATEPP  // Verify that there is a file for the date entered.

	type public Date X
	type public Number ER
	type public String RM, %MSKD 
	
	type ResultSet rs = Db.select("CID", "DAYENDLNRP", "PROVNPDT = :X")
	
	// Invalid date ~p1
	if rs.isEmpty() do Runtime.setErrMSG("DAYENDLNRP",1308,X.toString(%MSKD.get())) quit:ER
 
	type RecordCUVAR cuvar = Db.getRecord("CUVAR")

	// ~p1 source already posted
	if (X '> cuvar.provlpdt) do Runtime.setErrMSG("DAYENDLNRP",3080,X.toString(%MSKD.get())) quit:ER

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60011^49840^Eugene Titov^3227"	// Signature - LTD^TIME^USER^SIZE
