SRVPLN1	//Copy services plan to new service plan
	/*

	---- Revision History ------------------------------------------------
	
	08/23/05 - RussellDS - CR16911
		   		   
		   Rewrite to get it to work and clean up code.
		   
		   Removed old revision history.
	*/
	
	quit
	

CPY	// Copy

	type public String ER, RM

	type Date FEEDT, DEEDTNW
	type String %READ, %TAB(), ET, PLAN, PLANNW, UTBL(,), VFMQ

	set %ProcessMode = 0


	set %TAB("PLAN") = ".PLAN10/HLP=[DEP]FEEPLN/TBL=[FEEPLN]PLAN:DISTINCT"
	set %TAB("FEEDT") = ".EFD1/TBL=[FEEPLN]FEEDT:QU ""[FEEPLN]PLAN=<<PLAN>>"""
	set %TAB("PLANNW") = ".PLANNW1/HLP=[DEP]FEEPLN/XPP=D POSPLN^SRVPLN1"
	set %TAB("FEEDTNW") = ".EFD1/XPP=D CHKDT^SRVPLN1"

	set %READ = "@@%FN,,,PLAN/REQ,FEEDT/REQ,,PLANNW/REQ,FEEDTNW/REQ"
	do ^UTLREAD quit:VFMQ = "Q"

	set UTBL("FEEPLN", PLAN) = ""
	// Record locked by another user
	lock +UTBL("FEEPLN",PLAN):2 else  set ET="RECLOC" do ^UTLERR quit
	// Record locked by another user
	lock +UTBL("FEEPLN",PLANNW):2 else  do { quit
		
		lock -UTBL("FEEPLN",PLAN)
		set ET="RECLOC"
		do ^UTLERR
	}
	
	do FILE quit:ER
	
	// Service plan ~p1 copied to plan ~p2
	set RM=$$^MSG(2489, PLAN, PLANNW)
	set ER="W"

	quit


POSPLN	//

	type public Boolean ER
	type public String RM, X

	if 'X.isNull(), X?.E1P.E do {
	
		set ER = 1
		// Punctuation characters not allowed
		set RM=$$^MSG(2281)
	}

	quit


CHKDT	//

	type public Boolean ER
	type public String RM, X

	quit:X.isNull()
	
	if (X < %SystemDate) do {
		
		set ER=1
		// Effective date must be the same as or after the system date
		set RM=$$^MSG(879)
	}
	
	else  if Db.isDefined("FEEPLN","PLAN=:PLANNW,FEEDT=:X") do {
		
		set ER=1
		// Plan already on file
		set RM=$$^MSG(2185)
	}

	quit


FILE	// File data

	type public Date FEEDT, FEEDTNW
	type public String PLAN, PLANNW
	
	type String FEECAT, FEETYP, QUERY
	
	type RecordFEEPLN feepln = Db.getRecord("FEEPLN","PLAN=:PLAN,FEEDT=:FEEDT")

	type RecordFEEPLN newfeepln=feepln.copy()
	set newfeepln.plan = PLANNW
	set newfeepln.feedt = FEEDTNW
	set newfeepln.cmpdate = ""
	set newfeepln.cmpuid = ""
	do newfeepln.setMode(0)
	do newfeepln.save()

	type ResultSet rs = Db.select("FEECAT,FEETYP","FEESRV","PLAN=:PLAN AND FEEDT=:FEEDT")
	
	while rs.next() do {
		set FEECAT=rs.getCol("FEECAT")
		set FEETYP=rs.getCol("FEETYP")
	
		type RecordFEESRV feesrv=Db.getRecord("FEESRV","PLAN=:PLAN,FEEDT=:FEEDT,FEECAT=:FEECAT,FEETYP=:FEETYP")

		type RecordFEESRV newfeesrv=feesrv.copy()
		set newfeesrv.plan = PLANNW
		set newfeesrv.feedt = FEEDTNW
		set newfeesrv.feecat = FEECAT
		set newfeesrv.feetyp = FEETYP
		do newfeesrv.setMode(0)
		do newfeesrv.save()
	}

	type ResultSet rs2=Db.select("FEECAT,FEETYP,QUERY","FEEQRY","PLAN=:PLAN AND FEEDT=:FEEDT")
	
	while rs2.next() do {
		set FEECAT=rs2.getCol("FEECAT")
		set FEETYP=rs2.getCol("FEETYP")
		set QUERY=rs2.getCol("QUERY")

		type RecordFEEQRY feeqry=Db.getRecord("FEEQRY","PLAN=:PLAN,FEEDT=:FEEDT,FEECAT=:FEECAT,FEETYP=:FEETYP,QUERY=:QUERY")

		type RecordFEEQRY newfeeqry=feeqry.copy()
		set newfeeqry.plan = PLANNW
		set newfeeqry.feedt = FEEDTNW
		set newfeeqry.feecat = FEECAT
		set newfeeqry.feetyp = FEETYP
		set newfeeqry.query = QUERY
		do newfeeqry.setMode(0)
		do newfeeqry.save()
		}
	
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60139^42764^Dan Russell^3039"	// Signature - LTD^TIME^USER^SIZE
