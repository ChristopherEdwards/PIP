MRPC048(return,versn,OACN,NACN,MAIL,cidlist)
  /*
	ORIG:	Neal Gorman - 02/18/97
	DESC: Transfer Accounts Between Customers
	
	Utility is used to transfer deposit and loan accounts (cidlist) from
	one customer (OACN) to a second (NACN).  The user can indicate
	(MAIL) to where future mailings should be sent.
	
	[SCATBL5]RPCID=48
	
	KEYWORDS: Client/Server, Customer Utilities
	
	ARGUMENTS:
		. return					/TYP=T/REQ
								/MECH=REFNAM:W
	
 		. versn		^MRPC048 version number		/TYP=N/REQ
				Current version = 1		/MECH=VAL
	
		. OACN		From customer account		/TYP=N/REQ
								/MECH=VAL
	
		. NACN		To customer account		/TYP=N/REQ
								/MECH=VAL
	
		. MAIL		Mailing address option		/TYP=N/NOREQ
								/MECH=VAL/DEF=3
				1  No change to mailing address
				2  Use destination CIF legal address
				3  Use destination CIF mailing address
	
		. cidlist	List of accounts to change	/TYP=T/REQ
								/MECH=VAL
	
	
	RETURNS:
		. $$		Error message			/TYP=T
				Null = No error
	
    	RELATED:
		. $$^PBSMRPC - MRPC Service Class Driver
	

---- Revision History ------------------------------------------------
	
	02/28/02 - GRAY - 43583
		   Converted from M to PSL.
	

 */
	
	new cnt,CID,CIFTA,ERRDSC,error,OK2PROC
	
	// Version number of client message is not compatible with server
	if $G(versn)'=1 quit $$ERRMSG^PBSUTL($$^MSG(2951))
	
	if $G(MAIL)="" set MAIL=3
	
	// Build array CIFTA as used by routine ^CIFTA
	for cnt=1:1 set CID=$P(cidlist,",",cnt) quit:CID=""  do {
	
		// Initialize indicator of whether OK to process, and error type
		set OK2PROC=1,error=""
	
		if 'Db.isDefined("ACN","CID") do {
			set OK2PROC=0
			set error=$S($G(RM)'="":RM,1:$$^MSG(2341,CID))
			}
	
		set $P(CIFTA(cnt),"|",1)=OK2PROC
		set $P(CIFTA(cnt),"|",5)=CID
 		set $P(CIFTA(cnt),"|",9)=error
		}
	
	// Process accounts
	do FILE^CIFTA(0)
	
	// Build array of error descriptions from processing routine
	do ERRDSC^CIFTA
	
	// Build output message.  Records which failed are returned to client
	set (cnt,return)=""
	for  set cnt=$O(CIFTA(cnt)) quit:cnt=""  do {
		new cid,out,error
	
		// Ignore if processed successfully
		if $P(CIFTA(cnt),"|",1) quit
		
		set error=$P(CIFTA(cnt),"|",9)
		if $G(ERRDSC(error))'="" set error=ERRDSC(error)
		
		set cid=$P(CIFTA(cnt),"|",5)
		set out=cid_$C(9)_error
		
		set return=return_out_$C(13,10)
		}
	
	set return=$$V2LV^MSG(return)
	
 	quit ""	

vSIG()	quit "59886^43585^Sanchez SCM Administrator^2230"	// Signature - LTD^TIME^USER^SIZE
