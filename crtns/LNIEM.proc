public LNIEM(RecordLN ln)	// Find remaining term of IEM loan
	/*
	       ORIG:  Chuck Hardy (6721) - 11/15/86

	---- Revision History ------------------------------------------------
	10/13/05 - S.Krishnan - CR16885
		   Top of the procedure is made as Public.

        07/26/05 - SkariahV- CR16679
	           Removed #WARN and #OPTIMIZE directives.
	              	
	12/12/03 - CARROLLJ - CR7239
		   Corrected precedece errors in P3 linetag.

	06/20/03 - GRAY - 51351
		   Corrected error, PSL warning, unscoped variable,I.

	03/27/03 - GRAY - 51351
		   Converted to PSL.

	*/
	

	type Number A,AF,B,BN,C,CAF,FV,IR1,NUMCH,ONP,PPINC,PT,Z

	set (A,C)=1
	set BN=0

	// Distribution 1 Annual Factor
	set AF=ln.dist1af

	// Payment Percentage Increase
	set PPINC=ln.ppinc
	set PPINC=PPINC/100

	set ONP=+ln.ambas

	// Number of Payments Per Change
	set CAF=ln.caf
	set IR1=(+ln.irn)/(AF*100)

	// Principal and Interest Payment
	set PT=ln.pmtpi

	// Number of Payment Changes
	set NUMCH=ln.numch
	set FV=+ln.bal

	for  set B=A+CAF-1 do { quit:FV<0
		if C=(NUMCH+1) set B=ONP
		set Z=B-A+1 do P1(.ln)

		if FV<0 quit
		set PT=$J((PT*(1+PPINC))+.0099,0,2) 
		set PT(C)=PT 
		set C=C+1
		set A=B+1
		}
	quit


P1(RecordLN ln)	// 
	
	/*
	Calculate the ending balance at each payment change if IEM, stop
	calculations at the point where the balance goes negative
	*/

	type Number F,I,J,N
	type Public Number BN,CAF,FV,XFV,Z

	if Z'>CAF do P3(.ln) set BN=BN+CAF quit

	set N=Z\CAF set F=Z#CAF

	for J=1:1:N set Z=CAF set BN=BN+CAF do P3(.ln) quit:FV<0

	if FV<0 do {
		set FV=XFV 
		set BN=BN-CAF 
		for I=1:1 set Z=1 set BN=BN+1 do P3(.ln) quit:FV<0
		}

	if FV<0 set ln.ambas=BN quit

	if F for I=1:1:F set BN=BN+1 set Z=1 do P3(.ln) quit:FV<0

	if FV<0 set ln.ambas=BN quit

	quit


P3(RecordLN ln)	//

	type Public Number BN,CAF,CID,FV,PT,Z
	type Number EXP,I,IR1,POW
	type Public Number XFV

	//set IR1=+ln.irn/(ln.dist1af*100) 
	set IR1=(+ln.irn)/(ln.dist1af*100) 
	set EXP=1+IR1 
	set POW=EXP 
	set XFV=FV

	for I=2:1:Z set EXP=EXP*POW
	//set FV=$$^SCARND(((1-EXP/IR1)*PT)+(FV*EXP),0,CID.get())
	set FV=$$^SCARND((((1-EXP)/IR1)*PT)+(FV*EXP),0,CID.get())

	quit

vSIG()	quit "60186^23900^S. Krishnan^1993"	// Signature - LTD^TIME^USER^SIZE
