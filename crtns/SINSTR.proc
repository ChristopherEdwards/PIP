SINSTR	//;Private;Customer Settlement Instructions Routine
	/*

	   Origin: Matt Lessig 04/01/93
	   Description: Customer Settlement Instructions Routine
	   		This routine is used for the creation and maintenance of
	   		customer settlement instructions.
	   Inputs:
	         . System        %FN,%LIBS,%UID

	   Returns:
	         . ER    Error indicator         /TYPE=T
	                 Returns ER=1 if an error, otherwise ER="W"

	         . RM    Return message          /TYPE=T
	                 Returns error message if ER=1, otherwise
	                 completion message

	  ---- Revision History------------------------------------------------

	   10/07/02 - CHHABRIAS - 49451
		      Converted to PSL  
		
	  --------------------------------------------------------------------
	*/


NEW 	//

	do INIT(0)
	quit


UPD 	//

	do INIT(1)
	quit


DIS 	//

	do INIT(2)
	quit


DEL	//

	do INIT(3)
        quit


INIT(%ProcessMode)	//

	new VFMQ,OLNTB
	set %PG=0
	set %PAGE=1
	type RecordSINSTR fSINSTR
	do VPG(.fSINSTR)
	quit


VPG(RecordSINSTR fSINSTR)	// Page control

	new FINISH
	set FINISH=0
	for  do { quit:FINISH
		if %PG=0 do VPG00 if VFMQ="Q" set FINISH=1 quit
		if %PG>0 do VPG01(.fSINSTR)
		if "DFQ"[VFMQ do VER(.fSINSTR) set FINISH=1 quit
		set %PG=%PG+1
		}
	quit


VPG00	// Set up

	if %ProcessMode=0 do {
		set %TAB("ACN")="[SINSTR]ACN/XPP=D CUS^UACN1"
		set %TAB("CRCD")="[SINSTR]CRCD"
		set %TAB("FXPROD")="[SINSTR]FXPROD/TBL=[STBLFXPROD]"
		set %TAB("PAYRECV")="[SINSTR]PAYRECV/XPP=D PRPP^SINSTR"
		}
	else  do {
		set %TAB("ACN")="[SINSTR]ACN/TBL=[SINSTR]ACN:DISTINCT/XPP=D CUS^UACN1"
		set %TAB("CRCD")="[SINSTR]CRCD/TBL=[SINSTR]CRCD:DISTINCT:QU ""[SINSTR]ACN=<<ACN>>"""
		set %TAB("FXPROD")="[SINSTR]FXPROD/TBL=[SINSTR]FXPROD:DISTINCT:QU ""[SINSTR]ACN=<<ACN>> AND [SINSTR]CRCD=<<CRCD>>"""
		set %TAB("PAYRECV")="[SINSTR]PAYRECV/TBL=[SINSTR]PAYRECV:DISTINCT:QU ""[SINSTR]ACN=<<ACN>> AND [SINSTR]CRCD=<<CRCD>> AND [SINSTR]FXPROD=<<FXPROD>>"""		
		}

	set %TAB("IO")=$$IO^SCATAB($I)

	set %READ="@@%FN,,,ACN/REQ,CRCD/REQ,FXPROD/REQ,PAYRECV/REQ"
	set %NOPRMT="N"
	if %ProcessMode=2 set %READ=%READ_",,IO#1"

	do ^UTLREAD

	if "Q"[VFMQ quit

	if %ProcessMode=2,IO'=$I do OPEN^SCAIO

	quit


VPG01(RecordSINSTR fSINSTR)	// Screen

	set fSINSTR=Db.getRecord("SINSTR","ACN=:ACN,CRCD=:CRCD,FXPROD=:FXPROD,PAYRECV=:PAYRECV",1)

	// Record locked by another user
	lock +SINSTR(ACN,CRCD,FXPROD,PAYRECV):2 else  set VFMQ="Q" set ER=1 set RM=$$^MSG(2333) quit

	do DRV^USID(%ProcessMode,"SINSTR",.fSINSTR)

	quit


PRPP	// Paying/Receiving Post-Processor

	// Record already exists
	if Db.isDefined("SINSTR","ACN=:ACN,CRCD=:CRCD,FXPROD=:FXPROD,PAYRECV=:X") do {
		set ER=1
		set RM=$$^MSG(2327)
		}
	quit


VER(RecordSINSTR fSINSTR)

	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit

	do FILE(.fSINSTR)
	do END
	quit


FILE(RecordSINSTR fSINSTR)	// File data

	if %ProcessMode=3 do {
		do Db.delete("SINSTR","ACN=:ACN AND CRCD=:CRCD AND FXPROD=:FXPROD AND PAYRECV=:PAYRECV")
		}
	else  do fSINSTR.save()
	quit


END	// End process

	kill %TAB
	if $D(ACN)&$D(CRCD)&$D(FXPROD)&$D(PAYRECV) lock -SINSTR(ACN,CRCD,FXPROD,PAYRECV)
	if $G(ER)!(%ProcessMode=2)!(%ProcessMode=4) quit

	if VFMQ="Q" do {

		// Customer Settlement Instructions not created
		if %ProcessMode=0 set RM=$$^MSG(7627) quit

		// Customer Settlement Instructions not modified
		if %ProcessMode=1 set RM=$$^MSG(7629) quit

		// Customer Settlement Instructions not deleted
		set RM=$$^MSG(7628)
		}
	else  do {

		// Customer Settlement Instructions created
		if %ProcessMode=0 set RM=$$^MSG(7624) quit

		// Customer Settlement Instructions modified
		if %ProcessMode=1 set RM=$$^MSG(7626) quit

		// Customer Settlement Instructions deleted
		set RM=$$^MSG(7625)
		}

	set ER="W"
	quit

vSIG()	quit "59886^43610^Sanchez SCM Administrator^3546"	// Signature - LTD^TIME^USER^SIZE
