PROC98E		/*
	Public;Assignment expressions for 1098-E CDI
	
	LIBRARY:
	
	$$ EDATE98E	1098-E End Date
	$$ IYTD98E	1098-E Interest Paid Year To Date
	$$ IPY98E	1098-E Interest Paid Prior Year
	
	---- Revision History ------------------------------------------------
	
	04/17/07 - RussellDS - CR26387
		   Eliminated use of UHFETCH.
	
	11/03/05 - TITOVE - CR 17287
		   Cleaned up as part of DBI3 project.

	12/18/03 - CARROLLJ - CR7239
		   Added #ACCEPT prior to setting %SystemDate.

	10/15/01 - MBUIM - 47791
		   Modified sections IYTD98E and IPY98E to define CID.   

	07/19/00 - Hillanbrand - 39610
	           Revised and modified for tax-year.
	
	07/03/00 - MAGERAM - 39610
		   Converted all line tags to PSL.
	-------------------------------------------------------------------
	*/

	quit


public	EDATE98E(Number CID)	// 1098-E End Date
	/*
	
	   This procedure will compute the 1098-E eligible end date
	
	   Arguments:
	         . CID   Account Number  /TYP=N/REQ/METH=VAL
	
	   Returns:
	         . D     1098-E End Date    /TYP=D
	
	   Example:
	         . $$EDATE98E^PROC1098E(CID)
	
	*/
	
	type Date DATE
	
	if 'CID.exists() quit ""

	type RecordLN ln = Db.getRecord("LN", "CID = :CID", 1)
	
	if 'ln.getMode() quit ""
	
	// If start date is null then date = date of first payment
	// End date is last day of month (date + 60 months)
	if 'ln.sdate98e.isNull() set DATE = $$ADDMJD^SCADAT(ln.sdate98e,60,1)
	
	if ln.sdate98e.isNull() set DATE = $$ADDMJD^SCADAT(ln.dfp,60,1)
	
	set DATE = $$EOMJD^SCADAT(DATE,1)
	
	quit DATE
	

public	IYTD98E(Number CID)	// 1098-E Interest Paid Year To Date
	/*
	
	   This routine will compute the amount of 1098-E eligible interest paid
	   during the system year.
	
	   Arguments:
	         . CID   Account Number  /TYP=N/REQ/METH=VAL
	
	   Returns:
	         . $     1098-E Interest Paid Year-To-Date    /TYP=$
	
	   Example:
	         . $$IYTD98E^PROC1098E(CID)
	
	
	*/

	type Date EDATE, FDATE, LDATE, SDATE
	type Number INT = 0
	type String INTREG = ""
	
	if 'CID.exists() quit ""
	
	type RecordLN ln = Db.getRecord("LN", "CID = :CID", 1)
	
	if 'ln.getMode() quit ""

	set SDATE = ln.sdate98e
	set EDATE = ln.edate98e
			
	if SDATE.isNull() set SDATE = ln.dfp
	
	// Beginning of Current Tax Year
	set FDATE = $$BOTY^SCADAT(%SystemDate,1)         
	
	// End of Current Tax Year
	set LDATE = $$EOTY^SCADAT(%SystemDate,1)         
	
	if (SDATE'>FDATE),(EDATE '< LDATE) set INT = ln.itytd
	else  if 'SDATE.isNull() do FINDINT
	
	quit INT
	

public	IPY98E(Number CID)	// 1098-E Interest Paid Prior Year
	/*
	
	   This routine will compute the amount of 1098-E eligible interest paid
	   during the last year.
	
	   Arguments:
	         . CID   Account Number  /TYP=N/REQ/METH=VAL
	
	   Returns:
	         . $     1098-E Interest Paid Prior Year  /TYP=$
	
	   Example:
	         . $$IPY98E^PROC1098E(CID)
	
	
	*/

	type Date EDATE, FDATE, LDATE, SDATE
	type Number INT = 0
	type String INTREG = ""
	
	if 'CID.exists() quit ""

	type RecordLN ln = Db.getRecord("LN", "CID = :CID", 1)
	
	if 'ln.getMode() quit ""
	
	set SDATE = ln.sdate98e
	set EDATE = ln.edate98e
			
	if SDATE.isNull() set SDATE = ln.dfp
	
	// Beginning of tax year
	set FDATE = $$BOTY^SCADAT($$BOTY^SCADAT(%SystemDate,1)-1,1)      
	
	// End of tax Year
	set LDATE = $$EOTY^SCADAT(FDATE,1)       
	
	if (SDATE '> FDATE),(EDATE '< LDATE) set INT = ln.ipty
	else  if 'SDATE.isNull() do FINDINT
	
	quit INT
	

FINDINT	// Find Interest

	type public Date EDATE, FDATE, LDATE, SDATE
	type public Number CID, INT
	type public String INTREG
	
	type Number TSEQ, vsql
	type String HIST, Q(,), exe
	
	// Start date greater than reporting period last date
	if (SDATE > LDATE) quit                    
	
	// End date less than reporting period first date
	if (EDATE < FDATE) quit                    
	
	// Transactions Posted between SDATE AND EDATE - Reset SDATE and EDATE
	// to only get transactions in reporting period of 1 year
	if (EDATE > LDATE) set EDATE = LDATE
	if (SDATE < FDATE) set SDATE = FDATE
	
	type ResultSet rs = Db.select("TSEQ", "HIST", "CID=:CID AND TJD>=:SDATE AND TJD<=:EDATE", "TSEQ DESC")
	
	while rs.next() do {
		
		set INTREG = $$XH01^LNCDI(CID, rs.getCol("TSEQ"))
	
		set INT = INT + INTREG.piece($C(124),3)
	}
	
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60739^53899^Dan Russell^4051"	// Signature - LTD^TIME^USER^SIZE
