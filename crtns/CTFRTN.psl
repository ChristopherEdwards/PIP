CTFRTN		/*
	PROCEDURE:  CTFRTN
	ORIG: GOLATOS - 06/11/2001
	DESC: Commission/Tax/Fee Plan Calc Method Compiler
 
	---- Comments --------------------------------------------------------
	CTFRTN  Commission/Tax/Fee Plan Calculation Method Compiler   ;
	Copyright(c)2001 Sanchez Computer Associates, Inc.  All Rights Reserved
		ORIG:  Stephen Golato - 15 Jun 2001
		DESC:  Commission/Tax/Fee Plan Calculation Method Compiler
          
	This routine will compile a routine to be used during
	Commission/Tax/Fee processing for a commission/tax/fee calculated
	method defined in the user table.  Any time the data item
	UTBLCTFPLN.COMCALC is modified, this routine will be called to compile
	a new routine.  The compiled routine will be called CTFRTN1.
	
	---- Revision History ------------------------------------------------

	04/10/02 - Dan Russell - 43583
		   Modified to generate PSL instead of M code.
  	
          
	06/15/01 - GOLATOS - 44647
		   Created

	*/

	catch error {
		do ZE^UTLERR
		set ER=1
		}

	new CMPERR,CODE,TAB
	set ER=0
	set TAB=$C(9)

	
	do addcode("public CTFRTN1(RecordDEP dep)"_TAB_"// Commission/Tax/Fee Plan Calculation Method Compiled Program")
	do addcode(TAB_"// Last compiled:  "_$$DAT^%ZM(%CurrentDate,$G(%MSKD))_" "_$$TIM^%ZM_" - "_$G(%UID))
	do addcode("")
	do addcode(TAB_"// THIS IS A COMPILED ROUTINE.  Compiled by procedure CTFRTN")
	do addcode("")
	do addcode(TAB_"quit   // Cannot access from top")
	
	// Loop through commission/tax/fee plan user table and find all
	// calculation methods that are defined.
	
	type ResultSet ctfplnrs
	set ctfplnrs=Db.select("CTFPLN,COMCALC","UTBLCTFPLN","COMCALC IS NOT NULL")	

	if ctfplnrs.isEmpty()  quit

	while ctfplnrs.next() do bldcalc(ctfplnrs.getCol(1),ctfplnrs.getCol(2))

	quit:ER

	// Build compiled routine
	do BUILDRTN^UCGM(.CODE,"CTFRTN1",.CMPERR)
	if $D(CMPERR) do {
		new N
		set N=""
		for  set N=$O(CMPERR(N)) quit:N=""  do {
			if 'ER set ER=1,RM=CMPERR(N)
			write CMPERR(N),!
		}
	}
	
	//Completed at ~p1
	if 'ER set ER="W",RM=$$^MSG(591,$$TIM^%ZM(%CurrentTime,$G(%MSKC)))

	quit 
	

bldcalc(CTFPLN,COMCALC)	// Build calculation method for plan

	new INPUT,N,PSLOBJ,PSLQRY
	
	do addcode("")
	do addcode("public "_CTFPLN_"(RecordDEP dep)"_TAB_"// Calculation Method for Plan  "_CTFPLN)
	do addcode("")

	/* Convert code line into PSL code for loading necessary objects
	   and calculation.  Turn code into temporary simple query to
	   make use of UCQRYBLD.
	*/

	set INPUT("WHERE")="("_COMCALC_")=1"
	do ^UCQRYBLD(.INPUT,"DEP=dep",,.PSLOBJ,.PSLQRY)
	quit:ER
	
	set N=""
	// Insert lines to instantiate new objects
	for  set N=$O(PSLOBJ(N)) quit:N=""  do addcode(TAB_PSLOBJ(N,1))

	// Convert the query back to a simple formula and assignment
	do addcode(TAB_"set CAMT="_$E(PSLQRY(1),1,$L(PSLQRY(1))-2))
	do addcode("")
	do addcode(TAB_"quit")

	quit 

	
addcode(LINE)	// Add new line to CODE arry

	set CODE($O(CODE(""),-1)+1)=LINE
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43501^Sanchez SCM Administrator^2820"	// Signature - LTD^TIME^USER^SIZE
