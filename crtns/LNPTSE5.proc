LNPTSE5(RecordDEP dep, RecordTTX ttx, RecordTRN trn)

	/*
	       ORIG:  David Caliendo (5527) - 09/09/86
	       DESC:  Error Correct Escrow Paid in and out


	---- Revision History ------------------------------------------------
	
	01/31/06 - SmithCD - CR 19343 (16890)
		   . Removed Db.selectDbSet() for HIST in section FIND, so 
		     ^LNERB is not called multiple times (only supposed to 
		     be called once) and to avoid possibly collating the 
		     entire HIST table
		   . Removed the saving of trtype data (this is done in 
		     LNERB now)
		   . Replaced FO^LNERB with EXTERN^LNERB
		   . Cleaned up code in accordance with current standards
	
	06/08/01 - ARCILLAZ - 43583
		   Converted to PSL. Removed old revision history.

	*/

	type String TRTYPE
	
	// Affect Escrow Remittances
	if trn.pcfd11 do {
		set TRTYPE = $$FIELD^UTSO(ttx.tso, "ESC").piece(":", 2)
		if TRTYPE.isNull() quit
		
		type RecordTRTYPE trtype = Db.getRecord("TRTYPE", "CID=:dep.cid,TYP=:TRTYPE")

		do FIND(.trtype, .ttx, .dep)
		}

	do EXEC(.dep, .ttx) 

	quit


EXEC(RecordDEP dep,		// Deposit (escrow) account		/REF:RW
     RecordTTX ttx)		// Transaction				/REF:R

	type public RecordLN ln
	type public Number ZAMT
	type public String TB()
	
	type Number AMT

	set AMT = $select(ttx.itc1:ZAMT, 1:-ZAMT)
	set dep.bal = dep.bal + AMT 
	set dep.balcol = dep.balcol + AMT

	set dep.tld = %SystemDate
	
	if 'ln.exists() set ln = Db.getRecord("LN", "CID=:dep.aref")
	set ln.teb = ln.teb + AMT

	if ttx.itc1 set dep.cntcr = dep.cntcr - 1
	else  set dep.cntcr = dep.cntcr - 1

	set TB(dep.esc) = ZAMT_","_dep.aref
	set ZAMT = 0

	quit


FIND(RecordTRTYPE trtype,	// Escrow transfer type			/REF:W
     RecordTTX ttx,		// Transaction				/REF:R
     RecordDEP dep)		// Deposit (escrow) account		/REF:R

	// Find escrow remittance shortcut in escrow history
	
	type Date REMDT
	type String REMDATA
	type Number TSEQ
	
	type ResultSet rs = Db.select("TSEQ", "HIST", "CID=:dep.cid AND ERS=1", "TSEQ DESC")
	if rs.next() set TSEQ = rs.getCol("TSEQ")
	// No Transaction to Error Correct
	else  do Runtime.setErrMSG("TTX", 8551) quit

	type RecordHIST hist = Db.getRecord("HIST", "CID=:dep.cid,TSEQ=:TSEQ")
	
	// Set back original remittance amount data

	set REMDATA = hist.xhs16.piece("#", 7)

	if REMDATA.isNull() do Db.delete("RAMT", "CID=:dep.cid AND TYP=:trtype.trtype AND REMDT=:trtype.remld") quit
		
	set REMDT = REMDATA.piece(",", 1)

	type RecordRAMT ramt = Db.getRecord("RAMT", "CID=:dep.cid, TYP=:trtype.trtype, REMDT=:REMDT", 1)
		
	set ramt.remamt = REMDATA.piece(",", 2)
	set ramt.estflg = REMDATA.piece(",", 3)
	
	do ramt.bypassSave()

	set trtype.remptp = REMDATA.piece(",", 4)
	do trtype.bypassSave()

	do EXTERN^LNERB(.trtype, .ttx, .dep)

	quit

vSIG()	quit "60298^63532^Chad Smith^2590"	// Signature - LTD^TIME^USER^SIZE
