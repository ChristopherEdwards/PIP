RSHETCDI	  /*
ORIG: OFALTR - 09/13/2000
DESC: Computed for UTBLRSHEET0 and UTBLRSHEET

	LIBRARY:
	$$ EFDEND	End Dates for Rate Sheet Tables
	$$ RSSTART	Rate Sheet Start Date
	$$ RSEND	Rate Sheet End Date
	$$ HIGHBAL	High Balance

---- Comments --------------------------------------------------------

---- Revision History ------------------------------------------------

	12/21/05 - PRAKASHJ - CR17423
		   Removed Depreciated warnings. 
	
 */

	quit

EFDEND(String XCO,String XRATECD)	// End Date 
	/*
	The function is used to compute [UTBLRSHEET0]EFDEND field

	ARGUMENTS:
	. CO		Company Code		/TYP=T/REQ/MECH=VAL
	. RATECD	Rate Category Code	/TYP=T/REQ/MECH=VAL

	RETURNS:
	  $$	Rate Sheet End Dates
	
	EXAMPLE: $$EFDEND^RSHETCDI(CO,RATECD)
	*/
	
	type Date XEFD,EDDT
	
	set XEFD=%SystemDate
 
	type ResultSet rs=Db.select("TEFD","UTBLRSHEET","CO=:XCO and RATECD=:XRATECD and TEFD>:XEFD","TEFD ASC")
	if rs.next() do {
		set EDDT=rs.getCol("TEFD")
		}
	if EDDT.get().isNull() quit ""
	quit EDDT-1

RSSTART(String XCO,String XRATECD)	//Rate Sheet Start Date
        /*
        The function is used to compute [UTBLRSHEET0]RSSTART field

 	RSSTART is the most recent date for which all the current rates
	were in effect for this Company Code

        ARGUMENTS:
        . CO            Company Code            /TYP=T/REQ/MECH=VAL
        . RATECD        Rate Category Code      /TYP=T/REQ/MECH=VAL
 
        RETURNS:
          $$    Rate Sheet Start Date
 
        EXAMPLE: $$RSSTART^RSHETCDI(CO,RATECD)
        */

	type Date XTJD

	set XTJD=%SystemDate
	type ResultSet rs=Db.select("TEFD","UTBLRSHEET","CO=:XCO AND TEFD<=:XTJD","TEFD DESC")
	if 'rs.isEmpty(),rs.next() quit rs.getCol("TEFD")
		
	quit ""

RSEND(String XCO,String XRATECD)    //Rate Sheet End Date
        /*
        The function is used to compute [UTBLRSHEET0]RSSEND field

	RSEND is the last date for which all of the current rates for this
	company code are scheduled to be in effect
  
        ARGUMENTS:
        . CO            Company Code            /TYP=T/REQ/MECH=VAL
        . RATECD        Rate Category Code      /TYP=T/REQ/MECH=VAL
 
        RETURNS:
          $$    Rate Sheet End Date
 
        EXAMPLE: $$RSEND^RSHETCDI(CO,RATECD)
        */
 
        type Date XTJD

        set XTJD=%SystemDate
       	type ResultSet rs=Db.select("TEFD","UTBLRSHEET","CO=:XCO AND TEFD>:XTJD","TEFD ASC")
	if 'rs.isEmpty(),rs.next() quit (rs.getCol("TEFD")-1)
       
	quit ""

HIGHBAL(String XCO,String XRATECD,Date XEFD,Number XTYP,Number XSEQ,Number XLOWBAL)	//High Balance
        /*
        The function is used to compute [UTBLRSHEET]HIGHBAL field
 
        ARGUMENTS:
        . CO            Company Code            /TYP=T/REQ/MECH=VAL
        . RATECD        Rate Category Code      /TYP=T/REQ/MECH=VAL
	. EFD		Effective Date		/TYP=D/REQ/MECH=VAL
	. TYP		Product Type		/TYP=N/REQ/MECH=VAL
	. SEQ		Sequence Number		/TYP=N/REQ/MECH=VAL
	. LOWBAL	Low Balance		/TYP=$/REQ/MECH=VAL
	RETURNS:
	  $$    High Balance
 
	EXAMPLE: $$HIGHBAL^RSHETCDI(CO,RATECD,EFD,TYP,SEQ,LOWBAL)
        */

	type Number HBAL
	type ResultSet rs=Db.select("HIGHBAL","UTBLRSHEET","CO=:XCO and RATECD=:XRATECD and TEFD=:XEFD and TYP=:XTYP and SEQ=:XSEQ and LOWBAL>:XLOWBAL","LOWBAL ASC")
	if rs.next() do {
		set HBAL=rs.getCol("HIGHBAL")
		}
	
	if +HBAL=0 quit ""
	quit (HBAL-1)
 #OPTION ResultClass ON
Public String vSIG()	quit "60295^32933^Jaimano Prakash^3198"	// Signature - LTD^TIME^USER^SIZE
