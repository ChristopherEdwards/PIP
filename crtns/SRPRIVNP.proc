SRPRIVNP		/*
	 ORIG: SANTHUMS - 08/04/2006
	 DESC: Start Revised Privacy Notice Period
	
	---- Comments --------------------------------------------------------
	This procedure will reset the current values of the columns 
	CIF.PRIV and CIF.SOL to a new unique value 3 by means of one 
	of the following entries:
	Defining one of the available options 0, 1, 2; or
	Selecting multiple options, 0, 1 or 1, 2; or
	Entering ALL, which would mean options 0, 1 and 2
	  	  	   
	Function SRPRIVNP executes this procedure.
	
	---- Revision History ------------------------------------------------
	
	08/04/06 - SANTHUMS - CR 22468
	
	  	  Created new Procedure	
	*/
	
	do START
	
	quit
	
START
	type String %READ,%TAB,VFMQ
	type String PRIV,SOL
	
	set %TAB("PRIV") = "/DES=Privacy Code values to be reset to 3/LEN=3/TBL=[STBLCIFPRIV]/XPP=do VALID^SRPRIVNP" 
	set %TAB("SOL") = "/DES=Solicitation Code values to be reset to 3/LEN=3/TBL=[STBLCIFSOL]/XPP=do VALID^SRPRIVNP"
	
	set (PRIV,SOL) = "ALL"
	
	set %READ = "@@%FN,,,PRIV/REQ,SOL/REQ"
	
	do ^UTLREAD if VFMQ = "Q" quit   
	
	do PROCESS(PRIV,SOL)
	
	quit
	

PROCESS(String PRIV, 	//Privacy Code  		
	String SOL)	//Solicitation Code
	
	type Number I
	
	if PRIV = "ALL" do {
		for I = 0:1:2 do CALPRIV(I)
		}
		
		
	if PRIV '= "ALL" do PRIVSAV(PRIV)	

	if SOL = "ALL" do {
		for I = 0:1:2 do CALSOL(I)
		}
			
	if SOL '= "ALL" do SOLSAV(SOL)			
	
	quit

	
PRIVSAV(String PRIV)	// Privacy Code

	type Number J
	type String ZPRIV
		
	if PRIV["," do {
	
		for J = 1:1:2 do {
		
			set ZPRIV = PRIV.piece(",",J)
			
			if 'ZPRIV.isNull() do CALPRIV(ZPRIV)
			}	
		}
	
	else  do CALPRIV(PRIV)
		
	quit

		
CALPRIV(String PRIV)	// Privacy Code

	type Number ACN
	
	type public ResultSet rs = Db.select("ACN","CIF","PRIV=:PRIV")
			
	while rs.next() do {
		set ACN = rs.getCol("ACN")
		do UPREC(1,ACN)
		}
	
	quit	
	
	
SOLSAV(String SOL)	// Solicitation Code

	type Number J
	type String ZSOL
	
	if SOL["," do {
	
		for J = 1:1:2 do {
		
			set ZSOL = SOL.piece(",",J)
			
			if 'ZSOL.isNull() do CALSOL(ZSOL)
					
			}
		}		
	
	else  do CALSOL(SOL)
		
	quit
	
		
CALSOL(String SOL)	// Solicitation Code 

	type Number ACN

	type public ResultSet rs = Db.select("ACN","CIF","SOL=:SOL")
			
	while rs.next() do {
		set ACN = rs.getCol("ACN")						
		do UPREC(0,ACN)
		}		

	quit


UPREC(Number PSFLAG,	// Indicator to identify Privacy Code and Solicitation Code
      Number ACN)       // Customer Number 
	
	type RecordCIF cif = Db.getRecord("CIF","ACN=:ACN")
				
	do cif.setAuditFlag(1)

	if PSFLAG = 1 set cif.priv = 3

	if PSFLAG = 0 set cif.sol = 3		

	set cif.revnotdt = %SystemDate
				
	do cif.save()
	
	quit
			
	
VALID   //Option 3 is not allowed to select from dropdowns	
	
	type public String X
	
	type Number J
	type String DELM
	
	
	//Invalid 0ption
	if X["3" do Runtime.setErrMSG("CIF",1411) quit	
	
	if (X '= "ALL")&(X.isNumber() = 0) do {

		for J = 1:1:3 do {
	   	
	   		set DELM = X.extract(J)
		
			if DELM.isNumber() = 0 set X = X.translate(DELM,",")
		
	   		}
		}		
	quit
	
	
       	

vSIG()	quit "60648^51795^Diane Spatola^2859"	// Signature - LTD^TIME^USER^SIZE
