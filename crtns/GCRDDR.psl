GCRDDR	/*  Public;Maintain daily scoring user table

	       ORIG:  Michael Berlin 06/11/98
	  ---- Revision History ------------------------------------------------

	   11/28/05 - NATRAJAH - 13734
		      General DBI3 cleanup.   	

	   07/25/02 - DATTAR - 49451
		      Converted to PSL

 	   10/13/98 - HILLANBRAND - ARQ49804:007
 	              Added table description.  Corrected update error on
 	              UTBLGCRDS.
	 
          -----------------------------------------------------------------------
 	*/

	quit
		
NEW	//
	do INIT(0) 
	quit 


UPD	//
	do INIT(1) 
	quit 


INQ	//
	do INIT(2) 
	quit 


DEL	//
	do INIT(3) 
	quit


INIT(%ProcessMode) //
	type Number %PAGE,%PG,PG,RPT,SEQ,%SVPG
	type String VFMQ
	set (%SVPG,%PG,RPT,PG)=0
	if %ProcessMode set %PAGE=1
	type RecordUTBLGCRDS fUTBLGCR
	do VPG(.fUTBLGCR)

	quit


VPG(RecordUTBLGCRDS fUTBLGCR)	//Page control
	type Boolean FINISH
	type Public Boolean ER
	type Public Number %PAGE,%PG,PG,%SVPG
	type Public String VFMQ
	set %SVPG=%PG
	set FINISH=0
	for  do { quit:FINISH
		if %PG=0 do VPG00(.fUTBLGCR) if ER!(VFMQ="Q") set FINISH=1 quit

		if %PG>0 do VPG01(.fUTBLGCR) if ER!(VFMQ="Q") set FINISH=1 quit

		if "DFQ"[VFMQ do VER(.fUTBLGCR) set FINISH=1 quit

		// Next screen
	 	if VFMQ=%SVPG do {
			set %PG=$P(%PG+1,".",1) 
			set %PAGE=$S(%ProcessMode:%PAGE,1:%SVPG+1) 
			}
	
		// Previous Screen
		else  do {
			set %PG=$P(%SVPG-1,".",1) 
			set PG=PG-2 
			}
		}
	quit


VPG00(RecordUTBLGCRDS fUTBLGCR)	//Set up
	type Public String %NOPRMT,%READ,%TAB,TBLNM,VFMQ
	if %ProcessMode=0 set %TAB("TBLNM")="[UTBLGCRDS]TBLNM/XPP=D TBL^GCRDDR(X)"
	else  do {
		set %TAB("TBLNM")="[UTBLGCRDS]TBLNM/TBL=[UTBLGCRDS]:QU ""[UTBLGCRDS]"""
		if %ProcessMode=2 set %TAB("IO")=$$IO^SCATAB($I)
		}

	set %READ="@@%FN,,,TBLNM/REQ" 
	set %NOPRMT="N"
	if %ProcessMode=2 set %READ=%READ_",IO/REQ"

	do ^UTLREAD

	if VFMQ="Q" quit 
	set fUTBLGCR=Db.getRecord("UTBLGCRDS","TBLNM=:TBLNM",1)
	do fUTBLGCR.setAuditFlag(1)
	if %ProcessMode do LOADDATA
	quit 

 	
TBL(TBLNM) //
	type Public String ET
	if Db.isDefined("UTBLGCRDS","TBLNM=:TBLNM") set ET="RECOF" do ERR quit
	quit 


LOADDATA // Load GCR Daily Scoring data into GCRDAILY array
	/*
	   Position: for GCRDAILY(SEQ)=  1-Percentage
	                                 2-O/D Soring Options
 	                                 3-GCR Decrement for O/D
 	                                 4-Returned Deposit Scoring Options
 	                                 5-GCR Decrement for Returned Dep
	*/
	type Public Number SEQ
	type Public String GCRDAILY,OLDGCRD
	set SEQ=0
	type DbSet rs=Db.selectDbSet("UTBLGCRDD","TBLNM=:TBLNM")
	while rs.next() do {
		type RecordUTBLGCRDD utblgcrd=rs.getRecord("UTBLGCRDD")
		set SEQ=SEQ+1
		set GCRDAILY(SEQ)=utblgcrd.percent_"|"_utblgcrd.odscropt_"|"_utblgcrd.gcrodd_"|"_utblgcrd.rnscropt_"|"_utblgcrd.gcrrtd
		set OLDGCRD(utblgcrd.percent)=""
		}
	quit 


VPG01(RecordUTBLGCRDS fUTBLGCR) //Screen

	type Public Number %PAGE,PG,%REPEAT,RP,RPT,%SVPG
	type Public String VFMQ
	type Number %MODS
	
	if %SVPG>VFMQ set VFMQ=%SVPG
	else  set VFMQ=$P(%SVPG-1,".",1)
	set %REPEAT=13 
	set %MODS=PG*%REPEAT+1
	do RPTS 

	set RP=$P(RPT/%REPEAT,".",1)
	if %ProcessMode,%PAGE<RP set %PAGE=RP

	do DRV^USID(%ProcessMode,"GCRDSTBL",.fUTBLGCR)

	if VFMQ="Q"!(VFMQ="F") quit 
	set PG=PG+1
	quit 


RPTS	// Set pages needed by setting RPT equal to the entries in the GCRDAILY
	/*
 	   array plus one.  So if 13 arrays are filled and 1 more is added then
 	   the first screen will repeat for additional entries to be added or
 	   viewed.
 	
	*/

	type Number I
	type Public Number %MODS,RPT
	type Public String GCRDAILY
	
	for I=%MODS:1 quit:'$D(GCRDAILY(I))  do {

		// Change logical (0) to null for compare
		if $$RTBAR^%ZFUNC(GCRDAILY(I))="" quit 

		set RPT=I
		}
	set RPT=RPT+1
	quit 


ERR	//
	type Public Boolean ER
	type Public String VFMQ
	set ER=1 

	do DSPBP^UTLERR

	set VFMQ="Q"
	quit 


VER(RecordUTBLGCRDS fUTBLGCR)	//
	type Public Boolean ER
	type Public String VFMQ
	if %ProcessMode=2!(%ProcessMode=4)!(VFMQ="Q") do END quit
	if %ProcessMode=0!(%ProcessMode=3) do { quit
		do CRTGCRDS(.fUTBLGCR)

		if ER set VFMQ="Q" 
		do END
		}
	if %ProcessMode=1 do UPDGCRDS(.fUTBLGCR) if ER set VFMQ="Q"
	do END 
	quit


CRTGCRDS(RecordUTBLGCRDS fUTBLGCR) // GCR Daily Scoring

	/*
 	   Position: for GCRDAILY(SEQ)=  
	      1-Percentage
 	      2-O/D Soring Options
 	      3-GCR Decrement for O/D
 	      4-Returned Deposit Scoring Options
 	      5-GCR Decrement for Returned Dep
	*/
	type Number PERCENT
	type Public Number GCRDAILY,SEQ
	type Public String TBLNM
	set SEQ="" 
	if %ProcessMode=0 do {
		do fUTBLGCR.save()
		for  set SEQ=$O(GCRDAILY(SEQ)) quit:SEQ=""  do {
			set PERCENT=$P(GCRDAILY(SEQ),"|",1)
			if PERCENT="" quit
			type RecordUTBLGCRDD utblgcrd=Class.new("RecordUTBLGCRDD")			
			set utblgcrd.tblnm=TBLNM
			set utblgcrd.percent=PERCENT
			set utblgcrd.odscropt=$P(GCRDAILY(SEQ),"|",2)
			set utblgcrd.rnscropt=$P(GCRDAILY(SEQ),"|",4)		
			set utblgcrd.gcrodd=$P(GCRDAILY(SEQ),"|",3) 
			set utblgcrd.gcrrtd=$P(GCRDAILY(SEQ),"|",5)
			do utblgcrd.save()
			}
		}
	else  do {
		do Db.delete("UTBLGCRDS","TBLNM=:TBLNM")
		do Db.delete("UTBLGCRDD","TBLNM=:TBLNM")
		}
	quit 


UPDGCRDS(RecordUTBLGCRDS fUTBLGCR) // Update GCR Daily Scoring array fUTBLGCD

	/*
 	   Position: for GCRDAILY(SEQ)=  1-Percentage
 	                                 2-O/D Soring Options
 	                                 3-GCR Decrement for O/D
 	                                 4-Returned Deposit Scoring Options
 	                                 5-GCR Decrement for Returned Dep
 	
 	   Updates and insert
	*/
	type Public String GCRDAILY,OLDGCRD,NEWGCRD,TBLNM
	type Public Number SEQ
	type Public Boolean ER
	type Number OLDPER,PERCENT
	set SEQ="" 
	do fUTBLGCR.save()
	for  set SEQ=$O(GCRDAILY(SEQ)) quit:SEQ=""  quit:ER  do {
		set PERCENT=$P(GCRDAILY(SEQ),"|",1) 
		if PERCENT="" quit
		set NEWGCRD(PERCENT)=""
		type RecordUTBLGCRDD utblgcrd=Db.getRecord("UTBLGCRDD","TBLNM=:TBLNM,PERCENT=:PERCENT",1)
		do utblgcrd.setAuditFlag(1)
		set utblgcrd.tblnm=TBLNM
		set utblgcrd.percent=PERCENT
		set utblgcrd.odscropt=$P(GCRDAILY(SEQ),"|",2)
		set utblgcrd.rnscropt=$P(GCRDAILY(SEQ),"|",4)
		set utblgcrd.gcrodd=$P(GCRDAILY(SEQ),"|",3)
		set utblgcrd.gcrrtd=$P(GCRDAILY(SEQ),"|",5)
		do utblgcrd.save()
		}

	new OLDPER
	set OLDPER=""	
	for  set OLDPER=$O(OLDGCRD(OLDPER)) quit:OLDPER=""!ER  do { 
		if '$D(NEWGCRD(OLDPER)) do Db.delete("UTBLGCRDD","TBLNM=:TBLNM AND PERCENT=:OLDPER")
		}
	quit 


END	//

	type Public String ER,RM,TBLNM,VFMQ
	if $G(ER)!(%ProcessMode=2)!(%ProcessMode=4) quit

	if VFMQ="Q" do {
		//~p1 not created
		if %ProcessMode=0 set RM=$$^MSG(6709,TBLNM) quit     

		//~p1 not modified
		if %ProcessMode=1 set RM=$$^MSG(6710,TBLNM) quit    

		//~p1 not deleted
		set RM=$$^MSG(6711,TBLNM)     
		}
	else  do {

		//~p1 created
		if %ProcessMode=0 set RM=$$^MSG(6712,TBLNM) quit

		//~p1 modified	
		if %ProcessMode=1 set RM=$$^MSG(6713,TBLNM) quit 

		//~p1 deleted
		set RM=$$^MSG(3028,TBLNM)     
		}
	set ER="W"
	quit 
 #OPTION ResultClass ON
Public String vSIG()	quit "60232^23736^Hari Natrajan^6723"	// Signature - LTD^TIME^USER^SIZE
