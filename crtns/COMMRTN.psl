COMMRTN		/*
	ORIG: GOLATOS - 06/11/2001
	DESC: Commission Plan Calc Method Compiler
 
	---- Comments --------------------------------------------------------
	COMRTN  Commission Plan Calculation Method Compiler   ;
	Copyright(c)2001 Sanchez Computer Associates, Inc.  All Rights Reserved
		ORIG:  Vince Arpa - 09 Jul 1998
		DESC:  Commission Plan Calculation Method Compiler
          
	This routine will compile a routine to be used during Commission
	processing for a commission calculated method defined in the user
	table.  Any time the  data item  UTBLCOMPLN.COMCALC is modified,
	this routine will be called to compile a new routine.  The compiled
	routine will be called COMRTN1.

	References to other routines
 
		%ZM        (TIM)          -
		%ZRTNCMP                  -
		%ZT        (SETZT)        -
		DBSLOD                    -
		DBSPARS                   - Data item parser
		MSG                       - Generic Message Handler
		UTLERR     (DSP22)        - SCA error log and display utility
        
  	
	---- Revision History ------------------------------------------------
	
	09/29/05 - SPR - 17008
		   Added a scope for variable COMAMT in the bldcalc section.

        07/27/05 - SkariahV- CR16679
	           Removed #WARN directive.
	              
	
	02/13/03 - Dan Russell - 51351
		Modified to generate PSL instead of M code.
		
		Removed old change history.
  	
          
	06/15/01 - GOLATOS - 44647
		Converted to PSL.  Added variable nextline and modified
		section rtn. Removed calculation formula entries from the
		UTBLCTFPLN table. The new CTFRTN procedure will handle
		the entries for the UTBLCTFPLN table.  

	*/ 

	// I18N=OFF
	
		
	type Public Number ER=0
	type Public String %MSKC,%MSKD,%UserID,RM
		
	type String CMPERR,CODE,TAB
	
	
	catch error {
		do ZE^UTLERR
		set ER=1
		}
	
	set ER=0
	set TAB=$CHAR(9)

	
	do addcode("public COMRTN1"_TAB_"// Commission Plan Calculation Method Compiled Program")
	do addcode(TAB_"// Last compiled:  "_$$DAT^%ZM(%CurrentDate,%MSKD.get())_" "_$$TIM^%ZM_" - "_%UserID.get())
	do addcode("")
	do addcode(TAB_"// THIS IS A COMPILED ROUTINE.  Compiled by procedure COMRTN")
	do addcode("")
	do addcode(TAB_"quit   // Cannot access from top")
	do addcode("")
	
	// Loop through commission plan user and find all calculation methods that are defined.
	
	type ResultSet complnrs=Db.select("CPLAN,COMCALC","UTBLCOMPLN","COMCALC IS NOT NULL")	

	if complnrs.isEmpty() quit

	while complnrs.next() do bldcalc(complnrs.getCol(1),complnrs.getCol(2))

	quit:ER

	// Build compiled routine
	do BUILDRTN^UCGM(.CODE,"COMRTN1",.CMPERR)
	if CMPERR.data() do {
		type Number N
		set N=""
		for  set N=CMPERR(N).order() quit:N=""  do {
			if 'ER set ER=1,RM=CMPERR(N)
			write CMPERR(N),!
		}
	}
	
	//Completed at ~p1
	if 'ER set ER="W",RM=$$^MSG(591,$$TIM^%ZM(%CurrentTime,%MSKC.get()))

	quit 

	
bldcalc(String COMPLN,	// Private - Build calculation method for plan
	    String COMCALC)
	    
	type Public Number ER

	type Number N
	type String INPUT(),PSLOBJ(),PSLQRY(),TAB
	
	set TAB=$CHAR(9)
	
	do addcode("")
	do addcode("public "_COMPLN_TAB_"// Calculation Method for Plan  "_COMPLN)
	do addcode("")

	/* Convert code line into PSL code for loading necessary objects
	   and calculation.  Turn code into temporary simple query to
	   make use of UCQRYBLD.
	*/

	set INPUT("WHERE")="("_COMCALC_")=1"
	do ^UCQRYBLD(.INPUT,,,.PSLOBJ,.PSLQRY)
	quit:ER
	
	set N=""
	// Insert lines to instantiate new objects
	for  set N=PSLOBJ(N).order() quit:N=""  do addcode(TAB_PSLOBJ(N,1))

	// Convert the query back to a simple formula and assignment
	do addcode(TAB_"type Public String COMAMT")
	do addcode(TAB_"set COMAMT="_PSLQRY(1).extract(1,PSLQRY(1).length()-2))
	do addcode("")
	do addcode(TAB_"quit")

	quit

	
addcode(String LINE)	// Private - Add new line to CODE arry

	type Public String CODE()

	set CODE(CODE("").order(-1)+1)=LINE
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60173^35138^Renga SP^3744"	// Signature - LTD^TIME^USER^SIZE
