UTLCOLL(FID,Q,%S,opt)	//;PBS -  - V4.0 Generic File Collater
	/*
	       ORIG:  Pete Chenard 1/17/90
	  CALLED BY:
	      CALLS:
	   PROJ #'S:
	       DESC: This routine is a generic file collates.  It will produce
	             a string that will contain MUMPS code.  This will be the
	             code that will collate through the DATA QWIK file that is
	             specified and will embed any queries that are defined in
	             the Q array.  The following variables are needed:

			INPUT:
				FID     - Valid DATA QWIK file name
				Q(x,x)  - Query array set up by ^DBSDRY and ^DBSQRYA

			OUTPUT:
				%S      - MUMPS code used to collate through the file
				%G      - The global defined all the way to the bottom level
	                                     (e.g., %G="^DTJ(TJD,CLD,GRP,TYP,CID,TSEQ)"

	             The result of the line X %S will be a call to line tag
	             EXEC, which must exist in the routine that calls this
	             utility.  The EXEC subroutine will process one record at
	             a time.

	             For an example of how this utility is used, see routine
	             ^UTLDOCB.

	  ---------- Revision History -----------------------------------------
	  05/21/2002 - TELIV - 49794
	   	       Converted to PSL

	*/

INIT	//
	
	new DFID,I,%K,func,q,quot

	set (DI,QUIT)="" 
	set opt=$G(opt)
	set func=$S(opt:"$ZP",1:"$O")
	set quot=$C(34)
	do INI0 

	quit

INI0	// Construct collating code

	if '$D(vfsn(FID)) do fsn^DBSDD(.vfsn,FID)

	if $D(vfsn(FID)) do {
		set z=$P(vfsn(FID),"|",2)
		set %G=$P(z,"(",1)_"("
		set %S=""

		for i=1:1 set x=$P(z,",",i) quit:x=""  do {
			set x=$P(x,".",$L(x,"."))
			if x["(" set x=$P(x,"(",2)
			do BLD(i,x)
			}
		}


	set %S=%S_"D EXEC"

	quit

BLD(I,%K)	//
	
	set %G=$P(%G,")",1)_$S(I=1:%K,1:","_%K)_")"

	if '(($E(%K)?1A)!($E(%K)="%")) quit
	set @%K=$S($D(MIN(I)):MIN(I),1:"")

	if $D(MIN(I)) set %S=%S_"S "_%K_"="_quot_MIN(I)_quot_" "

	// I18N=OFF
	set %S=%S_"F  S "_%K_"="_func_"("_%G_") Q:"_%K_"=""""  "
	if $D(MAX(I)) set %S=%S_"Q:"_%K_$$COND(MAX(I))_quot_MAX(I)_quot_"  "
	if $D(Q(I)) for q=1:1 quit:$G(Q(I,q))=""  set %S=%S_Q(I,q)_" "
	kill Q(I),MIN(I),MAX(I)
	// I18N=ON

	quit


COND(x)	//Conditional symbol.  Return either a ">" if MAX(I) is a number or
	//ascii "follows" ]] if MAX(I) contains alpha characters.

	quit $S((x?.N):">",1:"]]")

vSIG()	quit "59886^43629^Sanchez SCM Administrator^2206"	// Signature - LTD^TIME^USER^SIZE
