DEPSTP		/*
	       ORIG:  Allan R. Mattson (6915) - 08/13/86
	
	      INPUT: RecordTTX
	     OUTPUT: OVR(CID,"OVR",OVR)
	
	---- Revision History ------------------------------------------------

	06/13/02 - ZWITKOWITSM - 43583
		   Converted to PSL.

	----------------------------------------------------------------------
	*/

	quit


Public DEP(RecordACN acn,RecordTTX ttx)	// deposit account stop payments

	new AMT,CK,DC,DID1,DID2,DTYP1,DTYP2,FCID,FOUND,INST,TSO

	do INIT(.ttx)

	// look for stops and build OVR array

	// customer and account stops
	set FOUND=$$STP2^STPPROC(.acn,1)

	// check stops
	if CK'="" set FOUND=$$STP3^STPPROC(.acn,CK,%SystemDate,1)

	// check amount stops
	set FOUND=$$STP4^STPPROC(.acn,AMT,1)

	// document stops
	if DTYP1'="",DID1'="" set FOUND=$$STPDOC^STPPROC(DTYP1,DID1,1)

	quit


Public ONTHEM(RecordTTX ttx)	// look for stops on on-them specific information

	new AMT,CK,DC,DID1,DID2,DTYP1,DTYP2,FCID,FOUND,INST,TSO

	do INIT(.ttx)

	// must have institution ID
	if INST="" quit
	// must have an account number
	if FCID="" quit
	// must have a check number
	if CK="" quit

	// look for stops and build OVR array

	// on-them account stops
	set FOUND=$$STP6^STPPROC(INST,FCID,1)

	// on-them check number
	set FOUND=$$STP7^STPPROC(INST,FCID,CK,1)

	// document stops
	set FOUND=$$STPDOC^STPPROC(DTYP1,DID1,1)
	set FOUND=$$STPDOC^STPPROC(DTYP2,DID2,1)

	// check AMT against institution cashing limit
	set FOUND=$$CSHLIM^CHKPROC(.ttx,INST,AMT,1)

	quit


Public ACH(RecordACN acn,RecordTTX ttx)	// ACH stop payments

	new AMT,CK,DC,DID1,DID2,DTYP1,DTYP2,FCID,FOUND,INST,TSO

	do INIT(.ttx)

	set FOUND=$$STP5^STPPROC(.acn,%UserStation,AMT,DC,1)

	quit


INIT(RecordTTX ttx)	// Initialize variables

	new UTSO

	// Transaction Amount
	set AMT=+ttx.tamt
	// Internal Transaction Code
	set DC=ttx.itc1
	// Transaction Source of Funds
	set TSO=ttx.tso

	do OUT^UTSO(.UTSO,TSO)

	// check number
	set CK=$G(UTSO("CK"))
	// document ID 1
	set DID1=$G(UTSO("DID1"))
	// document ID 2
	set DID2=$G(UTSO("DID2"))
	// document type 1
	set DTYP1=$G(UTSO("DTYP1"))
	// document type 2
	set DTYP2=$G(UTSO("DTYP2"))
	// on-them account number
	set FCID=$G(UTSO("FCID"))
	// institution ID
	set INST=$G(UTSO("INST"))

	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "59886^43512^Sanchez SCM Administrator^2127"	// Signature - LTD^TIME^USER^SIZE
