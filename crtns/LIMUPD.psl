LIMUPD	
	/* This routine updates EXPCNTRY,EXPCNSOV and EXPIND tables
	   if Customer number was changed.
	
	   Imput:  - CID   Account Number	   TYP=N/REQ=1
		   - ClS   Class (LOAN or DEPOSIT) TYP=N/REQ=1
		   - NCUS  New Customer Number 	   TYP=N/REQ=1
		   - OCUS  Old Customer Number 	   TYP=N/REQ=1
	
	--- Revision History --------------------------------------------------

	09/17/05 - KumarSS - CR 16667
		   Removed the deprecated methods.
		   
	-----------------------------------------------------------------------
	*/

	quit


	//---------------------------------------------------------------------
START(Number CID,		// Account Number
      String CLS,		// Class
      Number NCUST,		// New Customer Number
      Number OCUST)		// Old Customer Number
	//---------------------------------------------------------------------
	
	type Boolean DONE,GOVT,NGOVT,OGOVT
	type Number TOTCOM,TOTOUT
	type String NCNTRY,NSIC,OCNTRY,OSIC,PCNTRY,SIC,TMP

	set DONE=0
	
	type RecordCIF ncif=Db.getRecord("CIF","ACN=:NCUST")
	
	set NCNTRY=ncif.pcntry
	set NGOVT=ncif.govt
	set NSIC=ncif.sic
	
	type RecordCIF ocif=Db.getRecord("CIF","ACN=:OCUST")
	
	set OCNTRY=ocif.pcntry
	set OGOVT=ocif.govt
	set OSIC=ocif.sic

	set TMP=$$@CLS^PROCLIM(CID,0)
	set TOTCOM=TMP.piece("|",1)
	set TOTOUT=TMP.piece("|",2)
	
	if NCNTRY'=OCNTRY do {

		// Old Country
		type RecordEXPCNTRY oexpcntry=Db.getRecord("EXPCNTRY","CNTRY=:OCNTRY")

		if '(oexpcntry.totcomexp-TOTCOM),'(oexpcntry.totoutexp-TOTOUT) do Db.delete("EXPCNTRY","CNTRY=:OCNTRY")
		else  do {

			set oexpcntry.totcomexp=(oexpcntry.totcomexp-TOTCOM).roundDec()
			set oexpcntry.totoutexp=(oexpcntry.totoutexp-TOTOUT).roundDec()
			
			do oexpcntry.save()
			}

		//  New Country
		type RecordEXPCNTRY nexpcntry=Db.getRecord("EXPCNTRY","CNTRY=:NCNTRY")

		if 'nexpcntry.totcomexp,'nexpcntry.totoutexp do {
			
			set nexpcntry.cntry=NCNTRY
			set nexpcntry.totcomexp=TOTCOM
			set nexpcntry.totoutexp=TOTOUT
			
			do nexpcntry.save()
			}
		else  do {

			set nexpcntry.totcomexp=(nexpcntry.totcomexp+TOTCOM).roundDec()
			set nexpcntry.totoutexp=(nexpcntry.totoutexp+TOTOUT).roundDec()

			do nexpcntry.save()
			}

		if OGOVT do {

			type RecordEXPCNSOV expcnsov=Db.getRecord("EXPCNSOV","CNTRY=:OCNTRY")

			if '(expcnsov.totcomexp-TOTCOM),'(expcnsov.totoutexp-TOTOUT) do Db.delete("EXPCNSOV","CNTRY=:OCNTRY")
			else  do {

				set expcnsov.totcomexp=(expcnsov.totcomexp-TOTCOM).roundDec()
				set expcnsov.totoutexp=(expcnsov.totoutexp-TOTOUT).roundDec()

				do expcnsov.save()
				}
			set DONE=1
			}
			
		if NGOVT do {

			type RecordEXPCNSOV expcnsov=Db.getRecord("EXPCNSOV","CNTRY=:NCNTRY")

			if 'expcnsov.totcomexp,'expcnsov.totoutexp do {
				
				set expcnsov.cntry=NCNTRY
				set expcnsov.totcomexp=TOTCOM
				set expcnsov.totoutexp=TOTOUT
				
				do expcnsov.save()
				quit 
				}

			set expcnsov.totcomexp=(expcnsov.totcomexp+TOTCOM).roundDec()
			set expcnsov.totoutexp=(expcnsov.totoutexp+TOTOUT).roundDec()

			do expcnsov.save()
			set DONE=1
			}
		}
	
	if (NGOVT'=OGOVT),'DONE do {

		// Old GOVT
		type RecordEXPCNSOV oexpcnsov=Db.getRecord("EXPCNSOV","CNTRY=:OCNTRY")

		if '(oexpcnsov.totcomexp-TOTCOM),'(oexpcnsov.totoutexp-TOTOUT) do Db.delete("EXPCNSOV","CNTRY=:OCNTRY")
		else  do {

			set oexpcnsov.totcomexp=(oexpcnsov.totcomexp-TOTCOM).roundDec()
			set oexpcnsov.totoutexp=(oexpcnsov.totoutexp-TOTOUT).roundDec()
			
			do oexpcnsov.save()
			}

		// New GOVT
		type RecordEXPCNSOV nexpcnsov=Db.getRecord("EXPCNSOV","CNTRY=:NCNTRY")

		if 'nexpcnsov.totcomexp,'nexpcnsov.totoutexp do {

			set nexpcnsov.cntry=NCNTRY
			set nexpcnsov.totcomexp=(oexpcnsov.totcomexp-TOTCOM).roundDec()
			set nexpcnsov.totoutexp=(oexpcnsov.totoutexp-TOTOUT).roundDec()

			do nexpcnsov.save()
			quit
			}
		
		set nexpcnsov.totcomexp=(nexpcnsov.totcomexp+TOTCOM).roundDec()
		set nexpcnsov.totoutexp=(nexpcnsov.totoutexp+TOTOUT).roundDec()

		do nexpcnsov.save()
		}
	
	if NSIC'=OSIC do {

		// Old Industy Code
		type RecordEXPIND oexpind=Db.getRecord("EXPIND","IND=:OSIC",1)

		if '(oexpind.totcomexp-TOTCOM),'(oexpind.totoutexp-TOTOUT) do Db.delete("EXPIND","IND=:OSIC")
		else  do {
			
			set oexpind.ind=OSIC
			set oexpind.totcomexp=(oexpind.totcomexp-TOTCOM).roundDec()
			set oexpind.totoutexp=(oexpind.totoutexp-TOTOUT).roundDec()

			do oexpind.save()
			}

		// New Industy Code
		type RecordEXPIND nexpind=Db.getRecord("EXPIND","IND=:NSIC",1)
		
		if 'nexpind.totcomexp,'nexpind.totoutexp do {

			set nexpind.ind=NSIC
			set nexpind.totcomexp=nexpind.totcomexp.roundDec()
			set nexpind.totoutexp=nexpind.totoutexp.roundDec()

			do nexpind.save()
			quit 
			}

		set nexpind.totcomexp=(nexpind.totcomexp+TOTCOM).roundDec()
		set nexpind.totoutexp=(nexpind.totoutexp+TOTOUT).roundDec()

		do nexpind.save()
		}
	quit
 #OPTION ResultClass ON
Public String vSIG()	quit "60164^40542^Sudanthiran S. Kumar^4610"	// Signature - LTD^TIME^USER^SIZE
