public	MRPC141(String RETURN,	// Return data
		Number VERSN,	// MRPC Version Number (Current version = 1)
		Number CID)	// Account Number
	/*
	 ORIG: Eugene Titov - 05/22/2006
	 DESC: Average Balance Status Reports

	---- Comments --------------------------------------------------------

	This RPC calculates and formats data to be returned for the
	following reports:
	
	- Deposit Average Balance Status report
	- Loan Average Balance Status report
	
	The main purpose of this RPC is to significantly lower the time it
	takes to compute average balance fields from DEP and LN tables.
	As opposed to directly selecting those fields via SQL, this RPC:
	
	1. Calls UAVB six times instead of eighteen for loans and four
	   times instead of sixteen for deposits.
	
	2. Avoids lenghty communications between Oracle and Profile.
	 	
	---- Revision History ------------------------------------------------
	
	09/22/06 - KUMARB - CR22464
		   Modified section DEPAVG to replace the call $$BALAVL^DEPCDI 
		   with dep.balavl.

	05/22/06 - TITOVE - CR 20978
		   Created the MRPC.
	
	*/
	type String DATA
	
	// Version number of client message is not compatible with server 
	if (VERSN '= 1) quit $$ERRMSG^PBSUTL($$^MSG(2951))

	type RecordACN acn = Db.getRecord("ACN", "CID = :CID", 1)
	
	// Account not on file
	if (acn.getMode() = 0) quit $$ERRMSG^PBSUTL($$^MSG(5046))
	
	if (acn.cls = "D") do DEPAVG(.acn, .DATA)
	
	if (acn.cls = "L") do LNAVG(.acn, .DATA)

	// Format Return
	set RETURN = $$V2LV^MSG(DATA)

	quit ""


LNAVG(RecordLN ln,		// Loan account
      String DATA)		// Column values string			//REF:W
	 
	type String TAB, XLN01, XLN02, XLN03, XLN04, XLN18, XLN19
	
	set TAB = $char(9)

	type RecordCIF cif = Db.getRecord("CIF", "ACN = :ln.acn")

	set DATA = ln.cid_TAB_ln.type_TAB_cif.nam_TAB_$select('ln.aruf:+ln.bal,1:ln.bal-ln.udbal)
	set DATA = DATA_TAB_ln.odt.toString()_TAB_ln.bldt.toString()_TAB_ln.dtc.toString()_TAB_ln.bndt.toString()_TAB_ln.tld.toString()

	do ADBM1^UAVB1
	
        // LN.ADBM1, LN.HBM1, LN.LBM1
        set DATA = DATA_TAB_+XLN03.piece("|",1)_TAB_+XLN03.piece("|",2)_TAB_+XLN03.piece("|",3)
        
        do ADBM2^UAVB1
        
        // LN.ADBM2, LN.HBM2, LN.LBM2
        set DATA = DATA_TAB_+XLN04.piece("|",1)_TAB_+XLN04.piece("|",2)_TAB_+XLN04.piece("|",3)
        
        do ADBC1^UAVB1
        
        // LN.ADBC1, LN.HBC1, LN.LBC1
        set DATA = DATA_TAB_+XLN01.piece("|",1)_TAB_+XLN01.piece("|",2)_TAB_+XLN01.piece("|",3)
        
        do ADBC2^UAVB1
        
        // LN.ADBC2, LN.HBC2, LN.LBC2
        set DATA = DATA_TAB_+XLN02.piece("|",1)_TAB_+XLN02.piece("|",2)_TAB_+XLN02.piece("|",3)
        
        do ADBY1^UAVB1
        
        // LN.ADBY1, LN.HBY1, LN.LBY1
        set DATA = DATA_TAB_+XLN18.piece("|",1)_TAB_+XLN18.piece("|",2)_TAB_+XLN18.piece("|",3)
        
        do ADBP1^UAVB1
        
        // LN.ADBP1, LN.HBY2, LN.LBY2
        set DATA = DATA_TAB_+XLN19.piece("|",1)_TAB_+XLN19.piece("|",2)_TAB_+XLN19.piece("|",3)

	quit


DEPAVG(RecordDEP dep,		// Deposit account
       String DATA)		// Column values string			//REF:W
	
	type Number BALAVL
	type String TAB, XDEP01, XDEP02, XDEP03, XDEP04
	
	set BALAVL = dep.balavl
	set TAB = $char(9)
	
	set DATA = dep.type_TAB_dep.boo_TAB_dep.title1_TAB_dep.title2_TAB_dep.title3_TAB_dep.title4_TAB_dep.bal
	set DATA = DATA_TAB_dep.odt.toString()_TAB_BALAVL_TAB_dep.dtc.toString()_TAB_dep.balcol_TAB_dep.tld.toString()
         
	do ADBM1^UAVB1
        
	// DEP.ADBM1, DEP.ADCM1, DEP.HBM1, DEP.LBM1
	set DATA = DATA_TAB_+XDEP01.piece("|",1)_TAB_+XDEP01.piece("|",5)_TAB_+XDEP01.piece("|",2)_TAB_+XDEP01.piece("|",3)
         
	do ADBM2^UAVB1

	// DEP.ADBM2, DEP.ADCM2, DEP.HBM2, DEP.LBM2         
	set DATA = DATA_TAB_+XDEP02.piece("|",1)_TAB_+XDEP02.piece("|",5)_TAB_+XDEP02.piece("|",2)_TAB_+XDEP02.piece("|",3)

	do ADBS1^UAVB1
	
	// DEP.ADBM2, DEP.ADCM2, DEP.HBM2, DEP.LBM2
	set DATA = DATA_TAB_+XDEP03.piece("|",1)_TAB_+XDEP03.piece("|",5)_TAB_+XDEP03.piece("|",2)_TAB_+XDEP03.piece("|",3)
         
	do ADBS2^UAVB1
	
	// DEP.ADBS2, DEP.ADCS2, DEP.HBS2, DEP.LBS2
	set DATA = DATA_TAB_+XDEP04.piece("|",1)_TAB_+XDEP04.piece("|",5)_TAB_+XDEP04.piece("|",2)_TAB_+XDEP04.piece("|",3)
	
	set DATA = DATA_TAB_dep.cid_TAB_dep.lnm

	quit
	

vSIG()	quit "60530^27697^Balasubramonian Sankar^4081"	// Signature - LTD^TIME^USER^SIZE
